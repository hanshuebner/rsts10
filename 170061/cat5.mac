	.TITLE	CAT5
	.IDENT /03.01/

;
; COPYRIGHT 1974, 1979, 1989, DIGITAL EQUIPMENT CORP., MAYNARD, MASS.
; COPYRIGHT   1974,  DIGITAL  EQUIPMENT  CORP.,  MAYNARD,   MASS.
;
; THIS SOFTWARE IS FURNISHED TO PURCHASER UNDER A LICENSE FOR USE
; ON A  SINGLE COMPUTER SYSTEM AND CAN BE  COPIED (WITH INCLUSION
; OF DEC'S COPYRIGHT  NOTICE) ONLY FOR USE IN SUCH SYSTEM, EXCEPT
; AS MAY OTHERWISE BE PROVIDED IN WRITING BY DEC.
;
; THE  INFORMATION  IN THIS DOCUMENT IS SUBJECT TO CHANGE WITHOUT
; NOTICE  AND  SHOULD NOT BE CONSTRUED AS A COMMITMENT BY DIGITAL
; EQUIPMENT CORPORATION.
;
; DEC  ASSUMES NO  RESPONSIBILITY  FOR  THE  USE  OR  RELIABILITY
; OF  ITS  SOFTWARE ON  EQUIPMENT WHICH  IS  NOT SUPPLIED BY DEC.
;
; D. N. CUTLER/C. MONIA 13-MAY-74
;
; MODIFICATIONS:
;
;	S. RUZICH	02-OCT-79
;
;		SR043 -	REMOVE LOCAL DATA, MAKING ROUTINE SUITABLE FOR
;			PIC LIBRARY OR SUPERVISOR MODE LIBRARY
; MODIFIED FOR RSX-11M-PLUS V4.2 BY:
;
; 	R. SPINNEY	20-JAN-89	03.01
;		RWS014 -- PUT MODULE IN PURE CODE PSECT
;
;
; ASCII TO RAD50 CONVERSION ROUTINE
;
; LOCAL DATA
;

	.MCALL .PCODE


;+
; **-$CAT5-CONVERT ASCII TO RAD50
;
; INPUTS:
;
;	R0=ADDRESS OF NEXT CHARACTER IN INPUT BUFFER.
;	R1=PERIOD DISPOSITION FLAG.
;		IF R1 EQ 0, THEN PERIOD IS A TERMINATOR.
;		IF R1 NE 0, THEN PERIOD IS A RAD50 CHARACTER.
;
; OUTPUTS:
;
;	C=1 IF SCAN WAS TERMINATED BY A NON-RAD50 CHARACTER.
;	C=0 IF THREE RAD50 CHARACTERS WERE CONVERTED.
;	NOTE: IF EITHER CASE THE FOLLOWING VALUES ARE RETURNED.
;		R0=ADDRESS OF NEXT CHARACTER IN INPUT BUFFER.
;		R1=PACKED RAD50 CHARACTERS.
;		R2=TERMINAL CHARACTER.
;-

	.PCODE			; PURE CODE PSECT

$CAT5::	SAVRG			;SAVE NONVOLITILE REGISTERS
	MOV	SP,R4		;SAVE STACK POINTER
	MOV	#1,-(SP)	;PUSH THE THIRD CONVERSION CONSTANT
	MOV	#50,-(SP)	; ...SECOND
	MOV	#<50*50>,-(SP)	; ...FIRST
	MOV	SP,R3		;POINTER TO CONVERSION TABLE
	MOV	R1,-(SP)	;SAVE PERIOD DISPOSITION FLAG
	CLR	R1		;CLEAR ACCUMULATION
10$:	MOVB	(R0)+,R5	;GET NEXT CHARACTER
	CMPB	#'Z,R5		;RAD50 CHARACTER?
	BLO	60$		;IF LO NO
	CMPB	#'A,R5		;ALPHABETIC?
	BLOS	50$		;IF LOS YES
	CMPB	#'9,R5		;RAD50 CHARACTER?
	BLO	60$		;IF LO NO
	CMPB	#'0,R5		;NUMERIC?
	BLOS	40$		;IF LOS YES
	CMPB	#'$,R5		;"$" SIGN?
	BEQ	30$		;YES
	CMPB	#' ,R5		;"SPACE"?
	BEQ	60$		;YES
	TST	(SP)		;PERIOD RAD50 CHARACTER?
	BEQ	60$		;IF EQ NO
	CMPB	#'.,R5		;"."?
	BEQ	40$		;YES
	BR	60$		;
20$:	SUB	#' -11,R5	;SPACE
30$:	SUB	#11-22,R5	;"$"
40$:	SUB	#22-100,R5	;PERIOD/DIGIT
50$:	SUB	#100,R5		;ALPHABETIC


	.IF	DF	V1145

	MUL	(R3),R5		;SCALE RAD50 CHARACTER

	.ENDC


	.IF	NDF	V1145

	MOV	R0,-(SP)	;SAVE NEXT BYTE ADDRESS
	MOV	(R3),R0		;GET CONVERSION CONSTANT
	MOV	R1,-(SP)	;SAVE ACCUMULATION
	MOV	R5,R1		;SET MULTIPLIER
	CALL	$MUL		;SCALE RAD50 CHARACTER
	MOV	R1,R5		;RESULT TO R5
	MOV	(SP)+,R1	;RETRIEVE PREVIOUS ACCUMULATION
	MOV	(SP)+,R0	;RETRIEVE NEXT BYTE ADDRESS

	.ENDC


	ADD	R5,R1		;ACCUMULATE RESULT
	CMP	(R3)+,#1	;CONVERTED THREE YET?
	BGT 	10$		;IF GT NO
	BR	70$		;IF EQ YES - CARRY IS CLEAR
60$:	SEC			;SET CARRY
70$:	MOVB	-1(R0),R2	;GET TERMINAL CHARACTER, RETAIN CARRY
	MOV	R4,SP		;RESTORE STACK POINTER
	RETURN			;

	.END
