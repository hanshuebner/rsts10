	.IIF DF	I$$MPR	.TITLE	VEXTA
	.IIF NDF I$$MPR	.TITLE	VEXT

	.IDENT	/06.07/
;
;		COPYRIGHT (c) 1983, 1988 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD
;	 MASSACHUSETTS.  ALL RIGHTS RESERVED.
;
; THIS  SOFTWARE  IS  FURNISHED  UNDER  A LICENSE AND MAY BE USED
; AND  COPIED  ONLY IN  ACCORDANCE WITH THE TERMS OF SUCH LICENSE
; AND WITH  THE INCLUSION  OF THE ABOVE  COPYRIGHT  NOTICE.  THIS
; SOFTWARE  OR ANY OTHER  COPIES  THEREOF, MAY NOT BE PROVIDED OR
; OTHERWISE MADE  AVAILABLE TO ANY OTHER PERSON.  NO TITLE TO AND
; OWNERSHIP OF THE SOFTWARE IS HEREBY TRANSFERED.
;
; THE INFORMATION  IN THIS DOCUMENT IS SUBJECT  TO CHANGE WITHOUT
; NOTICE AND SHOULD NOT BE CONSTRUED AS A COMMITMENT  BY  DIGITAL
; EQUIPMENT CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE OR RELIABILITY OF
; ITS SOFTWARE ON EQUIPMENT THAT IS NOT SUPPLIED BY DIGITAL.
;
;
;
; C. MONIA 26-FEB-76
;
; MODIFIED FOR RSX-11M/M-PLUS V4.0/V2.0 BY:
;
;	C. B. PETROVIC
;	J. E. POLLACK
;
; MODIFIED FOR RSX-11M/M-PLUS V4.1/V2.1 BY:
;
;	C. B. PETROVIC
;
; MODIFIED BY:
;
;	C. B. PETROVIC	10-JUN-83	06.04
;		CBP091		ADD PASCAL-11 DEFINITIONS
;
;	T. M. MARTIN	4-OCT-84	06.05
;		TMM100		ADD PDI IMPURE AREA POINTER
;
;	D. P. RABAHY	7-NOV-84	06.06
;		DPR007		ADD DIBOL VECTOR
;
;	L. B. McCulley	22-Nov-88	06.07
;		LBM146 -- Add vector for PDP-11 C
;
;
; TASK-RESIDENT IMPURE AREA VECTOR EXTENSIONS
;
; LOCAL MACROS
;
; DEFINE VECTOR EXTENSION TABLE ENTRY AND OFFSET
;
; DFEXT	OFFSET,SECTN
;
; WHERE:
;
;	OFFSET=GLOBAL OFFSET NAME
;	SECTN=NAMED .PSECT HAVING THE ATTRIBUTES D,RW,GBL,REL,OVR
;

	.MACRO	DFEXT	OFFSET,SECTN
	.ASECT
	.BLKW	-1
	.IF NDF	I$$MPR
OFFSET::.BLKW	0
	.IFTF
$$$=-.
	.IFF
	.PSECT	SECTN	D,RW,GBL,REL,OVR
$$1=.
	.PSECT
	.IF DF	SIZE
	.PSECT	$$VEX0,D,GBL,OVR
.=$$0
	.BLKW	<SIZE-$$$>/2
	.WORD	$$1
	.PSECT
	.ENDC
	.ENDC
	.ENDM

;
; EQUATED SYMBOLS
;
; INITIALIZE OFFSET LOCATION COUNTER
;

$$$=0				;

;
; LOCAL DATA
;
; ALLOCATE STORAGE TO VECTOR EXTENSIONS
;
; GLOBAL SYMBOL $VEXTA MUST BE LOCATED AT THE INTERSECTION OF
; ADJACENT PSECTS $$VEX0 AND $$VEX1. $$VEX1 IS RESERVED FOR USER-
; DEFINED VECTORS THAT ARE REFERENCED VIA POSITIVE OFFSETS FROM THE
; SPECIFIED GLOBAL. DEC-DEFINED VECTORS ARE LOCATED IN PSECT $$VEX0
; AND ARE REFERENCED THROUGH NEGATIVE OFFSETS RELATIVE TO $VEXTA
;
; THE ACTUAL STORAGE IS ALLOCATED BY DEFINING ASSEMBLY CONDITIONAL
; I$$MPR. IF THIS VARIABLE IS UNSPECIFIED ONLY THE GLOBAL OFFSETS
; ARE ESTABLISHED.
;

	.IF	NDF	I$$MPR		; EHM000
	.IF	DF	R$$IAS		; EHM000
	.ASECT				; EHM000
.=10					; EHM000
$VEXT::	.BLKW	0			; EHM000 DEFINE ABS ADDR OF
					; POINTER TO VECTOR EXTENSION AREA
.=0					; EHM000
	.PSECT				; EHM000
	.ENDC				; EHM000
	.ENDC				; EHM000

	.IF DF	I$$MPR

	.IF DF	R$$IAS

	.ASECT

.=10
				; EHM000
	.WORD	$VEXTA		; POINT TO VECTOR EXTENSION AREA
.=0

	.ENDC

;
; DEC-DEFINED VECTOR DEFINITIONS
;

	.IFT

	.PSECT	$$VEX0,D,GBL,OVR

$$0=.

	.IFTF

;***********************************************************
;
; INSERT DEC-DEFINED VECTORS AND OFFSETS HERE
;
;************************************************************

	DFEXT	V$RMS,$$RMS4	; RMS-11 WORK AREA VECTOR
	DFEXT	V$DBMS,$$DBMS	; DBMS-11 WORK AREA VECTOR
	DFEXT	V$CBL,$$CBL	; SMALL COBAL WORK AREA VECTOR
	DFEXT	V$FMS,$$FMS	; FORMS DRIVER WORK AREA VECTOR
	DFEXT	V$BP2,$$BP2	; BASIC-PLUS-2 WORK AREA VECTOR
	DFEXT	V$FEI,$$FEI	; FORTRAN EXTENSIONS WORK AREA VECTOR
	DFEXT	V$MSG,$$MSG	; MESSAGE SERVICE WORK AREA
	DFEXT	V$SRT,$$SRT	; SORT-11 IMPURE AREA VECTOR
	DFEXT	V$MGE,$$MGE	; MERGE IMPURE AREA VECTOR
	DFEXT	V$POS,$$POS	; P/OS IMPURE AREA VECTOR
	DFEXT	V$SUM,$$SUM	; 'POSSUM' IMPURE AREA VECTOR
	DFEXT	V$PAS,$$PAS	; PASCAL-11 IMPURE AREA VECTOR
	DFEXT	V$PDL,$$PDL	; POSITIONAL DEVICE LIBRARY VECTOR
	DFEXT	V$DBL,$$DBL	; DIBOL RUNTIME SYSTEM VECTOR
	DFEXT	V$C,$$C		; PDP-11 C IMPURE AREA VECTOR

SIZE=$$$			; SIZE OF DEC VECTOR SPACE

	.IFT

	.PSECT	$$VEX0

.=$$0				;
	.BLKW	SIZE/2		; ALLOCATE VECTOR STORAGE

;
; DEFINE START OF USER-ALLOCATED VECTORS
;

	.IFT

	.PSECT	$$VEX1,D,GBL,OVR

$VEXTA::.BLKW	0		;

	.ENDC

;+
; **-VEXT-DEFINE VECTOR EXTENSION OFFSETS
; **-VEXTA-ALLOCATE TASK IMPURE VECTOR EXTENSION AREA
;
; THE PURPOSE OF THIS MODULE IS TO ALLOCATE SPACE FOR IMPURE WORK
; AREA POINTERS THAT CAN BE REFERENCED BY ROUTINES WITHIN A
; RESIDENT LIBRARY. THE ADDRESS OF THIS AREA (LOCATION $VEXTA)
; IS CONTAINED AT ABSOLUTE ADDRESS $VEXT (IN THE TASK HEADER
; UNDER RSX-11M).
;
; SPACE THAT IS RESERVED FOR DEC-SUPPLIED SOFTWARE IS ALLOCATED
; BELOW GLOBAL $VEXTA. USER VECTORS ARE LOCATED ABOVE THIS SYMBOL.
;
; VECTOR STORAGE IS RESERVED WITHIN TWO PHYSICALLY ADJACENT PSECTS,
; EACH HAVING THE 'OVR' ATTRIBUTE. THE CONTENT OF A USER-DEFINED

; VECTOR IS ESTABLISHED BY REFERENCING PSECT $$VEX1 FROM THE MODULE
; THAT DEFINES THE IMPURE STORAGE AREA AS SHOWN BELOW:
;
; EXAMPLE:
;
;
;		.GLOBL	$VEXTA		; LINK VECTOR EXTENSION AREA
;
;		.PSECT	$$VEX1,D,GBL,OVR
; $$$=.
;		.BLKW	N	; STEP PAST PREVIOUSLY ASSIGNED VECTORS
; LABEL:	.WORD	IMPURE	; SET POINTER TO IMPURE AREA
; OFFSET==$$$-LABEL		; GLOBALLY DEFINE OFFSET
;
;		.PSECT	DATA
; IMPURE:			; START OF IMPURE AREA
;
; IT IS RECOMMENDED THAT USERS CENTRALIZE ALL OFFSET DEFINITIONS WITHIN
; A SINGLE MODULE FROM WHICH THE ACTUAL VECTOR SPACE ALLOCATION IS MADE.
; THE SOURCE SHOULD BE CONDITIONALIZED TO CREATE TWO OBJECT MODULES;
; ONE THAT RESERVES THE ACTUAL VECTOR STORAGE WITHIN THE TASK IMAGE
; AND A SECOND THAT DEFINES THE GLOBAL OFFSETS TO BE REFERENCED BY SUB-
; ROUTINES THAT ARE PART OF THE RESIDENT LIBRARY.
;
; NOTE IN THE EXAMPLE ABOVE THAT THE GLOBAL SYMBOL IS INTENTIONALLY
; REDEFINED BY THE MODULE THAT RESERVES WORK AREA STORAGE. THE TASK
; BUILDER WILL REPORT AN ERROR IF THIS VALUE DIFFERS FROM THE CENTRA-
; LIZED DEFINITION.
;
;
; DEC-RESERVED VECTORS ARE DEFINED SOLELY WITHIN THIS MODULE THROUGH
; THE DFEXT MACRO.
;
;
; THE VECTOR CAN BE LOCATED THROUGH A SEQUENCE OF INSTRUCTIONS
; SIMILAR TO THAT BELOW:
;
;	MOV	@#$VEXT,R0	; GET ADDRESS OF VECTOR EXTENSIONS
;	MOV	OFFSET(R0),R0	; POINT TO IMPURE AREA
;
;-

	.END
