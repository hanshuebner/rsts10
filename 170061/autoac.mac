	.TITLE	$AUTOA
	.IDENT	/01.1/
 
;
; COPYRIGHT (C) 1978
; DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE FOR USE ONLY ON A
; SINGLE COMPUTER SYSTEM AND MAY  BE  COPIED   ONLY  WITH  THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE,  OR
; ANY OTHER COPIES THEREOF, MAY NOT BE PROVIDED  OR  OTHERWISE
; MADE AVAILABLE TO ANY OTHER PERSON   EXCEPT FOR  USE ON SUCH
; SYSTEM AND TO ONE WHO AGREES TO THESE LICENSE  TERMS.  TITLE
; TO AND OWNERSHIP OF THE SOFTWARE SHALL AT ALL  TIMES  REMAIN
; IN DEC.
;
; THE INFORMATION IN THIS DOCUMENT IS SUBJECT TO CHANGE WITHOUT
; NOTICE AND SHOULD NOT BE CONSTRUED AS A COMMITMENT BY DIGITAL
; EQUIPMENT CORPORATION.
;
; DEC ASSUMES NO RESPONSIBILITY FOR THE USE OR RELIABILITY OF
; ITS SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DEC.
;         
;
; VERSION 01
;
; J.A. KASSON	07-APR-78
;
; MODIFICATIONS:
;
;	R. E. CALDWELL	13-JUL-79
;
;		RC002 -- SUPPORT CSM INSTRUCTION 
; 
	.MCALL	DIR$,CSM$
 
 
	WDBDF	,,S
; GLOBAL DATA
;
	
;
; SUPERVISOR MODE LIBRARY COMPLETION ROUTINE ADDRESS'
;
	.PSECT	$$CRVC
 
$CRVC::	.BLKW	8.
 
 
	.PSECT	$$AUTA
 
; 
; LOCAL DATA
;
 
NALVC:	.BLKW	2		; NEW AUTO LOAD VECTOR 
CRAD:	.BLKW	1		; COMPLETION ROUTINE ADDRESS
SRAD:	.BLKW	1		; ADDRESS OF SUPERVISOR ENTRY POINT
 
;
;+
; **-$AUTOA - SUPERVISOR MODE LIBRARY AUTO LOAD SUBROUTINE
;		EACH SUPERVISOR MODE LIBRARY LINKED TO HAS
;		A SEPARATE ENTRY (AUTOA,AUTOB,...AUTOH).
;		THE ROUTINE PICKS UP THE SUPERVISOR MODE
;		LIBRARY'S COMPLETION ROUTINE ADDRESS, CALLS
;		$AUTO, AND ISSUE THE SCAL$ DIRECTIVE, WHICH
;		DOES SWITCH TO SUPERVISOR MODE AND GIVES
;		CONTROL TO THE SUPORVISOR ROUTINE
;
;-
;
;
 
  
$AUTOA::
	MOV	$CRVC,-(SP)	; GET ADDRESS OF COMPLETION ROUTINE
	JMP	AUTOS
 
 
	.PSECT	$$AUTS,I,LCL,RW,REL,OVR
 
AUTOS:
	MOV	(SP)+,CRAD 	; SAVE COMPLETION ROUTINE ADDRESS 
	MOV	R1,-(SP)	; SAVE REGISTER
	MOV	2(SP),R1	; GET ADDRESS OF SEGMENT DESCRIPTOR
	MOV	(R1),NALVC	; GET ADDRESS OF SEGMENT DESCRIPTOR
	MOV	#20$,NALVC+2	; PUT RETURN ADDRESS IN NEW AUTO LOAD VECTOR
	MOV	2(R1),SRAD 	; SAVE ADDRESS OF SUPERVISOR ROUTINE 
	MOV	(R1),R1		; GET ADDRESS OF SEGMENT DESCRIPTOR
	MOV	T$RWDP(R1),R1	; GET ADDRESS OF WINDOW BLOCK DESCRIPTOR
	BIS	#WS.SIS,W.NSTS(R1) ; SET SUPERVISOR FLAG
	MOV	(SP)+,R1	; RESTORE R1
	TST	(SP)+		; CLEAN STACK
	MOV	#NALVC,-(SP)	; PUT NEW VECTOR ADDESS ON STACK
	JMP 	$AUTO		; AUTO LOAD SEGMENT
20$:
	CSM$	SRAD		; ISSUE CSM MACRO
	RETURN			; 
 
	.END
