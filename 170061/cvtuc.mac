	.TITLE	CVTUC
	.IDENT	/00.01/

;
; COPYRIGHT 1975, 1989, DIGITAL EQUIPMENT CORP., MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED TO PURCHASER UNDER A LICENSE FOR USE
; ON A SINGLE COMPUTER SYSTEM  AND  CAN BE COPIED (WITH INCLUSION
; OF DEC'S COPYRIGHT NOTICE)  ONLY FOR USE IN SUCH SYSTEM, EXCEPT
; AS MAY OTHERWISE BE PROVIDED IN WRITING BY DEC.
;
; THE INFORMATION IN  THIS DOCUMENT IS  QUBJECT TO CHANGE WITHOUT
; NOTICE AND SHOULD NOT  BE CONSTRUED AS  A COMMITMENT BY DIGITAL
; EQUIPMENT CORPORATION.
;
; DEC ASSUMES  NO  RESPONSIBILITY  FOR  THE  USE  OR  RELIABILITY
; OF ITS  SOFTWARE  ON  EQUIPMENT  WHICH IS NOT  SUPPLIED BY DEC.
;
; C. MONIA 23-JAN-75
;
;
; MODIFIED FOR RSX-11M-PLUS V4.2 BY:
;
;	R. SPINNEY	18-JAN-89	00.01
;		RWS014 -- PUT MODULE IN PURE CODE PSECT
;
; CONVERT ASCII TEXT TO UPPER CASE
;
	.MCALL	.PCODE

;+
; **-$CVTUC-CONVERT TEXT TO UPPER CASE
;
; THIS SUBROUTINE IS CALLED TO PERFORM A BYTE BY BYTE TRANSFER
; OF AN ASCII STRING CONVERTING ALL ALPHABETIC CHARACTERS
; TO UPPER CASE.
;
; INPUTS:
;
;	R0=SOURCE ADDRESS
;	R1=DESTINATION ADDRESS
;	R2=BYTE COUNT
;
;	IF R0=R1 THEN THE STRING IS CONVERTED IN PLACE.
;
;
; OUTPUTS:
;
;	R0=ADDRESS OF NEXT BYTE AFTER INPUT STRING
;	R1=ADDRESS OF NEXT BYTE AFTER OUTPUT STRING
;
;	THE STRING IS MOVED FROM THE LOCATIONS POINTED TO BY
; R0 TO THE LOCATIONS SPECIFIED BY R1. ALL CHARACTERS BETWEEN
; LOWER CASE 'A' AND LOWER CASE 'Z' ARE CONVERTED TO UPPER CASE
;
; R2 IS DESTROYED, R3 - R5 ARE PRESERVED
;
;-

	.PCODE			; PURE CODE PSECT
$CVTUC::			;
	CMPB	(R0),#141	; CHARACTER IN LOWER CASE RANGE?
	BLO	10$		; IF LO NO
	CMPB	(R0),#172	; CHARACTER OUTSIDE LOWER CASE RANGE?
	BHI	10$		; IF HI YES
	BICB	#40,(R0)	; CONVERT TO UPPER CASE
10$:				;
	MOVB	(R0)+,(R1)+	; MOVE CHARACTER
	SOB	R2,$CVTUC	; REPEAT
	RETURN			;

	.END
