;
; COPYRIGHT 1980, DIGITAL EQUIPMENT CORP., MAYNARD, MASS 01754
;
; THIS SOFTWARE IS FURNISHED TO PURCHASER UNDER A LICENSE FOR USE
; ON A SINGLE COMPUTER SYSTEM AND CAN BE COPIED (WITH INCLUSION
; OF DEC'S COPYRIGHT NOTICE) ONLY FOR USE IN SUCH SYSTEM, EXCEPT
; AS MAY OTHERWISE BE PROVIDED IN WRITING BY DEC.
;
; THE INFORMATION IN THIS DOCUMENT IS SUBJECT TO CHANGE WITHOUT
; NOTICE AND SHOULD NOT BE CONSTRUED AS A COMMITMENT BY DIGITAL
; EQUIPMENT CORPORATION.
;
; DEC ASSUMES NO RESPONSIBILITY FOR THE USE OR RELIABILITY
; OF ITS SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DEC.
;
;
; VERSION 001
;
; WRITTEN BY:
;		S.J. MORRIS	24-NOV-80
;
; MODIFIED BY:
;
;  FEK001	31-AUG-82	CORRECT 'TST #FIRQB' ERROR CHECK
;+
;
; CLOSE AND QUE FILE TO PRINTER
;
;	PRINT$ FDB
;
; WHERE:
;
;	FDB=ADDRESS OF FDB OF FILE TO SUBMIT.
;
;-

	.MACRO	PRINT$ FDB
	.MCALL	CLOSE$,LDR0$,RETURN
	LDR0$	FDB			;++RSTS V7.1 LOAD FDB ADDRESS INTO R0
	TST	R0			;++RSTS V7.0 ANYTHING THERE?
	BEQ	30$			;++RSTS V7.0 NOPE
	TST	F.BDB(R0)		;++RSTS V7.1 FILE OPEN?
	BEQ	30$			;++RSTS V7.1 IF EQ NO
	CLOSE$	R0			;++RSTS V7.0 ...AND CLOSE FILE 
	BCS	30$			;++RSTS V7.0 OPEN FAILED?
	MOV	R1,-(SP)		;++RSTS V7.0 SAVE WORKING REGISTER
	MOV	R0,-(SP)		;++RSTS V7.0 ...AND ANOTHER
	MOV	#FIRQB+FQBSIZ,R1	;++RSTS V7.0 -> END OF FIRQB
	MOV	#FQBSIZ/2,R0		;++RSTS V7.0 FIRQB SIZE IN WORDS
10$:	CLR	-(R1)			;++RSTS V7.0 CLEAR FIRQB
	SOB	R0,10$			;++RSTS V7.0
	MOV	(SP)+,R0		;++RSTS V7.0 RESTORE -> FDB
	CMP	(R1)+,(R1)+		;++RSTS V7.0 -> FQFIL
	MOVB	#UU.SPL,-1(R1)		;++RSTS V7.0 FQFUN
	CLR	(R1)+			;++RSTS V7.0 FQFIL
	MOV	F.FNB+N.DID(R0),(R1)+	;++RSTS V7.0 FQPPN
	MOV	F.FNB+N.FNAM(R0),(R1)+	;++RSTS V7.0 FQNAM1
	MOV	F.FNB+N.FNAM+2(R0),(R1)+;++RSTS V7.0 FQNAM1+2
	MOV	F.FNB+N.FTYP(R0),(R1)+	;++RSTS V7.0 FQEXT
	MOV	#"LP,(R1)		;++RSTS V7.0 FQSIZ
	ADD	#FQDEV-FQSIZ,R1		;++RSTS V7.0 -> FQDEV
	MOV	F.FNB+N.DVNM(R0),(R1)+	;++RSTS V7.0 FQDEV
	MOVB	F.FNB+N.UNIT(R0),(R1)+	;++RSTS V7.0 FQDEVN
	MOVB	#377,(R1)+		;++RSTS V7.0 MAKE IT SPECIFIC
	CMP	#"SY,-4(R1)		;++RSTS V7.0 BUT WAS IT "SY:"?
	BNE	20$			;++RSTS V7.0 NOPE
	CLR	-(R1)			;++RSTS V7.0 MAKE IT "SY:" AGAIN
20$:	MOV	(SP)+,R1		;++RSTS V7.0 RESTORE WORKING REGISTER
	.UUO				;++RSTS V7.0 DO IT
	TSTB	@#FIRQB			;++RSTS V7.1 ERRORS?		;FEK001
	BEQ	30$			;++RSTS V7.1 NO ERRORS OK
	SEC				;++RSTS V7.1 UUO FAILED
30$:
	.ENDM


