	.TITLE	ULAIN
	.IDENT	/01/
 
; COPYRIGHT (C) 1978
; DIGITAL EQUIPMENT CORPORATION, MAYNARD MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LISENSE FOR USE ONLY ON A
; SINGLE COMPUTER SYSTEM AND MAY  BE  COPIED   ONLY  WITH  THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE,  OR
; ANY OTHER COPIES THEREOF, MAY NOT BE PROVIDED  OR  OTHERWISE
; MADE AVAILABLE TO ANY OTHER  PERSOM  EXCEPT FOR  USE ON SUCH
; SYSTEM AND TO ONE WHO AGREES TO THESE LICENSE  TERMS.  TITLE
; TO AND OWNERSHIP OF THE SOFTWARE SHALL AT ALL  TIMES  REMAIN
; IN DEC.
;
; THE INFORMATION IN THIS DOCUMENT IS SUBJECT TO CHANGE WITHOUT
; NOTICE AND SHOULD NOT BE CONSTRUED AS A COMMITMENT BY DIGITAL
; EQUIPMENT CORPORATION.
;
; DEC ASSUMES NO RESPONSIBILITY FOR THE USE OR RELIABILITY OF
; ITS SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DEC.
;
; THIS SOFTWARE IS FURNISHED TO PURCHASER UNDER A LICENSE FOR USE
; ON A SINGLE COMPUTER SYSTEM AND CAN BE COPIED (WITH INCLUSION
; OF DEC'S COPYRIGHT NOTICE) ONLY FOR USE IN SUCH SYSTEM, EXCEPT
; AS MAY OTHERWISE BE PROVEDED IN WRITING BY DEC.
 
; THE INFORMATION IN THIS DOCUMENT IS SUBJECT TO CHANGE WITHOUT
; NOTICE AND SHOULD NOT BE CONSTRUED AS A COMMITMENT BY DIGITAL
; EQUIPMENT CORPORATION.
;
;
; VERSION 01
;
;
; BERNARD ALIMONTI	18-NOV-78
;
;
;
; UNIVERSAL LIBRARY ACCESS INITIALIZATION
;
;
 
 
;
; MACRO CALLS
;
 
	.IF NDF S$$LIB
 
	.MCALL	GET$
 
	.IFF
 
	.MCALL	GET$S
 
	.ENDC
 
 
 
 
;
; DIRECT ASSIGNMENTS
;
 
LHSIZE	=56			;UNI LIBRARY HEADER SIZE
ID	=2			;LIBRARY FILE ID CODE
UNI	=2			;UNI LIB TYPE CODE
 
 
 
 
;+
;
; **-$$ULAIN-UNIVERSAL LIBRARY ACCESS INITIALIZATION
;
; THIS ROUTINE DOES THE INITIALIZATION FOR THE SET-UP OF ACCESSING AN UNIVERSAL
; LIBRARY--VALIDATION OF FILE BEING A UNIVERSAL LIBRARY AND INITIALIZ-
; ATION OF CRUCIAL DATA.
;
; INPUTS:
;
;	R0 = FDB ADDRESS OF THE UNIVERSAL LIBRARY
;
;	LIB. FILE SET FOR ACCESS IN LOCATE MODE
;
; OUTPUTS:
;	R0, R3-R5 = UNCHANGED
;	R1 = GARBAGE
;	R2 = ADDRESS OF LIBRARY HEADER RECORD -- F.NBRD(R0)
;
;	LIB. FILE SET FOR ACCESS IN LOCATE MODE
;	LIB. FILE REC TYPE SET TO FIXED LENGTH
;	LIB. FILE REC SIZE SET TO SIZE OF UNIVERSAL LIB. HEADER
;
;	C-BIT SET = ERROR
;	C-BIT CLEARED = NO ERROR
;
;	F.ERR  = INTERPRETED AS USUAL, EXCEPT IE.BHD IS INTERPRETED AS
;			"FILE NOT A UNIVERSAL LIBRARY" OR "BAD LIBRARY HEADER"
;
;-
 
 
 
 
 
 
 
 
$ULAIN::SAVRG
	CLR	R1		;CLEAR R1 IN PREP OF POINT
	MOV	#1,R2		;MOVE 1 TO R1 IN PREP OF POINT
	CLR	R3		;CLEAR R3 IN PREP OF POINT
	CALL	.POINT		;POINT TO LIBRARY FILE HEADER
	BCS	20$		;IF CS, POINT ERROR, SO RETURN
	MOVB	#R.FIX,F.RTYP(R0) ;INDICATE FIXED LENGTH REC
	MOV	#LHSIZE,F.RSIZ(R0) ;SET REC SIZE TO SIZE OF LIB HEADER
 
 
	.IF	NDF	S$$LIB
 
 
	GET$	R0		;GET LIBRARY HEADER
 
 
	.IFF
 
 
	GET$S	R0		;GET LIBRARY HEADER
 
 
	.ENDC
 
 
	BCS	20$		;IF CS, INPUT ERROR, SO REPORT IT
	MOV	F.NRBD+2(R0),R2	;POINT R2 TO INPUT RECORD
5$:	CMPB	#ID,1(R2)	;LIBRARY FILE?
	BNE	15$		;IF NE, NO, SO BRANCH TO REPORT ERROR
	CMPB	#UNI,(R2)	;UNI LIBRARY TYPE?
	BEQ	20$		;IF EQ, YES, SO CONTINUE
15$:	MOV	#IE.BHD,F.ERR(R0) ;INDICATE BAD LIB HDER OR NOT UNI LIB
	SEC			;SET C-BIT TO INDICATE ERROR
20$:	RETURN
 
 
	.END
