	.TITLE	C5TA
	.IDENT	/02.01/

;
; COPYRIGHT 1974, 1989, DIGITAL EQUIPMENT CORP., MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED TO PURCHASER UNDER A LICENSE FOR USE
; ON A  SINGLE COMPUTER SYSTEM AND CAN BE  COPIED (WITH INCLUSION
; OF DEC'S COPYRIGHT  NOTICE) ONLY FOR USE IN SUCH SYSTEM, EXCEPT
; AS MAY OTHERWISE BE PROVIDED IN WRITING BY DEC.
;
; THE  INFORMATION  IN THIS DOCUMENT IS SUBJECT TO CHANGE WITHOUT
; NOTICE  AND  SHOULD NOT BE CONSTRUED AS A COMMITMENT BY DIGITAL
; EQUIPMENT CORPORATION.
;
; DEC  ASSUMES NO  RESPONSIBILITY  FOR  THE  USE  OR  RELIABILITY
; OF  ITS  SOFTWARE ON  EQUIPMENT WHICH  IS  NOT SUPPLIED BY DEC.
;
; D.N. CUTLER 11-SEP-72
;
; MODIFIED FOR RSX-11M-PLUS V4.2 BY:
;	
;	R. SPINNEY	18-JAN-89	02.01
;		RWS014 -- PUT MODULE IN PURE CODE PSECT
;
;+
	.MCALL	.PCODE

; **-$C5TA-RADIX 50 TO ASCII CONVERSION
;
; INPUTS:
;
;	R0=ADDRESS TO STORE FIRST BYTE IN OUTPUT STRING.
;	R1=RAD50 WORD TO BE CONVERTED
;
; OUTPUTS:
;
;	NUMBER IS CONVERTED TO DECIMAL AND STORED IN CONSECUTIVE BYTES.
;	R0=ADDRESS OF NEXT BYTE AFTER LAST DIGIT STORED.
;-

	.PCODE			;PURE CODE PSECT

$C5TA::	MOV	R0,R2		;SAVE BYTE ADDRESS
	CALL	CVTC		;CONVERT FIRST CHAR
	MOV	R0,-(SP)	;SAVE IT
	CALL	CVTC		;CONVERT SECOND CHAR
	MOV	R0,-(SP)	;SAVE IT
	CALL	CVTC		;CONVERT THIRD CHAR
	MOVB	R0,(R2)+	;STORE IT
	MOVB	(SP)+,(R2)+	;STORE SECOND CHAR
	MOVB	(SP)+,(R2)+	;STORE FIRST CHARACTER
	MOV	R2,R0		;SAVE TERMINAL ADDRESS
	RETURN			;

;
;	CONVERT RAD50 CHARACTER
;


	.IF	DF	V1145

CVTC:	CLR	R0		;CLEAR UPPER PART
	DIV	#50,R0		;DIVIDE EM UP

	.ENDC


	.IF	NDF	V1145

CVTC:	MOV	R1,R0		;DIVIDEND TO R0
	MOV	#50,R1		;DIVISOR TO R1
	CALL	$DIV		;DIVIDE EM UP

	.ENDC


	MOV	R1,-(SP)	;SAVE REMAINDER
	MOV	R0,R1		;QUOTIENT TO R1
	MOV	(SP)+,R0	;RETRIEVE REMAINDER
	BEQ	3$		;IF EQ BLANK
	CMP	R0,#33		;TEST MIDDLE
	BLT	2$		;IF LT ALPHA
	BEQ	1$		;IF EQ DOLLAR
	ADD	#22-11,R0	;
1$:	ADD	#11-100,R0	;
2$:	ADD	#100-40,R0	;
3$:	ADD	#40,R0		;
	RETURN			;

	.END
