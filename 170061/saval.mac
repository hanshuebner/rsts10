	.TITLE	SAVAL
	.IDENT /00.01/

;
; COPYRIGHT 1974, 1989, DIGITAL EQUIPMENT CORP., MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED TO PURCHASER UNDER A LICENSE FOR USE
; ON A  SINGLE COMPUTER SYSTEM AND CAN BE COPIED (WITH INCLUSION
; OF DEC'S COPYRIGHT NOTICE) ONLY FOR USE IN SUCH SYSTEM, EXCEPT
; AS MAY OTHERWISE BE PROVIDED IN WRITING BY DEC.
;
; THE  INFORMATION IN THIS DOCUMENT IS SUBJECT TO CHANGE WITHOUT
; NOTICE AND  SHOULD NOT BE CONSTRUED AS A COMMITMENT BY DIGITAL
; EQUIPMENT CORPORATION.
;
; DEC  ASSUMES NO  RESPONSIBILITY  FOR  THE  USE  OR RELIABILITY
; OF ITS  SOFTWARE ON  EQUIPMENT WHICH  IS NOT SUPPLIED BY DEC.
;
; C. MONIA 25-JUN-74
; MODIFIED FOR RSX-11M-PLUS V4.2 BY:
;
; 	R. SPINNEY	20-JAN-89	00.01
;		RWS014 -- PUT MODULE IN PURE CODE PSECT
;
; SUBROUTINE TO SAVE AND RESTORE R0 - R5
;
; MACRO LIBRARY CALLS
;
 
	.MCALL CALL, RETURN, .PCODE
 
;+
; **-$SAVAL-SAVE AND RESTORE R0-R5
;
; THIS MODULE IS ENTERED VIA A 'JSR	PC,$SAVAL' TO SAVE AND
; SUBSEQUENTLY RESTORE REGISTERS R0 THROUGH R5. AFTER STORING THE
; REGISTER CONTENTS ON THE STACK A CO-ROUTINE CALL IS MADE TO
; THE CALLING ROUTINE. A SUBSEQUENT 'RTS	PC' CAUSES THIS
; SUBROUTINE TO RESTORE ALL REGISTERS AND EXIT VIA AN  'RTS	PC'.
;
; ALL REGISTER CONTENTS ARE PRESERVED.
;-
 
	.PCODE				; PURE CODE PSECT

$SAVAL::				;
	MOV	R4,-(SP)		; SAVE R4
	MOV	R3,-(SP)		; SAVE R3
	MOV	R2,-(SP)		; SAVE R2
	MOV	R1,-(SP)		; SAVE R1
	MOV	R0,-(SP)		; SAVE R0
	MOV	12(SP),-(SP)		; COPY RETURN
	MOV	R5,14(SP)		; SAVE R5
	CALL	@(SP)+			; CALL THE CALLER
	MOV	(SP)+,R0		; RESTORE R0
	MOV	(SP)+,R1		; RESTORE R1
	MOV	(SP)+,R2		; RESTORE R2
	MOV	(SP)+,R3		; RESTORE R3
	MOV	(SP)+,R4		; RESTORE R4
	MOV	(SP)+,R5		; RESTORE R5
	RETURN				;
 
	.END
