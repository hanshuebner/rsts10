TITLE	RT11,<EMULATOR FOR RSTS (V03)>,14,18-APR-85,ABC/SJK/PRL/SDL

;
;		COPYRIGHT (c) 1974, 1985 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

.ENABL	LC

; MACROS FOR THE EMULATOR	"Expansion means complexity"
;
;				 - C. Northcote Parkinson

.MACRO	EMTDEF	NAME,TITLE
.SBTTL	EMT NAME Entry - TITLE
NAME:
.ENDM	EMTDEF

.EQUATE	.PRIV,		EMT!377
.EQUATE	.SETFQB,	EMT!360

; Macro to generate text for GETUNQ
.MACRO	UNQTXT	TXT
.NLIST
.DSABL	CRF
$$$$$$	= 0
.IRPC	X,<TXT>
.IF EQ	''X-'\
$$$$$$	= 200
.IFF
	.BYTE	''X+$$$$$$
.ENDC
.ENDM	; .IRPC
	.BYTE	377
.ENABL	CRF
.LIST
.ENDM	UNQTXT

; Macro to create KMON commands
.MACRO	KMCMD	ENT,TXT,TTL
.SBTTL	KMON Command "TXT" - TTL (Entry is ENT)
	TMPORG	KMDISP
.NLIST
	+	ENT
.LIST
	TMPORG	KMTEXT
	UNQTXT	<TXT>
	UNORG
ENT:
.ENDM	KMCMD

; Macro to do .WRITE (preserve XRBC)					;PRL001
.MACRO	WRITE.								;PRL001
	 MOV	XRB+XRBC,-(SP)	;save XRBC				;PRL001
	 .PRIV,	.WRITE							;PRL001
	 MOV	(SP)+,XRB+XRBC	;restore XRBC				;PRL001
.ENDM	WRITE.								;PRLOO1

.MACRO	EMTER	N
	 TRAP	N
.ENDM	EMTER

.MACRO	KMEROR	TXT,FTL
.NLIST
.IF B	<FTL>
	JSR	R0,KMEROR
.IFF
	JSR	R0,KMERRF
.ENDC
	.ASCIZ	"?TXT?"
	.EVEN
.LIST
.ENDM	KMEROR

.MACRO	MONERR	TXT
.NLIST
	TMPORG	TEXT
.DSABL	CRF
$$$$$$	= .
.ENABL	CRF
	.ASCII	"TXT"<200>
UNORG
	JSR	R2,MONERR
.DSABL	CRF
	+	$$$$$$
.ENABL	CRF
.LIST
.ENDM	MONERR

.MACRO	ERROR	TXT,CODE	; Generate call to error message print
.NLIST
	TMPORG	TEXT
.DSABL	CRF
$$$$$$	= .
.ENABL	CRF
	.ASCIZ	<CODE>"?TXT?"
	UNORG
	JSR	R0,MSG
.DSABL	CRF
	+	$$$$$$
.ENABL	CRF
.LIST
.ENDM	ERROR

.MACRO	.RCTRLO
	 EMT	355
.ENDM	.RCTRLO

.MACRO	.SRESET
	 EMT	352
.ENDM	.SRESET

.MACRO .LOOKUP .CHAN,.DEVBLK
.IF NB <.DEVBLK>
	 MOV	.DEVBLK,R0
.ENDC
	 EMT	20+.CHAN
.ENDM	.LOOKUP

.MACRO .READW .CHAN,.BUFF,.WCNT,.BLK
.IF NB <.BLK>
	 MOV	.BLK,R0
.ENDC
	 CLR	-(SP)
	 MOV	.WCNT,-(SP)
	 MOV	.BUFF,-(SP)
	 EMT	200+.CHAN
.ENDM	.READW

.MACRO .CLOSE .CHAN
	 EMT	160+.CHAN
.ENDM	.CLOSE

.MACRO	.EXIT
	 EMT	350
.ENDM	.EXIT

.MACRO	SETERR	CODE,DST=FIRQB,WORD
.DSABL	CRF
.GLOBL	CODE
.ENABL	CRF
.IF	B	<WORD>
	 MOVB	#CODE,DST
.IFF
	 MOV	#CODE,DST
.ENDC
.ENDM	SETERR

.MACRO	CHKERR	CODE,SRC=FIRQB
.DSABL	CRF
.GLOBL	CODE
.ENABL	CRF
	 CMPB	SRC,#CODE
.ENDM	CHKERR

.MACRO	GENERR	CODE,BYTE
.DSABL	CRF
.GLOBL	CODE
.ENABL	CRF
.IF	B	<BYTE>
	 .WORD	CODE
.IFF
	 .BYTE	CODE
.ENDC
.ENDM	GENERR

.SBTTL	Order the CSECTS in Memory

;				"A. A violent order is disorder; and
;				 B. A great disorder is an order.  These
;				 Two things are one."
;
;				 - Wallace Stevens, Connoisseur of Chaos

	ORG	RT11
	ORG	TBLS
	ORG	KMDISP
	ORG	KMTEXT
	ORG	TEXT
	ORG	PATCH
	ORG	VE

	ORG	RT11

	GLOBAL	<RT11,TBLS,KMDISP,KMTEXT,TEXT,PATCH,VE>

L$$IST	=	1	; Let's see the prefix files

; RTS parameters (globalized for RTSODT)

.EQUATE	O.ISIZ	, 8.	; Initial size
.EQUATE	O.MSIZ	, 2.	; Minimum size
.EQUATE	O.SIZE	,28.	; Maximum size
.EQUATE	O.DEXT	,<^RSAV> ; Default extension
.EQUATE	O.FLAG,<PF.EMT!PF.CSZ!PF.KBM!<.PRIV&377>> ; RTS characteristics

GLOBAL	<O.ISIZ,O.MSIZ,O.SIZE,O.DEXT,O.FLAG>

