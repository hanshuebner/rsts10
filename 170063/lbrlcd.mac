	.TITLE	LBRLCD
	.IDENT	/04/

;
; COPYRIGHT (C) 1974, 1976
; DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE FOR USE ONLY ON A
; SINGLE COMPUTER SYSTEM AND MAY  BE  COPIED   ONLY  WITH  THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE,  OR
; ANY OTHER COPIES THEREOF, MAY NOT BE PROVIDED  OR  OTHERWISE
; MADE AVAILABLE TO ANY OTHER PERSON   EXCEPT FOR  USE ON SUCH
; SYSTEM AND TO ONE WHO AGREES TO THESE LICENSE  TERMS.  TITLE
; TO AND OWNERSHIP OF THE SOFTWARE SHALL AT ALL  TIMES  REMAIN
; IN DEC.
;
; THE INFORMATION IN THIS DOCUMENT IS SUBJECT TO CHANGE WITHOUT
; NOTICE AND SHOULD NOT BE CONSTRUED AS A COMMITMENT BY DIGITAL
; EQUIPMENT CORPORATION.
;
; DEC ASSUMES NO RESPONSIBILITY FOR THE USE OR RELIABILITY OF
; ITS SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DEC.
;
; THIS SOFTWARE IS FURNISHED TO PURCHASER UNDER A LICENSE FOR USE
; ON A SINGLE COMPUTER SYSTEM AND CAN BE COPIED (WITH INCLUSION
; OF DEC'S COPYRIGHT NOTICE) ONLY FOR USE IN SUCH SYSTEM, EXCEPT
; AS MAY OTHERWISE BE PROVIDED IN WRITING BY DEC.
;
; THE INFORMATION IN THIS DOCUMENT IS SUBJECT TO CHANGE WITHOUT
; NOTICE AND SHOULD NOT BE CONSTRUED AS A COMMITMENT BY DIGITAL
; EQUIPMENT CORPORATION.
;
; VERSION 04
;
; WRITTEN BY:
;	GEORGE W. BERRY
;
; MODIFIED BY:
;	C.A. D'ELIA	16-JUN-74
;	C.A. D'ELIA	20-JUN-74  (VIRTUAL TABLES)
;	THOMAS J. MILLER 07-NOV-74
;
;	T. J. MILLER 11-JAN-76
;
;		TM001 -- ADDITION OF EXTRACT SWITCH (/EX).
;
;

;
; MACRO LIBRARY CALLS
;

	.MCALL	FDOFF$
	FDOFF$	DEF$L
	.MCALL	FCSBT$
	FCSBT$
	.MCALL	CSI$
	CSI$
	.MCALL	CLOSE$


;+
;
; **-$LISCD- LISTING COMMAND DECODER
;
; THIS ROUTINE SCANS THE COMMAND LINE FOR A LISTING OUTPUT
; SPECIFICATION.  IF IT IS PRESENT, THE LISTING ROUTINE,
; '$LIS' IS CALLED.  A DIAGNOSTIC IS ISSUED IF TOO MANY OUTPUT
; FILES ARE SPECIFIED.  FINALLY, THE LIBRARY FILE IS CLOSED AND
; THE LIBRARIAN IS RESTARTED OR TERMINATED.
;
; INPUTS:
;
;	R5 = $SWTCH ADDRESS
;
; OUTPUTS:
;
;	NONE
;
;-

$LISCD::
	BIT	#EX$SW,(R5)	;WAS /EX SPECIFIED?                             ; TM001
	BNE	60$		;IF NE YES, NO LISTING                          ; TM001
	MOV	#$LISPT,R4	;PREPARE TO SCAN FOR LISTING FILE
	MOV	#LISPT,(R4)	;INSURE PROPER FDB ADDRESS
	MOV	(R4),R0		;PUT LISTING FDB ADDR IN R0
	MOV	#$CSIBK,R1	;PUT CSI BLOCK ADDR IN R1

	.IF	GT,R$RSTS	;++RSTS 7.2
	BIS	#SP$SW,(R5)	;++RSTS 7.2 DEFAULT TO '/-SP'
	.ENDC			;++RSTS 7.2

	CALL	$SCANO		;SCAN THE COMMAND LINE
	BCC	40$		;BRANCH IF LISTING FILE FOUND
	BIT	#LI$SW,(R5)	;ELSE, TEST FOR /LI SWITCH
	BEQ	50$		;BRANCH -- NO LISTING DESIRED


	.IF NDF	Q$$CMO


	MOV	$CMOPT,(R4)	;ELSE, FORCE LISTING TO CMD OUTPUT

	.IFF

	CLR	(R4)		;ELSE, FORCE LISTING TO CMD OUTPUT


	.ENDC


	BIS	#SP$SW,(R5)	;FORCE NO SPOOLING
40$:	CALL	$LIS		;PRODUCE LIBRARY LISTINGS
50$:	BITB	#CS.MOR,$CSIBK+C.STAT  ;MORE OUTPUT FILES?
	BEQ	60$		;BRANCH IF NOT
	MOV	(PC)+,R1	;ELSE, DIAG -- TOO MANY OUTPUT FILES
	.BYTE	E$R24,S$V0
	CALL	$ERMSG		;SEND DIAGNOSTIC MESSAGE
60$:	MOV	$LBRPT,R0	;PUT LIBRARY FDB ADDR IN R0
	MOV	#R.FIX,F.RTYP(R0)  ;SET FIXED LENGTH RECORDS
	CLOSE$	R0		;CLOSE LIBRARY FILE
	CLOSE$	$WRKPT		;CLOSE WORK FILE
	JMP	$RSTRT		;ELSE, RESTART LIBRARIAN


	.END

