	.TITLE	LBRFLA
	.IDENT	/03/

;
; COPYRIGHT 1975, DIGITAL EQUIPMENT CORP., MAYNARD, MASS.  01754
; COPYRIGHT 1974, DIGITAL EQUIPMENT CORP., MAYNARD, MASS.  01754
; COPYRIGHT 1973, DIGITAL EQUIPMENT CORP., MAYNARD, MASS.  01754
;
;	DEC ASSUMES NO RESPONSIBLIITY FOR THE USE
;	OR RELIABILITY OF ITS SOFTWARE ON EQUIPMENT
;	WHICH IS NOT SUPPLIED BY DEC.
;
; THIS SOFTWARE IS FURNISHED TO PURCHASER UNDER A LICENSE FOR USE
; ON A SINGLE COMPUTER SYSTEM AND CAN BE COPIED (WITH INCLUSION
; OF DEC'S COPYRIGHT NOTICE) ONLY FOR USE IN SUCH SYSTEM, EXCEPT
; AS MAY OTHERWISE BE PROVIDED IN WRITING BY DEC.
;
; THE INFORMATION IN THIS DOCUMENT IS SUBJECT TO CHANGE WITHOUT
; NOTICE AND SHOULD NOT BE CONSTRUED AS A COMMITMENT BY DIGITAL
; EQUIPMENT CORPORATION.
;
; VERSION 03
;
; WRITTEN BY:
;	GEORGE W. BERRY
;
; MODIFIED BY:
;	C.A. D'ELIA	24-JUN-74
;	THOMAS J. MILLER 07-NOV-74
;
;

;
; MACRO CALLS
;
 
	.MCALL	FDOFF$
	FDOFF$	DEF$L

 
;+
;
; **-$FIXLA-RECOMPUTE L$LA
;
; THIS ROUTINE IS CALLED TO RECOMPUTE THE AVAILABLE FREE SPACE
; IN THE LIBRARY.  THE VALUE (IN BYTES) IS STORED IN THE HEADER
; AT LOCATIONS L$LA AND L$LA+2.
;
;-
$FIXLA::SAVRG			;SAVE R3-R5
	MOV	$LBRPT,R0	;POINT TO LBR FDB
	MOV	F.HIBK+2(R0),R5	;GET FILE SIZE IN BLOCKS
	SUB	L$NX,R5		;GET FREE BLOCKS
	INC	R5		;FUDGE FOR LAST BLOCK
 
 
	.IF DF	R$$EIS
 
 
	CLR	R4		;CLEAR HIGH ORDER
	ASHC	#9.,R4		;CONVERT TO BYTES
 
	.IFF
 
	SWAB	R5		;PERFORM SHIFT BY EIGHT BITS
	MOV	R5,R4		;COPY INTO R4 FOR COMBINED SHIFT
	CLRB	R5		;CLEAR LOW HALF OF R5
	BIC	R5,R4		;CLEAR HIGH HALF OF R4
	ASL	R5		;PERFORM NINTH SHIFT
	ROL	R4		;AND MAKE IT COMBINED RESULT
 
 
	.ENDC
 
 
	SUB	L$NX+2,R5	;COMPUTE FREE BYTES IN LAST BLOCK
	SBC	R4
	MOV	R4,L$LA		;AND STORE VALUE
	MOV	R5,L$LA+2	;DOUBLE PRECISION
	RETURN
 
	.END
