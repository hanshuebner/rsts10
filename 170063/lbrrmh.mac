	.TITLE	LBRRMH
	.IDENT	/03/

;
; COPYRIGHT 1974, DIGITAL EQUIPMENT CORP., MAYNARD, MASS.  01754
; COPYRIGHT 1973, DIGITAL EQUIPMENT CORP., MAYNARD, MASS.  01754
; COPYRIGHT 1978, DIGITAL EQUIPMENT CORP., MAYNARD, MASS.  01754
;
;	DEC ASSUMES NO RESPONSIBLIITY FOR THE USE
;	OR RELIABILITY OF ITS SOFTWARE ON EQUIPMENT
;	WHICH IS NOT SUPPLIED BY DEC.
;
; THIS SOFTWARE IS FURNISHED TO PURCHASER UNDER A LICENSE FOR USE
; ON A SINGLE COMPUTER SYSTEM AND CAN BE COPIED (WITH INCLUSION
; OF DEC'S COPYRIGHT NOTICE) ONLY FOR USE IN SUCH SYSTEM, EXCEPT
; AS MAY OTHERWISE BE PROVIDED IN WRITING BY DEC.
;
; THE INFORMATION IN THIS DOCUMENT IS SUBJECT TO CHANGE WITHOUT
; NOTICE AND SHOULD NOT BE CONSTRUED AS A COMMITMENT BY DIGITAL
; EQUIPMENT CORPORATION.
;
; VERSION 03
;
; WRITTEN BY:
;	GEORGE W. BERRY
;
; MODIFIED BY:
;	C.A. D'ELIA	24-JUN-74
;
;	B. ALIMONTI	2-OCT-78
;		BA002 -- ADDITION OF UNIVERSAL MODULE TYPE
;
;

;
; MACRO CALLS
;
 
	.MCALL	FCSBT$
	FCSBT$
	.MCALL	FDOFF$
	FDOFF$	DEF$L

 
	.IF NDF	S$$LIB
 
 
	.MCALL	GET$
 
	.IFF
 
	.MCALL	GET$S
 
 
	.ENDC
 

;+
;
; **-$RDMH=READ MODULE HEADER
;
; THIS ROUTINE READS A MODULE HEADER INTO CORE.
;
; INPUTS:
;
;	R1=POINTER TO BLOCK/BYTE IN BLOCK OF MODULE HEADER
;
; OUTPUTS:
;
;	C-BIT SET IF ERROR
;
;-
$RDMH::	SAVRG			;SAVE R3-R5
	MOV	(R1)+,R2	;GET VBN
	MOV	(R1),R3		;GET BYTE-IN-BLOCK OF HEADER
	CMP	#1000,R3	;EXACT END OF BLOCK?
	BNE	5$		;IF NE, NO, SO CONTINUE
	INC	R2		;CONVERT TO BYTE...
	CLR	R3		;...ZERO OF NEXT BLOCK
5$:	CLR	R1		;CLEAR HIGH VBN
	MOV	$LBRPT,R0
	MOVB	#R.VAR,F.RTYP(R0) ;SET TO VARIABLE LENGTH RECORDS
	CALL	.POINT		;POSITION FILE
	BCS	10$		;EXIT IF ERROR
 
 
	.IF NDF	S$$LIB
 
 
	GET$	R0,#$MHBUF,#MHSIZE ;READ MODULE HEADER
 
	.IFF
 
	GET$S	R0,#$MHBUF,#MHSIZE ;READ MODULE HEADER
 
 
	.ENDC
 
 
	BCS	10$		;IF CS, READ ERROR, SO EXIT
	CMPB	#2,L$TYP	;UNIVERSAL MODULE TYPE?
	BNE	10$		;IF NE, NO, SO NO NEED TO BLK ALINE
	INC	R2		;INDICATE NEXT BLOCK
	CLR	R3		;CLEAR BYTE NUMBER...BEGINNING OF BLOCK
	CALL	.POINT		;POINT TO BEGINNING OF NEXT BLOCK
10$:	RETURN


	.END
