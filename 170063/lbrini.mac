	.TITLE	LBRINI
	.IDENT	/03/

;
; COPYRIGHT 1975, DIGITAL EQUIPMENT CORP., MAYNARD, MASS.  01754
; COPYRIGHT 1974, DIGITAL EQUIPMENT CORP., MAYNARD, MASS.  01754
; COPYRIGHT 1973, DIGITAL EQUIPMENT CORP., MAYNARD, MASS.  01754
;
;	DEC ASSUMES NO RESPONSIBLIITY FOR THE USE
;	OR RELIABILITY OF ITS SOFTWARE ON EQUIPMENT
;	WHICH IS NOT SUPPLIED BY DEC.
;
; THIS SOFTWARE IS FURNISHED TO PURCHASER UNDER A LICENSE FOR USE
; ON A SINGLE COMPUTER SYSTEM AND CAN BE COPIED (WITH INCLUSION
; OF DEC'S COPYRIGHT NOTICE) ONLY FOR USE IN SUCH SYSTEM, EXCEPT
; AS MAY OTHERWISE BE PROVIDED IN WRITING BY DEC.
;
; THE INFORMATION IN THIS DOCUMENT IS SUBJECT TO CHANGE WITHOUT
; NOTICE AND SHOULD NOT BE CONSTRUED AS A COMMITMENT BY DIGITAL
; EQUIPMENT CORPORATION.
;
; VERSION 03
;
; WRITTEN BY:
;	GEORGE W. BERRY
;
; MODIFIED BY:
;	C.A. D'ELIA	13-MAY-74
;	C.A. D'ELIA	30-JUN-74 (VIRTUAL TABLES)
;	T.J. MILLER	16-JUL-75 (USE GTSK FOR CALCULATING SIZE)
;
;
 
;
; MACRO LIBRARY CALLS
;
 
	.MCALL	FDOFF$,GPRT$C,GTSK$C
	FDOFF$	DEF$L
 
;
; LOCAL DATA
;
 
ADRBND:	.LIMIT			;TKB WILL PROVIDE TASK'S ADDRESS LIMITS
 
 
;+
;
; *-$INIT-*-INITIALIZATION ROUTINE
;
; THIS ROUTINE INITIALIZES THE LIBRARIAN'S DATA BASE,
; READS A COMMAND, AND PERFORMS A SYNTAX CHECK ON IT.
;
; INPUTS:
;	NONE
;
; OUTPUTS:
;	R1 = ADDRESS OF CSI BLOCK ($CSIBK)
;	R5 = ADDRESS OF SWITCH WORD ($SWTCH)
;
;-

$INIT::
	MOV	#LBRPT,R0
	CLR	F.STBK(R0)	;CLEAR STATBK POINTER
	MOV	R0,$LBRPT	;RESTORE $LBRPT
	JSR	R5,$CLEAR	;CLEAR DATA BASE
	.WORD	DBASE
	.WORD	DSIZE
	JSR	R5,$CLEAR	;CLEAR L$BUF
	.WORD	L$BUF
	.WORD	256.

10$:	CALL	$GTCML		;GET A COMMAND LINE
	BCC	20$		;BRANCH IF GOOD READ
	JMP	$QUIT		;ELSE EXIT

20$:	MOV	#$CSIBK,R1	;POINT R1 TO CSI BLOCK
	CALL	$SYNTX		;ANALYSE LINE SYNTAX
	BCS	10$		;BRANCH IF BAD SYNTAX
	MOV	ADRBND+2,R0	;PUT ADDR OF FREE MEMORY IN R0
	GTSK$C	$OUTBF		;GET TASK PARAMETERS
	MOV	$OUTBF+G.TSTS,-(SP) ;PUSH TASK SIZE
	GPRT$C	,$OUTBF		;GET PARTITION PARAMETERS
	ADD	@#$DSW,(SP)	;CALC ADDR OF END OF PARTITION
	SUB	R0,(SP)		;CALC SIZE OF FREE MEMORY
	MOV	R0,$FRHD	;SETUP FREE MEMORY LIST HEAD
	CLR	(R0)+		;IT'S ONE CHUNK--ZERO LINK WORD
	MOV	(SP)+,(R0)	;PUT IT'S SIZE IN NEXT WORD
	CALL	$INIVM		;INITIALIZE WORK FILE SUB-SYSTEM
	BCC	40$		;BRANCH IF ALL WENT O.K.
	MOV	(PC)+,R1	;ASSUME WORK FILE OPEN FAILURE
	.BYTE	E$R31,S$V2	;THAT IS FATAL
	ADC	R0		;CHECK ALTERNATE STATUS RETURN
	BLT	30$		;BRANCH IF ASSUMPTION WAS CORRECT
	MOV	(PC)+,R1	;ELSE, MARK FOR DELETE FAILURE
	.BYTE	E$R32,S$V0	;IT'S ONLY A DIAGNOSTIC
30$:	CALL	$ERMSG		;NOTIFY USER (POSSIBLE RETURN)
40$:	MOV	#$SWTCH,R5	;POINT R5 TO LIBRARIAN'S SWITCH WORD
	MOV	#$CSIBK,R1	;POINT R1 TO CSI BLOCK
	RETURN
 
 
	.END


