	.TITLE	LBRRLH
	.IDENT	/03.02/

;
; COPYRIGHT (C) 1973, 1974, 1975, 1978, 1981
; DIGITAL EQUIPMENT COPRORATION, MAYNARD, MASS.
;
;	DEC ASSUMES NO RESPONSIBLIITY FOR THE USE
;	OR RELIABILITY OF ITS SOFTWARE ON EQUIPMENT
;	WHICH IS NOT SUPPLIED BY DEC.
;
; THIS SOFTWARE IS FURNISHED TO PURCHASER UNDER A LICENSE FOR USE
; ON A SINGLE COMPUTER SYSTEM AND CAN BE COPIED (WITH INCLUSION
; OF DEC'S COPYRIGHT NOTICE) ONLY FOR USE IN SUCH SYSTEM, EXCEPT
; AS MAY OTHERWISE BE PROVIDED IN WRITING BY DEC.
;
; THE INFORMATION IN THIS DOCUMENT IS SUBJECT TO CHANGE WITHOUT
; NOTICE AND SHOULD NOT BE CONSTRUED AS A COMMITMENT BY DIGITAL
; EQUIPMENT CORPORATION.
;
; ALL RIGHTS RESERVED
;
; VERSION 03.02
;
; WRITTEN BY:
;	GEORGE W. BERRY
;
; PREVIOUSLY MODIFIED BY:
;
;	C. A. D'ELIA
;	T. J. MILLER
;	B.    ALIMONTI
;
; MODIFIED BY:
;
;	M. JOHNSON	18-FEB-81
;		MJ010 -- ADD PRIVILEGE VIOLATION ERROR MESSAGE FOR THAT CASE
;
;

;
; MACRO CALLS
;
 
	.MCALL	FCSBT$
	FCSBT$
	.MCALL	FDOFF$
	FDOFF$	DEF$L

 
	.IF NDF	S$$LIB
 
 
	.MCALL	GET$,OPEN$
 
	.IFF
 
	.MCALL	GET$S
 
 
	.ENDC
 
 
 
;
; LOCAL DATA
;
 
SIZSAV:	.WORD	0		;LOC TO SAVE OUTPUT (LIBRARY) FILE'S F.RSIZ
TYPSAV:	.WORD	0		;LOC TO SAVE OUTPUT (LIBRARY) FILE'S F.RTYP
$NTLIB::.WORD	0		;"ASSUMED LIBRARY IS NOT A LIBRARY" FLAG
;
;
;

;+
; **-$RDLHD-READ LIBRARY HEADER
;
; THIS ROUTINE READS THE LIBRARY HEADER IN L$BUF.
; AND VALIDATES HEADER CONTENTS.
; INPUTS:
;
;	NONE
;
;
; OUTPUTS:
;
;	R1=NEXT AVAILABLE BUFFER
;
;-
$RDLHD::SAVRG			;SAVE R3-R5
	CALL	INIT		;POSITION FILE INITIALLY
	MOV	#L$BUF,R1	;GET BUFFER ADDRESS
	MOV	#512.,F.URBD(R0) ;INDICATE 512. BYTE BUFFER
	MOV	#512.,F.RSIZ(R0) ;SET RECORD SIZE
 
 
	.IF NDF	S$$LIB
 
 
	GET$	R0,R1		;GET LIBRARY HEADER
 
	.IFF
 
	GET$S	R0,R1		;GET LIBRARY HEADER
 
 
	.ENDC
 
 
	BCS	ERROR		;FATAL READ ERROR
	CLR	$NTLIB		;CLEAR "...NOT A LIBRARY" FLAG
	CMPB	#TY$,(R1)+	;VALIDATE HEADER RECORD.
	BLO	10$		;BRANCH IF BAD TYPE CODE
	CMPB	#ID$,(R1)+	;ID BYTE OK?
	BNE	10$		;BRANCH IF NOT O.K.
	MOV	L$NX,F.EFBK+2(R0) ;TELL FCS WHERE THE...
	MOV	L$NX+2,F.FFBY(R0) ;...END OF FILE IS
	ADD	#510.,R1	;PNT R1 TO NEXT BUFFER
	RETURN
 
10$:	INC	$NTLIB		;SET "...NOT A LIBRARY" FLAG
	MOV	(PC)+,R1	;BAD LIBRARY HEADER
	.BYTE	E$R09,S$V2
	BR	ERXIT

ERROR:	MOV	(PC)+,R1	;CAN'T READ LIBRARY FILE
	.BYTE	E$R01,S$V2
ERRNM:	MOV	R$NAME(R0),R2	;GIVE FILE NAME
ERXIT:	MOV	SIZSAV,F.RSIZ(R0) ;RESTORE FILE'S F.RSIZ
	MOVB	TYPSAV,F.RTYP(R0) ;RESTOTE FILE'S F.RTYP
	JMP	$ERMSG		;FATAL ERROR EXIT (NO RETURNS)


;+
;
; THIS ROUTINE POSITIONS THE LBR FILE AT THE HEADER BLOCK.
;
; INPUTS:
;
;	NONE
;
; OUTPUTS:
;
;	R0 = $LBRPT
;
;-
INIT:	MOV	$LBRPT,R0
	TST	F.BDB(R0)	;IS LIBRARY FILE ALREADY OPEN?
	BNE	10$		;BRANCH IF SO
	.IF NDF S$$LIB
	OPEN$	R0,R1		;OPEN LIBRARY FILE
	.IFF
	CALL	$OPEN		;OPEN LIBRARY FILE
	.ENDC
	BCC	10$		;BRANCH IF NO ERROR ON OPEN
	CMPB	#IE.PRI,F.ERR(R0)	; IS THE ERROR A PRIV. VIOLATION?
	BNE	5$			; IF NOT, JUMP
	MOV	(PC)+,R1
	.BYTE	E$R77,S$V2		; PRIVILEGE VIOLATION
	BR	ERRNM
5$:	MOV	(PC)+,R1
	.BYTE	E$R11,S$V2		; CAN'T OPEN FILE
	BR	ERRNM
10$:
	MOV	F.RSIZ(R0),SIZSAV ;SAVE LIBRARY FILE'S F.RSIZ
	MOVB	F.RTYP(R0),TYPSAV ;SAVE LIBRARY FILE'S F.RTYP
	MOVB	#R.FIX,F.RTYP(R0) ;SET TO FIXED LENGTH RECORDS
	CLR	R1
	MOV	#1,R2		;POINT TO VIRTUAL BLOCK 1 TO
	CLR	R3		;POINT TO START OF FILE
	CALL	.POINT
	RETURN


	.END
