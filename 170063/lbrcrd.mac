	.TITLE	LBRCRD
	.IDENT	/01/
 
 
;
; COPYRIGHT (C) 1979
; DIGITAL EQUIPMENT CORPORATION, MAYNARD MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LISENSE FOR USE ONLY ON A
; SINGLE COMPUTER SYSTEM AND MAY  BE  COPIED   ONLY  WITH  THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE,  OR
; ANY OTHER COPIES THEREOF, MAY NOT BE PROVIDED  OR  OTHERWISE
; MADE AVAILABLE TO ANY OTHER  PERSOM  EXCEPT FOR  USE ON SUCH
; SYSTEM AND TO ONE WHO AGREES TO THESE LICENSE  TERMS.  TITLE
; TO AND OWNERSHIP OF THE SOFTWARE SHALL AT ALL  TIMES  REMAIN
; IN DEC.
;
; THE INFORMATION IN THIS DOCUMENT IS SUBJECT TO CHANGE WITHOUT
; NOTICE AND SHOULD NOT BE CONSTRUED AS A COMMITMENT BY DIGITAL
; EQUIPMENT CORPORATION.
;
; DEC ASSUMES NO RESPONSIBILITY FOR THE USE OR RELIABILITY OF
; ITS SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DEC.
;
; THIS SOFTWARE IS FURNISHED TO PURCHASER UNDER A LICENSE FOR USE
; ON A SINGLE COMPUTER SYSTEM AND CAN BE COPIED (WITH INCLUSION
; OF DEC'S COPYRIGHT NOTICE) ONLY FOR USE IN SUCH SYSTEM, EXCEPT
; AS MAY OTHERWISE BE PROVEDED IN WRITING BY DEC.
 
; THE INFORMATION IN THIS DOCUMENT IS SUBJECT TO CHANGE WITHOUT
; NOTICE AND SHOULD NOT BE CONSTRUED AS A COMMITMENT BY DIGITAL
; EQUIPMENT CORPORATION.
;
;
;
; VERSION 01
;
; B. ALIMONTI
;
;
 
 
 
 										
;+										
; **$CARD-CONVERT ASCII TO RAD50
;
; THIS MODULE CONVERTS THE /IN, /RP, AND /MH OP. DISCRIPTIVE INFO. SWITCH VALUES
; OF THE UNIVERSAL LIBRARY FROM ASCII TO RADR50.  IT ALSO CONVERTS THE DESIRED 
; MODULE NAME OF THE /IN AND /RP SWITCHES FROM ASCII TO RAD50.
;										
; INPUTS:									
;										
;	R3 = STARTING ADDRESS OF THE SIX ASCII CHARACTERS			
;	R4 = STARTING ADDRESS OF WHERE THE RAD50 CHARACTERS ARE TO BE PLACED	
;										
; OUTPUTS:									
;										
;	R3 = UNCHANGED
;	R4 = STARTING ADDRESS OF WHERE NEXT RAD50 CHAR ARE TO BE PLACED
;
;	AN INDIVIDUAL ITEM OF OP. DISCRIPTIVE INFORMATION IS CONVERTED TO RAD50	
;
;	C-BIT SET = CONVERSION ERROR
;										
;-										
 										
$CARD::									
	SAVVR									
	CLR	(R4)		;CLEAR LOC IN PREP OF RECEIVING RAD50 CHAR	
	CLR	2(R4)		;CLEAR LOC IN PREP OF RECEIVING RAD50 CHAR	
	MOV	R3,R0		;R0 = ADDRESS OF ASCII CHAR. TO BE CONVERTED	
	MOV	#1,R1		;INDICATE "." IS VALID RAD50 CHAR
	CALL	$CAT5		;CONVERT FIRST 3 CHARS					
	BCC	5$		;IF CC, VALID RAD50 CHAR, BR TO CONTINUE	
	TST	R2		;INVALID CHAR A SPACE?				
	BNE	10$		;IF NE, YES, BR TO RETURN WITH C-BIT SET
	CMP	#1,R1		;WERE ANY (OF FIRST 3) CHARRS CONVERTED?
	BNE	1$		;IF NE, YES, SO BRANCH TO MOVE THEM TO BUFFER
	CLR	R1		;OTHERWISE MOVE IN NULL
1$:	MOV	R1,(R4)+	;MOVE RAD50 CHARS TO BUFFER
	BR	20$		;BRANCH TO MOVE NULL CHARS TO NEXT WORD OF BUFF
5$:	MOV	R1,(R4)+	;MOVE RAD50 CHARS TO BUFFER
	MOV	#1,R1		;INDICATE "." IS VALID RAD50 CHAR
	CALL	$CAT5		;CONVERT LAST 3 CHARS					
	BCC	22$		;IF CC, VALID CHAR, BRANCH TO MOVE TO BUFF		
	TST	R2		;IS INVALID CHAR A BLANK?			
	BEQ	15$		;IF EQ, NO, CONTINUE
10$:	SEC			;SET C-BIT TO INDICATE ERROR CONDITION
	BR	30$		;BR TO RETURN
15$:	CMP	#1,R1		;WERE ANY (OF LAST 3) CHARS CONVERTED?
	BNE	22$		;IF NE, YES, BRANCH TO MOVE THEM TO BUFFER
20$:	CLR	R1		;MOVE IN NULLS
22$:	MOV	R1,(R4)+	;MOVE RAD50 CHAR. TO MODULE HEADER		
	CLC			;INDICATE NO ERROR CONDITION
30$:	RETURN									
 
	.END
