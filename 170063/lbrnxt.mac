	.TITLE	LBRNXT
	.IDENT	/02/

;
; COPYRIGHT 1974, DIGITAL EQUIPMENT CORP., MAYNARD, MASS.  01754
; COPYRIGHT 1973, DIGITAL EQUIPMENT CORP., MAYNARD, MASS.  01754
;
;	DEC ASSUMES NO RESPONSIBLIITY FOR THE USE
;	OR RELIABILITY OF ITS SOFTWARE ON EQUIPMENT
;	WHICH IS NOT SUPPLIED BY DEC.
;
; THIS SOFTWARE IS FURNISHED TO PURCHASER UNDER A LICENSE FOR USE
; ON A SINGLE COMPUTER SYSTEM AND CAN BE COPIED (WITH INCLUSION
; OF DEC'S COPYRIGHT NOTICE) ONLY FOR USE IN SUCH SYSTEM, EXCEPT
; AS MAY OTHERWISE BE PROVIDED IN WRITING BY DEC.
;
; THE INFORMATION IN THIS DOCUMENT IS SUBJECT TO CHANGE WITHOUT
; NOTICE AND SHOULD NOT BE CONSTRUED AS A COMMITMENT BY DIGITAL
; EQUIPMENT CORPORATION.
;
; VERSION 02
;
; WRITTEN BY:
;	GEORGE W. BERRY
;
; MODIFIED BY:
;	C.A. D'ELIA	10-JUN-74
;
;
 
;+
;
; **-$NEXT- COMPUTE NEXT MODULE HEADER ADDRESS
;
; THIS ROUTINE COMPUTES THE ADDRESS OF THE NEXT MODULE HEADER.
;
; INPUTS:
;
;	R1 = VBN OF CURRENT MODULE HEADER
;	R2 = BYTE-IN-BLOCK OF CURRENT MODULE HEADER
;	MODULE HEADER BLOCK IS SET UP
;
; OUTPUTS:
;
;	R0 IS UNCHANGED
;	R1 = BLOCK OF NEXT MODULE HEADER
;	R2 = BYTE-IN-BLOCK OF NEXT MODULE HEADER
;
;-
$NEXT::	SAVRG			;SAVE R3-R5
	MOV	$MOSZ,R4	;GET MODULE SIZE
	MOV	$MOSZ+2,R5
	ADD	R2,R5		;COMPUTE UPDATED BYTE IN BLOCK
	ADC	R4		;(DOUBLE PRECISION)
	MOV	R5,R2
	BIC	#177000,R2	;R2 = NEXT M.H. BYTE-IN-BLOCK
 
 
	.IF DF	R$$EIS
 
 
	ASHC	#-9.,R4		;DIVIDE BY 512. TO GET BLOCKS
 
	.IFF
 
	CLRB	R5		;DIVIDE THE DOUBLE WORD  ...
	BISB	R4,R5		;... NUMBER IN R4-R5 ...
	SWAB	R5		;... BY ...
	CLRB	R4		;... 256.
	SWAB	R4		;
	ASR	R4		;MAKE FINAL RESULT A DIVISION ...
	ROR	R5		;... BY 512.
 
 
	.ENDC
 
 
	ADD	R5,R1		;AND UPDATE BLOCK NUMBER
	RETURN


	.END
