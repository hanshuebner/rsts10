	.INCLUDE /CMN:COMMON/
	.INCLUDE /CMN:KERNEL/
TITLE	DIAMON,<POST JOB TO DIAMOND>,0A,10-MAY-91,SJK

;
;		COPYRIGHT (c) 1974, 1991 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

.SBTTL	EDIT HISTORY FOR DIAMON
;+
;
;  000	RRF  06-MAR-81	CREATION - COPIED FROM V7.0-07
;
;-

;+
; TO USE DIAMOND THE FOLLOWING INFORMATION IS POSTED IN THE MAILBOX
; REGISTER:
;
;	IF NULL JOB IS JOB:	0
;
;	IF A REAL JOB IS JOB:	<RTS.IDX&377>*400 + <JOB&377>
;
;
; THE RTS INDEX IS DEFINED AS THE CURRENT POSITION OF THE RUN-TIME
; SYSTEM IN THE RUN-TIME SYSTEM LIST.
;
; THE CALL IS FROM THE SCHEDULER:
;
;	R1 -> JDB FOR JOB
;	R3 =  JOB NUMBER TIMES 2
;
;	CALL	DIAMON
;
;	ALL REGISTERS ARE (MUST BE) PRESERVED
;-

DEFORG	DIAMON

DIAMON::MOV	R5,-(SP)	;SAVE A REGISTER FOR SCRATCH
	MOV	R4,-(SP)	; ACTUALLY, WE NEED TWO
	MOV	R3,-(SP)	;  MORE REALISTICALLY WE NEED THREE
	CLR	R5		;START AT ZERO
	MOV	JDRTS(R3),R4	;GET THE POINTER TO JOB'S RTS BLOCK
	MOV	#RTSLST,R3	;GET THE POINTER TO THE SYSTEM'S RTS LIST
10$:	INC	R5		;INCREMENT THE COUNT
	MOV	(R3),R3		;GET THE NEXT POINTER
	BEQ	20$		;COULD NOT FIND THE RTS. POST ZERO
	CMP	R4,R3		;IS THIS THE RTS?
	BNE	10$		;NO, KEEP LOOKING
	SWAB	R5		;PUT THE COUNT IN THE HIGH BYTE
	BIC	#377,R5		;CLEAR THE LOW BYTE
	BR	30$		; AND SET THE INFORMATION IN THE MAILBOX

20$:	CLR	R5		;SAY CAN'T FIND THE DARN RTS
30$:	BISB	(SP),R5		;SET THE JOB NUMBER (WHICH IS STACKED)
	MOV	R5,@DIABOX	;POST IT TO DIAMOND
	MOV	(SP)+,R3	;RESTORE R3
	MOV	(SP)+,R4	;     AND R4
	MOV	(SP)+,R5	;      AND R5
	RETURN			;END OF ROUTINE

GLOBAL	<RTSLST,DIABOX>

.END
