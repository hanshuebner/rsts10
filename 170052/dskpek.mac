.INCLUDE /CMN:COMMON/
TITLE	DSKPEK,<Peek at the disk statistics>,0A,10-MAY-91,KPH

;
;		COPYRIGHT (c) 1974, 1991 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

.SBTTL	Edit history for DSKPEK

;+
; Edit	By	Date		Reason
;
; 000	KPH	16-Jul-84	Creation for Version 9.0
;
;-

.SBTTL	Peek at a word in memory

	.PSECT	ROCODE,RO,I,LCL,REL,CON

;+
; Peek at a word in memory
;
;	R5 -> Argument list
;		.WORD	3	(Number of arguments)
;		.WORD	RTNPTR	(Address to return data to)
;		.WORD	BASPTR	(Address of MMU address of table)
;		.WORD	OFFPTR	(Address of offset into table)
;
;	CALL	DSKPEK
;-

DSKPEK::MOV	#XRB,R0		;R0 -> XRB
	MOV	#2,(R0)+	;Count is two bytes
	CLR	(R0)+		;Clear returned byte count
	MOV	2(R5),(R0)+	;Set up address to return data to
	CLRB	(R0)+		;Skip the unused byte
	MOV	@4(R5),R3	;Pick up MMU address to return data
	CLR	R2		;And clear MSB
	ASHC	#6,R2		;Convert to a 22-bit address
	ADD	@6(R5),R3	;Now add in offset
	ADC	R2		;Double precision
	MOVB	R2,(R0)+	;Set up MSB of address
	MOV	R3,(R0)+	;And LSB of address
	CLR	(R0)+		;Clear one unused word
	CLR	(R0)+		;And another one
	.XPEEK			;Now pick up the data
	MOVB	@#FIRQB,R0	;Pick up returned error
	BEQ	10$		;None, that's good
	TRAP	377		;Error, let BP2 catch it
10$:	RETURN			;All done for now!

.END
