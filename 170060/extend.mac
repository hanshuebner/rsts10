	$BEGIN	EXTEND,<28.2>,<EXTEND THE FILE>
;
; THIS  SOFTWARE  IS FURNISHED UNDER A LICENSE AND MAY
; BE USED OR COPIED ONLY IN ACCORDANCE WITH THE  TERMS
; OF SUCH LICENSE.
;
; COPYRIGHT (c) 1985, 1983 BY DIGITAL EQUIPMENT CORPORATION.
; ALL RIGHTS RESERVED.
;
; PETER H. LIPMAN 11-AUG-73
;
; PREVIOUSLY MODIFIED BY
;
;	ELLEN R. SIMICH
;	S. RUZICH
;
; Modified By:
;
;	Tony Lekas	23-September-1983
;		TL287 - Do not extend the file if R1 and R2 are zero.
;			  Just clear FD.TRN
;
;+
; EXTEND THE FILE - OPEN OR CLOSED
; CALLING SEQUENCE:
;	CALL	.EXTND
; INPUTS:
;	R0=FDB
;	R1=NUMBER OF BLOCKS TO EXTEND FILE BY
;	R2=TYPE OF ALLOCATION,  1=CONTIGUOUS, 0=NON-CONTIG
; OUTPUTS:
;	C=0 IF SUCCESSFUL, F.HIBK ADJUSTED APPROPRIATELY
;	C=1 IF FAILURE, F.ERR TELLS WHAT HAPPENED
;	ALL REGISTERS PRESERVED
;-
.EXTND::.SAVR1
	MOV	#1,F.ERR(R0)
	MOV	R1,-(SP)
	MOV	R2,-(SP)
	CALL	..STFN		;SET UP FILE NAME BLOCK IF NECESSARY
	MOV	@R1,-(SP)	;SAVE THE FILE ID TO BE RESTORED LATER
	BCS	EXTBX1		;BRANCH IF PARSE, ..ALUN FAILED
	BNE	10$		;BRANCH IF FILE ID WAS SET UP
	CALL	..FIND		;FILE ID WASN'T SET, FIND THE FILE ID
	BCS	EXTBX1		;BRANCH IF CAN'T FIND IT
10$:	BITB	#FD.REC!FD.SQD,F.RCTL(R0) ;NOP IF RECORD ORIENTED OR SEQ
	BNE	EXTBX1
	MOV	2(SP),R1	;R1=TYPE OF ALLOCATION
	MOV	4(SP),R2	;R2=NO. OF BLOCKS TO ALLOCATE
	MOV	R1,-(SP)	; If R1 and R2 are zero then
	BIS	R2,(SP)+	; Just turn off auto-truncation
	BEQ	20$		; If EQ - No extend needed
	CALL	..EXT1		;ISSUE THE EXTEND QI/O AND WAIT
20$:	BICB	#FD.TRN,F.BKP1(R0) ; Turn off auto-truncate
EXTBX1:	MOV	(SP)+,F.FNB(R0)	;RESTORE THE FILE ID
	MOV	(SP)+,R2
	MOV	(SP)+,R1
	RETURN
;
;
	$END	EXTEND
;
;
	.END
