	$BEGIN	EOFCHK,<28.1>,<EOF CHECKING ROUTINES>
; 
; THIS  SOFTWARE  IS FURNISHED UNDER A LICENSE AND MAY
; BE USED OR COPIED ONLY IN ACCORDANCE WITH THE  TERMS
; OF SUCH LICENSE.
;  
; COPYRIGHT (c) 1982 BY DIGITAL EQUIPMENT CORPORATION.
; ALL RIGHTS RESERVED.
; 
; PETER H. LIPMAN 11-AUG-73
;
; PREVIOUSLY MODIFIED BY
;
;	S. RUZICH
;+
; SEE IF AT OR BEYOND EOF
; CALLING SEQUENCE:
;	CALL	..EFCK
; INPUTS:
;	R0=FDB
; OUTPUTS:
;	C=0 IF NOT AT EOF, C=1 IF EOF, F.ERR SET
;	FD.EFB IN F.BKP1=0 IF CURRENT VBN IS BEFORE
;	END OF FILE. =1 IF AT OR BEYOND EOF
;	R1 ALTERED. R0,R2-R5 PRESERVED
; OPERATION:
;	SETS UP FD.EFB ACCORDING TO WHETHER THE
; CURRENT VBN (F.VBN) IS BEFORE OR AFTER F.EFBK.
; THEN IF AT THE END OF FILE BLOCK, NREC AND FFBY
; ARE COMPARED TO SEE IF REALLY BEYOND EOF.
;
; ALTERNATE ENTRY:
;	CALL	..EFC1
; OPERATION:
; FD.EFB IS ASSUMED PROPERLY SET UP. IF 
; IT IS 1 THEN THE CHECK OF FFBY AND NREC
; IS MADE TO SEE IF BEYOND EOF.
;
; ALTERNATE ENTRY:
;	CALL	..CEFB
; OPERATION:
; MERELY COMPARE F.VBN AND F.EFBK FIELDS, IGNORING FIRST FREE BYTE.  THUS, IF
; THE FILE IS POSITIONED TO EOF BLOCK, THEN FILE IS CONSIDERED NOT BEYOND EOF.
;-
..EFCK::CALL	..SEFB		;SEE IF AT OR BEYOND EOF BLOCK
	BCS	EOFCKX		;BRANCH IF BEYOND
..EFC1::BITB	#FD.EFB,F.BKP1(R0) ;IS THIS THE EOF BLOCK
	BEQ	2$		;BRANCH IF NOT
; AT EOF BLOCK-CHECK RECORD POINTER
	MOV	F.NREC(R0),R1	;CALCULATE OFFSET OF RECORD PTR INTO BUFFER
	ADD	F.VBSZ(R0),R1
	SUB	F.EOBB(R0),R1
	CMP	F.FFBY(R0),R1	;
	BLE	4$		;BRANCH IF EOF
2$:	CLC
	RETURN
				;CLEAR RETURN ADDRESS FROM STACK
4$:	MOVB	#IE.EOF,F.ERR(R0) ;INDICATE EOF
	SEC
EOFCKX:	RETURN


;+
; IS CURRENT VBN AT OR BEYOND THE END OF FILE BLOCK
; CALLING SEQUENCE:
;	CALL	..SEFB
; INPUTS:
;	R0=FDB
; OUTPUTS:
;	C=0 IF BEFORE OR AT EFBK
;	C=1 IF BEYOND EFBK, F.ERR SET
;	FD.EFB IN F.BKP1(R0)=1 IF AT OR BEYOND EFKB
;		=0 IF BEFORE EFBK
;	ALL REGISTERS PRESERVED
;-
..SEFB::BICB	#FD.EFB,F.BKP1(R0)
	CALL	..CEFB
	BLO	10$		;BRANCH IF BEFORE EFBK
	BHI	20$		;BRANCH IF BEYOND EFBK
	TST	F.FFBY(R0)	;IF AT EFBK AND FFBY = 0 THEN
	BEQ	20$		;REALLY BEYOND EOF
	BISB	#FD.EFB,F.BKP1(R0)
10$:	CLC
	RETURN
20$:	MOVB	#IE.EOF,F.ERR(R0)
	BISB	#FD.EFB,F.BKP1(R0)
	SEC
	RETURN
..CEFB::CMP	F.VBN(R0),F.EFBK(R0)
	BNE	10$
	CMP	F.VBN+2(R0),F.EFBK+2(R0)
10$:	RETURN
;
;
	$END	EOFCHK
;
;
	.END
