	$BEGIN	XQIOI,<28.1>,<EXECUTE QI/O - INTERNAL>
; 
; THIS  SOFTWARE  IS FURNISHED UNDER A LICENSE AND MAY
; BE USED OR COPIED ONLY IN ACCORDANCE WITH THE  TERMS
; OF SUCH LICENSE.
;  
; COPYRIGHT (c) 1982 BY DIGITAL EQUIPMENT CORPORATION.
; ALL RIGHTS RESERVED.
; 
; PETER H. LIPMAN 10-OCT-73
;


;+
; BUILD THE STANDARD 6 WORD QI/O DPB IN SPECIFIED AREA
; AND ISSUE THE QI/O
; CALLING SEQUENCE:
;	CALL	..XQIO
; INPUTS:
;	R0=FDB
;	R4=I/O FUNCTION CODE
;	R3=DIRECTIVE CODE AND DPB SIZE
;	R5=DPB ADDRESS
; OUTPUTS:
;	R1=I/O STATUS BLOCK ADDRESS
;	R0,R4 PRESERVED, R2,R3,R5 ALTERED
;	C=0 IF OK, C=1 IF ERROR, F.ERR SET FROM
;	    DIRECTIVE STATUS WORD
; ALTERNATE ENTRY:
;	CALL	..XQI1
; INPUTS:
;	R1=I/O STATUS BLOCK ADDRESS OR 0
;	R2=I/O DONE AST ADDRESS OR 0
;	R0=FDB
;	R4=I/O FUNCTION CODE
;	R3=DIC & DPB SIZE
; OPERATION:
; THE SPECIALIZED QI/O FUNCTION PARAMETERS HAVE ALREADY
; BEEN PLACED IN THE QI/O DPB.  THIS ROUTINE PLACES THE
; STANDARD 6 WORDS INTO THE DPB, USING THE DEFAULT EVENT 
; FLAG IF F.EFN(R0)=0, AND USING THE SCRATCH I/O STATUS
; BLOCK IN THE FILE STORAGE REGION IF THE F.BDB(R0)=0.
; THEN IT ISSUES THE QI/O
;-
..XQIO::
	MOV	F.BDB(R0),R1	;ADDRESS OF I/O STATUS BLOCK
	BNE	10$		;UNLESS FILE ISN'T OPEN
	MOV	R5,R1		;IN WHICH CASE USE THE SCRATCH
	ADD	#A.IOST-A.DPB,R1 ;I/O STATUS BLOCK IN THE FSR
10$:	CLR	R2		;NO I/O DONE AST

..XQI1::MOV	R5,-(SP)	;PUSH DPB ADDRESS
	MOV	R3,(R5)+	;STORE DIRECTIVE CODE AND DPB SIZE
	MOV	R4,(R5)+	;STORE I/O FUNCTION CODE
	MOVB	F.LUN(R0),(R5)+	;STORE LUN
	CLRB	(R5)+		;ZERO PRIORITY (DEFAULT)
	MOVB	F.EFN(R0),R3	;R3=EFN IF SPECIFIED
	BNE	10$
	MOV	#FCSEFN,R3	;USE DEFAULT IF NOT SPECIFIED
10$:	MOV	R3,(R5)+	;STORE EVENT FLAG
	MOV	R1,(R5)+	;STORE I/O STATUS BLOCK ADDRESS
	MOV	R2,(R5)		;STORE I/O DONE AST ADDRESS
	DIR$			;ISSUE THE DIRECTIVE, DPB ADR ON STACK
	BCC	20$		;BRANCH IF SUCCESSFUL
	MOV	R1,R3		;ANY I/O STATUS BLOCK? - PRESERVE CARRY
	BEQ	20$		;BRANCH IF NO
	MOVB	@#$DSW,(R1)	;STORE ERROR CODE IN I/O STATUS BLOCK
	MOVB	#-1,F.ERR+1(R0)	;DISTINGUISH QI/O ERRORS FROM HANDLER
20$:	RETURN
;
;+
; INITIALIZE THE DPB FOR A QI/O
; CALLING SEQUENCE:
;	CALL	..IDPB
; INPUTS:
;	NONE
; OUTPUTS:
;	R5=ADDRESS OF PARAMETER AREA OF DPB
;	R0-R4 PRESERVED
; OPERATION:
;	ZEROS THE 6 WORDS OF QI/O PARAMETERS AND RETURNS THE ADDRESS
; OF THE BEGINNING OF THE PARAMETERS IN R5
;-
..IDPB::
	MOV	@#.FSRPT,R5
	ADD	#A.DPB+24.,R5	;R5=ADR OF END OF PARAMETER BLOCK
	MOV	R4,-(SP)		;SAVE R4
	MOV	#6,R4
10$:	CLR	-(R5)
	SOB	R4,10$
	MOV	(SP)+,R4
	RETURN
;
;
	$END	XQIOI
;
;
	.END
