	.TITLE	SAVR1
	.IDENT	/28.1/
; 
; THIS  SOFTWARE  IS FURNISHED UNDER A LICENSE AND MAY
; BE USED OR COPIED ONLY IN ACCORDANCE WITH THE  TERMS
; OF SUCH LICENSE.
;  
; COPYRIGHT (c) 1982 BY DIGITAL EQUIPMENT CORPORATION.
; ALL RIGHTS RESERVED.
; 
; PETER H. LIPMAN 21-MARCH-73
;
; PREVIOUSLY MODIFIED BY
;
;	S. RUZICH
;
	.PSECT	$$RESL,RO
;
	.MCALL	CALL,RETURN
;+
; SUBROUTINE TO SAVE REGISTERS R1-R5 AND RESTORE THEM ON RETURN
; CALLING SEQUENCE:
;	JSR R5,SAVR1
; INPUTS:
;	NONE
; OUPUTS:
;	NONE
; OPERATION:
;	THE ROUTINE IS A COROUTINE WHICH SAVES R1-R5
; ON THE STACK, AND THEN CALLS THE CALLER.  THE RETURN FROM THE
; CALLER THEN POPS THE SAVED REGISTERS AND DOES A RETURN.
;-

.SAVR1::MOV R4,-(SP)		;SAVE R4,R3,R2,R1
	MOV R3,-(SP)
	MOV R2,-(SP)
	MOV R1,-(SP)
	MOV R5,-(SP)		;PUT RETURN ADDRESS ON STACK
	MOV 10.(SP),R5		;RETRIEVE REAL R5
	CALL @(SP)+		;CALL THE CALLER
	MOV (SP)+,R1		;RESTORE R1,R2,R3,R4,R5
	MOV (SP)+,R2
	MOV (SP)+,R3
	MOV (SP)+,R4
	MOV (SP)+,R5
	RETURN
;
;
	.END
