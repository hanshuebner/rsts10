	$BEGIN	MRKDL,<28.1>,<MARK FOR DELETE - USER INTERFACE>
; 
; THIS  SOFTWARE  IS FURNISHED UNDER A LICENSE AND MAY
; BE USED OR COPIED ONLY IN ACCORDANCE WITH THE  TERMS
; OF SUCH LICENSE.
;  
; COPYRIGHT (c) 1982 BY DIGITAL EQUIPMENT CORPORATION.
; ALL RIGHTS RESERVED.
; 
; PETER H. LIPMAN 1-OCT-73
;
; PREVIOUSLY MODIFIED BY
;
;	LONI BROWN
;
;+
; ISSUE THE MARK FILE FOR DELETE QI/O - USER INTERFACE
;
; CALLING SEQUENCE:
;	CALL	.MRKDL
; INPUTS:
;	R0=FDB ADDRESS
; OUTPUTS:
;	C=0 IF OK, C=1 IF ERROR, F.ERR(R0) SET TO REASON
;	ALL REGISTERS PRESERVED
; OPERATION:
;	FILE MAY BE OPEN OR CLOSED, WILL NOT CLOSE IT IF OPEN
; JUST ISSUES THE MARK FOR DELETE, THE NEXT DEACCESS WILL CAUSE
; THE FILE TO REALLY BE DELETED IF IT WAS OPEN.
;-
.MRKDL::.SAVR1
	MOV	#1,F.ERR(R0)
	MOV	R0,R1
	ADD	#F.FNB,R1
	TST	F.BDB(R0)	;DON'T TRY ASSLUN IF OPEN
	BNE	20$
	CALL	..ALUN
	BCS	60$
20$:					; REF TAG C-BIT = 0
	BITB	#FD.DIR,F.RCTL(R0)	; IF NON-DIRECTORIED DEVICE
	BEQ	60$			; NOP CALL
	.IF	GT,R$RSTS
	BITB	#FA.TMP,F.FACC(R0)	;IF TEMPORARY FILE
	BNE	60$			;THEN DON'T DELETE
	.ENDC
	CALL	..MKDL
60$:	RETURN
;
;
	$END	MRKDL
;
;
	.END
