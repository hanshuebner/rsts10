;
; COPYRIGHT 1975, DIGITAL EQUIPMENT CORP., MAYNARD, MASS 01754
;
; THIS SOFTWARE IS FURNISHED TO PURCHASER UNDER A LICENSE FOR USE
; ON A SINGLE COMPUTER SYSTEM AND CAN BE COPIED (WITH INCLUSION
; OF DEC'S COPYRIGHT NOTICE) ONLY FOR USE IN SUCH SYSTEM, EXCEPT
; AS MAY OTHERWISE BE PROVIDED IN WRITING BY DEC.
;
; THE INFORMATION IN THIS DOCUMENT IS SUBJECT TO CHANGE WITHOUT
; NOTICE AND SHOULD NOT BE CONSTRUED AS A COMMITMENT BY DIGITAL
; EQUIPMENT CORPORATION.
;
; DEC ASSUMES NO RESPONSIBILITY FOR THE USE OR RELIABILITY
; OF ITS SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DEC.
;
;
; VERSION M07
;
; WRITTEN BY:
;		D.N. CUTLER	MAY-73
;
; MODIFIED BY:
;		C.A. D'ELIA	08-JAN-74
;		C.A. D'ELIA	01-APR-74
;		C.A. D'ELIA	01-AUG-74
;		C.A. D'ELIA	04-MAR-75
;		C.A. D'ELIA	10-NOV-75
;		C.A. D'ELIA	14-SEP-76
;	PW001	PETER WANNHEDEN	30-SEP-77
;		DON'T SPOOL IF DEVICE IS HAS TRANSPARENT SPOOLING
;		ENABLED (FCP WILL INITIATE SPOOLING).
;		CALL $DSPAT TO SEND DATA TO PRINT SYMBIONT.
;
;+
;
; SUBMIT FILE TO PRINT SYMBIONT
;
;	PRINT$ FDB,ERR
;
; WHERE:
;
;	FDB=ADDRESS OF FDB OF FILE TO SUBMIT.
;	ERR=ADDRESS OF ERROR ROUTINE.
;
;-

	.MACRO	PRINT$ FDB,ERR,?LBL,A,B,C,D,E,F,?LBL2
	.MCALL	CALL,CLOSE$,LDR0$,GLUN$S					;PW001
	LDR0$	FDB		;LOAD FDB ADDRESS INTO R0			;**-1
	TST	F.BDB(R0)	;FILE OPEN?
	BEQ	LBL		;IF EQ NO

	.IF	NE,0		;++RSTS 7.2

	MOV	R1,-(SP)	;SAVE R1 AND R2
	MOV	R2,-(SP)	;
	MOV	R3,-(SP)	;SAVE R3
	MOV	SP,R1		;SAVE POINTER TO END OF SEND BUFFER		;PW001
	SUB	#<8.*2>,SP	;ALLOCATE A GET LUN INFO BUFFER			;**-5
	MOV	SP,R2		;SAVE ITS ADDR IN R2
	MOVB	F.LUN(R0),R3	;GET LUN IN R3
	GLUN$S	R3,R2		;GET REAL DEVICE NAME AND UNIT
	MOV	G.LUCW(R2),R3	;SAVE CHAR. WORD 1				;PW001
	MOV	R0,R2		;COPY FDB ADDRESS				;PW001
	ADD	#F.FNB+N.DID+6,R2 ;POINT TO END OF DIRECTORY ID			;PW001
	MOV	-(R2),-(R1)	; PUSH DIRECTORY ID				;PW001
	MOV	-(R2),-(R1)	;						;PW001
	MOV	-(R2),-(R1)	;						;PW001
	ADD	#N.FID+6-N.DID,R2 ;POINT TO END OF FILE ID			;PW001
	MOV	-(R2),-(R1)	;PUSH FILE ID					;PW001
	MOV	-(R2),-(R1)	;						;PW001
	MOV	-(R2),-(R1)	;						;PW001
	CLRB	-(R1)		;CLEAR LUN INFO FLAGS BYTE			;PW001
	ADD	#N.FVER+2-N.FID,R2 ;POINT TO END OF FILENAME, TYPE, VERSION	;PW001
	MOV	-(R2),-(SP)	;PUSH FILE VERSION NUMBER			;PW001
	MOV	-(R2),-(SP)	;PUSH FILE TYPE					;PW001
	MOV	-(R2),-(SP)	;PUSH FILENAME					;PW001
	MOV	-(R2),-(SP)	;						;PW001
	MOV	-(R2),-(SP)	;						;PW001
	MOV	SP,R1		;SET POINTER TO SEND BUFFER			;PW001
	CLOSE$	R0		;CLOSE FILE					;**-16
	BCS	LBL2		;SKIP ON ERROR
	BIT	#FD.REC!FD.OSP,R3 ;RECORD ORIENTED OR SPOOLED DEVICE?		;PW001
	BNE	LBL2		;IF NE YES					;**-1
	MOV	R0,R2		;SAVE FDB ADDRESS				;PW001
	MOV	#^RPRT,R0	;GET "PRT" IN RAD-50				;PW001
	CALL	$DSPAT		;SEND DATA TO ...PRT OR PRT...			;PW001
	MOV	R0,F.ERR(R2)	;GET RETURN STATUS				;PW001
	MOV	R2,R0		;RESTORE FDB ADDRESS				;PW001
LBL2:	ROR	R1		;SAVE CARRY					;PW001
	ADD	#<13.*2>,SP	;CLEAN STACK					;PW001
	ROL	R1		;RESTORE CARRY					;**-9
	MOV	(SP)+,R3	;RESTORE R3
	MOV	(SP)+,R2	;RESTORE R1 AND R2
	MOV	(SP)+,R1	;

	.IFF			;++ RSTS V7.2

	CALL	.PRINT		;++ RSTS V7.2 CLOSE/PRINT FILE
	.GLOBL	.PRINT		;++ RSTS V7.2 .PRINT IS EXTERNAL

	.ENDC			;++ RSTS V7.2

	.IF NB	ERR
	BCC	LBL		;IF CC OKAY
	CALL	ERR		;CALL ERROR ROUTINE
	.ENDC
LBL:				;REF LABEL


	.ENDM	PRINT$
