	$BEGIN	RENAME,<28.2>,<RENAME FILE>
; 
; THIS  SOFTWARE  IS FURNISHED UNDER A LICENSE AND MAY
; BE USED OR COPIED ONLY IN ACCORDANCE WITH THE  TERMS
; OF SUCH LICENSE.
;  
; COPYRIGHT (c) 1982, 1991 BY DIGITAL EQUIPMENT CORPORATION.
; ALL RIGHTS RESERVED.
; 
; PETER H. LIPMAN 17-AUG-73
;
; PREVIOUSLY MODIFIED BY
;
;	LONI BROWN
;
; Modified for RSTS V10.1 by:
;
;	S. LeVan	19-Apr-1991	V28.2
;		Merge RSTS-specific code into RSX V4.3 version of module.
;



;+
; CLOSE (IF NECESSARY) AND RENAME AN EXISTING FILE TO A NEW NAME.
; CALLING SEQUENCE:
;	CALL	.RENAM
; INPUTS:
;	R0=FDB ADDRESS OF EXISTING FILE, CAN BE OPEN OR CLOSED
;	R1=FDB WITH THE NEW FILE NAME TO USE
; OUTPUTS:
;	C=0 FOR SUCCESS, C=1 FOR ERROR F.ERR TELLS WHAT KIND
;	R0 FDB WILL BE CLOSED IF IT WAS OPEN
;	ALL REGISTERS PRESERVED
;-
.RENAM::.SAVR1
	MOV	#1,F.ERR(R0)	;INIT ERROR CODE TO SUCCESSFUL
	MOV	R0,-(SP)
	MOV	R1,-(SP)	;SAVE THE FDB ADDRESSES
	MOV	F.FNB(R0),R2	;CLOSE FILE IF NECESSARY SAVING FILE ID
	CLOSE$	R0
	BCS	RENMX1
	MOV	R2,F.FNB(R0)
;
	.IF	EQ,R$RSTS
	CALL	..STFN		;DO THE PARSE FOR THE EXISTING FILE
				;UNLESS FNB IS ALREADY SET UP
	BCS	RENMX1
	TST	@R1		;SKIP THE FIND IF THE FILE ID IS SET
	BNE	10$
	CALL	..FIND		;LOOKUP THE FILE AND MAKE SURE IT'S REAL
	BCS	RENMX1		;BRANCH IF IT DOESN'T EXIST
;
; NOW SEE IF NEW FILE NAME IS OK, MUST NOT ALREADY BE IN USE!
10$:	MOV	@SP,R0
	CALL	..STFN		;DO PARSE IF NECESSARY
	BCS	RENMX3
	TST	N.FVER(R1)	;IF EXPLICIT VERSION NUMBER
	BEQ	20$
	CALL	..FIND		;THEN MUST SEE IF FILE EXISTS
	BCC	RNMBX2		;BRANCH IF IT DOES, ERROR
	CMPB	#IE.NSF,F.ERR(R0) ;MUST HAVE RETURNED "NO SUCH FILE"
	BNE	RENMX3		;ERROR IF IT DIDN'T
	MOV	#1,F.ERR(R0)	;RESET THE ERROR CONDITION
;
; NOW VERIFY THAT BOTH FDB'S REFER TO THE SAME DEVICE AND UNIT
	.IFF
10$:	CALL	..IFQB		;SET UP FIRQB FOR OLD FILE
	.IFT
20$:	MOV	2(SP),R1	;R1=EXISTING FILE FDB
	.IFTF
	CMP	F.DVNM(R0),F.DVNM(R1) ;IF NOT SAME DEVICE AND UNIT,
	BNE	RNMBX3		;THEN CANNOT RENAME
	CMP	F.UNIT(R0),F.UNIT(R1)
	BNE	RNMBX3

	.IFT
	MOV	F.FNB(R1),F.FNB(R0) ;MOVE EXISTING FILE ID TO
	MOV	F.FNB+2(R1),F.FNB+2(R0) ;THE NEW NAME FDB
	MOV	F.FNB+4(R1),F.FNB+4(R0)

	MOV	R0,R1
	ADD	#F.FNB,R1	;R1=FNB ADDRESS
	CALL	..ENTR		;..ENTR THE NEW FILENAME
	BCS	RENMX3

	MOV	2(SP),R0	;R0=EXISTING FILE FDB
	MOV	R0,R1
	ADD	#F.FNB,R1
	CALL	..RMOV		;REMOVE THE EXISTING NAME
	.IFF
	ADD	#F.FNB+N.FNAM,R1;POINT TO NEW FILE NAME
	MOV	(R1)+,FQNAM2(R5);PUT NEW NAME IN FIRQB
	MOV	(R1)+,FQNAM2+2(R5)
	TST	(R1)+		;SKIP THIRD WORD OF NAME
	ASSUME	N.FNAM+6,N.FTYP
	MOV	@R1,FQNAM2+4(R5);PUT NEW EXTENSION IN FIRQB
	MOV	.RENDL,FQSIZ(R5);SET IN DELETE/RENAME FLAG
	MOV	#RENFQ,R4	;AND FIP CODE FOR RENAME
	CALL	..FIP		;LET FIP RENAME IT
	CMPB	#20,@#IOSTS	;FILE ALREADY EXISTS? (FIEXST)
	BNE	30$
	MOVB	#IE.FEX,F.ERR(R0);SET FCS ERROR CODE
30$:
	.IFTF
	BR	RENMX1
;
; ERROR EXITS WITH R0 = FDB OF NEW FILE NAME
RNMBX3:	MOVB	#IE.2DV,F.ERR(R0) ;CANNOT RENAME ACROSS DEVICES
	.IFT
	BR	RENMX3
RNMBX2:	MOVB	#IE.FEX,F.ERR(R0) ;NEW FILE NAME ALREADY EXISTS
RENMX3:	MOV	(SP)+,R1
	MOV	(SP)+,R0
	MOVB	F.ERR(R1),F.ERR(R0) ;MOVE THE ERR CODE TO THE REAL FDB
	BR	RENMX2
;
; ERROR EXITS WITH R0 =FDB OF EXISTING FILE
	.ENDC
RENMX1:	MOV	(SP)+,R1
	MOV	(SP)+,R0
RENMX2:	CLR	F.FNB(R0)
	CLR	F.FNB(R1)
	CALLR	..FCSX

	.IF     GT,R$RSTS       ;++RSTS V7.2                                   

.RENDL::.WORD   0               ;++RSTS V7.2    DELETE/RENAME FLAG             
                                ;++RSTS V7.2    PATCH TO -1 TO DELETE          
        .ENDC                   ;++RSTS V7.2                                   
;
;
        $END    RENAME
;
;
	.END
