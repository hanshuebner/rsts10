	$BEGIN	DELETE,<28.1>,<DELETE THE FILE>
; 
; THIS  SOFTWARE  IS FURNISHED UNDER A LICENSE AND MAY
; BE USED OR COPIED ONLY IN ACCORDANCE WITH THE  TERMS
; OF SUCH LICENSE.
;  
; COPYRIGHT (c) 1982 BY DIGITAL EQUIPMENT CORPORATION.
; ALL RIGHTS RESERVED.
; 
; PETER H. LIPMAN 17-AUG-73
;


;+
; DELETE A FILE - EITHER OPEN OR CLOSED
;
; CALLING SEQUENCE:
;
;	CALL	.DELET
;
; INPUTS:
;
;	R0=FDB ADDRESS
;
; OUTPUTS:
;
;	C=0 IF SUCCESSFUL, C=1 IF ERROR, F.ERR SET WITH ERROR CODE
;	ALL REGISTERS PRESERVED
;
; OPERATION:
;
;	IF THE FILE WAS OPEN, IT IS CLOSED, REMOVED FROM THE DIRECTORY
; AND MARKED FOR DELETE.
;	IF THE FILE WAS CLOSED, A PARSE IS PERFORMED USING F.DSPT,
; F.DFNB AND THEN THE FILE IS REMOVED AND MARKED FOR DELETE.
;-
.DELET::.SAVR1
	MOV	#1,F.ERR(R0)
	TST	F.BDB(R0)	;IF FILE IS OPEN, CLOSE IT
	BEQ	DELCLS
	MOV	F.FNB(R0),R1	;PRESERVE THE FILE ID
	CLOSE$
	BCS	DELX2		;BRANCH IF CLOSE FAILED
	MOV	R1,F.FNB(R0)	;RESTORE FILE ID
DELCLS:	CALL	..STFN		;SET UP FNB IF NOT ALREADY SET
	BCS	DELX
	BITB	#FD.DIR,F.RCTL(R0)
	BEQ	DELX		;IF NOT DIRECTORY DEVICE, THEN NOP
	CALL	..DEL1
DELX:	CLR	F.FNB(R0)	;RESET THE FILE ID
	CALL	..FCSX
DELX2:	RETURN
;
;
	$END	DELETE
;
;
	.END
