TITLE	RWATR,<READ/WRITE FILE ATTRIBUTES>,14,18-APR-85,SSS

;
;		COPYRIGHT (c) 1974, 1985 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

;+
; READ OR WRITE FILE ATTRIBUTES
; CALLING SEQUENCE:
;	CALL	..ATR
; INPUTS:
;	R0=FDB
;	R4=NO. OF ATTRIBUTE WORDS TO WRITE, 0 TO READ
;	R5=FIRQB ADDRESS
; OUTPUTS:
;	R0-R3,R5 PRESERVED, R4=0
;	ATTRIBUTES ARE IN FIRQB STARTING AT FQPPN
;	STATUS RETURNED BY .UUO IS IN IOSTS
; OPERATION:
;	THE FIRQB IS SET UP FOR A .UUO CALL FOR READ/WRITE ATTRIBUTES.
;	IF ATTRIBUTES ARE TO BE WRITTEN, THEY ARE COPIED FROM THE FDB
;	INTO THE FIRQB, USING R4 AS A COUNTER OF WORDS TO BE COPIED.
;	THE .UUO CALL IS THEN EXECUTED TO READ OR WRITE ATTRIBUTES.
;	(NOTE: IF FILE DOES NOT HAVE ATTRIBUTES, ZEROES WILL BE
;	RETURNED ON A READ.)
;-
.ENABL	GBL
.PSECT	PURE$I	RO,I,LCL,REL,CON

..ATR::
	MOV	R1,-(SP)	;SAVE R1
	ADD	#FQFUN,R5	;POINT TO FUNCTION CODE SLOT
	MOVB	#UU.ATR,(R5)+	;SET UUO CODE FOR R/W ATTRIBUTES
	MOVB	F.LUN(R0),(R5)+	;SET CHANNEL = LUN
	MOVB	R4,(R5)+	;NUMBER OF WORDS TO WRITE
	BEQ	20$		;IF ZERO THEN A READ
	MOV	R0,R1		;POINT TO FDB
10$:	MOV	(R1)+,(R5)+	;TRANSFER WORDS FROM FDB TO FIRQB
	SOB	R4,10$
20$:	.UUO			;READ OR WRITE ATTRIBUTES
	MOV	#FIRQB,R5	;POINT TO FIRQB AGAIN
	MOV	(SP)+,R1	;RESTORE R1
	RETURN
	.END
