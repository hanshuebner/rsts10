	$BEGIN	DIRNAM,<17.0>,<BUILD DIRECTORY DESCRIPTOR FROM ATTRIBUTES>
; ALTERED:
;
; COPYRIGHT (C) 1979, DIGITAL EQUIPMENT CORP., MAYNARD MASS.

; THIS SOFTWARE IS FURNISHED TO PURCHASER UNDER A LICENSE FOR USE
; ON A SINGLE COMPUTER SYSTEM AND CAN BE COPIED (WITH INCLUSION
; OF DEC'S COPYRIGHT NOTICE) ONLY FOR USE IN SUCH SYSTEM, EXCEPT
; AS MAY OTHERWISE BE PROVIDED IN WRITING BY DEC.

; THE INFORMATION IN THIS DOCUMENT IS SUBJECT TO CHANGE WITHOUT
; NOTICE AND SHOULD NOT BE CONSTRUED AS A COMMITMENT BY DIGITAL
; EQUIPMENT CORPORATION.

; DEC ASSUMES NO RESPONSIBILITY FOR THE USE OR RELIABILITY
; OF ITS SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DEC.
;
; BRUCE LEAVITT 28-NOV-78
;
;+
; BUILD DEFAULT NAMED DIRECTORY DESCRIPTOR
;	THIS ROUTINE REPLACES THE DEFAULT DIRECTORY STRING '[]' THAT
;	WAS PLACED IN FSR2 BY PARDI, WITH THE ACTUAL DIRECTORY NAME.
;	THIS EXPANSION IS DONE ONLY WHEN THE FSR DEFAULT DESCRIPTOR
;	STRING IS ACTUALLY LOCATED IN THE FSR (I.E. A.DFDR+2
;	CONTAINS THE ADDRESS OF A.DFDR+4). SCS-11 REQUIRES TWO
;	ENTRIES IN THE MFD FOR EACH NAMED DIRECTORY, THE NAME FORM
;	(E.G. SOURCE.DIR;1) AND THE UIC FORM (E.G. 020005.DIR;1).
;	THE NAME FORM IS THE ACTUAL FILE NAME AND THE UIC FORM IS
;	ENTERED IN THE MFD AS A SYNONYM. THUS THE USER'S DEFAULT
;	DIRECTORY NAME IS FOUND (IF ONE EXISTS) BY FIRST FINDING
;	THE UIC FORM, AND THEN READING THE DIRECTORY ATTRIBUTES
;	(HA.NAM - JUST THE FILENAME).
;
; CALLING SEQUENCE:
;
;	CALL	..DNAM
;
; INPUTS:
;
;	R0=FDB ADDRESS
;	R1=FILE NAME BLOCK ADDRESS
;	R2=DIRECTORY STRING DESCRIPTOR
;	FILE NAME BLOCK CONTAINS DIRECTORY FILE ID IN N.FID (FROM ..FIND)
;
; OUTPUTS:
;
;	IF SUCCESSFUL, N.FNAM FILLED IN BY ACP PRIMITIVE
;		AND FSR2 DESCRIPTOR UPDATED WITH DIRECTORY NAME
;	IF FAILURE, F.ERR SET TO REASON
;	R0-R3 PRESERVED, R4-R5 DESTROYED
;-
 
;	DEFINITIONS
NUMNAM	=	<'0-22>*50*50		; .RAD50 /0  /


..DNAM::	
 
	.IF	DF,R$$SCS
 
	MOV	R3,-(SP)	; SAVE REGISTERS
	MOV	R0,-(SP)	; ...
	MOV	R1,-(SP)	; ...
	MOV	R2,-(SP)	; SAVE R2 ON TOP
 
	CALL	..IDPB		; INITIALIZE PARAMETER BLOCK
	SUB	#12.,SP		; BUILD ATTRIBUTE LIST ON STACK
	MOV	R1,(R5)		; POINT TO FID FROM ..FIND
	ADD	#N.DID,(R5)+	; ...
	MOV	SP,(R5)		; SET ATTRIBUTE LIST POINTER
	MOV	SP,R5		; POINT TO ATTRIBUTE LIST
	MOV	(PC)+,(R5)+	; ASK FOR READ OF FILENAME ONLY
	.BYTE	-HA.NAM,S.FNAM	;  AS 3 RAD50 WORDS
	MOV	R5,(R5)		; SET BUFFER POINTER ADDRESS
	ADD	#4,(R5)+	;  TO STACK FRAME
	CLR	(R5)+		; END-OF-LIST INDICATOR
	MOV	#IO.RAT,R4	; INDICATE READ ATTRIBUTES
	CALL	..QIOW		;  AND ISSUE QIO TO ACP
	BCS	30$		; IF THERE WAS A PROBLEM, QUIT
	ADD	#6,SP		; CLEAN ATTRIBUTE LIST OFF STACK
	CMP	#NUMNAM,(SP)	; IF NAME BEGINS WITH LETTER,
	BLOS	40$		; IF NAME STARTS WITH NUMBER, QUIT
	MOV	6(SP),R0	; GET DIRECTORY DESCRIPTOR (SAVED R2)
	MOV	2(R0),R0	; POINT TO STRING
	MOVB	#'[,(R0)+	; DROP BRACKET ON FRONT
	MOV	#3,R5		; CONVERT 3 WORDS
10$:	MOV	(SP)+,R1	; GET A WORD
	CALL	$C5TA		; CONVERT TO 3 ASCII CHARACTERS
	SOB	R5,10$		; CONTINUE UNTIL WE HAVE ALL 9
20$:	CMPB	#' ,-(R0)	; SEARCH BACK FOR 1ST NON-BLANK
	BEQ	20$		; SQUEEZE OUT ALL TRAILING BLANKS
	INC	R0		; POINT AFTER LAST CHARACTER IN NAME
	MOVB	#'],(R0)+	; DROP A BRACKET ON THE END
	MOV	(SP)+,R2	; GET DESCRIPTOR POINTER (RESTORE R2)
	SUB	2(R2),R0	; CALCULATE LENGTH
	MOV	R0,(R2)		; UPDATE DESCRIPTOR STRING LENGTH
	BR	45$		; GO RETURN
 
30$:	ADD	#6,SP		; CLEAN STACK
40$:	ADD	#6,SP		; CLEAN STACK
	MOV	(SP)+,R2	; RESTORE REGISTER
 
45$:	MOV	(SP)+,R1	; RESTORE REGISTERS
	MOV	(SP)+,R0	; ...
	MOV	(SP)+,R3	; ...
 
	.ENDC	;R$$SCS
 
50$:	CLC			; ALWAYS APPEAR SATISFIED
	RETURN
;
;
	$END	DIRNAM
;
	.END
