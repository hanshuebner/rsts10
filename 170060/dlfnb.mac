	$BEGIN	DLFNB,<28.2>,<DELETE FILE USING FNB>
; 
; THIS  SOFTWARE  IS FURNISHED UNDER A LICENSE AND MAY
; BE USED OR COPIED ONLY IN ACCORDANCE WITH THE  TERMS
; OF SUCH LICENSE.
;  
; COPYRIGHT (c) 1982, 1991 BY DIGITAL EQUIPMENT CORPORATION.
; ALL RIGHTS RESERVED.
; 
; PETER H. LIPMAN	29-APR-74
;
;
; Modified for RSTS/E V10.1 by:
;
;	S. LeVan	21-Oct-91	V28.2
;		SEL001 - Shrink code
;
;+
; DELETE A FILE BY FILE NAME BLOCK - EITHER OPEN OR CLOSED
;
; CALLING SEQUENCE:
;
;	CALL	.DLFNB
;
; INPUTS:
;
;	R0=FDB ADDRESS
;
; OUTPUTS:
;
;	C=0 IF SUCCESSFUL, C=1 IF ERROR, F.ERR SET TO ERROR CODE
;  ALL REGISTERS PRESERVED
;
; OPERATION:
;
;	IF THE FILE WAS OPEN, IT IS CLOSED, REMOVED FROM THE DIRECTORY, 
; AND MARKED FOR DELETE.
;	IF THE FILE WAS CLOSED, THE FILE NAME BLOCK PORTION OF THE FDB IS
; ASSUMED TO BE ALL SET UP WITH FILE NAME, TYPE, VERSION (MUST BE EXPLICIT),
; DIRECTORY ID, DEVICE NAME, AND UNIT. THE FILE IS REMOVED FROM THE DIRECTORY
; AND MARKED FOR DELETE.
;-
.DLFNB::.SAVR1			;SAVE THE REGISTERS
	MOV	#1,F.ERR(R0)	;INITIALIZE ERROR BYTE
	MOV	R0,R1		;
	ADD	#F.FNB,R1	;R1=FNB ADDRESS
	TST	F.BDB(R0)	;IS FILE OPEN?
	BNE	10$		;BRANCH IF YES
	CALL	..ALUN		;ASSIGN THE LUN
	BCS	20$		;BRANCH IF ERROR
10$:	CLOSE$	R0		;CLOSE THE FILE
	BCS	20$		;BRANCH IF ERROR
	BITB	#FD.DIR,F.RCTL(R0) ;IF NOT A DIRECTORY DEVICE
	BEQ	20$		;THEN SUCCESSFUL NOP, C=0
	CALL	..DEL1		;REMOVE AND MARK FOR DELETE
	;MOV	#0,(R1)		;ZERO THE FID (SET BY REMOVE), PRESERVE C;SEL001
	CLR	(R1)		;SEL001
20$:	RETURN			;
;
;
	$END	DLFNB
;
;
	.END
