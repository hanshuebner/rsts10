	$BEGIN	EXPLG,<1.2>,<EXPAND A FILESPEC>
;
; THIS  SOFTWARE  IS FURNISHED UNDER A LICENSE AND MAY
; BE USED OR COPIED ONLY IN ACCORDANCE WITH THE  TERMS
; OF SUCH LICENSE.
;
; COPYRIGHT (c) 1984 BY DIGITAL EQUIPMENT CORPORATION.
; ALL RIGHTS RESERVED.
;
; P. K. M. WEISS
;
; MODIFIED BY:
;
; P. K. M. WEISS	30-SEP-1987		1.2
;
;		PKW135	- CLEAR R3 FPR ..PFCS CALL
;
;+
; EXPAND A LOGICAL STRING
; CALLING SEQUENCE:
;	CALL	.EXPLG
; INPUTS:
;	R2=POINTER TO DEVICE, DIRECTORY, AND NAME DESCRIPTOR
; OUTPUTS:
;	R2=POINTER TO EXPANDED DEVICE, DIRECTORY, AND NAME DESCRIPTOR
;	R4=FSS STATUS CODE 
;	C=0 EXPANSION SUCCEEDED
;	C=1 EXPANSION FAILED
;	OTHER REGISTERS PRESERVED
;-
.EXPLG::
	.IF DF L$$GCL		;IF LOGICAL NAMES

	MOV	R2,-(SP)	;SAVE R2,
	MOV	R0,-(SP)	;R0,
	MOV	R1,-(SP)	;R1,
	MOV	R3,-(SP)	;R3,
	MOV	R5,-(SP)	;AND R5
	CLR	R0		;WE DON'T WANT TO ASSIGN A LUN
	CLR	R3		;WE DON'T HAVE A DEFAULT NAME BLOCK
	CLR	R4		;AND WE WANT TO USE THE PARSE AREA IN THE FSR
	CALL	..PFCS		;GO EXPAND THE STRING
	MOV	(SP)+,R5	;RESTORE R5,
	MOV	(SP)+,R3	;R3,
	MOV	(SP)+,R1	;R1,
	MOV	(SP)+,R0	;AND R0
	BCC	10$		;CARRY IS STILL FROM THE DIRECTIVE
	CMP	@#$DSW,#IE.SDP	;DIRECTIVE NOT SUPPORTED?
	BEQ	20$		;YES 
	SEC			;INDICATE FAILURE
10$:	MOV	(SP),(SP)+	;POP STACK WITHOUT AFFECTING CARRY
	RETURN

20$:	MOV	(SP)+,R2	;RESTORE R2
	CLC			;INDICATE SUCCESS

	.IFF			;L$$GCL

	MOV	#1,R4		;SET SUCCESS STATUS

	.IFTF			;L$$GCL

	RETURN

	.ENDC			;L$$GCL

	$END	.EXPLG
	.END
