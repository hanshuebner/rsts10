	$BEGIN	ELPARS,<30.06>,<EXECUTE SYSTEM PARSE DIRECTIVE>
;
; THIS  SOFTWARE  IS FURNISHED UNDER A LICENSE AND MAY
; BE USED OR COPIED ONLY IN ACCORDANCE WITH THE  TERMS
; OF SUCH LICENSE.
;
; COPYRIGHT (C) 1985, 1988 BY DIGITAL EQUIPMENT CORPORATION
; ALL RIGHTS RESERVED.
;
; PREVIOUSLY MODIFIED BY
;
;	ANDREW C. GOLDSTEIN
;	S. RUZICH
;	T. Lekas
;	M. S. Harvey
;	A. Conta
;
; THIS ROUTINE IS USED TO INTERFACE FCS TO EXTENDED FILE NAME PROCESSING
; FACILITIES WHERE AVAILABLE IN A SUPPORTING SYSTEM. IT IN EFFECT BYPASS
; THE FILE NAME PROCESSING BUILT INTO FCS AND ALLOWS THE SYSTEM
; DIRECTIVE TO PROCESS FILE NAME DATA FROM THE DSPT AND DEFAULT DATA
; INTO A FULLY PARSED FILE NAME BLOCK.
;
; ENTRY POINTS, NAMED ..ELP0 THRU ..ELP3 ARE PROVIDED TO FILL THE NEEDS
; OF, RESPECTIVELY, .PARSE, .PRSDV, .GTDID, AND .GTDIR.
;
; MODIFIED BY:
;
;	P. K. M. WEISS			13-MAY-1987	30.04
;		PKW126	-- REMOVE ELP0 AND ELP1 AND SIMPLIFY REMAINING CODE
;
;	P. K. M. WEISS			17-FEB-1988	30.05
;		PKW152	-- CLEAN UP FURTHER, RESTORE .ASLUN CALL
;
;	P. K. M. WEISS			3-MAY-1988	30.06
;		PKW154	-- SIGH - RESTORE SAVING OF DESCRIPTOR
;
; DEFINE PARSE MODES
;
PM.NOR	= 0			;NORMAL FCS MODE
PM.PDV	= 1			;PARSE DEVICE ONLY
PM.FNB	= 2			;PARSE FROM FNB

	.MCALL	DIR$
	.PAGE
	.IF	DF	V$$ACP
	.ENABL	LSB

;+
;	..ELP3 IS CALLED FOR .GTDIR, ..ELP2 IS CALLED FOR .GTDID
;	.GTDIR (AND ..ELP3) INCLUDES A DIRECTORY DESCRIPTOR, AND WANTS
;	THE DID FILLED IN IN THE FNB.  .GTDID (AND ..ELP2) FILLS IN BOTH
;	THE DID AND THE DIRECTORY DESCRIPTOR WITH THE DEFAULT DIRECTORY
;
; INPUTS:
;	R0 = FDB
;	R1 = FNB
;	R2 = ADDR OF DIRECTORY DESCRIPTOR (..ELP3 ONLY)
;
; OUTPUTS:
;	C = 1 IF ERROR (STATUS IN F.ERR)
;	  = 0 IF SUCCESS OR UNSUPPORTED
;	Z = 1 IF DIRECTIVE UNSUPPORTED
;	  = 0 IF DIRECTIVE SUPPORTED AND SUCCESS
;
;-

..ELP3::
	CLR	R4		;NO RETURN DIRECTORY DESCRIPTOR
	MOV	R2,R5		;INPUT DIRECTORY DESCRIPTOR
	SUB	#6,SP		;FAKE PUSH OF THREE WORDS
	BR	10$		;COMMON CODE
..ELP2::
	MOV	@#.FSRPT,R4	;GET FSR REGION
	ADD	#A.DFDR,R4	;SECOND WORD OF DEFAULT DIRECTORY DESCRIPTOR
	MOV	D.DFUN(R4),-(SP) ;SAVE THESE DESCRIPTORS...
	MOV	(R4)+,-(SP)	;...JUST IN CASE...
	MOV	(R4),-(SP)	;...THE DIRECTIVE IS NOT SUPPORTED
	MOV	R4,(R4)		;COPY THE ADDRESS TO THE DESCRIPTOR
	ADD	#A.EXDS-A.DFDR-2,(R4) ;EXPANDED DIRECTORY STRING BUFFER ADDRESS
	MOV	#S.EXDS,-(R4)	;SET EXPANDED DIRECTORY BUFFER LENGTH
	CLR	R5		;NO INPUT DIRECTORY DESCRIPTOR
	MOV	#-1,D.DFUN(R4)	;SMASH DEFAULT DID UNIT TO PREVENT CONFU
10$:	MOV	R4,-(SP)	;RETURN DIRECTORY DESCRIPTOR
	MOV	R1,-(SP)	;PARSE BACK INTO SAME FNB
	MOV	R5,-(SP)	;DIRECTORY DESCRIPTOR
	MOV	R1,-(SP)	;DFNB
	CLR	-(SP)		;DSPT
	CLR	-(SP)		;GET A WORD OF SPACE		
	BISB	F.LUN(R0),(SP)	;LUN NUMBER
	MOV	#PM.FNB,-(SP)	;FNB MODE
	MOV	#<8.*256.>+145.,-(SP) ;DIC AND SIZE
	DIR$			;EXECUTE THE DIRECTIVE
	MOV	@#$DSW,R5	;STASH THE COMPLETION STATUS
	BCS	53$		;If CS then error
	ADD	#6,SP		;Ignore the saved string
	BR	70$		;descriptors
53$:
	CMP	R5,#IE.SDP	;Is the directive supported?
	BNE	57$		;if NE yes
	TST	R4		;Any string returned?
	BNE	54$		;if EQ no
	ADD	#6,SP		;Fake pop of 3 words
	BR	55$
;
;	These must be restored only if R4 is not clear
;
54$:	MOV	(SP)+,2(R4)	;Restore string descriptors
	MOV	(SP)+,(R4)	;...
	MOV	(SP)+,D.DFUN(R4);...
55$:
	CMP	R5,#IE.SDP	; Directive unsupported so force
	BR	100$		; C=0 and Z=1
57$:
	ADD	#6,SP		;Ignore saved string descriptors
	CMPB	R5,#IE.BDI&377	;BAD DIRECTORY SYNTAX (PERHAPS WILDCARD)?
	BNE	80$		;NO - A REAL ERROR - GO RETURN IT WITH C
70$:
	CALL	.ASLUN		;ASSIGN THE LUN, PRESERVING R5, SETTNG F
	BCS	90$		;CS - REPORT ERROR ON ASSIGN
;
; IF IE.BDI WAS RETURNED, THEN SOMETHING FUNNY WAS FOUND IN THE DIRECTOR
; LIKELY A WILDCARD.  PASS THIS ERROR THROUGH ONLY IF THIS IS A MORE THA
; LEVEL DIRECTORY DEVICE; ELSE TURN IT INTO SUCCESS TO IGNORE THE DIRECT
; SPEC.  NOTE THAT IF A WILDCARD WAS FOUND, THEN THE DIRECTORY SPEC, E.
; "[*,*]", IS RETURNED IN A.EXDS AND 4,4 IS STUFFED INTO N.DID IN THE FN
;
; IF IE.BDI WAS NOT RETURNED, THE FOLLOWING TESTS WILL NOT AFFECT THE RE
; SAVING A FEW WORDS.
;
	BITB	#FD.DIR,F.RCTL(R0) 	;DIRECTORY DEVICE?
	BEQ	90$			;NO - RETURN SUCCESS
	BITB	#FD.SDI,F.RCTL(R0) 	;SINGLE DIRECTORY DEVICE?
	BNE	90$			;YES- RETURN SUCCESS
80$:	MOV	R5,F.ERR(R0)		;RETURN THE SAVED ERROR/SUCCESS
	TSTB	R5			;I/O, DSW ERROR OR SUCCESS?
	BGT	90$			;BRANCH IF SUCCESS
	SEC				;ERROR - MUST BE IE.BDI
90$:	CLZ				;INDICATE DIRECTIVE WAS SUPPORTE
100$:	RETURN				;

	.DSABL	LSB
	.IFF	;V$$ACP
;
; DUMMY ROUTINES WHICH INDICATE PARSE FAILURE DUE TO NO SUPPORT
;
..ELP3::
..ELP2::
	CLC			; CLEAR C-BIT
	SEZ			; SET THE Z-BIT
	RETURN			; AND RETURN

	.ENDC	;V$$ACP

	$END	ELPARS

	.END
