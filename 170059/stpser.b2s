1	SUB STPSER							      &
\	MODULE$ = "STPSER"						      &
\	ON ERROR GOTO 19000						      &
\	SUBEXIT IF CC.FLG%						      &
	!******************************************************************** &
	!								      &
	!	s u b p r o g r a m   t i t l e				      &
	!								      &
	!	  subprogram : STPSER.B2S				      &
	!	     version : V10.1					      &
	!	        edit : A					      &
	!	   edit date : 10-MAY-91				      &
	!	   author(s) : ACT/PRL/DBM				      &
	!								      &
	!******************************************************************** &
	!								      &
	!		  C O P Y R I G H T				      &
	!								      &
	!								      &
	!		      Copyright (C) 1984, 1991 by		      &
	!	Digital Equipment Corporation, Maynard, Mass.		      &
	!								      &
	!								      &
	! This software is furnished under a license and may be used and      &
	! copied  only  in accordance with the terms of such license and      &
	! with the  inclusion  of  the  above  copyright  notice.   This      &
	! software  or  any  other copies thereof may not be provided or      &
	! otherwise made available to any other person.  No title to and      &
	! ownership of the software is hereby transferred.		      &
	!								      &
	! The information in this software is subject to change  without      &
	! notice  and should not be construed as a commitment by Digital      &
	! Equipment Corporation.					      &
	!								      &
	! DIGITAL assumes no responsibility for the use  or  reliability      &
	! of its software on equipment that is not supplied by DIGITAL.	      &
	!								      &
	!******************************************************************** &
	!								      &
	!	s u b p r o g r a m   c a l l   f o r m a t		      &
	!								      &
	!	CALL STPSER						      &
	!								      &
	!	Argument	I/O	Usage				      &
	!	========	===	=====				      &
	!	ERRSTS%		O	error status			      &
	!				  0 = no error			      &
	!				  n = ERR value n		      &
	!								      &
	!******************************************************************** &
	!								      &
	!	m o d i f i c a t i o n   h i s t o r y   l o g		      &
	!								      &
	!								      &
	! VER/ED	EDIT DATE	REASON				      &
	! V9.0-02	02-Apr-84	(ACT) Creation for V9.0		      &
	! V9.0-03	15-Jun-84	(PRL) General cleanup		      &
	! V9.0-10	02-Feb-85	(PRL) Add server type to ACK message  &
	! V9.0-12	02-Mar-85	(PRL) Handle ?Server already stopped  &
	! =================================================================== &
	! V9.1-05	09-Aug-85	(PRL) Fix FN.KBX.TO.KB$ function      &
	! =================================================================== &
	! V9.5-01	10-Jun-87	(DBM) Fix FN.KBX.TO.KB$ function      &
	!								      &
	!******************************************************************** &
	!								      &
	!	s u b p r o g r a m   d e s c r i p t i o n		      &
	!								      &
	!  The STOP/SERVER command is used to stop serving on a specified     &
	!  server. If no qualifiers are present serving will be		      &
	!  stopped immediatley. The format for the command is as follows:     &
	!								      &
	!		STOP/SERVER server-name[:]			      &
	!								      &
	!  The following qualifiers are defined for this command:	      &
	!								      &
	!		/FILE_END					      &
	!		/JOB_END					      &
	!		/PAGE_END					      &
	!								      &
	!******************************************************************** &
	!								      &
	!	v a r i a b l e s  /  a r r a y s   u s e d		      &
	!								      &
	!	name			description			      &
	!	====			===========			      &
	!								      &
	!******************************************************************** &
	!								      &
	!	s u b r o u t i n e s   u s e d				      &
	!								      &
	!	line #/name		description			      &
	!	===========		===========			      &
	!	DISPLY			display text at user's KB:	      &
	!	INIPKT			initialize send packet		      &
	!	SEND			send packet to queue manager	      &
	!	RECV			receive packet from queue manager     &
	!	CHKSER			validate server-name		      &
	!								      &
	!	FET$CQ			(CUI) fetch command qualifier	      &
	!	FET$P			(CUI) fetch parameter		      &
	!								      &
	!******************************************************************** &
	!								      &
	!	m a p   d e c l a r a t i o n s				      &
	!								      &
	!	line #	name	description				      &
	!	======	====	===========				      &
	!	   700  PBSCMN  PBS common data (%INCLUDE PBSCMN.B2S)	      &
	!	   700	PBSMAP  PBS map fields (%INCLUDE PBSMAP.B2S)	      &
	!								      &
	!******************************************************************** &
	!								      &
	!	c o m m o n   d e c l a r a t i o n s			      &
	!								      &
	!	line #	name	description				      &
	!	======	====	===========				      &
	!	   700	PBSKEY	PBS CUI keywords (%INCLUDE PBSKEY.B2S)	      &
	!	   700 	CUICMN  CUI common data (%INCLUDE CUICMN.B2S)	      &
	!								      &
	!******************************************************************** &

700	!	i n c l u d e   r e q u i r e d   M A P s , C O M M O N s     &
	!								      &
	!								      &
	%INCLUDE "SYSTEM:PBSCMN.B2S"		!include QUEUE data section   &
	%INCLUDE "SYSTEM:PBSMAP.B2S"		!include QUEUE map sections   &
	%INCLUDE "SYSTEM:PBSKEY.B2S"		!include CUI keywords	      &
	%INCLUDE "CUI:CUICMN.B2S"		!include CUI data section     &

1000	!	m a i n   s u b p r o g r a m   l o g i c		      &
	!								      &
	!								      &
	CALL INIPKT				!initialize send packet	      &
\	SUBEXIT IF ERRSTS%			!exit if any errors	      &
		   OR CC.FLG%			!  or CTRL/C		      &
						!			      &
\	CALL FET$P BY REF (SER.T%)		!fetch server parameter	      &
\	SER.POS% = U.POS%			!save pos of name in cmd line &
\	SER$ = SEG$(U.STR$,1%,U.LEN%)		!get server specified	      &
\	Z% = 2%					!allow logical server name    &
\	CALL CHKSER (SER$,SER.TYP%,Z%)		!validate server	      &
\	SUBEXIT IF ERRSTS%			!exit if invalid server	      &
		   OR CC.FLG%			!  or CTRL/C		      &
						!			      &
\	SRV$ = FN.KBX.TO.KB$ (SER$)		!display standard KBn: name   &
\	SELECT SER.TYP%				!select via server type:      &
\	CASE = PRT.TYP%				!case = print server,	      &
\		GOSUB 10000			!  process print quals	      &
\		SUBEXIT IF ERRSTS%		!  exit if any errors	      &
			OR CC.FLG%		!    or CTRL/C		      &
\	CASE = BAT.TYP%				!case = batch server,	      &
\	END SELECT				!end select		      &
						!			      &
\	CALL FET$CQ BY REF (JEN.Q%)		!fetch /JOB_END qualifier     &
\	PKT.FLG% = PKT.FLG% OR PAF.EJO%		!set JOB_END bit in flag      &
		IF U.PRES%			!  if present		      &
						!			      &
\	PKT.CMD$ = CHR$(PD.CMD%)		!load PAUSE DEV command code  &
\	PKT.RTQ$ = CHR$(AK.FLG%)		!load ACK request flag	      &
\	PKT.QUE$ = CHR$(SER.TYP%)		!load queue type	      &
\	PKT.ID.DVL$ = CHR$(LEN(SER$))		!load server name length      &
\	PKT.ID.DEV$ = SER$			!load server name	      &
						!			      &
\	CALL SEND (PD.LEN%,SEND.RETRY%)		!send PAUSE DEV packet	      &
\	SUBEXIT IF ERRSTS%			!exit if any errors	      &
		   OR CC.FLG%			!  or CTRL/C		      &
			OR NOSEND%		!    or NOSEND flag set	      &
						!			      &
\	CALL RECV (RECV.WAIT%)			!wait for ACK		      &
\	SUBEXIT IF ERRSTS%			!exit if any errors	      &
		   OR CC.FLG%			!  or CTRL/C		      &
						!			      &
\	IF ASCII(PKT.CMD$) = AK.CMD% THEN	!if ACK received,	      &
		ERRSTS% = 0%			!  return success status      &
\		SELECT SER.TYP%			!  select via server type:    &
\		CASE = PRT.TYP%			!  case = print server	      &
\			Z$ = "Print s"		!    build prefix text	      &
\		CASE = BAT.TYP%			!  case = batch server	      &
\			Z$ = "Batch s"		!    build prefix text	      &
\		CASE ELSE			!  case else		      &
\			Z$ = "S"		!    don't prefix type	      &
\		END SELECT			!  end select		      &
						!			      &
\		U.POS% = 0%			!  disable repainting of cmd  &
\		CALL DISPLY (Z$+"erver "+SRV$+" stopped")		      &
						!  and display it	      &
\		SUBEXIT				!  and exit		      &
\	END IF					!end if			      &
						!			      &
						!NACK received:		      &
\	ERRSTS% = PKT.STS%			!save error status code	      &
\	U.POS% = SER.POS%			!set command line pointer     &
						!			      &
\	SELECT ERRSTS%				!select via error code:	      &
\	CASE = RMS.RNF%				!case = device not found,     &
\		Z$ = "?Server does not exist"	!  use not exist msg	      &
\	CASE = RMS.IOP%				!case = already started,      &
\		Z$ = "?Server already stopped"	!  use already stopped msg    &
\	CASE ELSE				!case else,		      &
\		FATAL% = TRUE%			!  set fatal error flag	      &
\		ERRNAM$ = MODULE$		!  return bad module name     &
\	END SELECT				!end select		      &
						!			      &
\	CALL DISPLY (Z$)			!display  error mesage	      &
		UNLESS FATAL%			!  unless fatal		      &
\	SUBEXIT					!exit			      &

10000	!	p r o c e s s   p r i n t   q u a l i f i e r s		      &
	!								      &
	!								      &
	ERRSTS% = 0%				!init error status	      &
\	Z$ = "?Qualifier conflicts with server"	!init conflict error text     &
						!			      &
\	CALL FET$CQ BY REF (FEN.Q%)		!fetch /FILE_END qualifier    &
\	IF U.PRES% THEN				!if present,		      &
		IF SER.TYP% = PRT.TYP% THEN	!  if a print server,	      &
			PKT.FLG% = PKT.FLG% OR PAF.EFI%			      &
						!    set FILE_END bit in flag &
		ELSE	ERRSTS% = ERR.BADNAM%	!  else return bad name error &
\			CALL DISPLY(Z$)		!    display error text	      &
\		END IF				!  end if		      &
\	END IF					!end if			      &
						!			      &
\	CALL FET$CQ BY REF (PEN.Q%)		!fetch /PAGE_END qualifier    &
\	IF U.PRES% THEN				!if present,		      &
		IF SER.TYP% = PRT.TYP% THEN	!  if a print server,	      &
			PKT.FLG% = PKT.FLG% OR PAF.EPG%			      &
						!    set PAGE_END bit in flag &
		ELSE	ERRSTS% = ERR.BADNAM%	!  else return bad name error &
\			CALL DISPLY(Z$)		!    display error text	      &
						!    display error message    &
\		END IF				!  end if		      &
\	END IF					!end if			      &
						!			      &
\	RETURN					!exit			      &

11000	!	p r o c e s s   b a t c h   q u a l i f i e r s		      &
	!								      &
	!								      &
	ERRSTS% = 0%				!init error status	      &
\	RETURN					!exit			      &

15500	!	f n . k b x . t o . k b $  ( SRV$ )			      &
	!								      &
	!								      &
	! inputs:	SRV$		-	server name in _KBcn: format  &
	!								      &
	! outputs:	FN.KBX.TO.KB$	-	server name in _KBn: format   &
	!								      &
	! uses:		FIRQB%()	-	sys call array		      &
	!								      &
	!								      &
	DEF FN.KBX.TO.KB$ (SRV$)					      &
\	IF LEFT(SRV$,3%) = "_KB" THEN		!if KB type server,	      &
		ON ERROR GOTO 15520		!else trap own errors	      &
\		CHANGE FSS$(SRV$,1%) TO FIRQB%	!  FSS the server name	      &
\		SRV$ = "_KB" + NUM1$(FIRQB%(25%)) + ":"			      &
						!  return full _KBn: name     &
			UNLESS (FIRQB%(30%) AND 128%)			      &
						!  unless invalid (removed)   &
\	END IF					!end if			      &
\	GOTO 15550				!skip to exit		      &

15520	RESUME 15550				!resume to exit		      &

15550	FN.KBX.TO.KB$ = SRV$			!return server name	      &
\	FNEND					!end FN.KBX.TO.KB$ function   &

19000	!	s t a n d a r d   e r r o r   h a n d l i n g		      &
	!								      &
	!								      &
	FATAL% = TRUE%				!set fatal error flag	      &
\	ERRSTS% = ERR				!save error no.		      &
\	ERRLIN% = ERL				!save error line no.	      &
\	ERRNAM$ = ERN$				!save module name	      &
\	RESUME 32767				!resume to exit		      &

32767	!	e n d   o f   s u b p r o g r a m			      &
	!								      &
	!								      &
	SUBEND
