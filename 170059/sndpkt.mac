.INCLUDE	%CMN:COMMON%
TITLE	SNDPKT,<SEND COMMAND PACKET TO PBS>,0A,10-MAY-91,PRL/KCG

;
;		COPYRIGHT (c) 1974, 1991 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

.SBTTL	SNDPKT Edit History
;+
; EDIT	BY	DATE		REASON
; 001	PRL	27-Jun-84	Creation
; 002	KCG	28-Jun-84	Load correct LOT
;-

.SBTTL	Program description

;+
; This routine is called by SEND.B2S in the PBSMGR task to send a
; command packet to the PBS queue manager.  It returns status in
; the global error status word ERRSTS.
;
; Calling sequence is:
;
;		CALL SNDPKT BY REF (PKT$,PKTLEN%)
;
;	where	PKT$	is the address of the packet string to send
;		PKTLEN%	is the length of the packet string
;
;-

.SBTTL	Define and order the PSECTS

	DEFORG	SNDPKT

.SBTTL	Main program logic

SNDPKT::MOV	 2(R5),R0		;R0 -> string address
	MOV	@4(R5),R1		;R1 =  no. chars in string
	MOV	#<<FQBSIZ+XRBSIZ>/2>,R3	;R3 = size of area to clear
	MOV	#FIRQB,R4		;R4 -> Area to clear
10$:	CLR	(R4)+			;Clear word of firqb or xrb
	SOB	R3,10$			;  for all words there
	MOVB	#-11.,@#FIRQB+FQFIL	;load send w privs subfun
	MOVB	QMNLOT,@#FIRQB+FQSIZM	;load QM's local obj type
	BISB	#200,@#FIRQB+FQSIZM	;ensure the LOT bit is set
	MOV	R0,@#XRB+XRLOC		;load buffer address
	MOV	R1,@#XRB+XRLEN		;load length of output buffer
	MOV	R1,@#XRB+XRBC		;load no. bytes to send
	.MESAG				;send the message
	CLR	ERRSTS			;clear error status
	BISB	@#FIRQB,ERRSTS		;load error status
	RTS	PC			;exit

GLOBAL	<QMNLOT,ERRSTS>
	.END
