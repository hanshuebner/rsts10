1	SUB GETREC (GET.MODE%,KEY.NO%,KEY.STR$)				      &
\	MODULE$ = "GETREC"						      &
\	ON ERROR GOTO 19000						      &
\	SUBEXIT IF CC.FLG%						      &
	!								      &
	!								      &
	!******************************************************************** &
	!								      &
	!	s u b p r o g r a m   t i t l e				      &
	!								      &
	!	  subprogram : GETREC.B2S				      &
	!	     version : V10.1					      &
	!	        edit : A					      &
	!	   edit date : 10-MAY-91				      &
	!	   author(s) : PRL/ACT					      &
	!								      &
	!******************************************************************** &
	!								      &
	!		  C O P Y R I G H T				      &
	!								      &
	!								      &
	!		      Copyright (C) 1982, 1991 by		      &
	!	Digital Equipment Corporation, Maynard, Mass.		      &
	!								      &
	!								      &
	! This software is furnished under a license and may be used and      &
	! copied  only  in accordance with the terms of such license and      &
	! with the  inclusion  of  the  above  copyright  notice.   This      &
	! software  or  any  other copies thereof may not be provided or      &
	! otherwise made available to any other person.  No title to and      &
	! ownership of the software is hereby transferred.		      &
	!								      &
	! The information in this software is subject to change  without      &
	! notice  and should not be construed as a commitment by Digital      &
	! Equipment Corporation.					      &
	!								      &
	! DIGITAL assumes no responsibility for the use  or  reliability      &
	! of its software on equipment that is not supplied by DIGITAL.	      &
	!								      &
	!******************************************************************** &
	!								      &
	!	s u b p r o g r a m   c a l l   f o r m a t		      &
	!								      &
	!	CALL GETREC (GET.MODE%, KEY.NO%, KEY.STR$)		      &
	!								      &
	!	Argument	I/O	Usage				      &
	!	========	===	=====				      &
	!	GET.MODE%	 I	get-record mode:		      &
	!				   0 = GET (next)		      &
	!				   1 = GET KEY.NO% EQ KEY.STR$	      &
	!				   2 = GET KEY.NO% GE KEY.STR$	      &
	!				   3 = GET KEY.NO% GT KEY.STR$	      &
	!	KEY.NO%		I	key number (0-2)		      &
	!	KEY.STR$	I	key string			      &
	!	ERRSTS%		 O	error status:			      &
	!				   0 = get succeeded		      &
	!				   n = ERR value		      &
	!								      &
	!******************************************************************** &
	!								      &
	!	m o d i f i c a t i o n   h i s t o r y   l o g		      &
	!								      &
	!								      &
	! VER/ED	EDIT DATE	REASON				      &
	! V8.0-01	17-Oct-82	(PRL) Creation for V8.0		      &
	! V8.0-02	28-Oct-82	(PRL) Update BP2 v2.0 error handling  &
	! V9.0-01	20-Jun-83	(PRL) Update for V9.0		      &
	! V9.0-07	20-Jun-84	(PRL) General cleanup		      &
	! 		08-Aug-84	(PRL) Use PBS.LOC$ to build file-spec &
	! 		10-Sep-84	(PRL) Exit if CTRL/C with bucket lock &
	!								      &
	!******************************************************************** &
	!								      &
	!	s u b p r o g r a m   d e s c r i p t i o n		      &
	!								      &
	!	This module is called to get records from the PBS file. If    &
	!	the PBS file's channel PBS.CHNL% is not in use, then the      &
	!	PBS file is opened ACCESS READ and ALLOW SHARED. The method   &
	!	for reading a record is determined by GET.MODE% parameter:    &
	!								      &
	!	GET.MODE% = 0%						      &
	!		A simple GET command is executed, causing the next    &
	!		record in the current key sequence to be read.  The   &
	!		KEY.NO% and KEY.STR$ arguments are not used.  The     &
	!		error ERR.EOF% is returned if the end of file is      &
	!		reached.					      &
	!								      &
	!	GET.MODE% = 1%						      &
	!		This mode returns the first record whose key value    &
	!		for key number KEY.NO% is equal to the key string     &
	!		arg KEY.STR$.  The error ERR.NOTFND% is returned in   &
	!		ERRSTS% if no record is found with a matching key.    &
	!								      &
	!	GET.MODE% = 2%						      &
	!		This mode returns the first record whose key value    &
	!		for key number KEY.NO% is greater than or equal to    &
	!		the key string arg KEY.STR$.  The error ERR.EOF% is   &
	!		returned in ERRSTS% if the end of the key sequence is &
	!		reached.					      &
	!								      &
	!	GET.MODE% = 3%						      &
	!		This mode returns the first record whose key value    &
	!		for key number KEY.NO% is greater than the key string &
	!		arg KEY.STR$.  The error ERR.EOF% is returned in      &
	!		ERRSTS% if the end of the key sequence is reached.    &
	!								      &
	!******************************************************************** &
	!								      &
	!	v a r i a b l e s  /  a r r a y s   u s e d		      &
	!								      &
	!	name		description				      &
	!	====		===========				      &
	!								      &
	!******************************************************************** &
	!								      &
	!	s u b r o u t i n e s   u s e d				      &
	!								      &
	!	line #/name		description			      &
	!	===========		===========			      &
	!	DISPLY			print error text at user's KB	      &
	!	10000			open PBS file			      &
	!								      &
	!******************************************************************** &
	!								      &
	!	f u n c t i o n s   u s e d				      &
	!								      &
	!	line #	name		description			      &
	!	======	====		===========			      &
	!	15000	FN.UNSIGNED	Convert integer to fl-pt	      &
	!								      &
	!******************************************************************** &
	!								      &
	!	m a p   d e c l a r a t i o n s				      &
	!								      &
	!	line #	name	description				      &
	!	======	====	===========				      &
	!	   700  PBSCMN	PBS common data (%INCLUDE PBSCMN.B2S)	      &
	!	   700	PBSMAP	PBS map fields (%INCLUDE PBSMAP.B2S)	      &
	!								      &
	!******************************************************************** &

700	!	i n c l u d e   r e q u i r e d   M A P s , C O M M O N s     &
	!								      &
	!								      &
	%INCLUDE "SYSTEM:PBSCMN.B2S"		!include PBS data section     &
	%INCLUDE "SYSTEM:PBSMAP.B2S"		!include PBS map sections     &
	%INCLUDE "CUI:CUICMN.B2S"		!include CUI data section     &

1000	!	m a i n   s u b p r o g r a m   l o g i c		      &
	!								      &
	!								      &
	ERRSTS% = 0%				!init success status	      &
\	RETRY.CNT% = 15%			!init retry counter	      &
\	IF BUFSIZ(PBS.CHNL%) = 0% THEN		!if PBS file not open,	      &
		GOSUB 10000			!  go open PBS file	      &
\		SUBEXIT IF ERRSTS%		!  exit if any errors	      &
			   OR CC.FLG%		!    or CTRL/C		      &
\	END IF					!end if			      &
						!			      &
\	SELECT GET.MODE%			!select via GET.MODE%:	      &
\	CASE = 0%				!case = GET NEXT mode,	      &
\		GET #PBS.CHNL%			!  do a simple GET	      &
\	CASE = 1%				!case = GET EQ mode,	      &
\		GET #PBS.CHNL%, KEY #KEY.NO% EQ KEY.STR$		      &
						!  do a GET EQ		      &
\	CASE = 2%				!case = GET GE mode,	      &
\		GET #PBS.CHNL%, KEY #KEY.NO% GE KEY.STR$		      &
						!  do a GET EQ		      &
\	CASE = 3%				!case = GET GT mode,	      &
\		GET #PBS.CHNL%, KEY #KEY.NO% GT KEY.STR$		      &
						!  do a GET EQ		      &
\	CASE ELSE				!case else,		      &
\		ERRSTS% = ERR.BADNAM%		!  return bad mode error      &
\	END SELECT				!end select		      &
						!			      &
\	GOTO 32000				!exit			      &

10000	!	o p e n   P B S   f i l e				      &
	!								      &
	!								      &
	!inputs:	PBS.FIL$	-	PBS file-spec		      &
	!		PBS.CHNL%	-	file channel no. (PBSCMN)     &
	!								      &
	!outputs:	ERRSTS%		-	error status:		      &
	!					   0 = success		      &
	!					   n = ERR value	      &
	!								      &
	!uses:		Z$		-	temp work string	      &
	!								      &
	!								      &
	ON ERROR GOTO 10050			!trap own errors	      &
\	RETURN IF CC.FLG%			!exit if CTRL/C		      &
\	Z$ = SYS (PRIV.ON$)			!enable privs		      &
\	Z$ = EDIT$(PBS.LOC$+PBS.FIL$,-2%)	!build PBS file-spec	      &
\	OPEN Z$ FOR INPUT AS FILE #PBS.CHNL%	!open PBS file:		      &
		,ORGANIZATION INDEXED		!  indexed (variable) file,   &
		,ACCESS READ			!  read only,		      &
		,ALLOW MODIFY			!  allow r/w access	      &
		,MAP PBS$1			!  mapped into packet data    &
\	Z$ = SYS (PRIV.OFF$)			!  disable privs	      &
\	ERRSTS% = 0%				!clear error status	      &

10010	ON ERROR GOTO 19000			!restore standard error trap  &
\	RETURN					!exit			      &

10050	ERRSTS% = ERR				!save ERR value		      &
\	U.POS% = 0%				!disable repainting cmd line  &
\	CALL DISPLY ("?Error opening PBS file "+Z$+CR.LF$+ERT$(ERR))	      &
						!display cause of error	      &
\	RESUME 10010				!resume to exit		      &

15000	!	f n . u n s i g n e d ( VALUE% )			      &
	!								      &
	!								      &
	!inputs:	VALUE%		-	16-bit unsigned integer	      &
	!								      &
	!outputs:	FN.UNSIGNED	-	corresponding FL-PT value     &
	!								      &
	!uses:		Z		-	work FL-PT		      &
	!								      &
	!								      &
	DEF FN.UNSIGNED (VALUE%)					      &
\	Z = VALUE%				!move to fl-pt variable	      &
\	Z = Z + 65536.				!normalize to 0		      &
		IF Z < 0.			!  if negative		      &
\	FN.UNSIGNED = Z				!  return fl-pt value	      &
\	FNEND					!end FN.UNSIGNED function     &

19000	!	s t a n d a r d   e r r o r   h a n d l i n g		      &
	!								      &
	!								      &
	IF ERR = ERR.BKTLCK% THEN		!if bucket locked,	      &
		RESUME 32767 IF CC.FLG%		!  exit if CTRL/C	      &
\		UNLOCK #PBS.CHNL%		!  unlock any locked bucket   &
\		SLEEP 2%			!  sleep a while	      &
\		RETRY.CNT% = RETRY.CNT% - 1%	!  decrement retry counter    &
\		IF RETRY.CNT% > 0% THEN		!  if more retries,	      &
			RESUME			!    resume to try again      &
\		END IF				!  end if		      &
\	END IF					!end if			      &
						!			      &
\	ERRSTS% = ERR				!return error value	      &
\	RESUME 32000				!resume to exit		      &
		IF ERRSTS% = ERR.NOTFND%	!  if rec-not-found error     &
		   OR ERRSTS% = ERR.EOF%	!    or end-of-file error     &
						!			      &
\	FATAL% = TRUE%				!set fatal error flag	      &
\	ERRLIN% = ERL				!save error line no.	      &
\	ERRNAM$ = ERN$				!save module name	      &
\	RESUME 32767				!resume to exit		      &

32000	!	e n d - o f - s u b p r o g r a m   p r o c e s s i n g	      &
	!								      &
	!								      &
	UNLOCK #PBS.CHNL%			!unlock any locked buckets    &

32767	!	e n d   o f   s u b p r o g r a m			      &
	!								      &
	!								      &
	SUBEND
