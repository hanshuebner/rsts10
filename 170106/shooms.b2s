1	SUB SHOOMS
	!
	!	s u b p r o g r a m   t i t l e
	!
	!	subprogram : SHOOMS.B2S
	!	   version : V10.1
	!	      edit : A
	!	 edit date : 10-MAY-91
	!	 author(s) : DBM
	!
	!********************************************************************
	!
	!		  C O P Y R I G H T
	!
	!
	!		      Copyright (C) 1987, 1991 by
	!	Digital Equipment Corporation, Maynard, Mass.
	!
	!
	! This software is furnished under a license and may be used and
	! copied  only  in accordance with the terms of such license and
	! with the  inclusion  of  the  above  copyright  notice.   This
	! software  or  any  other copies thereof may not be provided or
	! otherwise made available to any other person.  No title to and
	! ownership of the software is hereby transferred.
	!
	! The information in this software is subject to change  without
	! notice  and should not be construed as a commitment by Digital
	! Equipment Corporation.
	!
	! DIGITAL assumes no responsibility for the use  or  reliability
	! of its software on equipment that is not supplied by DIGITAL.
	!
	!********************************************************************
	!
	!	m o d i f i c a t i o n   h i s t o r y   l o g
	!
	!
	! VER/ED	EDIT DATE	REASON
	!
	! T10.0-01	07-Nov-88	(DBM) Creation
	!		28-Mar-89	(DBM) Change in header record layout
	!				      requires change in OSC handling
	!
	!********************************************************************

	!********************************************************************
	!
	!	p r o g r a m   d e s c r i p t i o n
	!
	!	SHOOMS.B2S handles the SHOW OPERATOR_SERVICES command.
	!
	!********************************************************************
	!
	!	c h a n n e l   a s s i g n m e n t s
	!
	!	channel #	file-spec	description
	!	=========	=========	===========
	!	  0		_KB:		User keyboard (BP2 default)
	!	  SYS.CHNL	OMS$:OMS.SYS    OMS work file
	!	  OUT.CHNL	user-specified	SHOW command /OUTPUT
	!
	!********************************************************************

	!********************************************************************
	!
	!	C o m p i l e   O P T I O N s
	!
	!********************************************************************

	OPTION	TYPE = EXPLICIT

	!********************************************************************
	!
	!	I N C L U D E   r e q u i r e d   M A P s , C O M M O N s
	!
	!********************************************************************

	%INCLUDE "CUI:CUICMN.E2S"		! Include CUI data section
	%INCLUDE "SYSTEM:CMNDEF.B2S"		! Include OMS common defs
	%INCLUDE "SYSTEM:OMSKEY.B2S"		! Include CUI key defs

	!********************************************************************
	!
	!	D E F I N E   C O N S T A N T S
	!
	!********************************************************************

	EXTERNAL WORD CONSTANT			! Define RSTS error codes:    &
						! (LB:ERR.STB)		      &
						! 			      &
			EOF,			!   ?End of file	      &
			NODEVC,			!   ?Not a valid device       &
			NOSUCH,			!   ?Can't find file	      &
			NOTAVL,			!   ?Device not available     &
			PRVIOL,			!   ?Protection violation     &
			WRGPAK			!   ?Pack IDs don't match

	!********************************************************************
	!
	!	D E C L A R E   F U N C T I O N S
	!
	!********************************************************************

	DECLARE WORD FUNCTION	FORMAT(STRING, STRING),	! Mini PRINT USING    &
				SHOW_LEVEL(STRING, WORD)! Status display routine

	!********************************************************************
	!
	!	D E C L A R E   V A R I A B L E S
	!
	!********************************************************************
	
	DECLARE	WORD	I, INDENT		! Loop index, indent amount

	DECLARE	STRING	SHOW_FMT,		! Format string for SHOW cmds &
			TEMP			! Temp string

	!	P E R F O R M   C O M M A N D   P R O C E S S I N G

 SHOW_OP_SER:	! Show Operator Services Command

	ON ERROR GOTO OPEN_ERR			! Local trap
	FQB = SYS(REGAIN.PRIVS)			! We may need privs for this...

	OPEN OMS.LOC + OMS.SYS			! Open the work file	      &
		FOR INPUT AS SYS.CHNL,		!   Must already exist	      &
		ORGANIZATION VIRTUAL,		!   Open in native mode	      &
		MAP OMSREQ, MODE 4096%		!   Open in read-regardless mode

	FQB = SYS(DROP.PRIVS)			! Done with the privs
	CALL GETSYS BY REF (0%, OMSINT)		! Read in the header
	CAUSE ERROR WRGPAK			! Consistancy checks:	      &
	  IF (OMS.VER <> PTCL.VER)		!   Version number check      &
	  OR (OMS.REV <> PTCL.REV)		!   Revision level check

	SHOW_FMT = LEFT$(OMS.TXT, OMS.LEN)	! Build format string for RSETs
	JUNK = FORMAT(SHOW_FMT, P.VER)		! Print package name & version
	IF OMS.DATE
	THEN
	  TEMP = TRM$(DATE$(OMS.DATE)) + " " + TRM$(TIME$(OMS.TIME))
	  JUNK = FORMAT("Last started", TEMP)	! Print date/time last started
	END IF
	IF OMS.ADAT
	THEN
	  TEMP = TRM$(DATE$(OMS.ADAT)) + " " + TRM$(TIME$(OMS.ATIM))
	  JUNK = FORMAT("Last modified", TEMP)	! Print date/time last modified
	END IF
	JUNK = SHOW_LEVEL("Log file", OMS.STS)

	FOR I = 0% TO OSCMAX			! Loop through KB list

	  ON ERROR GOTO KB_ERR			! New local error trap
	  FQB = SYS(REGAIN.PRIVS)		! Raise shields
	  FQB = SYS(UU.TRM + "1"C + CHR$(I))	! Get terminal char, part II
	  TEMP = SYS(DROP.PRIVS)		! Lower the shields
						! (Without destroying the FQB)

	  TEMP = "Operator _KB" + NUM1$(I) + ":"
	  JUNK = SHOW_LEVEL(TEMP, FQFLAG)	! Display OSC info	      &
	    IF (FQFLAG AND ML.ALL)		!   if KB defined as an OSC
	  GOTO NEXT_OSC				! On to next KB

 KB_ERR:
	FQB = SYS(DROP.PRIVS)			! Lower the shields
	RESUME NEXT_OSC IF ERR = NOTAVL		! Should be only error possible
	ON ERROR GO BACK			! Return anything else to caller

 NEXT_OSC:
	NEXT I					! Check next keyboard

 SHOW_OP_SER_END:
	CLOSE SYS.CHNL				! Done with work file
	EXIT SUB				! Get next command

 OPEN_ERR:
	SELECT ERR

	  CASE EOF, NOSUCH, NODEVC
	    TEMP = " not running"
	  CASE WRGPAK
	    TEMP = " version mismatch"
	  CASE ELSE
	    ON ERROR GO BACK			! Pass unknown errors to caller

	END SELECT				! Done error checking

	PRINT "?Operator/Message Services"; TEMP! Tell user what's wrong
	RESUME SHOW_OP_SER_END			! Resume to end of command

10000	!
	!	F U N C T I O N S
	!
	! Function to display message level for an OSC or operator services:

	DEF WORD SHOW_LEVEL(STRING HDR, WORD STS)

	  SELECT STS AND (ML.MSG OR ML.REQ)	! Get message level setting

	    CASE 0%	\ TEMP = "None"
	    CASE ML.MSG \ TEMP = "Messages"
	    CASE ML.REQ \ TEMP = "Requests"
	    CASE ELSE	\ TEMP = "Messages, Requests"

	  END SELECT
	  JUNK = FORMAT(HDR + " status", TEMP)

	END DEF

	! Private little PRINT USING function so whole module isn't pulled in

	DEF WORD FORMAT(STRING ITEM, STS)

	  RSET SHOW_FMT = ITEM
	  PRINT #OUT.CHNL, SHOW_FMT; ": "; STS

	END DEF

	END SUB
