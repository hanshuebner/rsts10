1	FUNCTION WORD MGRRCV(STRING RCVBUF, WORD WAIT_TIME, NOTIFY, STRING TEXT)

	!********************************************************************
	!
	!	s u b p r o g r a m   t i t l e
	!
	!	  subprogram : MGRRCV
	!	     version : V10.1
	!	        edit : A
	!	   edit date : 10-MAY-91
	!	   author(s) : DBM
	!
	!********************************************************************
	!
	!		  C O P Y R I G H T
	!
	!
	!		      Copyright (C) 1989, 1991 by
	!	Digital Equipment Corporation, Maynard, Mass.
	!
	!
	! This software is furnished under a license and may be used and
	! copied  only  in accordance with the terms of such license and
	! with the  inclusion  of  the  above  copyright  notice.   This
	! software  or  any  other copies thereof may not be provided or
	! otherwise made available to any other person.  No title to and
	! ownership of the software is hereby transferred.
	!
	! The information in this software is subject to change  without
	! notice  and should not be construed as a commitment by Digital
	! Equipment Corporation.
	!
	! DIGITAL assumes no responsibility for the use  or  reliability
	! of its software on equipment that is not supplied by DIGITAL.
	!
	!********************************************************************

	!********************************************************************
	!
	!	m o d i f i c a t i o n   h i s t o r y   l o g
	!
	!
	! VER/ED	EDIT DATE	REASON
	! V10.0-01	28-Mar-89	(DBM) Creation
	!
	!********************************************************************
	!
	!			d e s c r i p t i o n
	!
	!	This function receives an acknowledgment message from OMS.
	!	If a known error occurs, then the appropriate error message
	!	is printed, and the function returns FALSE.  If the subroutine
	!	succeeds, then the function returns TRUE.
	!
	!	Unknown errors are returned to the caller via ON ERROR GO BACK.
	!
	!********************************************************************

	OPTION	TYPE = EXPLICIT

	DECLARE WORD I				! Loop counter

	EXTERNAL WORD CONSTANT	HNGTTY,		! ?Keyboard wait exhausted    &
				WRGPAK,		! ?Pack IDs don't match	      &
				CTRLCE		! ?Programmable CTRL/C trap

	EXTERNAL WORD				! Patch points for OMSSUB     &
				OMS$LO,		!   OMS Local Object Type     &
				OMS$MM,		!   Message Max for declare   &
				OMS$PD,		!   Raise privs for declare   &
				OMS$PS,		!   Raise privs for send      &
				CCFLAG		!   CTRL/C typed flag

	%INCLUDE "SYSTEM:CMNDEF"		! Get request and format layouts
	%IF %DEBUG
	%THEN
	  DECLARE WORD FUNCTION DUMP_BUFFER(STRING, STRING)
	  DECLARE WORD CONSTANT	DMPLEN = 13%
	  DECLARE STRING TEMP

	  MAP (DMPBUF)	STRING	DMPBUF = DMPLEN	! Layout of dump display buffer
	  MAP (DMPBUF)	STRING	DMP.POS = 6%,	!   Position in string	      &
				DMP.ASC = 3%,	!   ASCII value of char	      &
				DMP.CHR = 4%	!   Printable character
	%END %IF

	MAP (OMSREQ)	STRING	FILL = 14%,	! Define parameter buffer     &
				PARBUF = 24%	!  12 bytes in, 12 bytes out

	MAP (OMSREQ)	STRING	FILL = 14%,	! Layout parameter buffer     &
			WORD	OMSCMD,		!   Command to send to OMS    &
			STRING	REQ.RTN = 6%,	!   Return receiver name      &
			WORD	REQ.CTX,	!   Context value	      &
				FILL,		!   Reserved		      &
				REQ.LEN,	!   Length of any reply	      &
				ERRSTS,		!   Error returned from call  &
				ERRFLD		!   ORP field in error

 MAIN:

	ERRSTS = 0%				! Assume success
	MGRRCV = TRUE				! Assume success
	EXIT FUNCTION UNLESS SEND		! All done if nothing was sent

	! Compute # of times we'll go through the receive loop
	I = (ABS(WAIT_TIME) + NOTIFY - 1.0) / NOTIFY

 RECEIVE_LOOP:
	JUNK = MOD(ABS%(WAIT_TIME), NOTIFY)	! Compute final wait time
	JUNK = NOTIFY IF (I > 1%) OR (JUNK = 0%)! Use notify timer for now
	JUNK = JUNK OR BITVAL(15%)		! Compute receiver remove flg &
	  IF (I <> 1%) OR (WAIT_TIME < 0%)

	CALL OMSRCV				! Check for acknowlegment     &
		BY REF (RCVBUF BY DESC,		!   Receive buffer	      &
			PARBUF,			!   Ptr to parameter buffer   &
			JUNK BY VALUE)		!   Wait time/receiver flag

	%IF %DEBUG
	%THEN
	  IF DEBUG
	  THEN
	    PRINT "Error status =";ERRSTS IF ERRSTS
	    JUNK = DUMP_BUFFER("Receive parameter buffer", PARBUF)
	  END IF
	%END %IF

	SELECT ERRSTS

	  CASE HNGTTY				! ?Keyboard wait exhausted
	    I = I - 1%				! Decrement retry count
	    IF I				! Any retries left?
	    THEN
	      PRINT "Waiting for "; TEXT; "..." IF LEN(TEXT)
	      GOTO RECEIVE_LOOP			! Yes, go try receive again
	    END IF
	    IF LEN(TEXT)
	    THEN
	      PRINT "%"; UNLESS WAIT_TIME < 0%	! Warning if we won't be back
	      PRINT "No "; TEXT; " received"	! Tell user current status
	    END IF
	    MGRRCV = FALSE			! Indicate failure

	  %IF %DEBUG
	  %THEN
	    CASE WRGPAK
	      PRINT "Wrong context value returned, message ignored"
	      GOTO RECEIVE_LOOP
	  %END %IF

	END SELECT

	%IF %DEBUG
	%THEN
	  DEF WORD DUMP_BUFFER(STRING HDR, BUF)

	    PRINT CR.LF; HDR; " ("; NUM1$(LEN(BUF)); " bytes):"
	    FOR I = 1% TO LEN(BUF)

	      RSET DMP.POS = NUM1$(I) + " ="
	      TEMP = SEG$(BUF, I, I)
	      JUNK = ASCII(TEMP)
	      RSET DMP.ASC = NUM1$(JUNK)
	      IF ((JUNK > 31%) AND (JUNK < 127%)) OR (JUNK > 159%)
	      THEN
		RSET DMP.CHR = "34"C + TEMP + "34"C
	      ELSE
		DMP.CHR = ""
	      END IF

	      PRINT IF CCPOS(0%) + DMPLEN > KB.WIDTH
	      PRINT DMPBUF;

	    NEXT I
	    PRINT IF CCPOS(0%)

	  END DEF
	%END %IF

	END FUNCTION
