1	SUB STPOMS
	!
	!	s u b p r o g r a m   t i t l e
	!
	!	subprogram : STPOMS.B2S
	!	   version : V10.1
	!	      edit : A
	!	 edit date : 10-MAY-91
	!	 author(s) : DBM
	!
	!********************************************************************
	!
	!		  C O P Y R I G H T
	!
	!
	!		      Copyright (C) 1989, 1991 by
	!	Digital Equipment Corporation, Maynard, Mass.
	!
	!
	! This software is furnished under a license and may be used and
	! copied  only  in accordance with the terms of such license and
	! with the  inclusion  of  the  above  copyright  notice.   This
	! software  or  any  other copies thereof may not be provided or
	! otherwise made available to any other person.  No title to and
	! ownership of the software is hereby transferred.
	!
	! The information in this software is subject to change  without
	! notice  and should not be construed as a commitment by Digital
	! Equipment Corporation.
	!
	! DIGITAL assumes no responsibility for the use  or  reliability
	! of its software on equipment that is not supplied by DIGITAL.
	!
	!********************************************************************
	!
	!	m o d i f i c a t i o n   h i s t o r y   l o g
	!
	!
	! VER/ED	EDIT DATE	REASON
	!
	! T10.0-01	03-Apr-89	(DBM) Creation
	!
	!********************************************************************

	!********************************************************************
	!
	!	p r o g r a m   d e s c r i p t i o n
	!
	!	STPOMS.B2S handles the STOP/OPERATOR_SERVICES command.
	!
	!********************************************************************
	!
	!	c h a n n e l   a s s i g n m e n t s
	!
	!	channel #	file-spec	description
	!	=========	=========	===========
	!	  0		_KB:		User keyboard (BP2 default)
	!
	!********************************************************************

	!********************************************************************
	!
	!	C o m p i l e   O P T I O N s
	!
	!********************************************************************

	OPTION	TYPE = EXPLICIT

	!********************************************************************
	!
	!	I N C L U D E   r e q u i r e d   M A P s , C O M M O N s
	!
	!********************************************************************

	%INCLUDE "CUI:CUICMN.E2S"		! Include CUI data section
	%INCLUDE "SYSTEM:CMNDEF.B2S"		! Include OMS common defs
	%INCLUDE "SYSTEM:OMSKEY.B2S"		! Include CUI key defs

	!********************************************************************
	!
	!	D E C L A R E   V A R I A B L E S
	!
	!********************************************************************
	
	DECLARE	STRING	TEMP			! Temporary string storage

	EXTERNAL WORD				! Patch points for OMSSUB     &
				OMS$LO,		!   OMS Local Object Type     &
				OMS$MM,		!   Message Max for declare   &
				OMS$PD,		!   Raise privs for declare   &
				OMS$PS		!   Raise privs for send

	MAP (OMSREQ)	STRING	FILL = 14%,	! Layout parameter buffer     &
			WORD	OMSCMD,		!   Command sent to OMS	      &
				FILL(6),	!   Skip to error status word &
				ERRSTS,		!   Error returned from call  &
				ERRFLD		!   ORP field in error

	MAP (CMDBUF)	STRING	CMDBUF = 2%	! Layout of command buffer:
	MAP (CMDBUF)	BYTE	FLD.ABO,	! Byte for OC$ABO	      &
				FLD.FLG		! Byte for /[NO]ABORT flag

	EXTERNAL WORD FUNCTION						      &
			PRIVED (STRING),	! Function to check for priv  &
			MGRSND (WORD, WORD),	! Function to send to OMS     &
			MGRRCV (STRING, WORD, WORD, STRING) ! FN to rcv OMS ack

	!	P E R F O R M   C O M M A N D   P R O C E S S I N G

 STOP_OMS:	! Stop Operator Services Command

	FLD.ABO = OC$ABO			! Set /[NO]ABORT field value
	CALL FET$CQ BY REF (ABO.Q)		! Check for /[NO]ABORT
	FLD.FLG = U.PRES AND (NOT U.NO)		! Set /ABORT flag, 0=NO, -1=YES

	GOTO STOP_OMS_END						      &
	  UNLESS PRIVED(PRIV.SWCTL)		! Get out unless priv'd user

						! Set up for OMSSUB calls:
	OMS$PD = TRUE				! Raise privs for declare
	OMS$PS = FALSE				! Drop privs for send

	ON ERROR GO BACK			! Pass ^C back to caller
	JUNK = CTRLC				! Starting now

	GOTO STOP_OMS_END			! Quit if...		      &
	  UNLESS MGRSND(OC.STP, 2%)		!   unable to send cmd packet

	GOTO STOP_OMS_END			! Quit if can't get ack	      &
	  UNLESS MGRRCV("", 90%, 15%, "acknowledgement")

	JUNK = RCTRLC				! Done with ^C trap
	IF ERRSTS
	THEN
	  TEMP = "?Program failure in " + P.NAM +	! This may be a bug   &
	    CR.LF + ERT$(ERRSTS) + " in OMSRCV"	!   Display error/module
	ELSE
	  TEMP = "stopped at " + TIME$(0%)	! Assume we stopped OMS
	  IF ERRFLD				! Were any requests pending?
	  THEN
	    IF U.NO				! Yes, is this a /NOABORT
	    THEN
	      TEMP = "will shut down"		!  Yes, say what will happen
	    END IF
	    TEMP = TEMP + " after aborting" + NUM$(ERRFLD) + "request"
	    TEMP = TEMP + "s" IF ERRFLD <> 1%
	  END IF
	  TEMP = "Operator/Message Services " + TEMP
	END IF
	PRINT TEMP

32767	!
 STOP_OMS_END:
	END SUB
