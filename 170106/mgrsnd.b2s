1	FUNCTION WORD MGRSND(WORD CMDVAL, CMDLEN)

	!********************************************************************
	!
	!	s u b p r o g r a m   t i t l e
	!
	!	  subprogram : PRIVED
	!	     version : V10.1
	!	        edit : A
	!	   edit date : 10-MAY-91
	!	   author(s) : DBM
	!
	!********************************************************************
	!
	!		  C O P Y R I G H T
	!
	!
	!		      Copyright (C) 1989, 1991 by
	!	Digital Equipment Corporation, Maynard, Mass.
	!
	!
	! This software is furnished under a license and may be used and
	! copied  only  in accordance with the terms of such license and
	! with the  inclusion  of  the  above  copyright  notice.   This
	! software  or  any  other copies thereof may not be provided or
	! otherwise made available to any other person.  No title to and
	! ownership of the software is hereby transferred.
	!
	! The information in this software is subject to change  without
	! notice  and should not be construed as a commitment by Digital
	! Equipment Corporation.
	!
	! DIGITAL assumes no responsibility for the use  or  reliability
	! of its software on equipment that is not supplied by DIGITAL.
	!
	!********************************************************************

	!********************************************************************
	!
	!	m o d i f i c a t i o n   h i s t o r y   l o g
	!
	!
	! VER/ED	EDIT DATE	REASON
	! V10.0-01	28-Mar-89	(DBM) Creation
	!
	!********************************************************************
	!
	!			d e s c r i p t i o n
	!
	!	This function returns a flag indicating whether or not
	!	the command in CMDBUF was sent to OMS.
	!
	!	If the send is unsuccessful, an error message is displayed
	!	and the function returns with a value of FALSE.  If the send
	!	succeeds, then the function returns TRUE.
	!
	!********************************************************************

	OPTION	TYPE = EXPLICIT

	%INCLUDE "SYSTEM:CMNDEF"		! Get FIRQB layouts

	EXTERNAL WORD CONSTANT	NOROOM,		! ?No room for user on device &
				NOSUCH,		! ?Can't find file or account &
				CTRLCE,		! ?Programmable CTRL/C trap   &
				NOBUFS		! ?No buffer space available

	DECLARE WORD I
	DECLARE STRING TEMP
	DECLARE STRING CONSTANT OMS = "Operator/Message Services"

	EXTERNAL WORD				! Patch points for OMSSUB     &
				OMS$LO,		!   OMS Local Object Type     &
				OMS$MM,		!   Message Max for declare   &
				OMS$PD,		!   Raise privs for declare   &
				OMS$PS,		!   Raise privs for send      &
				CCFLAG		!   CTRL/C typed flag

	%IF %DEBUG
	%THEN
	  DECLARE WORD FUNCTION DUMP_BUFFER(STRING, STRING)
	  DECLARE WORD CONSTANT	DMPLEN = 13%

	  MAP (DMPBUF)	STRING	DMPBUF = DMPLEN	! Layout of dump display buffer
	  MAP (DMPBUF)	STRING	DMP.POS = 6%,	!   Position in string	      &
				DMP.ASC = 3%,	!   ASCII value of char	      &
				DMP.CHR = 4%	!   Printable character
	%END %IF

	MAP (CMDBUF)	STRING	CMDSIZ = 512%	! Maximum command buffer size
	MAP DYNAMIC (CMDBUF)	STRING CMDTXT	! Variable length string =CMDLEN

	MAP (OMSREQ)	STRING	FILL = 14%,	! Define parameter buffer     &
				PARBUF = 24%	!  12 bytes in, 12 bytes out

	MAP (OMSREQ)	STRING	FILL = 14%,	! Layout parameter buffer     &
			WORD	OMSCMD,		!   Command to send to OMS    &
			STRING	REQ.RTN = 6%,	!   Return receiver name      &
			WORD	REQ.CTX,	!   Context value	      &
				FILL,		!   Reserved		      &
				REQ.LEN,	!   Length of any reply	      &
				ERRSTS,		!   Error returned from call  &
				ERRFLD		!   ORP field in error

 MAIN:

	REMAP (CMDBUF) CMDTXT = CMDLEN		! Map the used portion of CMDBUF
	OMSCMD = CMDVAL				! Set command in ORP
	MGRSND = TRUE				! Assume success
	ERRSTS = 0%				! Assume success

	RANDOMIZE				! Ensure random context value
	REQ.CTX = MAXINT * RND			! Get one
	REQ.RTN = "OMS_"			! Always use OMS_nn rcvr name

	%IF %DEBUG
	%THEN
	  IF DEBUG
	  THEN
	    JUNK = DUMP_BUFFER("Send parameter buffer", PARBUF)
	    JUNK = DUMP_BUFFER("Command buffer", CMDTXT)
	  END IF
	%END %IF
	EXIT FUNCTION UNLESS SEND

	CALL OMSSND				! Send the command to OMS     &	
		BY REF	("" BY DESC,		! No receive buffer	      &
			OMSCMD,			! Pointer to parameter buffer &
			0% BY VALUE,		! Return immediately	      &
			CMDTXT BY DESC,		! Command buffer descriptor   &
			JOB.NO BY VALUE)	! Declare receiver for job

	SELECT ERRSTS

	  CASE NOSUCH				! ?Can't find file or account
	    TEMP = "?" + OMS + " not running"

	  CASE NOROOM				! ?No room for user on device
	    TEMP = "?" + OMS + " message queue is full"

	  CASE NOBUFS				! ?No buffer space available
	    TEMP = "?Insufficient small buffers for message send/receive"

	  CASE ELSE				! Unknown error
	    TEMP = "?Program failure in "+P.NAM	!   This may be a bug	      &
	      +CR.LF+ERT$(ERRSTS)+" in OMSSND"	!   So display error/module

	END SELECT

	IF ERRSTS				! Was there an error?
	THEN
	  PRINT TEMP				! Yes, print error message
	  MGRSND = FALSE			!   Signal failure
	END IF

	%IF %DEBUG
	%THEN
	  DEF WORD DUMP_BUFFER(STRING HDR, BUF)

	    PRINT CR.LF; HDR; " ("; NUM1$(LEN(BUF)); " bytes):"
	    FOR I = 1% TO LEN(BUF)

	      RSET DMP.POS = NUM1$(I) + " ="
	      TEMP = SEG$(BUF, I, I)
	      JUNK = ASCII(TEMP)
	      RSET DMP.ASC = NUM1$(JUNK)
	      IF ((JUNK > 31%) AND (JUNK < 127%)) OR (JUNK > 159%)
	      THEN
		RSET DMP.CHR = "34"C + TEMP + "34"C
	      ELSE
		DMP.CHR = ""
	      END IF

	      PRINT IF CCPOS(0%) + DMPLEN > KB.WIDTH
	      PRINT DMPBUF;

	    NEXT I
	    PRINT IF CCPOS(0%)

	  END DEF
	%END %IF

	END FUNCTION
