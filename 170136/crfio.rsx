;  DEC/CMS REPLACEMENT HISTORY, Element CRFIO.RSX
;  *3    18-AUG-1986 11:18:37 WORRALL "Complete HEX listing support for version 5.5 fieldtest"
;  *2    14-APR-1986 23:15:50 SYSTEM "Update 5.4 of MACRO-11"
;  *1    28-MAR-1986 02:34:36 SYSTEM "Load MACRO-11 sources from V5.3"
;  DEC/CMS REPLACEMENT HISTORY, Element CRFIO.RSX
	.NLIST							;Edit Level 00
	.ENABL	LC,GBL
	.LIST
	.TITLE	CRFIO - Cross reference file I/O
	.SBTTL	CRFIO - Cross reference file I/O
	.SBTTL
	.SBTTL		.IDENT	/V05.05/
	.SBTTL
	.IDENT	/V05.05/
;****************************************************************************
;*									    *
;*                   COPYRIGHT (c)  1983, 1986                              *
;*          BY DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.                *
;*                   ALL RIGHTS RESERVED.                                   *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED  *
;*  ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE  *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER  *
;*  COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY  *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY  *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE  *
;*  AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT  *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE OR  RELIABILITY  OF  ITS  *
;*  SOFTWARE ON EQUIPMENT THAT IS NOT SUPPLIED BY DIGITAL.		    *
;*									    *
;****************************************************************************


;++
;  Facility:	MACRO-11  The PDP-11 macro assembler for RT/RSX/VMS and RSTS/E
;
;    Author:	Too many people to list here
;
;   Created:	From the dust and dirt
;
;  Abstract:	CRFIO - Cross reference file I/O
;
;     Externals	 Description
;     ---------	 -----------
;
;      Edit	Who	Date		Description of modification
;      ----	---	----		---------------------------
;--


	PURE	PUREI,I

;
; **-CRFDEF-*-OUTPUT CREF DEFINITION RECORD
; **-CRFREF-*-OUTPUT CREF REFERENCE RECORD
;
.ENABL LSB

CRFDEF::MOVB	#CR$DEF,$CRATT	;++022 SET CRF DEFN FLAG

CRFREF::BIT	#ED.CRF,EDMASK	;++022 IF CREF OUTPUT DISABLED?
	BNE	500$		;++022 IF NE YES
CRFERR::			;Entry point from ENDLN
	TST	PASS		;++022 IF THIS IS FIRST PASS, THEN
	BEQ	500$		;++022 JUST RETURN
	TSTB	CRFFIL		;++022 OR IF NO CRF FILE, THEN
	BEQ	500$		;++022 JUST RETURN
	MOV	R0,-(SP)	;++022 ELSE, SAVE R0
	MOV	CRMASK,-(SP)	;++022 COPY CREF OPTIONS MASK
	MOV	#$CRTBL,R0	;++022 GET ADDR OF CREF LIST TABLE
100$:	CMPB	ROLNDX,(R0)+	;++022 DOES ENTRY MATCH CURRENT LIST?
	BEQ	200$		;++022 IF EQ YES
	ROR	(SP)		;++022 SHIFT CONTROL MASK FOR THIS OPTION
	BR	100$		;++022 KEEP SEARCHING FOR LIST MATCHUP
200$:	ROR	(SP)+		;++022 SHIFT CONTROL MASK FOR THIS OPTION
	BCC	400$		;++022 IF CC THIS OPTION NOT ENABLED
	SUB	#$CRTBL+1,R0	;++022 CALC THE OPTION'S TABLE OFFSET
	MOVB	R0,$CRFMT	;++022 USE IT AS FORMAT NUMBER
	MOV	#$CRSYM,R0	;++022 GET ADDR OF CRF RECORD BUFFER
	MOV	SYMBOL,(R0)+	;++022 STORE THE SYMBOL NAME
	MOV	SYMBOL+2,(R0)+	;++022
	MOV	PAGNUM,(R0)+	;++022 STORE PAGE NUMBER
	MOV	LINNUM,(R0)+	;++022 STORE LINE NUMBER
	MOV	VALUE,(R0)+	;++022 STORE VALUE
	MOVB	FLAGS,-(SP)	;++022 COPY SYMBOL'S FLAGS
	BICB	#CR$DRF,(SP)	;++022 ASSUME NO DESTRUCTIVE REF
	TSTB	CRFDFL		;++022 CHECK FOR A DESTRUCTIVE REF
	BPL	300$		;++022 IF PL NOT TRUE THIS CASE
	BISB	#CR$DRF,(SP)	;++022 SET DESTRUCTIVE REF FLAG
300$:	BISB	(SP)+,(R0)	;++022 MERGE FLAGS INTO BUFFER
	$WRITE	CRF		;++022 OUTPUT THE CREF RECORD
400$:	MOV	(SP)+,R0	;++022 RESTORE R0
	CLRB	$CRATT		;++022 CLEAR CREF RECORD ATTRIB BYTE
500$:	RETURN			;++022
.DSABL LSB


	.END
