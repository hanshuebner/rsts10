;  DEC/CMS REPLACEMENT HISTORY, Element R5UNP.MAC
;  *3    18-AUG-1986 11:25:54 WORRALL "Complete HEX listing support for version 5.5 fieldtest"
;  *2    14-APR-1986 23:40:23 SYSTEM "Update 5.4 of MACRO-11"
;  *1    28-MAR-1986 02:43:30 SYSTEM "Load MACRO-11 sources from V5.3"
;  DEC/CMS REPLACEMENT HISTORY, Element R5UNP.MAC
	.NLIST							;Edit Level 00
	.ENABL	LC,GBL
	.LIST
	.TITLE	R5UNP - RADIX-50 to ASCII unpack
	.SBTTL	R5UNP - RADIX-50 to ASCII unpack
	.SBTTL
	.SBTTL		.IDENT	/V05.05/
	.SBTTL
	.IDENT	/V05.05/
;****************************************************************************
;*									    *
;*                   COPYRIGHT (c)  1983, 1986                              *
;*          BY DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.                *
;*                   ALL RIGHTS RESERVED.                                   *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED  *
;*  ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE  *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER  *
;*  COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY  *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY  *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE  *
;*  AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT  *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE OR  RELIABILITY  OF  ITS  *
;*  SOFTWARE ON EQUIPMENT THAT IS NOT SUPPLIED BY DIGITAL.		    *
;*									    *
;****************************************************************************


;++
;  Facility:	MACRO-11  The PDP-11 macro assembler for RT/RSX/VMS and RSTS/E
;
;    Author:	Too many people to list here
;
;   Created:	From the dust and dirt
;
;  Abstract:	R5UNP - RADIX-50 to ASCII unpack
;
;     Externals	 Description
;     ---------	 -----------
;
;      Edit	Who	Date		Description of modification
;      ----	---	----		---------------------------
;--


	PURE	PUREI,I

;+
; R50UNP
; R2 -> OUTPUT STRING
; SYMBOL, SYMBOL+2 = RAD50 TO UNPACK
; CALL RAD50
; R2 -> PAST OUTPUT STRING
; R0, R1, R3 DESTROYED
;-

R50UNP::MOV	R4,-(SP)	;SAVE R4
	MOV	#SYMBOL,R4	;POINT TO SYMBOL STORAGE
1$:	MOV	(R4)+,R1	;GET NEXT WORD
	MOV	#50*50,R3	;SET DIVISOR
	CALL	10$		;DIVIDE AND STUFF IT
	MOV	#50,R3
	CALL	10$		;AGAIN FOR NEXT
	MOV	R1,R0
	CALL	11$		;FINISH LAST GUY
	CMP	R4,#SYMBOL+4	;THROUGH?
	BNE	1$		;  NO
	MOV	(SP)+,R4	;YES, RESTORE REGISTER
	RETURN

10$:	CLR	R0
	DIV	R3,R0
11$:	TST	R0		;SPACE?
	BEQ	23$		;  YES
	CMP	R0,#33		;TEST MIDDLE
	BLT	22$		;ALPHA
	BEQ	21$		;DOLLAR
	ADD	#22-11,R0	;DOT OR DOLLAR
21$:	ADD	#11-100,R0
22$:	ADD	#100-40,R0
23$:	ADD	#40,R0
	MOVB	R0,(R2)+	;STUFF IT
	RETURN


	.END
