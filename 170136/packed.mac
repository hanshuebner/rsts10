;  DEC/CMS REPLACEMENT HISTORY, Element PACKED.MAC
;  *3    18-AUG-1986 11:25:18 WORRALL "Complete HEX listing support for version 5.5 fieldtest"
;  *2    14-APR-1986 23:37:54 SYSTEM "Update 5.4 of MACRO-11"
;  *1    28-MAR-1986 02:42:19 SYSTEM "Load MACRO-11 sources from V5.3"
;  DEC/CMS REPLACEMENT HISTORY, Element PACKED.MAC
	.NLIST							;Edit Level 00
	.ENABL	LC,GBL
	.LIST
	.TITLE	PACKED - Assemble packed decimal strings for CIS
	.SBTTL	PACKED - Assemble packed decimal strings for CIS
	.SBTTL
	.SBTTL		.IDENT	/V05.05/
	.SBTTL
	.IDENT	/V05.05/
;****************************************************************************
;*									    *
;*                   COPYRIGHT (c)  1983, 1986                              *
;*          BY DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.                *
;*                   ALL RIGHTS RESERVED.                                   *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED  *
;*  ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE  *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER  *
;*  COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY  *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY  *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE  *
;*  AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT  *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE OR  RELIABILITY  OF  ITS  *
;*  SOFTWARE ON EQUIPMENT THAT IS NOT SUPPLIED BY DIGITAL.		    *
;*									    *
;****************************************************************************


;++
;  Facility:	MACRO-11  The PDP-11 macro assembler for RT/RSX/VMS and RSTS/E
;
;    Author:	Too many people to list here
;
;   Created:	From the dust and dirt
;
;  Abstract:	PACKED - Assemble packed decimal strings for CIS
;
;     Externals	 Description
;     ---------	 -----------
;
;      Edit	Who	Date		Description of modification
;      ----	---	----		---------------------------
;--
	PURE	PUREI,I

.IF	NDF,XCIS	;CIS Instruction set support

;+
; DIRECTIVE .PACKED +/-NNNNNNN,<LABEL>
;-

PACKE::	MOV	#14,R3		;ASSUME + SIGN
	SETNB			;SCAN TO NON BLANK
	CMP	#'+,R5		;"+"?
	BEQ	2$		;YES
	CMP	#'-,R5		;"-"?
	BEQ	1$		;YES, SET MINUS NIBBLE
	MOV	#17,R3		;ELSE DEFAULT TO UNSIGNED
	BR	3$
1$:	INC	R3		;CHANGE TO MINUS
2$:	GETNB			;BYPASS SIGN
3$:	CLR	R1		;ZERO OUT DIGIT COUNTER
	MOV	CHRPNT,R2	;SAVE SCAN POINTER
4$:	SUB	#'0,R5		;NORMALIZE DIGIT
	CMP	R5,#9.		;RANGE CHECK FOR [0,9]
	BHI	5$		;NON DECIMAL DIGIT SEEN, PROCESS STRING
	INC	R1		;TALLY THE DIGIT
	CMP	R1,#31.		;DECIMAL STRING LIMITED TO 31 DIGITS
	BHI	6$		;ERROR IF EXCEEDED MAX
	GETCHR			;GET NEXT CHAR
	BR	4$		;ITERATE
6$:	ERROR	Q
	RETURN			;SET ERROR AND EXIT
5$:	DEC	R2		;RESET SCAN POINTER TO START OF NUMBER
	MOV	R2,CHRPNT
	MOV	R1,PAKSIZ	;SAVE THE SIZE
	CLR	R5		;ASSUME LEADING NULL DIGIT
	BIT	#1,R1		;TEST IF ODD NUMBER OF DIGITS
	BEQ	20$		;BRANCH IF SO, PAD THE HIGH NIBBLE
10$:	CALL	100$		;GET A BCD DIGIT
	.REPT	4		;SHIFT TO HIGH NIBBLE
	ASL	R5
	.ENDR
20$:	MOV	R5,(R4)		;SET "VALUE"
	CALL	100$		;GET NEXT BCD DIGIT
	ADD	R5,(R4)		;ADD TO PREV NIBBLE
	SETIMM			;SET AS IMMEDIATE DATA
	STCODE			;PUT IN CODE ROLL
	TST	R1		;TEST IF STRING EXHAUSTED
	BGE	10$		;BRANCH IF NO
	SETNB			;RESET SCAN CHAR TO FIRST NON BLANK CHAR
	INC	ARGCNT		;TALLY THE NUMERIC STRING AS AN ARGUMENT
	GSARG			;TEST IF SYMBOLIC ARG FOR LENGTH
	BEQ	110$		;EXIT IF NO SYMBOL
	CLR	MODE		;SET TO ABS VALUE
	MOV	PAKSIZ,VALUE	;SET VALUE TO NUMBER OF DIGITS IN STRING
	JMP	ASGMTF		;FINISH IN ASSIGNMENT PROCESSOR
100$:	GETCHR			;GET NEXT DIGIT
	SUB	#'0,R5		;CONVERT TO BCD
	DEC	R1		;TEST COUNT
	BGE	110$		;RETURN IF NOT END OF STRING
	MOV	R3,R5		;ELSE SET SIGN NIBBLE
110$:	RETURN			;RETURN THE BCD DIGIT OR SIGN

.IF	DF,RT11			;If RT-11

	.PSECT	IMPURE,D,GBL,RW

.IFF;	DF,RT11			;If RT-11

	.PSECT	IMPURE,D,RW

.ENDC;	DF,RT11			;If RT-11

PAKSIZ:	.BLKW	1

.ENDC;	NDF,XCIS	;CIS Instruction set support


	.END
