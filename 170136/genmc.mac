;  DEC/CMS REPLACEMENT HISTORY, Element GENMC.MAC
;  *3    18-AUG-1986 11:20:56 WORRALL "Complete HEX listing support for version 5.5 fieldtest"
;  *2    14-APR-1986 23:22:05 SYSTEM "Update 5.4 of MACRO-11"
;  *1    28-MAR-1986 02:36:45 SYSTEM "Load MACRO-11 sources from V5.3"
;  DEC/CMS REPLACEMENT HISTORY, Element GENMC.MAC
	.NLIST
	.ENABL	LC,GBL
	.SBTTL
	.SBTTL
	.SBTTL	GENMC	- MACRO-11 Assembly macros		;Edit level 00
	.SBTTL
	.SBTTL		.IDENT	/V05.05/
	.SBTTL
;****************************************************************************
;*									    *
;*                   COPYRIGHT (c)  1983, 1986                              *
;*          BY DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.                *
;*                   ALL RIGHTS RESERVED.                                   *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED  *
;*  ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE  *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER  *
;*  COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY  *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY  *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE  *
;*  AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT  *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE OR  RELIABILITY  OF  ITS  *
;*  SOFTWARE ON EQUIPMENT THAT IS NOT SUPPLIED BY DIGITAL.		    *
;*									    *
;****************************************************************************


;++
;  Facility:	MACRO-11  The PDP-11 macro assembler for RT/RSX/VMS and RSTS/E
;
;    Author:	Too many people to list here
;
;   Created:	From the dust and dirt
;
;  Abstract:	GENMC contains common macro definitions for MACRO-11.
;
;     Externals	 Description
;     ---------	 -----------
;
;      Edit	Who	Date		Description of modification
;      ----	---	----		---------------------------
;--
.DSABL CRF


.IF	NE,0
;+
; PSECT
; This MACROS allows us to call psects by resonable names
;-
.MACRO	PSECT	NAME
	...... = 0

.IF	IDN,<NAME>,<PURE_BYTE_DATA>	;Local pure_byte_data
	.PSECT	PUREB,RO,D,LCL
	...... = 1			;Flag valid macro name given
.ENDC;	IDN,<NAME>,<PURE_BYTE_DATA>

.IF	IDN,<NAME>,<USR_BLOCK_DATA>	;Global root data for RT-11
.IF	EQ,<SYSTEM-RT11>
	.PSECT	USRDAT,RW,D,GBL
.ENDC;	EQ,<SYSTEM-RT11>
	...... = 1			;Flag valid macro name given
.ENDC;	IDN,<NAME>,<USR_BLOCK_DATA>

.IIF EQ,......	.ERROR	;Macro PSECT called with illegal PSECT name!
.ENDM	PSECT
.ENDC;	NE,0


;+
; .ASSUME
; Verify an assumption to be correct
;-

.MACRO	.ASSUME	A1,CND,A2
.IF CND	<A1>-<A2>
.IFF
.ERROR	;"A1 CND A2" Is not valid!
.ENDC
.ENDM	.ASSUME


;+
; .BR
; Verify that one routine falls into another.  The target, 'LAB', must
; immediately follow the .BR
;-

.MACRO	.BR	LAB
.IF NE	LAB-.
.ERROR	;"LAB" has moved!
.ENDC ;NE LAB-.
.ENDM	.BR


;+
; .BLKW0	AMT=1,VALUE=0
; This macro generates a number of .WORD 0's, and optionally assigns a
; value other than zero to the block of words.
;-

.MACRO	.BLKW0	AMT=1,VALUE=0
.NLIST BEX
.REPT	AMT
	.WORD	VALUE
.ENDR
.LIST BEX
.ENDM	.BLKW0


;+
; .BLKB0	AMT=1,VALUE=0
; This macro generates a number of .BYTE 0's, and optionally assigns a
; value other than zero to the block of bytes.
;-

.MACRO	.BLKB0	AMT=1,VALUE=0
.NLIST BEX
.REPT	AMT
	.BYTE	VALUE
.ENDR
.LIST BEX
.ENDM	.BLKB0


;+
; GENCAL
; Generate macro that calls subroutine of same name
;-

.MACRO	GENCAL NAME
 .MACRO	NAME
	CALL	NAME
 .ENDM	NAME
.ENDM	GENCAL

; Generate call macros

	GENCAL	SAVREG		;Save registers
	GENCAL	EXPR		;Call the expression evaluator
	GENCAL	TERM
	GENCAL	RELEXP
	GENCAL	RELTST
	GENCAL	ABSEXP
	GENCAL	ABSTST
	GENCAL	ABSERR
	GENCAL	GLBEXP
	GENCAL	ABSTRM		;Absolute term
	GENCAL	RELTRM		;Relocatable term
	GENCAL	GLBTRM		;Global term
	GENCAL	GETSYM
	GENCAL	SETSYM
	GENCAL	GETR50
	GENCAL	SETR50
	GENCAL	TSTR50
	GENCAL	GETNB
	GENCAL	SETNB
	GENCAL	GETCHR
	GENCAL	SETCHR
	GENCAL	GSARG
	GENCAL	TSTARG
	GENCAL	SETIMM
	GENCAL	SETDSP
	GENCAL	STCODE
	GENCAL	SSRCH
	GENCAL	OSRCH
	GENCAL	MSRCH
	GENCAL	LSRCH
	GENCAL	SETPF0
	GENCAL	SETPF1
	GENCAL	DNC
	GENCAL	CVTNUM
	GENCAL	R50UNP
	GENCAL	MOVBYT
	GENCAL	SETXPR
	GENCAL	CRFREF		;Cross reference a symbol
	GENCAL	CRFDEF		;Ditto, defining it


; Character scan macros

.MACRO	CHSCAN	TABLE		;Character scan
	MOV	#TABLE,R0
	CALL	CHSCAN
.ENDM	CHSCAN

.MACRO	GCHTBL	CHAR,	ADDR	;GEN Character scan table
	.WORD	ADDR,	CHAR
.ENDM	GCHTBL


; Roll handler routine calls

.MACRO	SEARCH	ROLNUM		;Binary search
	MOV	#ROLNUM,R0
	CALL	SEARCH
.ENDM	SEARCH

.MACRO	SCAN	ROLNUM		;Linear scan
	MOV	#ROLNUM,R0
	CALL	SCAN
.ENDM	SCAN

.MACRO	SCANW	ROLNUM		;Linear scan, one word
	MOV	#ROLNUM,R0
	CALL	SCANW
.ENDM	SCANW

.MACRO	NEXT	ROLNUM		;Fetch next entry
	MOV	#ROLNUM,R0
	CALL	NEXT
.ENDM	NEXT

.MACRO	APPEND	ROLNUM		;Append to end of roll
	MOV	#ROLNUM,R0
	CALL	APPEND
.ENDM	APPEND

.MACRO	ZAP	ROLNUM		;Clear roll
	MOV	#ROLNUM,R0
	CALL	ZAP
.ENDM	ZAP

	GENCAL	INSERT		;Insert (Must be preceded by one
				;of the above to set pointers)
	GENCAL	SETROL		;Save and set regs for above


; Listing macros

.MACRO	PUTKB	ADDR
	MOV	ADDR,R0
	CALL	PUTKB
.ENDM	PUTKB

.MACRO	PUTLP	ADDR
	MOV	ADDR,R0
	CALL	PUTLP
.ENDM	PUTLP

.MACRO	PUTKBL	ADDR
	MOV	ADDR,R0
	CALL	PUTKBL
.ENDM	PUTKBL

.MACRO	PUTLIN	ADDR
.IF	DIF,<ADDR>,<R0>
	MOV	ADDR,R0
.ENDC;	DIF,<ADDR>,<R0>
	CALL	PUTLIN
.ENDM	PUTLIN


; PDP-11/45-40 Instruction macros

.IF	NDF,PDPV45

.MACRO	MUL	SRC,DST
	.GLOBL	$$$MUL
	.IIF DIF <SRC>,<R3>,	MOV	SRC,R3
	.IIF DIF <DST>,<R0>,	.ERROR	;Illegal MUL args
	CALL	@$$$MUL
.ENDM	MUL

.MACRO	DIV	SRC,DST
	.GLOBL	$$$DIV
	.IIF DIF <SRC>,<R3>,	MOV	SRC,R3
	.IIF DIF <DST>,<R0>,	.ERROR	;Illegal DIV args
	CALL	@$$$DIV
.ENDM	DIV

.MACRO	SOB	REG,ADDR
	DEC	REG
	BNE	ADDR
.ENDM	SOB

.ENDC;	NDF,PDPV45


; Error and set nonzero macros

.MACRO	ERROR	ARG
	BIS	#ERR.'ARG,ERRBTS
.ENDM	ERROR

.MACRO	SETNZ	ADDR	;Set addr to non-zero for t/f flags
	MOV	SP,ADDR
.ENDM	SETNZ


; Virtual memory macro calls

.IRP	NAME,<CVRL,CVRL2,ALBLK,ALVRS,ALVRT,LCKPG,UNLPG,WRMPG>
.MACRO	NAME
	CALL	$'NAME
.ENDM	NAME
.ENDR

.IRP	NAME,<LOCPG,ULCPG,WMCPG,ALPAG>
.MACRO	NAME
	CALL	$'NAME
.ENDM	NAME
.ENDR


; Save nonvolatile registers

.MACRO	SAVRG
	JSR	R5,$SAVRG
.ENDM	SAVRG

; Save volatile registers

.MACRO	SAVVR
	JSR	R2,$SAVVR
.ENDM	SAVVR


.ENABL CRF
.LIST
