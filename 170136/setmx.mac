;  DEC/CMS REPLACEMENT HISTORY, Element SETMX.MAC
;  *3    18-AUG-1986 11:27:40 WORRALL "Complete HEX listing support for version 5.5 fieldtest"
;  *2    14-APR-1986 23:56:31 SYSTEM "Update 5.4 of MACRO-11"
;  *1    28-MAR-1986 02:46:10 SYSTEM "Load MACRO-11 sources from V5.3"
;  DEC/CMS REPLACEMENT HISTORY, Element SETMX.MAC
	.NLIST							;Edit Level 01
	.ENABL	LC,GBL
	.LIST
	.TITLE	SETMX - Set maximum location counter
	.SBTTL	SETMX - Set maximum location counter
	.SBTTL
	.SBTTL		.IDENT	/V05.05/
	.SBTTL
	.IDENT	/V05.05/
;****************************************************************************
;*									    *
;*                   COPYRIGHT (c)  1983, 1986                              *
;*          BY DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.                *
;*                   ALL RIGHTS RESERVED.                                   *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED  *
;*  ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE  *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER  *
;*  COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY  *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY  *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE  *
;*  AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT  *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE OR  RELIABILITY  OF  ITS  *
;*  SOFTWARE ON EQUIPMENT THAT IS NOT SUPPLIED BY DIGITAL.		    *
;*									    *
;****************************************************************************


;++
;  Facility:	MACRO-11  The PDP-11 macro assembler for RT/RSX/VMS and RSTS/E
;
;    Author:	Too many people to list here
;
;   Created:	From the dust and dirt
;
;  Abstract:	SETMX - Set maximum location counter
;
;     Externals	 Description
;     ---------	 -----------
;
;      Edit	Who	Date		Description of modification
;      ----	---	----		---------------------------
;	001	Jrw	02-Nov-83	Give phase error if psect is
;					bigger in pass2 than pass1!
;--


	PURE	PUREI,I

;+
; **-SETMAX-SET MAXIMUM LOCATION COUNTER
;-

SETMAX::SAVREG			;SAVE REGISTERS
	MOV	#CLCNAM,R1
	MOV	#SYMBOL,R2
	MOV	(R1)+,(R2)+	;++021 COPY PSECT NAME INTO ...
	MOV	(R1)+,(R2)+	;++021 SYMBOL AREA
	CALL	$FNDSC		;++021 FETCH CURRENT PSECT ENTRY
	BEQ	10$		;Branch if not found, was a new entry
	TST	PASS		;Is this pass-2?
	BEQ	10$		;Branch if not, don't check max
	CMP	4(R1),4(R2)	;Are we attempting to up the max from pass1?
	BLOS	20$		;Branch if not, ok.
	ERROR	P		;Else give phase error
	BR	20$		;Continue and setup CLCFGS,CLCSEC and CLCLOC

10$:	MOV	(R1)+,(R2)+	;Move CLCFGS and CLCSEC
20$:	CALL	XMIT2		;Move CLCLOC/CLCMAX
	JMP	INSERT		;UPDATE ROLL AND EXIT


;+
; **-$FNDSC-FIND MATCHING PSECT ENTRY
;
; INPUTS:
;	PSECT NAME IN SYMBOL AREA
;
; OUTPUTS:
;	Z-BIT SET IF NOT FOUND (ALSO PSECT NAME ADDED TO IN-CORE
;		PSECT NAME TABLE)
;	Z-BIT CLEAR IF MATCH FOUND
;	R3 POINTS TO SECTOR FLAGS BYTE
;	R4 LOST
;
;-

$FNDSC::MOV	#SYMBOL+4,R4	;++021 POINT R4 INTO SYMBOL AREA
	MOV	R4,R3		;++021 LEAVE R3 POINTING AT SECTOR
	MOV	-(R4),-(SP)	;++021 SAVE PSECT NAME ON THE STACK
	MOV	-(R4),-(SP)	;++021
	CLR	$LSPNT		;++021 INIT POINTER TO START OF LIST
100$:	NEXT	SECLST		;++021 GET A PSECT ENTRY
	BEQ	200$		;++021 IF EQ END OF LIST
	CMP	(SP),(R4)	;++021 COMPARE W/ 1ST HALF OF NAME
	BNE	100$		;++021 IF NE NO MATCH
	CMP	2(SP),2(R4)	;++021 COMPARE W/ 2ND HALF OF NAME
	BNE	100$		;++021 IF NE NO MATCH
	CMP	(SP)+,(SP)+	;++021 ELSE, CLEAN STACK
	BR	300$		;++021 AND BRANCH TO RETURN
200$:	MOV	(SP)+,(R4)+	;++021 RESTORE PSECT NAME
	MOV	(SP)+,(R4)	;++021
	MOV	$LSCNT+SECLST,R4  ;++021 GET NEXT AVAIL PSECT NUMBER
	CMP	R4,#NU$SEC	;++021 COMP W/ NAME TABLE COUNT
	BGE	300$		;++021 IF GE TABLE IS FULL
	ASL	R4		;++021 ELSE, CONVERT NUMBER TO A
	ASL	R4		;++021 DOUBLE-WORD OFFSET
	ADD	#$NAMSC,R4	;++029 ADD IN START OF TABLE ADDR
	MOV	-4(R3),(R4)+	;++021 COPY NAME INTO THE TABLE
	MOV	-2(R3),(R4)+	;++021
300$:	TST	R0		;++021 RETURN R0 STATUS
	RETURN			;++021


	.END
