;  DEC/CMS REPLACEMENT HISTORY, Element LISTC.MAC
;  *4    25-JUN-1987 17:45:55 STEVENS "Changed .PAGE in macro so page eject occurs only if listed"
;  *3    18-AUG-1986 11:23:19 WORRALL "Complete HEX listing support for version 5.5 fieldtest"
;  *2    14-APR-1986 23:27:58 SYSTEM "Update 5.4 of MACRO-11"
;  *1    28-MAR-1986 02:39:25 SYSTEM "Load MACRO-11 sources from V5.3"
;  DEC/CMS REPLACEMENT HISTORY, Element LISTC.MAC
	.NLIST							;Edit Level 00
	.ENABL	LC,GBL
	.LIST
	.TITLE	LISTC - Listing control directives
	.SBTTL	LISTC - Listing control directives
	.SBTTL
	.SBTTL		.IDENT	/V05.05/
	.SBTTL
	.IDENT	/V05.05/
;****************************************************************************
;*									    *
;*                   COPYRIGHT (c)  1983, 1986                              *
;*          BY DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.                *
;*                   ALL RIGHTS RESERVED.                                   *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED  *
;*  ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE  *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER  *
;*  COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY  *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY  *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE  *
;*  AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT  *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE OR  RELIABILITY  OF  ITS  *
;*  SOFTWARE ON EQUIPMENT THAT IS NOT SUPPLIED BY DIGITAL.		    *
;*									    *
;****************************************************************************


;++
;  Facility:	MACRO-11  The PDP-11 macro assembler for RT/RSX/VMS and RSTS/E
;
;    Author:	Too many people to list here
;
;   Created:	From the dust and dirt
;
;  Abstract:	LISTC - Listing control directives
;
;     Externals	 Description
;     ---------	 -----------
;
;      Edit	Who	Date		Description of modification
;      ----	---	----		---------------------------
;--


	PURE	PUREI,I

;+
; **-LIST-LIST/NOLIST PRINT CONTROL FUNCTION
; **-PAGE-PAGE EJECT
;-
.ENABL LSB

LIST::				;++011 R3=0 IMPLIES ".LIST"
				;++011 R3=-1 IMPLIES ".NLIST"
	ASL	R3		;++011 NOW MAKE R3 EITHER 0 OR -2
	INC	R3		;NOW 1/-1
1$:	TSTARG			;TEST FOR ANOTHER ARGUMENT
	BNE	2$		;  VALID
	TST	ARGCNT		;NULL, FIRST?
	BNE	7$		;  NO, WE'RE THROUGH
	INC	ARGCNT		;YES, MARK IT
2$:	GETSYM			;TRY FOR A SYMBOL
$NLIST::SCANW	LCDROL		;LOOK IT UP IN THE TABLE
	BEQ	6$		;  ERROR IF NOT FOUND
	CLR	R2
	SEC
3$:	ROL	R2
	SOB	R0,3$
	TST	EXMFLG		;CALLED FROM COMMAND STRING?
	BEQ	11$		;  NO
	BIS	R2,LCMCSI	;YES, SET DISABLE BITS
	BR	12$		;  AND SKIP TEST
11$:	BIT	R2,LCMCSI	;THIS FLAG OFF LIMITS?
	BNE	5$		;  YES
12$:	BIC	R2,LCMASK
	BIT	R2,#LC.		;NULL?
	BEQ	4$		;  NO
	CALL	PAGEX		;SET LISTING CONTROL
	ADD	R3,LCLVL	;YES, UPDATE LEVEL COUNT
	BEQ	5$		;DON'T SET FLAG IF BACK TO ZERO
4$:	TST	R3
	BPL	5$		;.LIST, BRANCH
	BIS	R2,LCMASK
5$:	BR	1$		;TRY FOR MORE
6$:	ERROR	A
7$:	RETURN
.DSABL LSB

PAGE::	TST	LCLVL		;.NLIST currently in effect?
	BLT	PAGEX		;Branch if so, don't simulate <FF>!
	TST	MACLVL		;Doing a MACRO expansion?			[+GTS]
	BEQ	1$		;Branch if not, simulate <FF>!			[+GTS]
	BIT	#LC.ME,LCMASK	;Is MACRO expansion being listed?		[+GTS]
	BEQ	1$		;Branch if so, simulate <FF>!			[+GTS]
	BIT	#LC.MEB,LCMASK	;Is MACRO expansion binary being listed?	[+GTS]
	BNE	PAGEX		;Branch if not, don't simulate <FF>!		[+GTS]
1$:	TST	LPPCNT		;Already going to top of form?			[+GTS]
	BEQ	PAGEX		;Branch if so, don't simulate <FF>!		[+GTS]
	TST	FFCNT		;Is form feed pending?				[+GTS]
	BNE	PAGEX		;Branch if so, don't simulate <FF>!		[+GTS]
	INC	FFCNT		;SIMULATE FORM FEED AFTER THIS LINE
PAGEX:	BIS	#LC.LD,LCFLAG	;FLAG AS LISTING DIRECTIVE
	RETURN


	.END
