;  DEC/CMS REPLACEMENT HISTORY, Element GTKNM.RSX
;  *3    18-AUG-1986 11:21:16 WORRALL "Complete HEX listing support for version 5.5 fieldtest"
;  *2    14-APR-1986 23:23:14 SYSTEM "Update 5.4 of MACRO-11"
;  *1    28-MAR-1986 02:37:12 SYSTEM "Load MACRO-11 sources from V5.3"
;  DEC/CMS REPLACEMENT HISTORY, Element GTKNM.RSX
.IF	NDF,V$$VMS	;If not compiling for VAX/VMS
	.TITLE	GTKNM	GET 3-CHARACTER TASK NAME
.IFF;	NDF,V$$VMS	;If not compiling for VAX/VMS
	.TITLE	GTKNMV	GET 3-CHARACTER TASK NAME (VAX/VMS)
.IFTF;	NDF,V$$VMS	;If not compiling for VAX/VMS

	.IDENT	/V05.05/		;RSX SPECIFIC SOURCE MODULE

;
;
; COPYRIGHT (c) 1981 BY DIGITAL EQUIPMENT CORPORATION.
; ALL RIGHTS RESERVED.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED
; OR COPIED ONLY IN ACCORDANCE WITH THGE TERMS OF SUCH LICENSE.
;
;
; VERSION	01.03
; B. SCHREIBER	4-JAN-78
;
; C. PUTNAM	11-OCT-78
;
;	CBP019	-- FIX BUG IN TASK NAME DECISION LOGIC
;
; C. PUTNAM	09-APR-79
;
;	CBP043 -- ALLOW TASK NAMES OF FOR TTAN-TTXN AND VTAN-VTXN
;
; J. LAWLER	13-APR-81
;
;	JML018 -- Support task names of the form HT0 - HT7
;
;	JML027 -- Change Psect to support I/D/Multiuser task
;
; J. WORRALL	03-JUN-82
;
;	JRW01	-- Correct psect and ident for macro-11 use only, allow
;		   null module to be created for VAX/VMS.
;
; THIS ROUTINE RETURNS THE 3-CHARACTER TASK NAME IN RADIX-50.
;


;+
; $GTKNM -- GET TASK NAME
;
; THIS ROUTINE RETURNS THE 3 CHARACTER TASK NAME IN RADIX-50
; IN R1.
;
; INPUTS:
;
;	R1	3 CHARACTER DEFAULT TASK NAME IN RADIX-50
;		(USED ONLY IF TASK NAME IS OF FORM "TTNN")
;
; OUTPUTS:
;
;	R1	3 CHARACTER TASK NAME IN RADIX 50.
;
; SUGGESTED CALLING SEQUENCE:
;
;	MOV	(PC)+,R1
;	.RAD50	/TSK/		;"TSK" IS DEFAULT TASK NAME
;	CALL	$GTKNM
;	MOV	#BUF,R0
;	CALL	$C5TA
;
;-

;;	PURE.I
	PURE	PUREI,I		;Macro-11's pure code psect.

$GTKNM::

.IFT;	NDF,V$$VMS	;If not compiling for VAX/VMS

.IF	NDF,R$RSTS	;If not RSTS/E

	.MCALL	GTSK$S

	SUB	#32.,SP		;RESERVE SPACE ON STACK FOR DIRECTIVE
	MOV	SP,R0		;POINT FOR DIRECTIVE
	GTSK$S	R0		;GET TASK PARAMETERS
	MOV	R1,-(SP)	;SAVE DEFAULT TASK NAME
	MOV	(R0),R1		;(TASK NAME IS FIRST TWO WORDS OF BUFFER)
	CMP	R1,#^RJOB	;IS TASK NAME JOBXXX (FOR IAS)?
	BEQ	7$		;IF EQ YES--USE DEFAULT NAME
	CMP	R1,#^RHT0	; Is task name of form HTNN
	BLO	10$		; If lt, no
	CMP	R1,#^RHT7	; Maybe..
	BLOS	7$		; Yes
	CMP	R1,#^RTTA	; IS TASK NAME OF FORM "TTAN"?
	BLO	10$		; IF LO NO
	CMP	R1,#^RTTX	; MAYBE...
	BLOS	7$		; IF LOS YES - USE DEFAULT
	CMP	R1,#^RTT0	;IS TASK NAME OF FORM "TTNN"
	BLO	10$		;IF LO NO
	CMP	R1,#^RTT7	;MAYBE...
	BLOS	7$		;IF LOS YES--USE DEFAULT
	BR	10$		; NO - TRY OTHER FORMS
7$:	MOV	(SP)+,R1	;YES--GET DEFAULT TASK NAME
	BR	20$		;RETURN
10$:	CMP	R1,#^RVTA	; IS TASK NAME OF FORM "VTAN"?
	BLO	12$		; IF LO NO
	CMP 	R1,#^RVTX	; MAYBE...
	BLOS	11$		; IF LOS YES - USE DEFAULT
	CMP	R1,#^RVT0	; IS TASK NAME OF FORM "VTNN"?
	BLO	12$		; IF LO - NO
	CMP	R1,#^RVT7	; MAYBE...
	BHI	12$		; IF HI - DEFINITELY NOT
11$:	MOV	(SP)+,R1	; YES - GET DEFAULT TASK NAME
	BR	20$		; RETURN
12$:	TST	(SP)+		; POP DEFAULT NAME OFF STACK
	CMP	(R0)+,#^R...	;IS TASK NAME "...XXX"
	BEQ	15$		;IF EQ YES--USE 2ND WORD
	CMP	-2(R0),#^R$$$	;NO--IS IT "$$$XXX" (FOR IAS)?
	BNE	20$		;IF NE NO--NAME IS CORRECT
15$:	MOV	(R0),R1		;YES--RETURN SECOND WORD OF NAME
20$:	ADD	#32.,SP		;RESTORE STACK

.ENDC;	NDF,R$RSTS	;If not RSTS/E

.ENDC;	NDF,V$$VMS	;If not compiling for VAX/VMS

	RETURN


	.END
