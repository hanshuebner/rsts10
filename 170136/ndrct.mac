;  DEC/CMS REPLACEMENT HISTORY, Element NDRCT.MAC
;  *3    18-AUG-1986 11:25:06 WORRALL "Complete HEX listing support for version 5.5 fieldtest"
;  *2    14-APR-1986 23:37:08 SYSTEM "Update 5.4 of MACRO-11"
;  *1    28-MAR-1986 02:42:02 SYSTEM "Load MACRO-11 sources from V5.3"
;  DEC/CMS REPLACEMENT HISTORY, Element NDRCT.MAC
	.NLIST							;Edit Level 00
	.ENABL	LC,GBL
	.LIST
	.TITLE	NDRCT - .NCHR/.NTYPE Directives
	.SBTTL	NDRCT - .NCHR/.NTYPE Directives
	.SBTTL
	.SBTTL		.IDENT	/V05.05/
	.SBTTL
	.IDENT	/V05.05/
;****************************************************************************
;*									    *
;*                   COPYRIGHT (c)  1983, 1986                              *
;*          BY DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.                *
;*                   ALL RIGHTS RESERVED.                                   *
;* 									    *
;*  THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED  *
;*  ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE  *
;*  INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER  *
;*  COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY  *
;*  OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY  *
;*  TRANSFERRED.							    *
;* 									    *
;*  THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE  *
;*  AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT  *
;*  CORPORATION.							    *
;* 									    *
;*  DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE OR  RELIABILITY  OF  ITS  *
;*  SOFTWARE ON EQUIPMENT THAT IS NOT SUPPLIED BY DIGITAL.		    *
;*									    *
;****************************************************************************


;++
;  Facility:	MACRO-11  The PDP-11 macro assembler for RT/RSX/VMS and RSTS/E
;
;    Author:	Too many people to list here
;
;   Created:	From the dust and dirt
;
;  Abstract:	NDRCT - .NCHR/.NTYPE Directives
;
;     Externals	 Description
;     ---------	 -----------
;
;      Edit	Who	Date		Description of modification
;      ----	---	----		---------------------------
;--


	PURE	PUREI,I

;+
; **-NARG-NUMBER OF ARGUMENTS
; **-NCHR-NUMBER OF CHARACTERS
; **-NTYPE-TYPE OF EXPRESSION
;-

NCHR::	CALL	NDARG		;GET (& CHECK) SYMBOLIC ARGUMENT
	CALL	GMARG		;ISOLATE ARGUMENT
	BEQ	NTYPEX		;  ZERO IF NULL
	TST	R5		;QUICK TEST FOR COMPLETION
	BEQ	2$		;  YES
1$:	INC	R3		;BUMP COUNT
	GETCHR			;GET THE NEXT CHARACTER
	BNE	1$		;LOOP IF NOT END
2$:	CALL	RMARG		;REMOVE ARG DELIMITERS
	BR	NTYPEX


NARG::				;++017
	CALL	MACTST		;++017 CHECK FOR MACRO EXPANSION AND ...
				;++017 ... GET (& CHECK) SYMBOLIC ARGUMENT
	MOV	MSBCNT+2,R3	;++017 PUT ARGUMENT COUNT IN R3
	BR	NTYPEX		;++017 LEAVE THROUGH COMMON CODE
NTYPE::				;++017
	CALL	MACTST		;++017 CHECK FOR MACRO EXPANSION AND ...
				;++017 ... GET (& CHECK) SYMBOLIC ARGUMENT
	TSTARG			;BYPASS ANY COMMAS
	MOV	#SYMBOL,R1
	MOV	(R1)+,-(SP)	;PRESERVE SYMBOL
	MOV	(R1)+,-(SP)
	CALL	AEXP		;EVALUATE
	MOV	R0,R3		;SET RESULT
	ZAP	CODLST		;++021 EMPTY CODE LIST
	MOV	(SP)+,-(R1)	;RESTORE SYMBOL
	MOV	(SP)+,-(R1)

NTYPEX:	CLR	MODE		;CLEAR MODE
	MOV	R3,VALUE	;  AND SET VALUE
	JMP	ASGMTF		;EXIT THROUGH ASSIGNMENT

MACTST:	TST	MACLVL		;++017 ARE WE IN A MACRO EXPANSION?
	BNE	NDARG		;++017 YES, GET ARGUMENT
	TST	(SP)+		;++017 NO, PURGE RETURN ADDRESS
	JMP	OPCERR		;++017 DECLARE OPCODE ERROR

NDARG:	CALL	GSARG		;++017 GET SYMBOLIC ARGUMENT
	BEQ	10$		;++017 ERROR IF NULL
	RETURN			;++017 ELSE, CONTINUE PROCESSING
10$:	TST	(SP)+		;++017 CLEAN STACK
	ERROR	A		;++017 DECLARE ASSEMBLY ERROR
	BR	NTYPEX


	.END
