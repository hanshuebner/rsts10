.IIF	NDF	L$$IST,	.NLIST
.IIF	NDF	L$$IST,	.DSABL	CRF
TITLE	FETDEF,<Feature Patch Bit Definitions>,0A,10-MAY-91,DWF/JFM

;
;		COPYRIGHT (c) 1974, 1991 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

.SBTTL	FETDEF EDIT HISTORY
;+
;			[RSTS/E V9.6]
;  000  DWF  19-Feb-88	Creation
;  001	DWF  12-Apr-88	Use macros to build and manipulate the FETPAT bits
;
;			[RSTS/E V9.7]
;  002	JFM  27-Apr-89	Add LAT line default {DIALUP!LOCAL} bit
;  003	FEK  05-Oct-89	Add two OPSER feature bits
;-

.SBTTL	Define the FETPAT bits

FETSIZ	=:	4	; Number of feature patch bytes

;+
;	**********  NOTE  ***********
;
;	Any changes to this module may affect RES, CFG, TERCLS, LATDSP,
;	LATPRT, QUMRUN, and SPLRUN.
;-

.MACRO	FETDEF	NAME
.IF	GE	$$$$$$-FETSIZ
.ERROR	; Feature bit NAME does not fit
.ENDC
O'NAME	=:	$$$$$$
F'NAME	=:	$$$$$0
.IRP	NUM,<\$$$$$$/2>
..FPW'NUM	=	..FPW'NUM ! $$$$$0
.ENDR
.IF	LT	$$$$$0
$$$$$0	=	1
$$$$$$	=	$$$$$$ + 2
.IFF
$$$$$0	=	$$$$$0 * 2
.ENDC
.ENDM	FETDEF

$$$$$$	=	0
$$$$$0	=	1
..FPW0	=	0
..FPW1	=	0

	FETDEF	FB.HNG		;Immediate hangup if set, else delayed
	FETDEF	FB.LTT		;Enable LAT tracing if set, else disabled
	FETDEF	FB.LOC		;Default LAT lines to local if set, 	;002
				;  otherwise dialup 			;002
	FETDEF	QUMRUN		;disable "job waiting for other form"	;003
				; message				;003
	FETDEF	SPLRUN		;disable extra FFs at start of job	;003

; Macro for manipulating FETPAT bits

.MACRO	FETBIT	LIST,SYM0,SYM1
SYM0	=	0
SYM1	=	0
.IRP	BIT,<LIST>
.IF	EQ	SYM1
SYM0	=	O'BIT
SYM1	=	F'BIT
.IFF
.IF	NE	SYM0-O'BIT
.ERROR	SYM0	; Privilege BIT is in wrong word
.IFF
SYM1	=	SYM1 ! F'BIT
.ENDC
.ENDC
.ENDR
.ENDM	FETBIT

.MACRO	FETOP	OPCODE,LIST,WHERE
FETBIT	<LIST>,$$$$$$,$$$$$0
.IF	EQ	$$$$$$
	 OPCODE	#$$$$$0,WHERE
.IFF
.NTYPE	$$$$$1,WHERE
.IF	EQ	<$$$$$1 & 67> - 67
	 OPCODE	#$$$$$0,WHERE+$$$$$$
.IFF
.IF	EQ	$$$$$1-37
	 OPCODE	#$$$$$0,WHERE+$$$$$$
.IFF
.IF	EQ	<$$$$$1 & 70> - 60
	 OPCODE	#$$$$$0,$$$$$$+WHERE
.IFF
.IF	EQ	<$$$$$1 & 70> - 10
	 OPCODE	#$$$$$0,$$$$$$'WHERE
.IFF
.ERROR	; Improper argument "WHERE"
.ENDC
.ENDC
.ENDC
.ENDC
.ENDC
.ENDM	FETOP

.MACRO	TSTFET	LIST,WHERE
	FETOP	BIT,<LIST>,WHERE
.ENDM	TSTFET

.MACRO	CLRFET	LIST,WHERE
	FETOP	BIC,<LIST>,WHERE
.ENDM	CLRFET

.MACRO	SETFET	LIST,WHERE
	FETOP	BIS,<LIST>,WHERE
.ENDM	SETFET

.IIF	NDF	L$$IST,	.ENABL	CRF
.IIF	NDF	L$$IST,	.LIST
