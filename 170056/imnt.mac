TITLE	IMNT,<RES MOUNT FILE>,0A,10-MAY-91,JDB

;
;		COPYRIGHT (c) 1974, 1991 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

.SBTTL EDIT HISTORY							;MNB001

;+
;
;  001  21-FEB-83  MNB  CHANGE "CLEAN" MESSAGE TO "REBUILD"		;MNB001
;  002  25-APR-84  JDB  MODIFIED FOR INSTALL OPTION			;JDB001
;  003	14-Jul-87  PJH	Make DCS >16 disks illegal			;003
;
;-

.SBTTL	TEXT

	I.MESS
	.ENABL	LC
NOLAB:	.ASCIZ	"This volume has no label"<200>
NOTCLN:	.ASCIZ	"Input disk should be REBUILT"<200>			;MNB001
	.EVEN
	UNORG

.SBTTL	INMNT - MOUNT FOR INPUT

.ENABL	LSB

INMNT:	TSTB	MAGTAP		;IS THIS MAGTAPE ?
	BEQ	20$		;NO
	CALL	DENSET,R5	;SET MAGTAPE DENSITY
	  JMP	90$		;COULDN'T READ TAPE
	  BR	70$		;ERROR ON DENSITY SET
10$:	  JMP	SAVIN		;GO CHECK FOR SAVE VOLUME

20$:	BIT	#OPRE!OPIN,OPFLG	;IS THIS A RESTORE OR INSTALL?	;JDB002
	BNE	10$		;YES - MUST BE A SAVE VOLUME
	CALL	CHKRST		;SEE IF THIS IS A RSTS DISK
	BCS	80$		;NO - ERROR
	BIT	#DIRTY,MNTFLG	;WAS IT DIRTY?
	BEQ	30$		;NO, KEEP GOING
	WARN	NOTCLN		;LET EM KNOW
30$:	CALL	CHKID		;CHECK SPECIFIED PACKID
	BCS	90$		;DIDN'T MATCH - ERROR
	CALL	ALLOC		;COMPUTE # OF ALLOCATED PACK CLUSTERS
	BCS	70$		;USER DOESN'T WANT TO USE THIS DISK
40$:	MOV	#ORGDSK,R0	;POINT TO ORIGINAL DISK TABLE
	CMPB	MNTPCS(R4),#16.	;large disk in use?			;003
	BLOS	45$		;no, so ok				;003
	MESSAGE	<"SAVRES cannot be used with disks of clustersize greater than 16"<200>> ;003
	BR	70$		;set error				;003

45$:	MOV	(R4),(R0)+		;ORIGINAL DISK TYPE
	MOVB	MNTIDX(R4),(R0)+	;ORIGINAL DISK INDEX
	MOVB	MNTPCS(R4),(R0)+	;ORIGINAL DISK PACK CLUSTER SIZE
	MOV	MNTDCR(R4),(R0)+	;ORIGINAL DISK SIZE (LSB)
	MOV	MNTSZL(R4),(R0)+	;ORIGINAL DISK SIZE (MSB)
	MOV	MNTSZM(R4),(R0)+	;ORIGINAL DISK CREATION DATE
	MOV	R1,(R0)		;MOVE IN NUMBER OF ALLOCATED PC'S
	CALL	RSTBAD		;READ IN SAVE VOLUME BAD BLOCK FILE
	BCS	100$		;ERROR ON READ
	CALL	CHBBI		;CHOOSE THE CORRECT BB FILE FOR INPUT
				; R5 WILL POINT TO IT ON RETURN
	TST	R1		;R1 = # OF BAD BLOCKS
	BEQ	60$		;IF NONE, ALL DONE
	MOV	#SATBUF,R0	;BUFFER WHERE READ IN BAD BLOCKS ARE
	CALL	WSORT		;SORT THEM
	MOV	R1,-(SP)	;SAVE BAD BLOCK COUNT
50$:	MOV	(R0)+,R2	;GET DCN
	CALLX	RIB2CN		;CONVERT TO PCN
	MOV	R3,(R5)+	;MOVE PCN TO DESTINATION TABLE
	SOB	R1,50$		;DO FOR ALL DCN'S
	CALLX	GETTTY		;KEEP CLOCK GOING
	MOV	R5,NEWIBB	;FIRST UNEXPECTED INPUT BAD BLOCK GOES HERE
	MOV	R5,CNBBI	;THE CURRENT SLOT FOR NEW ENTRIES
	MOV	(SP)+,R1	;RESTORE COUNT
60$:	MOV	R1,NBADI	;NUMBER OF INPUT DEVICE BAD BLOCKS	
	TST	(PC)+		;WE WERE SUCCESSFUL
70$:	SEC
	RETURN

80$:	CALL	CHKSAV		;IS THIS A SAVE VOLUME ?
	  BCC	90$		;YES
	BISB	#FMTUND,MNTFMT(R4)	;FORMAT UNDEFINED
	MOV	#NOLAB,ERRADR	;%%% DEVICE HAS NO LABEL
	BR	70$

90$:	MOV	#WRGVOL,ERRADR	;%%% WRONG VOLUME
	BR	70$

100$:	MOV	#NORBAD,ERRADR	;%%% COULDN'T READ BAD BLOCK FILE
	CMP	R1,MAXBB	;TOO MANY BAD BLOCKS ?
	BLE	70$		;NO
	MOV	#TMBBI,ERRADR	;%%% TOO MANY BAD BLOCKS ON INPUT DISK
	BR	70$

.DSABL	LSB

GLOBAL	<NBADI,CNBBI,NEWIBB,SATBUF,MAXBB,OPFLG,ORGDSK,MNTFLG>

.SBTTL	SAVE VOLUME ON INPUT

.ENABL	LSB

SAVIN:	CALL	CHKSAV		;IS THIS A SAVE VOLUME ?
	  BCS	70$		;NO - ERROR
	CALL	CHKID		;CHECK SPECIFIED PACKID
	BCS	80$		;DIDN'T MATCH - ERROR
	MOV	#SSVOL,R0	;POINT TO SSVOL TABLE
	CMPB	NXTVOL,VOLSEQ	;IS THIS WHAT THE SEQ # SHOULD BE?
	BNE	80$		;NO - WRONG VOLUME
	MOVB	MAGTAP,R1	;GET MAGTAPE INDICATOR
	CMPB	#1,VOLSEQ	;THIS VOLUME # 1 ?
	BEQ	10$		;YES
	CMPB	R1,SSHIDX(R0)	;CURRENT VOLUME OF CORRECT HANDLER INDEX ?
	BNE	80$		;NO - WRONG VOLUME
	CMP	MNTDCR(R4),SSDCR(R0)	;SAME CREATION DATE?
	BNE	80$		;NO - WRONG VOLUME
	CMP	MNTTCR(R4),SSTCR(R0)	;SAME CREATION TIME?
	BNE	80$		;NO - WRONG VOLUME
	BR	30$		;CONTINUE

10$:	MOVB	R1,SSHIDX(R0)	;SET UP CORRECT HANDLER INDEX FOR SAVE SET
	MOV	MNTPID(R4),SSNAM(R0)	;PACKID/SSN WORD 1
	MOV	MNTPID+2(R4),SSNAM+2(R0);PACKID/SSN WORD 2
	MOV	MNTDEN(R4),SSDEN(R0)	;MAGTAPE DENSITY
	MOV	MNTDCR(R4),SSDCR(R0)	;DATE OF CREATION
	MOV	MNTTCR(R4),SSTCR(R0)	;TIME OF CREATION
	MOV	MNTEXP(R4),SSEXP(R0)	;EXPIRATION DATE
	MOV	#SATBUF+4+SLOTYP,R1	;POINT TO ORIGINAL DISK INFO
					; IN THE SAVE LABEL
	MOV	#ORGDSK,R2	;POINT TO ORIGINAL DISK TABLE
	MOV	#12.,R5		;PREPARE TO FILL IT IN
20$:	MOVB	(R1)+,(R2)+	;MOVE EM IN
	SOB	R5,20$		;AS FOLLOWS,
				;ORIG. DISK TYPE
				;ORIG. DISK INDEX
				;ORIG. DISK PCS
				;ORIG. DISK CREATION DATE
				;ORIG. DISK SIZE (LSB)
				;ORIG. DISK SIZE (MSB)
				;ORIG. DISK # ALLOC. PC'S
30$:	MOV	(R4),(R0)	;SAVE SET DEFAULT DEVICE TYPE
	MOVB	MNTIDX(R4),SSDIDX(R0)	;SAVE SET DEFAULT DEVICE INDEX
	MOVB	VOLSEQ,SSSEQ(R0)	;VOLUME SEQUENCE NUMBER
	TSTB	MAGTAP		;IS THIS MAGTAPE ?
	BNE	50$		;YES - WE'RE DONE
	MOV	#SATBUF+4+SLSATL,R1	;POINT TO SATT LENGTH IN SAVE LABEL
	MOV	#SSVOL+SSSATL,R0	;POINT TO SATT LENGTH IN SSVOL
	MOV	(R1)+,(R0)+	;SATT LENGTH
	MOV	(R1)+,(R0)+	;PTR. TO SATT 1
	MOV	(R1)+,(R0)+	;PTR. TO SATT 2
	MOV	(R1)+,(R0)+	;PTR. TO DATA
	MOV	(R1),(R0)	;PTR. TO DIRECTORY BLOCKS
	CALL	SAVBAD		;READ IN SAVE VOLUME BAD BLOCK FILE
	BCS	90$		;ERROR ON READ
	CALL	CHBBI		;CHOOSE THE CORRECT INPUT BB FILE
				; R5 WILL POINT TO IT ON RETURN
	TST	R1		;R1 = # OF BAD BLOCKS
	BEQ	40$		;IF NONE, ALL DONE
	MOV	#SATBUF,R0	;WHERE READ-IN BAD BLOCK LBN'S ARE
	CALL	LBNFBN		;PUT THE FBN'S THERE
	MOV	R5,NEWIBB	;FIRST UNEXPECTED INPUT BAD BLOCK GOES HERE
	MOV	R5,CNBBI	;THE CURRENT SLOT FOR NEW ENTRIES
40$:	MOV	R1,NBADI	;NUMBER OF INPUT BAD BLOCKS
50$:	TST	(PC)+		;ALL DONE
60$:	SEC
	RETURN

70$:	CALL	CHKRST		;IS THIS A RSTS DISK ?
	BCC	80$		;YES
	BISB	#FMTUND,MNTFMT(R4)	;FORMAT UNDEFINED
	MOV	#NOLAB,ERRADR	;%%% DEVICE HAS NO LABEL
	BR	60$

80$:	MOV	#WRGVOL,ERRADR	;%%% WRONG VOLUME
	BR	60$

90$:	MOV	#NORBAD,ERRADR	;%%% COULDN'T READ BAD BLOCK FILE
	CMP	R1,MAXBB	;ERROR TOO MANY BAD BLOCKS ?
	BLE	60$		;NO
	MOV	#TMBBI,ERRADR	;%%% TOO MANY BAD BLOCKS ON INPUT DISK
	BR	60$

.DSABL	LSB
GLOBAL	<MAXBB,SSVOL,SATBUF,ORGDSK,NEWIBB,CNBBI,NBADI>

.SBTTL	WSORT - WORD TABLE SORTER

;+
; WSORT - SORT A TABLE OF WORD ENTRIES USING A SIMPLE BUBBLE
;
;	CALL:	R0 = ADDRESS OF TABLE
;		R1 = # ENTRIES
;		CALL	WSORT
;
;	RETURN:	IT'S SORTED
;-

.ENABL	LSB

WSORT:	REGSCR
	MOV	#-1,R5		;;FOR DUPLICATES
	MOV	R0,R3		;SAVE ADDRESS
10$:	DEC	R1		;NUMBER OF TIMES TO LOOP
	BLE	70$		;THERE IS NOTHING TO DO
	INC	R5		;;INC THE COUNT
	MOV	R1,R2		;KEEP A COPY FOR NEXT TIME
	CLR	R4		;ZERO CHANGE COUNTER
20$:	CMP	(R0)+,(R0)	;COMPARE WORD
	BLO	50$		;ORDER OK
	BNE	40$		;NOT EQUAL - GO DO SWITCH
	DEC	TOS.R1(SP)	;WE WILL RETURN WITH ONE LESS ENTRY
	DEC	R1		;ONE LESS ENTRY TO CHECK
	MOV	R2,-(SP)	;SAVE R2
	ADD	R5,R2		;;UP THE COUNT
	MOV	R0,-(SP)	;SAVE R0
30$:	MOV	2(R0),(R0)+	;PULL WORD UP
	SOB	R2,30$		;PULL ALL PAST EQUAL PAIR
	MOV	(SP)+,R0	;RESTORE R0
	MOV	(SP)+,R2	;RESTORE R2
	BR	50$		;EXIT TO MAIN COMPARISON LOOP

40$:	MOV	-2(R0),-(SP)	;DO
	MOV	(R0),-2(R0)	;  THE
	MOV	(SP)+,(R0)	;     SWITCH
	INC	R4		;INDICATE WE DID ONE
50$:	SOB	R2,20$		;AND LOOP
60$:	TST	R4		;ANY CHANGES ?
	BEQ	70$		;NO - WE'RE DONE
	MOV	R3,R0		;BACK TO THE BEGINNING OF TABLE
	BR	10$		;AND AROUND AGAIN

70$:	CALLX	GETTTY		;KEEP CLOCK GOING
	RETURN

.DSABL	LSB

.SBTTL	CHKID - CHECK SPECIFIED PACKID/SSN

;+
; CHKID - COMPARE SPECIFIED PACKID/SSN WITH WHAT IS ON THE RSTS DISK
;	  OR SAVE SET
;
;	CALL:	R4 = ADDRESS OF MOUNT TABLE BEING USED
;		CALL	CHKID
;
;	RETURN:	C-BIT INDICATES SUCCESS OR FAILURE
;-

.ENABL	LSB

CHKID:	BIT	#PACKID,MNTFLG	;PACKID/SSN SPECIFIED ?
	BEQ	10$		;NO - EXIT
	CMP	SPCPID,MNTPID(R4)	;COMPARE FIRST WORD
	BNE	20$		;NOT EQUAL - EXIT ERROR
	CMP	SPCPID+2,MNTPID+2(R4)	;SECOND WORD
	BNE	20$		;NOT EQUAL - EXIT ERROR
10$:	TST	(PC)+
20$:	SEC
	RETURN

.DSABL	LSB

GLOBAL	<MNTFLG,SPCPID>

