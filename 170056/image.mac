TITLE	IMAGE,<SAV/RES IMAGE>,0A,10-MAY-91,<AWR/NMC/JBL>

;
;		COPYRIGHT (c) 1974, 1991 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

.SBTTL	TRANSFER MACROS

.MACRO	I.MESS
	TMPORG	IMGTXT
.NLIST	BEX
.ENDM	I.MESS

.MACRO	I.DATA
	TMPORG	IMGCTL
.NLIST	BEX
.ENDM	I.DATA

.MACRO	I.ADDR
	TMPORG	IMGADR
.NLIST	BEX
.ENDM	I.ADDR

.MACRO	DE2TBL	SYM,LEN,VAL=0,I=2	;MACRO DEFINES 'PAIRED' TABLES.
.IIF B	<SYM>	.ERROR	;NO NAME GIVEN FOR TABLE BEING DEFINED?
.IIF B	<LEN>	.ERROR	;ZERO LENGTH TABLE BEING DEFINED?
	TMPORG	IMGADR
.EVEN
SYM:
	UNORG
.REPT	I
	TMPORG	IMGCTL
	.EVEN
	$$$$$$=.
	.BLKW0	LEN,VAL
	UNORG
	TMPORG	IMGADR
	.WORD	$$$$$$
	UNORG
.ENDR
.ENDM	DE2TBL

.SBTTL	MACROS

.MACRO	ERF	M
	 CALL	PRTERF
.IF NB	<M>
MESSAG	<M>
.IFF
.ERROR	;NO MESSAGE FOR ERF?
.ENDC
	 EXIT
.ENDM

.MACRO	WARN	M
.NLIST
	 CALL	WARN,R5
.IF	NB	M
	  .WORD	M
.ENDC
.LIST
.ENDM	WARN

.MACRO	MOVTXT	FROM,TO
.NLIST
	 CALL	MOVTXT,R5
	  .WORD	FROM
	  .WORD	TO
.LIST
.ENDM	MOVTXT

.MACRO	PRTID	C
.NLIST
.IF	NB	<C>
	MOV	C,R2		;SET UP FOR PRTNAM
.ENDC
	 CALL	PRTNAM		;PRINT THE ID
.LIST
.ENDM	PRTID

.MACRO	PRTIDC	C
.NLIST
	PRTID	C
	CALLX	TYPECR		; AND A <CR>
.LIST
.ENDM	PRTIDC

.MACRO	DECZER	C
.NLIST
.IF NB <C>
	MOVB	C,R0
.ENDC
	CALLX	DECZER
.LIST
.ENDM	DECZER

.MACRO	TYPDEV	C
.NLIST
.IF	NB	<C>
	MOV	C,R4		;SET UP FOR TYPING
.ENDC
	CALL	TYPDEV		;AND PRINT DEVICE/UNIT/COLON
.LIST
.ENDM	TYPDEV

.MACRO	PRDAYT	C
.NLIST
.IF NB <C>
	MOV	C,R0
.ENDC
	CALL	PRDAYT		;PRINT 'DAY-OF-WEEK, DD-MMM-YY'
.LIST
.ENDM	PRDAYT

.SBTTL	ORDER THE CSECTS
	DEFORG	IMG		;CODE
	DEFORG	IMGCTL		;DATA
	DEFORG	IMGTXT		;TRANSFER TEXT
	DEFORG	IMGADR		;TABLE ADDRESSES
	ORG	IMG		;TRANSFER CSECT


.SBTTL	IMAGE TEXT 

	I.MESS
	.ENABL	LC

;	OUR NAME
IMMSG:	.ASCIZ	"IMAGE copy"
	.EVEN
	UNORG

.SBTTL	IMAGE

.ENABL	LSB

IMAGE::	MOV	#IMMSG,RTNNAM	;SET UP 'IMAGE COPY' AS ROUTINE NAME
	CALL	PRBEGC		;PRINT BEGIN MESSAGE AND CLEAR TOTALS, ETC.
	CLR	RELPTR		;CLEAR RELOCATION TABLE POINTER
	CALL	SETBF2		;SET UP SATBF2
	MOV	#VERBLK+4,R0	;SET UP IN CASE OF VERIFY
	MOV	CBADI,(R0)+
	MOV	CBADO,(R0)
	CALL	SETCH1		;CLEAR THE PCN STORAGE TABLE AND XFER FLAGS
	CALL	PCSCON		;SET UP PCS CONSTANTS

; LOOP THROUGH ALL CLUSTERS IN SATT TO FIND THOSE ALLOCATED

	CLR	PCNUM		;ZERO A PACK CLUSTER # COUNTER
	MOV	SATSYS+BITS,R2	;R2 = # OF CLUSTERS IN SOURCE SATT
	CLR	R3		;R3 = SATT BYTE POINTER
	MOV	#1,R5		;R5 = BIT IN SATT BYTE WE'RE CHECKING
				;START AT BIT ZERO
10$:	CALL	XFRFIL		;FILL THE PCN TABLE WITH ALLOCATED PCN'S
	MOV	PCNRD,R0	;ANY MORE IN TABLE? (CNTCTG WANTS COUNT IN R0)
	BEQ	30$		;NO, ALL DONE
	MOV	#XFRPCN,R4	;POINT TO BEGINNING OF TABLE
	CLR	PCNRD		;GETRST RESETS THIS
20$:	MOV	PCNUM,-(SP)	;SAVE OUR PCNUM (CHANGED BY CNTCTG)
	CALL	CNTCTG		;HOW MANY STARTING FROM HERE ARE CONTIG?
	CALL	GETRST		;GET 'XFRCNT' CHUNKS OF CONTIG PC'S
	MOV	(SP)+,PCNUM	;RESTORE PCNUM
	BCS	40$		;UNKNOWN BAD BLOCK AND /NOERROR SPECIFIED?
	TST	R0		;ANYTHING LEFT IN TABLE ?
	BNE	20$		;YES - GO GET IT
	CALL	PUTRST		;GO DUMP THE BUFFER - DOESN'T COME BACK
				; IF VOL FULL OR OTHER FATAL ERROR
	CALL	SETCH1		;CLEAR TABLES/FLAGS AND RESET INPUT CHANNEL
	BR	10$		;GO CHECK THE REST OF THE SATT

30$:	BIT	#VE,SWTWRD	;VERIFY SPECIFIED ?
	BEQ	40$		;NO
	CALL	VERIMA		;YES - DO IT

; BASIC TRANSFER (AND VERIFY) IS DONE

40$:	CALL	FINRST		;CHECK FOR UNEXPECTED INPUT BAD BLOCKS
				; AND BAD COMPARES AND UPDATE DIRECTORIES
	MOV	BLKXFV,BLKXF	;FOR IMAGE, VOLUME TOTAL IS
	MOV	BLKXFV+2,BLKXF+2 ; ALSO OVERALL TOTAL
	CALL	ENDOP		;PRINT ENDING MESSAGE (AND STATS)
	CALLRX	SRDONE		;BACK TO ONLSAV OR INIT

GLOBAL	<BLKXF,CBADI,CBADO,PCNUM,RELPTR,RTNNAM,SATSYS,SWTWRD,XFRPCN>
.DSABL	LSB

.SBTTL	VERIMA - IMAGE VERIFY

;+
; VERIMA - IMAGE VERIFY MAINLINE
;
;	CALL:	CALL	VERIMA
;
;	RETURN:	C=1 - /NOERROR SPECIFIED AND A COMPARE ERROR
;		C=0 - COMPARISON COMPLETE
;		      ANY BAD COMPARES CAUSED ENTRIES IN BADVER
;-

.ENABL	LSB

VERIMA::REGSCR			;SAVE REGISTERS
	MOV	#VEMSG,RTNNAM	;ROUTINE NAME IS NOW 'VERIFY PASS'
	CALL	PRBEG		;LET EM KNOW WE'RE STARTING
	CALL	SETREL		;SET UP RELOCATION COUNT
	CALL	PTRSAV,R5	;SAVE CBADI/CBADO
	CALL	SETCH1		;SELECT INPUT CHANNEL
	MOV	(PC),VFY	;INDICATE VERIFY IN PROGRESS
	MOV	#VERBLK+4,R0	;POINT TO VERIFY INFO. BLOCK
	MOV	(R0)+,CBADI	;GET OLD CBADI
	MOV	(R0),CBADO	;GET OLD CBADO
	MOV	#1,PCNUM	;START WITH PC 1 (SKIP MFD, SEPARATE CHECK)
	MOV	SATSYS+BITS,R2	;R2 = # OF CLUSTERS IN SOURCE SATT
	DEC	R2		;ONE LESS THAN TOTAL PC'S ON DISK
	CLR	R3		;R3 = SATT BYTE POINTER
	MOV	#2,R5		;R5 = BIT IN SATT BYTE WE'RE CHECKING
10$:	CALL	XFRFIL		;FILL THE PCN TABLE WITH ALLOCATED PCN'S
	MOV	PCNRD,R0	;ANY MORE IN TABLE? (CNTCTG WANTS COUNT IN R0)
	BEQ	30$		;NO, ALL DONE
	MOV	#XFRPCN,R4	;POINT TO BEGINNING OF TABLE
	CLR	PCNRD		;GETRST RESETS IT
20$:	MOV	PCNUM,-(SP)	;SAVE OUR PCNUM
	CALL	CNTCTG		;HOW MANY STARTING FROM HERE ARE CONTIG?
	CALL	GETRST		;GET 'XFRCNT' CHUNKS OF CONTIG PC'S
	MOV	(SP)+,PCNUM	;RESTORE PCNUM
	TST	R0		;ANY LEFT?
	BNE	20$		;YES, GO DO EM
	CALL	SETCH2		;SELECT THE OUTPUT CHANNEL & RESET XFER PTR
	CALL	COMPAR		;DO COMPARE/WRITE CHECK
	BCS	40$		;NO GOOD (/NOERROR)
	CALL	SETCH1		;RESELECT THE INPUT
	BR	10$		;GO CHECK THE REST OF THE SATT

30$:	CALL	PRTDIF		;PRINT # DIFFERENCES FOUND
	MOV	TVERRV,TVERR	;VOLUME TOT IS ALSO TOT TOT
	CLR	VFY		;CLEAR VERIFY FLAG
40$:	CALL	PTRRES,R5	;RESTORE CBADI/CBADO
	MOV	#IMMSG,RTNNAM	;ROUTINE NAME IS ONCE AGAIN 'IMAGE'
	RETURN

.DSABL	LSB
GLOBAL	<PCNUM,TVERR,RTNNAM,CBADI,CBADO,SATSYS>

.SBTTL	ILLEGAL CALLS

.ENABL	LSB

REWOFF:
REWIND:
SAVD2F:
ADVEOF:	JMP	ILLCAL		;ILLEGAL CALL - GO DIE

.DSABL	LSB

