.IIF	NDF	L$$IST,	.NLIST
.IIF	NDF	L$$IST,	.DSABL	CRF
.NLIST	TTM
.LIST	TOC,MEB,BEX
.DSABL	GBL
.ENABL	REG,LC
.TITLE	COMMON	COMMON DEFINITIONS
.SBTTL
.SBTTL	COMMON	COMMON DEFINITIONS
.SBTTL
.SBTTL	EDIT:		DATE:		BY:
.SBTTL	 08	      23-FEB-82		MHB/ABC/SJK/SHG/MJG/RTW/GPK/MND/BGN
.sbttl
.sbttl
.sbttl              Copyright 1974,1990 by Digital Equipment Corporation
.sbttl                             All rights reserved.
.sbttl
.sbttl     This software is furnished under a license and may be used and copied
.sbttl     only  in  accordance  with  the  terms  of  such license and with the
.sbttl     inclusion of the above copyright notice.  This software or any copies
.sbttl     shall not be provided to any other person.   No title to or ownership
.sbttl     of the software is hereby transferred.
.sbttl
.sbttl     The information in this software is subject to change without notice.
.sbttl
.sbttl     DIGITAL  assumes  no  responsibility  for the  use, functionality, or
.sbttl     reliability of its  software  on equipment  which  is not supplied by
.sbttl     DIGITAL.
.sbttl

.SBTTL	EDIT HISTORY FOR COMMON
;+
;
;  000	RRF  06-MAR-81	CREATION - COPIED FROM V7.0-07
;  001	TG   27-Jun-80	Put Namdir stuff in
;  002	RTW  21-aug-80	Add Handler index for dmp
;  003	TG   20-DEC-80	Changed IDENT for new project
;  004	GPK  16-Feb-81	Added XPEEK and DDCU stuff
;  005	TG   16-Feb-81	Changed IDENT for new project
;  006  GPK  10-Apr-81	Add UU.TB3, add .ENABL REG,LC
;  007  MND  15-Apr-81	Removed comments to Named directories
;  008  BGN  23-Feb-82  Change sysvee to 7.2
;
;-

	.SBTTL

; GENERAL MACROS

.MACRO	UNORG
.ENDM	UNORG

.MACRO	ORG	SECT,OFFSET
.MACRO	UNORG
.LIST	BEX
.CSECT	SECT
.ENDM	UNORG
	TMPORG	SECT,<OFFSET>
.ENDM	ORG

.MACRO	DEFORG	SECT
	ORG	SECT
.DSABL	CRF
.IIF	NE	.-SECT,	.ERROR	;DEFORG NOT AT BEGINNING OF SECT
.GLOBL	SECT
.ENABL	CRF
.SBTTL	DEFINE IN LIBRARY "SECT"
.ENDM	DEFORG

.MACRO	TMPORG	SECT,OFFSET
.LIST	BEX
.CSECT	SECT
.DSABL	CRF
.IF	NDF	SECT
.ENABL	CRF
SECT:
.DSABL	CRF
.ENDC
.ENABL	CRF
.IF	NB	<OFFSET>
.	=	OFFSET+SECT
.ENDC
.ENDM	TMPORG

.MACRO	INCLUDE	LIST
.IRP	SECT,<LIST>
.GLOBL	SECT
.SBTTL	INCLUDE FROM LIBRARY "SECT"
.ENDM
.ENDM	INCLUDE

.MACRO	.DSECT	START,CREF
.IIF	B	<CREF>,	.DSABL	CRF
.IIF	DF	L$$IST,	.DSABL	CRF
.ASECT
.	=	0
.IIF	DF	L$$IST,	.ENABL	CRF
.IIF	B	<CREF>,	.ENABL	CRF
.IF	NB	<START>
.	=	START
.ENDC
.ENDM	.DSECT

.MACRO	.BSECT	START,CREF
.IIF	B	<CREF>,	.DSABL	CRF
.IIF	DF	L$$IST,	.DSABL	CRF
.ASECT
.	=	1
.IF	IDN	<START>,<HIGH>
.	=	400
.ENDC
.IIF	DF	L$$IST,	.ENABL	CRF
.IIF	B	<CREF>,	.ENABL	CRF
.ENDM	.BSECT

.MACRO	.EQUATE	SYMBOL,VALUE,CREF
.IIF	B	<CREF>,	.DSABL	CRF
.IIF	DF	L$$IST,	.DSABL	CRF
.ASECT
.IIF	DF	L$$IST,	.ENABL	CRF
.IIF	B	<CREF>,	.ENABL	CRF
.	=	VALUE
.LIST
SYMBOL:
.NLIST
.IIF	B	<CREF>,	.DSABL	CRF
.IIF	DF	L$$IST,	.DSABL	CRF
	UNORG
.IIF	DF	L$$IST,	.ENABL	CRF
.IIF	B	<CREF>,	.ENABL	CRF
.ENDM	.EQUATE

.MACRO	SWAPR	REG1,REG2
.NTYPE	$$$$$1,REG1
.NTYPE	$$$$$2,REG2
.IIF NE	<$$$$$1!$$$$$2>&70, .ERROR	; ARGUMENTS TO SWAPR MUST BE REGISTERS
	 XOR	REG1,REG2
	 XOR	REG2,REG1
	 XOR	REG1,REG2
.ENDM	SWAPR

.MACRO	.BLKW0	AMT=1,VALUE=0
.NLIST
.REPT	AMT
	.WORD	VALUE
.ENDR
.LIST
.ENDM	.BLKW0

.MACRO	.BLKB0	AMT=1,VALUE=0
.NLIST
.REPT	AMT
	.BYTE	VALUE
.ENDR
.LIST
.ENDM	.BLKB0

.MACRO	GLOBAL	LIST
.GLOBL	LIST
.ENDM	GLOBAL

.MACRO	RETURN	REG=PC
	 RTS	REG
.ENDM	RETURN

.MACRO	JMPX	LABEL
	 JMP	@#LABEL
.DSABL	CRF
.GLOBL	LABEL
.ENABL	CRF
.ENDM	JMPX

.MACRO	CALL	SUBR,REG=PC,LIST
	 JSR	REG,SUBR
.IF	NB	<LIST>
	  .WORD	LIST
.ENDC
.ENDM	CALL

.MACRO	CALLR	SUBR
	 JMP	SUBR
.ENDM	CALLR

.MACRO	CALLX	SUBR,REG=PC,LIST
	CALL	@#SUBR,REG,<LIST>
.DSABL	CRF
.GLOBL	SUBR
.ENABL	CRF
.ENDM	CALLX

.MACRO	CALLRX	SUBR
	CALLR	@#SUBR
.DSABL	CRF
.GLOBL	SUBR
.ENABL	CRF
.ENDM	CALLRX

.MACRO	.ASSUME	ARG1,COND,ARG2
.IF	COND	<ARG1>-<ARG2>
.IFF
.ERROR	ARG1 ;"COND ARG2" FAILS
.ENDC
.ENDM	.ASSUME

.MACRO	.BR	ARG
.IF	NE	.-<ARG>
.ERROR	;ROUTINE "ARG" MISPLACED
.ENDC
.ENDM	.BR

.MACRO	.CALLR	ARG
.IF	NE	.-<ARG>
.ERROR	;SUBROUTINE "ARG" MISPLACED
.ENDC
.ENDM	.CALLR

.MACRO	PUSH	LIST
.IF B	<LIST>
	 CLR	-(SP)
.IFF
.IRP	LOC,<LIST>
	 MOV	LOC,-(SP)
.ENDM	;.IRP
.ENDC	;.IF B <LIST>
.ENDM	PUSH

.MACRO	POP	LIST
.IF B	<LIST>
	 TST	(SP)+
.IFF
.IRP	LOC,<LIST>
	 MOV	 (SP)+,LOC
.ENDM	;.IRP
.ENDC	;.IF B <LIST>
.ENDM	POP

.MACRO	MOVPIC	LBL,LOC
.NTYPE	$$$$$1,LBL
.NTYPE	$$$$$2,LOC
.IF NE	$$$$$1-27
	.ERROR	; LBL IS NOT A LITERAL REFERENCE
.IFF
.IF EQ	$$$$$2-46
	 MOV	PC,LOC
	 ADD	LBL-.,(SP)
.IFF
.IIF NE	$$$$$2&70,	.ERROR	; LOC IS NOT A REGISTER OR -(SP) REFERENCE
	 MOV	PC,LOC
	 ADD	LBL-.,LOC
.ENDC	;$$$$$2-46
.ENDC	;$$$$$1-27
.ENDM	MOVPIC

; SYSTEM LEVEL DEFINITIONS

.EQUATE	SYSVEL,"08,NOCREF ;MAJOR LEVEL
.EQUATE	SYSVEE,".0,NOCREF ;MINOR LEVEL

RSTS$$ = -1

.MACRO	TITLE	NAME,DESC,NU,LEDITDATE,EDITORS
.LIST
.TITLE	NAME	DESC
.IDENT	"08.0'NU"
.SBTTL
.SBTTL	NAME	DESC
.SBTTL
.SBTTL	EDIT:		DATE:		BY:
.SBTTL	 NU	      LEDITDATE		EDITORS
.SBTTL
.NLIST
$$$VER	=	<NU'.-<NU'./10.*10.>>*400+<NU'./10.>+"00
.ENDM	TITLE

; TRANSFER CONTROL BLOCK (XRB)

; USED BY USER TO INITIATE AN I/O REQUEST
;	AND FOR MONITOR/USER DATA REQUESTS.

.DSECT	,NOCREF

XRLEN:	.BLKW		;LENGTH OF I/O BUFFER IN BYTES
XRBC:	.BLKW		;BYTE COUNT FOR TRANSFER
XRLOC:	.BLKW		;POINTER TO I/O BUFFER
XRCI:	.BLKB		;CHANNEL NUMBER TIMES 2 FOR TRANSFER
XRBLKM:	.BLKB		;RANDOM ACCESS BLOCK NUMBER (MSB)
XRBLK:	.BLKW		;RANDOM ACCESS BLOCK NUMBER (LSB)
XRTIME:	.BLKW		;WAIT TIME FOR TERMINAL INPUT
XRMOD:	.BLKW		;MODIFIERS
XRBSIZ:			;SIZE OF THE XRB IN BYTES

; FILE REQUEST QUEUE BLOCK (FIRQB)  (PRONOUNCED 'FURK-BE')

; ALL REQUESTS FOR FILE PROCESSING ARE MADE BY SETTING THE NECESSARY PARAMETERS
;	IN THE FIRQB, AND CALLING THE MONITOR WITH "CALFIP".

.DSECT	,NOCREF

	.BLKB		;RESERVED FOR RETURNED ERROR CODE
	.BLKB		;RESERVED BYTE
FQJOB:	.BLKB		;HOLDS YOUR JOB NUMBER TIMES 2
FQFUN:	.BLKB		;FUNCTION REQUESTED
FQERNO:			;ERROR MESSAGE CODE AND TEXT BEGIN
FQFIL:	.BLKB		;CHANNEL NUMBER TIMES 2
FQSIZM:	.BLKB		;FILE SIZE IN BLOCKS (MSB)
FQPPN:	.BLKW		;PROJECT-PROGRAMMER NUMBER
FQNAM1:	.BLKW	2	;2 WORD FILENAME IN RADIX 50
FQEXT:	.BLKW		;.EXT IN RADIX 50
FQSIZ:	.BLKW		;FILE SIZE IN BLOCKS (LSB)
FQNAM2:			;3 WORD NEW FILENAME.EXT IN RADIX 50
FQBUFL:	.BLKW		;DEFAULT BUFFER LENGTH
FQMODE:	.BLKW		;MODE INDICATOR
FQFLAG:	.BLKW		;OPENED FILE'S FLAG WORD AS RETURNED
FQPFLG:	.BLKB		;"PROTECTION CODE REAL" INDICATOR
FQPROT:	.BLKB		;NEW PROTECTION CODE
FQDEV:	.BLKW		;2 BYTE ASCII DEVICE NAME
FQDEVN:	.BLKB		;1 BYTE UNIT NUMBER
	.BLKB		;"UNIT NUMBER REAL" INDICATOR
FQCLUS:	.BLKW		;FILE CLUSTER SIZE FOR FILE CREATES
FQNENT:	.BLKW		;NUMBER OF ENTRIES ON DIRECTORY LOOKUP
FQBSIZ:			;SIZE OF THE FIRQB IN BYTES

; MONITOR CALLS (EMT'S)

.DSECT	+EMT,NOCREF

CALFIP:	.BLKW		;CALL FIP, WITH FIRQB LOADED
.READ:	.BLKW		;READ
.WRITE:	.BLKW		;WRITE
.CORE:	.BLKW		;CHANGE USER MEMORY SIZE
.SLEEP:	.BLKW		;SLEEP JOB FOR N SECONDS
.PEEK:	.BLKW		;PEEK AT MEMORY
.SPEC:	.BLKW		;SPECIAL FUNCTION
.TTAPE:	.BLKW		;ENTER TAPE MODE
.TTECH:	.BLKW		;ENABLE ECHO
.TTNCH:	.BLKW		;DISABLE ECHO
.TTDDT:	.BLKW		;DDT SUBMODE
.TTRST:	.BLKW		;CANCEL ^O EFFECT
.TIME:	.BLKW		;GET TIMING INFORMATION
.POSTN:	.BLKW		;GET DEVICE'S HORIZONTAL POSITION
.DATE:	.BLKW		;GET CURRENT DATE & TIME
.SET:	.BLKW		;SET KEYWORD BIT(S)
.STAT:	.BLKW		;GET MY STATISTICS
.RUN:	.BLKW		;RUN A NEW PROGRAM
.NAME:	.BLKW		;INSTALL A NEW PROGRAM NAME
.EXIT:	.BLKW		;EXIT TO DEFAULT RUN-TIME SYSTEM
.RTS:	.BLKW		;CHANGE TO A NEW RUN-TIME SYSTEM
.ERLOG:	.BLKW		;LOG AN ERROR FROM THE RUN-TIME SYSTEM
.LOGS:	.BLKW		;CHECK FOR LOGICAL DEVICES
.CLEAR:	.BLKW		;CLEAR KEYWORD BIT(S)
.MESAG:	.BLKW		;MESSAGE SEND/RECEIVE
.CCL:	.BLKW		;CCL CHECKER
.FSS:	.BLKW		;FILE STRING SCANNER
.UUO:	.BLKW		;UUO HOOK
.CHAIN:	.BLKW		;CHAIN TO A NEW PROGRAM
.PLAS:	.BLKW		;RESIDENT LIBRARY CONTROL
.RSX:	.BLKW		;ENTER RSX EMULATION
.ULOG:	.BLKW		;ASSIGN/REASSIGN/DEASSIGN DEVICE/USER LOGICAL
.XPEEK:	.BLKW		;EXTENDED BLOCK-MODE PEEK

; FIP (FIRQB @ FQFUN) FUNCTION CODES

.DSECT	,NOCREF

CLSFQ:	.BLKW		;CLOSE AN OPEN CHANNEL
OPNFQ:	.BLKW		;OPEN A CHANNEL
CREFQ:	.BLKW		;CREATE/EXTEND/OPEN A CHANNEL
DLNFQ:	.BLKW		;DELETE A FILE BY NAME
RENFQ:	.BLKW		;RENAME A FILE
DIRFQ:	.BLKW		;DIRECTORY INFORMATION
UUOFQ:	.BLKW		;PROCESS UUO
ERRFQ:	.BLKW		;GET ERROR MESSAGE TEXT
RSTFQ:	.BLKW		;RESET (CLOSE) [ALL] CHANNEL[S EXCEPT 0]
LOKFQ:	.BLKW		;LOOKUP A FILE
ASSFQ:	.BLKW		;ASSIGN A DEVICE
DEAFQ:	.BLKW		;DEASSIGN A DEVICE
DALFQ:	.BLKW		;DEASSIGN ALL DEVICES
CRTFQ:	.BLKW		;CREATE/EXTEND/OPEN A UNIQUE .TMP FILE ON DISK
CRBFQ:	.BLKW		;CREATE/EXTEND/OPEN A COMPILED IMAGE FILE ON DISK

; .PLAS (FIRQB @ FQERNO) FUNCTION CODES

.DSECT	,NOCREF

ATRFQ:	.BLKW		;ATTACH REGION (RESIDENT LIBRARY)
DTRFQ:	.BLKW		;DETACH REGION (RESIDENT LIBRARY)
CRAFQ:	.BLKW		;CREATE ADDRESS WINDOW
ELAFQ:	.BLKW		;ELIMINATE ADDRESS WINDOW
MAPFQ:	.BLKW		;MAP ADDRESS WINDOW
UMPFQ:	.BLKW		;UNMAP ADDRESS WINDOW

; .UUO/UUOFQ SUBFUNCTION CODES

.DSECT	-29.,NOCREF

UU.TB3:	.BLKB		;-29., MONITOR TABLES PART 3
UU.SPL:	.BLKB		;-28., ONE-SHOT SPOOLING REQUEST
UU.DMP:	.BLKB		;-27., ONLINE MONITOR SNAPSHOT
UU.FIL:	.BLKB		;-26., FILE UTILITY
UU.ATR:	.BLKB		;-25., READ/WRITE FILE ATTRIBUTES
UU.CCL:	.BLKB		;-24., CCL COMMAND ADD/DELETE
	.BLKB		;-23., RESERVED (BASIC-PLUS TERMINATING FILE STRING SCAN)
	.BLKB		;-22., RESERVED (BASIC-PLUS SPECIAL RUN PRIORITY)
	.BLKB		;-21., RESERVED (BASIC-PLUS PRIVILEGE DROP/RESTORE)
	.BLKB		;-20., RESERVED (BASIC-PLUS MEMORY LOCK/UNLOCK)
UU.LOG:	.BLKB		;-19., SET NUMBER OF ALLOWED LOGINS
UU.RTS:	.BLKB		;-18., RUN-TIME SYSTEM & RESIDENT LIBRARY CONTROL
UU.NAM:	.BLKB		;-17., SET FILE'S RUN-TIME SYSTEM NAME
UU.DIE:	.BLKB		;-16., SPECIAL SHUTUP LOGOUT
UU.ACT:	.BLKB		;-15., ACCOUNTING INFORMATION DUMP
UU.DAT:	.BLKB		;-14., DATE/TIME CHANGER
UU.PRI:	.BLKB		;-13., PRIORITY, RUN BURST, MAXIMUM CHANGER
UU.TB2:	.BLKB		;-12., 2ND PART OF MONITOR TABLES
UU.BCK:	.BLKB		;-11., BACKUP FILE ACCOUNTING CHANGER
	.BLKB		;-10., RESERVED (BASIC-PLUS FILE STRING SCAN)
UU.HNG:	.BLKB		; -9., HANGUP/ENABLE A DATASET
UU.FCB:	.BLKB		; -8., GET FCB/DDB INFORMATION
	.BLKB		; -7., RESERVED (BASIC-PLUS CONTROL/C TRAP ENABLE)
UU.POK:	.BLKB		; -6., POKE MONITOR MEMORY
	.BLKB		; -5., RESERVED (BASIC-PLUS SEND TO TERMINAL)
	.BLKB		; -4., RESERVED (BASIC-PLUS FORCE TO TERMINAL)
UU.TB1:	.BLKB		; -3., 1ST PART OF MONITOR TABLES
UU.NLG:	.BLKB		; -2., SET NUMBER OF ALLOWED LOGINS TO 1
UU.YLG:	.BLKB		; -1., SET NUMBER OF ALLOWED LOGINS TO MAXIMUM
UU.PAS:	.BLKB		; +0., CREATE AN ACCOUNT
UU.DLU:	.BLKB		; +1., DELETE AN ACCOUNT
UU.CLN:	.BLKB		; +2., CLEAN A DISK PACK
UU.MNT:	.BLKB		; +3., DISK PACK MOUNT/DISMOUNT
UU.LIN:	.BLKB		; +4., LOGIN
UU.BYE:	.BLKB		; +5., LOGOUT
UU.ATT:	.BLKB		; +6., ATTACH
UU.DET:	.BLKB		; +7., DETACH
UU.CHU:	.BLKB		; +8., CHANGE PASSWORD/QUOTA
UU.ERR:	.BLKB		; +9., GET ERROR MESSAGE TEXT
UU.ASS:	.BLKB		;+10., ASSIGN
UU.DEA:	.BLKB		;+11., DEASSIGN
UU.DAL:	.BLKB		;+12., DEASSIGN ALL
UU.ZER:	.BLKB		;+13., ZERO DEVICE
UU.RAD:	.BLKB		;+14., READ ACCOUNTING INFORMATION
UU.DIR:	.BLKB		;+15., GET DIRECTORY INFORMATION
UU.TRM:	.BLKB		;+16., SET TERMINAL CHARACTERISTICS
UU.LOK:	.BLKB		;+17., WILDCARD DIRECTORY LOOKUP
	.BLKB		;+18., RESERVED (BASIC-PLUS OLD MESSAGE SEND/RECEIVE)
UU.CHE:	.BLKB		;+19., CACHE ENABLE/DISABLE
UU.CNV:	.BLKB		;+20., CONVERT DATE/TIME TO ASCII
UU.SLN:	.BLKB		;+21., SET/CLEAR SYSTEM-WIDE LOGICAL NAMES
	.BLKB		;+22., RESERVED (BASIC-PLUS MESSAGE SEND/RECEIVE)
UU.SWP:	.BLKB		;+23., ADD/REMOVE SWAP, OVERLAY, ERROR MSG FILES
UU.JOB:	.BLKB		;+24., JOB CREATION
UU.PPN:	.BLKB		;+25., WILDCARD PPN LOOKUP
UU.SYS:	.BLKB		;+26., RETURN JOB STATUS INFORMATION
UU.KMC:	.BLKB		;+27., CONNECT KMC-11 TO ANOTHER DEVICE
UU.NME:	.BLKB		;+28., RESERVED

; I/O RELATED DEFINITIONS

; HANDLER INDEXES

.DSECT	,NOCREF

DSKHND:	.BLKW		;DISK(S)
TTYHND:	.BLKW		;TERMINAL(S)
DTAHND:	.BLKW		;DECTAPE
LPTHND:	.BLKW		;LINE PRINTER(S)
PTRHND:	.BLKW		;PAPER TAPE READER
PTPHND:	.BLKW		;PAPER TAPE PUNCH
CDRHND:	.BLKW		;CARD READER
MTAHND:	.BLKW		;MAGTAPE(S)
PKBHND:	.BLKW		;PSEUDO-KEYBOARDS
RXDHND:	.BLKW		;FLOPPY DISK
RJEHND:	.BLKW		;2780 RJE
NULHND:	.BLKW		;NULL DEVICE
DMCHND:	.BLKW		;DMC11 DEVICE
AUDHND:	.BLKW		;AUTO-DIAL DEVICE (DN11)
PLTHND:	.BLKW		;X-Y PLOTTER
DT2HND:	.BLKW		;DECTAPE II
KMCHND:	.BLKW		;KMC11 MICROPROCESSOR
IBMHND:	.BLKW		;IBM INTERCONNECT
MBXHND:	.BLKW		;MAILBOX (RESERVED FOR VAX-11)
DMPHND:	.BLKW		;DMP11/DMV11 DEVICE

.BSECT	HIGH,NOCREF	;FLAG BITS FOR AN OPEN FILE

DDNFS:	.BLKB	.	;FILE(DEVICE) IS NON-FILE-STRUCTURED
DDRLO:	.BLKB	.	;FILE(DEVICE) IS READ-LOCKED
DDWLO:	.BLKB	.	;FILE(DEVICE) IS WRITE-LOCKED
FLGPOS:	.BLKB	.	;FILE(DEVICE) KEEPS ITS OWN POSITION
FLGMOD:	.BLKB	.	;FILE(DEVICE) ACCEPTS MODIFIERS
FLGFRC:	.BLKB	.	;FILE(DEVICE) IS BYTE ORIENTED (NON-BLOCKED)
FLGKB:	.BLKB	.	;FILE(DEVICE) IS INTERACTIVE (KEYBOARD TYPE)
FLGRND:	.BLKB	.	;FILE(DEVICE) IS RANDOM ACCESS

; JOB UNIQUE LOW MEMORY LAYOUT

.DSECT	,NOCREF

	.BLKW	30	;JOB CONTROLLED
	.BLKW	15	;RESERVED FOR MONITOR CONTEXT USE
	.BLKW	30	;RESERVED FOR MONITOR FPP CONTEXT USE
	.BLKW	103	;JOB'S SP STACK AREA
USRSP:			;DEFAULT (INITIAL) JOB SP STACK SETTING
KEY:	.BLKW		;KEYWORD OF JOB'S CURRENT STATUS
FIRQB:	.BLKB	FQBSIZ	;FILE REQUEST QUEUE BLOCK
XRB:	.BLKB	XRBSIZ	;TRANSFER CONTROL BLOCK
CORCMN:	.BLKB	200	;CCL LINE COMMON
	.BLKW	26	;JOB CONTROLLED
USRPPN:	.BLKW		;USER'S ASSIGNABLE PPN
USRPRT:	.BLKW		;USER'S ASSIGNABLE PROTECTION CODE
USRLOG:	.BLKW	4*4	;USER'S LOGICAL DEVICE TABLE
NSTORG:			;END OF LOW MEMORY FIXED LAYOUT

.BSECT	HIGH,NOCREF	;KEYWORD STATUS BITS

JFSPRI:	.BLKB	.	;JOB IS AT SPECIAL RUN PRIORITY
JFFPP:	.BLKB	.	;SAVE/RESTORE ASYNC FPP CONTEXT
JFPRIV:	.BLKB	.	;JOB HAS PERMANENT PRIV'S
JFSYS:	.BLKB	.	;JOB HAS TEMPORARY PRIV'S ACTIVE
JFNOPR:	.BLKB	.	;JOB IS NOT YET LOGGED IN
JFBIG:	.BLKB	.	;JOB CAN EXCEED ITS PRIVATE MEMORY MAX
JFLOCK:	.BLKB	.	;JOB IS NOT TO BE SWAPPED

; RUN-TIME SYSTEM UNIQUE HIGH MEMORY LAYOUT

.DSECT	177776,NOCREF

	.BLKW	-1	;UNAVAILABLE WORD
P.SIZE:	.BLKW	-1	;MAX SIZE FOR A USER'S IMAGE IN K
P.2CC:	.BLKW	-1	;ADDRESS OF 2 QUICK CONTROL/C'S
P.CC:	.BLKW	-1	; "      "  CONTROL/C TRAPPING
P.FPP:	.BLKW	-1	; "      "  @244 TRAPS (FPP)
P.TRAP:	.BLKW	-1	; "      "  @34 TRAPS (TRAP)
P.EMT:	.BLKW	-1	; "      "  @30 TRAPS (NON-MONITOR CALL)
P.IOT:	.BLKW	-1	; "      "  @20 TRAPS (IOT)
P.BPT:	.BLKW	-1	; "      "  @14 TRAPS (ODT OR T-BIT)
P.BAD:	.BLKW	-1	; "      "  BAD ERROR RECOVERY
P.RUN:	.BLKW	-1	; "      "  ENTRY TO RUN A NEW PROGRAM
P.NEW:	.BLKW	-1	; "      "  A NEW ENTRY
P.STRT:	.BLKW	-1	; "      "  SYSTEM START
P.CRAS:	.BLKW	-1	; "      "  CRASH RECOVERY
P.FIS:	.BLKW	-1	; "      "  @244 TRAPS (FIS)
P.MSIZ:	.BLKW	-1	;MIN SIZE FOR A USER'S IMAGE IN K
P.ISIZ:	.BLKW	-1	;(HISTORICAL...)
P.DEXT:	.BLKW	-1	;DEFAULT EXECUTABLE EXTENSION (RAD50)
P.FLAG:	.BLKW	-1	;FLAG WORD DESCRIBING THE RTS
	.BLKW		;DUMMY TO GET BACK TO THE
P.OFF:			; STARTING ADDRESS OF THESE "VECTORS"

.BSECT	HIGH,NOCREF	;FLAG BITS IN P.FLAG WORD

PF.KBM:	.BLKB	.	;RTS IS A KEYBOARD MONITOR
PF.1US:	.BLKB	.	;RTS ALLOWS ONLY 1 USER (NO SHARING)
PF.RW:	.BLKB	.	;RTS WANTS TO RUN MAPPED READ/WRITE
PF.NER:	.BLKB	.	;RTS DOESN'T WANT ITS ERRORS LOGGED
PF.REM:	.BLKB	.	;UNCONDITIONAL REMOVE FROM MEMORY ON EXIT
PF.CSZ:	.BLKB	.	;RUN SIZE IS COMPUTED FROM FILE SIZE
PF.SLA:	.BLKB	.	;(SAVE THE LOAD ADDRESS, M.PHYA)
PF.EMT:	.BLKB	.	;RTS WANTS EMT CODE IN LOW BYTE AS A PREFIX

; NOTES ON THE ABOVE PSEUDO-VECTOR CELLS:
;	THE FOLLOWING ARE ENTRY POINTS INTO THE RTS:
;		P.BAD,	P.RUN,	P.NEW,	P.STRT,	P.CRAS
;	THE FOLLOWING ARE SYNCHRONOUS TRAPS INTO THE RTS:
;		P.TRAP,	P.EMT,	P.IOT,	P.BPT,	P.FIS
;	THE FOLLOWING ARE ASYNCHRONOUS TRAPS INTO THE RTS:
;		P.2CC,	P.CC,	P.FPP,	P.BAD
;	THE FOLLOWING ARE FOR INFORMATION ONLY:
;		P.SIZE,	P.MSIZ,	P.ISIZ,	P.DEXT,	P.FLAG

.IIF	NDF	L$$IST,	.ENABL	CRF
.IIF	NDF	L$$IST,	.LIST

