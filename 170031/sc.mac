TITLE	SC,<SCAN - IO STRING INTERP>,08,13-MAY-86,TPH/MHB/JDM

;                   Copyright 1974,1990 by Digital Equipment Corporation
;                                  All rights reserved.
;
;          This software is furnished under a license and may be used and copied
;          only  in  accordance  with  the  terms  of  such license and with the
;          inclusion of the above copyright notice.  This software or any copies
;          shall not be provided to any other person.   No title to or ownership
;          of the software is hereby transferred.
;
;          The information in this software is subject to change without notice.
;
;          DIGITAL  assumes  no  responsibility  for the  use, functionality, or
;          reliability of its  software  on equipment  which  is not supplied by
;          DIGITAL.

; LIST OF FLAGS

SC$CLU	=	     1	;/CLUSTERSIZE:N SEEN
SC$MOD	=	     2	;/MODE:N SEEN
SC$SIZ	=	     4	;/FILESIZE:N SEEN
SC$POS	=	    10	;/POSITION:N SEEN
;	=	    20	;UNUSED
;	=	    40	;UNUSED
;	=	   100	;UNUSED
;	=	   200	;UNUSED
SCFIL	=	   400	;FILE NAME SEEN
SCEXT	=	  1000	;. FOR EXTENSION SEEN
SCPPN	=	  2000	;PPN SEEN
SCPROT	=	  4000	;< FOR PROTECTION SEEN
SCDEV	=	 10000	;: FOR DEVICE SEEN
SCLOG	=	 20000	;A LOGICAL DEVICE WAS USED
SCQUES	=	 40000	;A "?" HAS OCCURED IN THIS FIELD
SCWILD	=	100000	;SOMETHING WILD HAS BEEN ENCOUNTERED

S$FIL	=	     1	;A FILE NAME EXISTS
S$FILS	=	     2	;  THE FILE NAME IS "*"
S$FILQ	=	     4	;  THE FILE NAME CONTAINS "?" (ONE OR MORE)
S$EXT	=	    10	;A . FOR THE EXTENSION EXISTS
S$EXTE	=	    20	;  THE EXTENSION ALSO EXISTS
S$EXTS	=	    40	;    THE EXTENSION IS "*"
S$EXTQ	=	   100	;    THE EXTENSION CONTAINS "?" (ONE OR MORE)
S$PPN	=	   200	;AN ACCOUNT NUMBER EXISTS
S$PPNJ	=	   400	;  THE PROJECT NUMBER IS "*"
S$PPNG	=	  1000	;  THE PROGRAMMER NUMBER IS "*"
S$PROT	=	  2000	;A PROTECTION CODE EXISTS
S$DPRO	=	  4000	;(THE DEFAULT PROTECTION CODE WAS USED)
S$DEV	=	 10000	;A : FOR THE DEVICE NAME EXISTS
S$DEVE	=	 20000	;  THE DEVICE NAME ALSO EXISTS
S$DEVL	=	 40000	;    THE DEVICE NAME WAS A LOGICAL NAME
S$DEVX	=	100000	;      THE LOGICAL DEVICE IS NOT NOW VALID

;CALL:
;	R1	R1 STACK
;	R2	CHANNEL*2
;	R4	SMALL BUFFER POINTER
;	JSR	PC,SCAN
;RTRN:
;	R1	R1 STACK, STRING POPPED OFF
;	R4	SMALL BUFFER WITH JOB,FIL,PPN,NAM,EXT,PROT,DEV,DEVN
;	R0,R2,R5	PRESERVED
;	R1	SPDA

	DEFORG	SC

.ENABL	LSB

OPNRR1::JSR	PC,GETBUF	;GET THE FIRQB
	MOV	#PSTJS,-(SP)	;POP THE STRING LATER
	BR	10$		;JOIN UP

OPNR20::JSR	PC,GETBUF	;USUAL SYSTEM ENTRY
SCAN::	MOV	#PSTJS,-(SP)	;POP STRING WHEN SCANNED
CSI::	CLR	-(R1)		;FILE SIZE = 0
	CLR	-(R1)		;MODE = 0
	CLR	-(R1)		;CLUSTER SIZE = 0
10$:	ADD	#6,R1		;PRE-POP THE 3 PARAMETERS
	JSR	R5,SAVREG	;REALLY SAVE THEM
	MOV	#XRB+XRBSIZ,R5	;GET XRB POINTER
	MOV	R5,R0		;SAVE A COPY OF IT FOR LATER
20$:	CLR	-(R5)		;CLEAR XRB
	CMP	R5,#XRB+XRLOC	;UP TO STR POINTER YET?
	BHI	20$		;NO
	MOV	R1,(R5)		;YES, SET R1 STACK AND
	ADD	PNTR(R1),(R5)	; FORM STRING POINTER
	MOV	LENGTH(R1),-(R5);SET STRING'S LENGTH
	CMP	14(SP),#UUOCSI	;SPECIAL SYS() CALL?
	BNE	30$		;NO
	ADD	#2,XRLOC-XRBC(R5) ;YES, SKIP THE FIRST
	SUB	#2,(R5)		; BYTES
30$:	CMP	(R4)+,(R4)+	;INDEX TO FQFIL
	MOVB	R2,(R4)+	;SET UP FQFIL
	CLRB	(R4)+		;CLEAR FQSIZM CORRECTLY
	CLR	(R4)+		;CLEAR FQPPN
	CLR	(R4)+		;CLEAR FQNAM1
	CLR	(R4)+		;CLEAR FQNAM1+2
	CLR	(R4)+		;CLEAR FQEXT
	MOV	-(R1),(R4)+	;SET FQSIZ CORRECTLY
	TST	(R4)+		;SKIP TO FQMODE
	MOV	-(R1),(R4)+	;SET FQMODE CORRECTLY
	TST	(R4)+		;SKIP TO FQPROT-1
	CLR	(R4)+		;CLEAR FQPROT-1
	CLR	(R4)+		;CLEAR FQDEV
	CLR	(R4)+		;CLEAR FQDEVN
	MOV	-(R1),(R4)+	;SET FQCLUS CORRECTLY

GLOBAL	<GETBUF,PSTJS,SAVREG,UUOCSI>

	MOV	(R5),-(R5)	;COPY LENGTH TO XRLEN
	BEQ	40$		;LENGTH=0
	.FSS			;ELSE CALL THE MONITOR
	TSTB	IOSTS		;ERROR?
	BNE	100$		;YES, DIE
40$:	MOV	SPDA,R2		;GET DATA AREA POINTER
	MOV	-(R0),STATUS(R2) ;SET GENERIC DEVICE FLAGS FROM XRB+14
	MOV	-(R0),R3	;GET SECOND SET OF FLAGS FROM XRB+12
	MOV	-(R0),R5	;GET FIRST SET OF FLAGS FROM XRB+10
	BIT	R5,#S$DPRO!S$PROT ;PROTECTION CODE RETURNED?
	BNE	50$		;YES
	MOV	USRPRT,FQPROT-1-FQNENT(R4) ;NO, SET DEFAULT
	BEQ	50$		;NO DEFAULT
	BIS	#S$DPRO,R5	;DEFAULT EXISTS, SAY WE DID IT
50$:	MOV	R5,FSSFLG	;SAVE THE .FSS FLAGS
	CMP	14(SP),#UUOCSI	;SPECIAL SYS() CALL?
	BNE	60$		;NO
	MOVB	FQSIZM-FQNENT(R4),FQFUN-FQNENT(R4) ;MOVE SIZE MSB TO HERE
	MOV	(R4),FQFLAG-FQNENT(R4) ;MOVE /POS:N TO HERE
	MOV	R5,(R4)		;RETURN FLAG BITS HERE
	MOV	-(R4),FQNAM2-FQCLUS(R4) ;RE-SAVE CLUSTER SIZE
	MOV	R3,(R4)		; AND SAVE SECOND SET OF FLAGS
	CMPB	6(SP),#-23.	;SPECIAL TERMINATING CALL?
	BNE	70$		;NO, ALL MUST BE PARSED
	MOV	XRB+XRBC,RECOUN(R2) ;SET # NOT SCANNED
	BR	80$		;NOW EXIT

60$:	TST	R3		;WILD AND/OR UNTRAN LOGICAL?
	BMI	90$		;YES, ERROR
70$:	TST	XRB+XRBC	;ANYTHING UNSCANNED?
	BNE	90$		;YES, ERROR
80$:	JSR	R5,RESREG	;RESTORE REGISTERS
	RTS	PC		; AND EXIT

90$:	SETERR	BADNAM		;SAY NAME ILLEGAL
	TST	R5		;REALLY UNTRAN LOGICAL?
	BPL	100$		;NO
	SETERR	NODEVC		;YES, SO SAY IT
100$:	IOTERR	!FATAL		;AND DIE

.DSABL	LSB

GLOBAL	<UUOCSI,RESREG>

.END

