TITLE	DMPHIS,<PC HISTOGRAM DUMP>,0A,10-MAY-91,MHB/TG/KPH

;
;		COPYRIGHT (c) 1974, 1991 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

.SBTTL	EDIT HISTORY FOR DMPHIS
;+
;
;  000  RRF  06-MAR-81	CREATION - COPIED FROM V7.0-07
;  001	TG   01-Jan-81	Created from DUMPER
;  002	KPH  23-Mar-84	Dump HISTOG.LIB resident library
;
;-


	.SBTTL	DEFINITIONS

	.ENABL	LC
;+
;	This program dumps the contents of the resident library
;	HISTOG.LIB into a file
;-


	.MCALL	..V2.., .CSISPC, .WRITW, .CLOSE, .PRINT, .ENTER, .EXIT

	..V2..

;
;	Handy defs
;


	CHN=0		;CHANNEL NUMBER TO USE
	WINAPR=5	;Map windows through APR5			;002
	.PRIV=104377	;Use a RSTS EMT					;002
	.CLRFQB=104370	;Clear out the FIRQB				;002

	.SBTTL	MAINLINE CODE


	.NLIST 	MEB

	DEFORG	DMPHIS

DMPHIS::.CLRFQB			;Clear out the FIRQB			;002
.ASSUME	ATRFQ	EQ	0						;002
	MOV	#^RHIS,@#FIRQB+12 ;Set up region name			;002
	MOV	#^RTOG,@#FIRQB+14 ; as 'HISTOG'				;002
	INC	@#FIRQB+FQMODE	;Request read-only access		;002
	.PRIV,	.PLAS		;Now attach to the region		;002
	TSTB	@#FIRQB		;Error?					;002
	BNE	60$		;Yes, so get out			;002

	MOV	@#FIRQB+FQPPN,R1 ;R1 = Resident library ID		;002
	MOV	@#FIRQB+FQNAM1,R2 ;R2 = Size of library in slivers	;002
	ASH	#-5,R2		;R2 = Size of library in K-words	;002

	.CLRFQB			;Clear out the FIRQB			;002
	MOVB	#CRAFQ,@#FIRQB+FQFIL ;Function is create address window	;002
	MOVB	#WINAPR,@#FIRQB+7 ;Set up the APR value			;002
	MOV	#40,@#FIRQB+12	;Window size is 1K words		;002
	.PRIV,	.PLAS		;Now create the window			;002
	TSTB	@#FIRQB		;Did it work?				;002
	BNE	70$		;No, so get out				;002

	MOVB	@#FIRQB+FQPPN,R3 ;R3 = Window ID of created window	;002

; At this point:							;002
;									;002
;	R1 = Library ID							;002
;	R2 = Size of library in K-words					;002
;	R3 = Window ID of created window				;002

	.CSISPC	#OUTPUT,#EXT	;GET FILE TO DUMP INTO
	BCS	20$		;WHOOPS, BAD FILE NAME
	TST	INFNME		;ANYTHING THERE ?
	BEQ	DMPHIS		;TRY AGAIN
	TST	INFEXT		;ANY EXT ???
	BNE	10$		;YEP
	MOV	#^RDMP,INFEXT	;SET IN THE DEFAULT
10$:	MOV	R2,R4		;R4 = Size of library in K-words	;002
	ASL	R4		;Now R4 has blocks/2 needed
	ASL	R4		;Now R4 has blocks needed for area
	.ENTER	#AREA,#CHN,#INFIL1,R4 ;OPEN THE FILE PLEASE		;002
	BCS	30$		;TOO BAD (OPEN)
	CLR	R5		;Set up the starting block number to write ;002
	CLR	R4		;And the starting .PLAS offset		;002

11$:	.CLRFQB			;Clear out the FIRQB			;002
	MOVB	#MAPFQ,@#FIRQB+FQFIL ;Function is MAP a window		;002
	MOVB	R3,@#FIRQB+FQPPN ;Set up the window ID			;002
	MOV	R1,@#FIRQB+FQEXT ;Set up the library ID			;002
	MOV	R4,@#FIRQB+FQSIZ ;And the offset into the library	;002
	.PRIV,	.PLAS		;Now map the library			;002
	TSTB	@#FIRQB		;Did it work?				;002
	BNE	80$		;No, so get out				;002

12$:	.WRITW	#AREA,#CHN,#<20000*WINAPR>,#1024.,R5 ;NOW WRITE IT OUT	;002
	BCS	40$		;WHOOPS, BAD WRITE
	ADD	#4,R5		;Update block number to write		;002
	ADD	#40,R4		;And update mapping offset		;002
	SOB	R2,11$		;Now loop for all the K-words to write	;002

	.CLOSE	#CHN		;NOW CLOSE THE FILE
	BCS	50$		;WHOOPS, BAD CLOSE
	.PRINT	#DONE		;ALL DONE, SO SAY SO
	BR	GETOUT		;AND GO AWAY

20$:	.PRINT	#CSIERR		;TELL THE WORLD
	BR	GETOUT		;AND GO AWAY

30$:	.PRINT	#OPNERR		;SAY OPEN ERROR
	BR	GETOUT		;AND GET OUT

40$:	.PRINT	#WRTERR		;SAY WRITE ERROR
	BR	GETOUT		;AND GET OUT

50$:	.PRINT	#CLSERR		;SAY CLOSE ERROR
	BR	GETOUT		;AND GET OUT				;002

60$:	.PRINT	#ERRATT		;Say attach error			;002
	BR	GETOUT		;And get out				;002

70$:	.PRINT	#ERRCRA		;Say create address window error	;002
	BR	GETOUT		;And get out				;002

80$:	.PRINT	#ERRMAP		;Say error mapping window		;002
	.BR	GETOUT		;And get out				;002

GETOUT:	.EXIT			;EXIT


	.SBTTL	DATA AREA


	.NLIST	BEX

DONE:	.ASCIZ	"Data dump completed"
CSIERR:	.ASCIZ	"Bad file name"
OPNERR:	.ASCIZ	"Open error opening output file"
WRTERR:	.ASCIZ	"Write error while dumping data to file"
CLSERR:	.ASCIZ	"Close error while closing data file"
ERRATT:	.ASCIZ	"Error attaching to resident library"			;002
ERRCRA:	.ASCIZ	"Error creating address window"				;002
ERRMAP:	.ASCIZ	"Error mapping address window"				;002

	.EVEN
EXT:	.RAD50	/DMP........./	;DEFAULT EXTENSION (IN,OUT1,OUT2,OUT3)
OUTPUT:	.BLKW0	5.*3.		;THREE OUTPUT SPECS
INFIL1:	.BLKW0			;DEVICE NAME
INFNME:	.BLKW0	2.		;FILE NAME
INFEXT:	.BLKW0			;EXT
INFIL2:	.BLKW0	4.*5.		;FIVE MORE AFTER THE FIRST
AREA:	.BLKW0	5.		;FOR ARGUMENT BLOCK

	.LIST	BEX

	.END	DMPHIS
