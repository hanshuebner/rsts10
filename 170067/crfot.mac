	.TITLE	CRFOT
	.IDENT	/01/

;
; COPYRIGHT   1974,  DIGITAL  EQUIPMENT  CORP.,  MAYNARD,   MASS.
; COPYRIGHT   1975,  DIGITAL  EQUIPMENT  CORP.,  MAYNARD,   MASS.
;
; THIS SOFTWARE IS FURNISHED TO PURCHASER UNDER A LICENSE FOR USE
; ON A SINGLE COMPUTER SYSTEM  AND  CAN BE COPIED (WITH INCLUSION
; OF DEC'S COPYRIGHT NOTICE)  ONLY FOR USE IN SUCH SYSTEM, EXCEPT
; AS MAY OTHERWISE BE PROVIDED IN WRITING BY DEC.
;
; THE INFORMATION IN  THIS DOCUMENT IS  SUBJECT TO CHANGE WITHOUT
; NOTICE AND SHOULD NOT  BE CONSTRUED AS  A COMMITMENT BY DIGITAL
; EQUIPMENT CORPORATION.
;
; DEC ASSUMES  NO  RESPONSIBILITY  FOR  THE  USE  OR  RELIABILITY
; OF ITS  SOFTWARE  ON  EQUIPMENT  WHICH IS NOT  SUPPLIED BY DEC.
;
; VERSION 01
;
; C. MONIA 02-JAN-75
;
; MODIFICATIONS:
;
;		CM002 -- 31-OCT-75
;
;			 ADD MACRO CREF
;
;
; CREF RECORD OUTPUT SUBROUTINES
;
; MACRO LIBRARY CALLS
;

	.MCALL	PUT$S

;+
; **-$CRFSH-FLUSH CREF RECORD BUFFER
;
; THIS SUBROUTINE IS CALLED TO UNCONDITIONALLY OUTPUT
; A RECORD TO THE CREF OUTPUT FILE. A CARRIAGE RETURN-LINEFEED
; IS APPENDED TO THE RECORD IF IMPLIED CARRIAGE CONTROL IS
; NOT IN EFFECT (IE. FD.CR RESET).
;
;
; INPUTS:
;
;	R0=POINTER TO NEXT BYTE IN RECORD BUFFER
;	$CRODB=OUTPUT FILE DPB
;
; OUTPUTS:
;
;	THE CURRENT RECORD IS WRITTEN
;	R0=POINTER TO NEXT AVAILABLE BYTE
;	$BYTCT IS SET TO ZERO
;	$NBYTE POINTS TO THE FIRST BYTE IN THE FCS RECORD BUFFER
;
; R2 - R5 ARE PRESERVED
;
;-

$CRFSH::			;
	MOV	R0,R1		; COPY BYTE POINTER
	MOV	#$CRODB,R0	; POINT TO FDB FOR OUTPUT FILE
	BITB	#FD.CR,F.RATT(R0) ; IMPLIED CARRIAGE CONTROL?
	BNE	10$		; IF NE YES
	MOVB	#CR,(R1)+	; INSERT A CARRIAGE RETURN
	MOVB	#LF,(R1)+	; INSERT A LINE-FEED
10$:				;
	SUB	F.NRBD+2(R0),R1	; COMPUTE BYTE COUNT
	PUT$S	,,R1		; WRITE THE OUTPUT RECORD
	BCS	20$		; IF C/S I/O ERROR
	CLR	$BYTCT		; RESET BYTE COUNT
	MOV	F.NRBD+2(R0),R0	; GET ADDRESS OF NEXT BYTE
	MOV	R0,$NBYTE	; SAVE ADDRESS
	RETURN			;


;
; I/O ERROR ON OUTPUT
;

20$:				;
	MOV	R0,R2		; COPY FDB ADDRESS
	ADD	#<F.FNB+N.FNAM>,R2 ; POINT TO FILENAME
	ERROR$	E$R3,S$V2	; FATAL ERROR, NO RETURN

;+
; **-$CRFOT-CONDITIONALLY WRITE AN OUTPUT RECORD
;
; THIS SUBROUTINE IS CALLED TO CONDITIONALLY WRITE
; A RECORD INTO THE CREF OUTPUT FILE. THE RECORD
; IS OUTPUT WHENEVER INSUFFICIENT ROOM EXITS TO THE RIGHT
; OF THE CARRIAGE POSITION TO PRINT R$ECMN NUMBER OF BYTES
;
; INPUTS:
;
;	R0=ADDRESS OF NEXT OUTPUT BYTE
;	R1=LENGTH OF STRING
;	$BYTCT=CURRENT HORIZONTAL POSITION
;	$CRFIF=CREF INPUT FILE HEADER FLAGS WORD
;	$CRODB=CREF OUTPUT FDB
;
; OUTPUTS:
;
;	C/SET: OUTPUT RECORD NOT WRITTEN
;
;	R0=ADDRESS OF NEXT BYTE
;	$BYTCT=UPDATED HORIZONTAL POSITION
;	$NBYTE=UPDATED BYTE POINTER
;
;	C/CLEAR: OUTPUT RECORD WRITTEN
;
;	OUTPUTS AS DECRIBED ABOVE
;
;
; R2 - R5 ARE PRESERVED
;-

$CRFOT::			;
	MOV	R0,$NBYTE	; SAVE ADDRESS OF NEXT BYTE
	MOV	R1,-(SP)	; SAVE BYTE COUNT
	SUB	R1,R0		; BACK UP THE POINTER
	MOV	#$BYTCT,R1	; GET ADDRESS OF POSITION COUNT
10$:				;
	INC	(R1)		; INCREMENT BYTE COUNT
	CMPB	(R0)+,#HT	; CHARACTER = TAB
	BNE	20$		; IF NE NO
	ADD	#7,(R1)		; BUMP HORIZONTAL POSITION
	BIC	#7,(R1)		; ROUND TO NEXT TAB STOP
20$:				;
	DEC	(SP)		; DECREMENT BYTE COUNT
	BNE	10$		; IF NE CONTINUE
	MOV	#<<$RECBE-$RECBF>-R$ECMN>,-(SP) ; GET RECORD SIZE
	BIT	#HD$SH,$CRFIF	; SHORT LINE FORMAT REQUESTED?
	BEQ	25$		; IF EQ NO
	MOV	#S$HFMT-R$ECMN,(SP) ; SET SHORT LINE SIZE
25$:				;
	CMP	(R1),(SP)+	; TIME TO OUTPUT A LINE?
	BLO	30$		; IF LO NO
	CALL	$CRFSH		; OUTPUT THE RECORD
30$:				;
	MOV	(SP)+,R1	; RESTORE BYTE COUNT
	RETURN			;

	.END
