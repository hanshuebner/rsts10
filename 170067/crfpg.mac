	.TITLE	CRFPG
	.IDENT	/01/

;
; COPYRIGHT   1974,  DIGITAL  EQUIPMENT  CORP.,  MAYNARD,   MASS.
; COPYRIGHT   1975,  DIGITAL  EQUIPMENT  CORP.,   MAYNARD,   MASS.
;
; THIS SOFTWARE IS FURNISHED TO PURCHASER UNDER A LICENSE FOR USE
; ON A SINGLE COMPUTER SYSTEM  AND  CAN BE COPIED (WITH INCLUSION
; OF DEC'S COPYRIGHT NOTICE)  ONLY FOR USE IN SUCH SYSTEM, EXCEPT
; AS MAY OTHERWISE BE PROVIDED IN WRITING BY DEC.
;
; THE INFORMATION IN  THIS DOCUMENT IS  SUBJECT TO CHANGE WITHOUT
; NOTICE AND SHOULD NOT  BE CONSTRUED AS  A COMMITMENT BY DIGITAL
; EQUIPMENT CORPORATION.
;
; DEC ASSUMES  NO  RESPONSIBILITY  FOR  THE  USE  OR  RELIABILITY
; OF ITS  SOFTWARE  ON  EQUIPMENT  WHICH IS NOT  SUPPLIED BY DEC.
;
; VERSION 00
;
; C. MONIA 30-DEC-74
;
; OUTPUT CREF PAGE HEADER
; MODIFICATIONS:
;
;		CM002 -- 31-OCT-75
;
;			 ADD MACRO CREF
;
;
; LOCAL DATA
;
; CREF PAGE HEADER TEMPLATES
;

PGHD0:	.ASCIZ	/%F%3R  CREATED BY  %2R ON %Y AT %2Z	PAGE %D%N/
PGHD1:	.ASCIZ	/%56<%VA CROSS REFERENCE%40>%2R %2R%N/
PGHD2:	.ASCIZ	/%VA%N/

	.EVEN

;
; PARAMETER LIST FOR PAGE HEADER
;

PGPRM:	.BLKW	23.		;

;+
; **-$CRFPG-FORMAT AND OUTPUT A CREF PAGE HEADER
;
; THIS SUBROUTINE IS CALLED TO FORMAT AND OUTPUT
; PORTIONS OF THE CREF PAGE HEADER TO THE OUTPUT
; FILE RECORD BUFFER. THE HEADER CONSISTS OF THREE
; SECTIONS:
;
;	. NEW PAGE AND TABLE NAME
;	. CREF IDENTIFICATION
;	. TABLE HEADING
;
; AFTER EACH SECTION IS FORMATTED A CO-ROUTINE CALL
; IS MADE TO THE CALLER. WHEN THE LAST SECTION IS COM-
; PLETE, THE ROUTINE EXITS VIA A RETURN.
;
; INPUTS:
;
;	ON FIRST CALL:
;
;
;	R4=ADDRESS OF TABLE DESCRIPTOR
;	R5=ADDRESS OF FORMAT CONTROL TABLE ENTRY
;	$CRFDT=CREATION DATE OF CREF FILE
;	$CRODB=CREF OUTPUT FILE FDB
;	$PAGNO=CURRENT PAGE NUMBER
;	$NBYTE=ADDRESS OF NEXT BYTE IN OUTPUT RECORD
;
;	ON SUBSEQUENT CALLS:
;
;	R0=ADDRESS OF NEXT BYTE IN OUTPUT RECORD
;	R2=ADDRESS OF NEXT PARAMETER LIST ENTRY
;
; OUTPUTS:
;
;	R0=ADDRESS OF NEXT BYTE IN OUTPUT RECORD
;	R1=LENGTH OF OUTPUT STRING
;	R2=ADDRESS OF NEXT PARAMETER LIST ENTRY
;
;-

$CRFPG::			;
	MOV	#PGPRM,R2	; GET ADDRESS OF PAGE  HEADER PARAMETER LIST
	MOV	R2,-(SP)	; SAVE  ADDRESS OF LIST
	MOV	#<$CRODB+F.FNB+N.FNAM>,R0 ; GET ADDRESS OF FILENAME
	MOV	(R0)+,(R2)+	; COPY FILE NAME
	MOV	(R0)+,(R2)+	; ...
	MOV	(R0)+,(R2)+	; ...
	MOV	F$MCNM(R5),(R2)+ ; COPY CREF TASK NAME
	MOV	F$MCNM+2(R5),(R2)+ ; ...
	MOV	#$CRFDT,R0	; POINT TO DATE
	MOV	#5,R1		; SET WORD COUNT
10$:				;
	MOV	(R0)+,(R2)+	; GET DATE
	SOB	R1,10$		; ...
	MOV	$PAGNO,(R2)+	; GET PAGE NUMBER
	MOV	T$DNAM(R4),(R2)+ ; SET TABLE NAME
	MOV	T$DNAM+2(R4),(R2)+ ; ...
	MOV	#$CRIDN,R0	; GET ADDRESS OF CREF I/D
	MOV	(R0)+,(R2)+	; COPY I/D
	MOV	(R0)+,(R2)+	; ...
	MOV	(R0)+,(R2)+	; ...
	MOV	(R0)+,(R2)+	; ...
	MOV	T$DPGH(R4),(R2)+ ; SET LINE DESCRIPTOR FOR REMAINDER OF HEADER
	MOV	T$DPGH+2(R4),(R2)+ ; ...
	MOV	(SP)+,R2	; RESTORE PARAMETER LIST ADDRESS
	MOV	$NBYTE,R0	; POINT TO ADDRESS OF NEXT BYTE
	MOV	#PGHD0,R1	; POINT TO FORMAT STRING
	CALL	$EDMSG		; CONVERT TO ASCII
	CALL	$CRFSH		; OUTPUT FILE NAME
	MOV	#PGHD1,R1	; GET ADDRESS OF NEXT FORMAT STRING
	CALL	$EDMSG		; CONVERT  SECOND PART OF PAGE HEADER
	CALL	$CRFSH		; OUTPUT CROSS REFERENCE NAME
	MOV	#PGHD2,R1	; GET LAST FORMAT STRING
	CALL	$EDMSG		; CONVERT LAST PART OF  HEADER
	CALLR	$CRFSH		; OUTPUT CROSS REFERENCE LABEL

	.END
