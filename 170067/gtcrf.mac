	.TITLE	GTCRF
	.IDENT	/00/

;
; COPYRIGHT   1974,  DIGITAL  EQUIPMENT  CORP.,  MAYNARD,   MASS.
;
; THIS SOFTWARE IS FURNISHED TO PURCHASER UNDER A LICENSE FOR USE
; ON A SINGLE COMPUTER SYSTEM  AND  CAN BE COPIED (WITH INCLUSION
; OF DEC'S COPYRIGHT NOTICE)  ONLY FOR USE IN SUCH SYSTEM, EXCEPT
; AS MAY OTHERWISE BE PROVIDED IN WRITING BY DEC.
;
; THE INFORMATION IN  THIS DOCUMENT IS  SUBJECT TO CHANGE WITHOUT
; NOTICE AND SHOULD NOT  BE CONSTRUED AS  A COMMITMENT BY DIGITAL
; EQUIPMENT CORPORATION.
;
; DEC ASSUMES  NO  RESPONSIBILITY  FOR  THE  USE  OR  RELIABILITY
; OF ITS  SOFTWARE  ON  EQUIPMENT  WHICH IS NOT  SUPPLIED BY DEC.
;
; VERSION 00
;
; C. MONIA 26-DEC-74
;
; READ CREF INPUT FILE
;
; MACRO LIBRARY CALLS
;

	.MCALL	GET$S

;+
; **-$GTCRF-READ CREF INPUT FILE RECORD
;
; THIS SUBROUTINE IS CALLED TO GET SUCCESSIVE RECORDS FROM
; THE CREF INPUT FILE. ON THE INITIAL CALL THE HEADER RECORD
; IS READ AND A CO-ROUTINE CALL IS MADE TO THE CALLER. SUBSE-
; QUENT CO-ROUTINE CALLS ARE MADE TO RETRIEVE EACH DATA
; RECORD IN SEQUENCE. UPON ENCOUNTERING END-OF-FILE THIS SUB-
; ROUTINE EXITS WITH C-SET VIA A 'RETURN'.
;
; INPUTS:
;
;	$CRIDB=ADDRESS OF CREF INPUT FDB. FILE MUST
;	       BE OPEN FOR READ.
;
; OUTPUTS:
;
;	C-CLEAR: HAVE NEXT RECORD
;
;	R1=ADDRESS OF NEXT RECORD
;
;	C-SET: END OF FILE
;
; R3 - R5 PRESERVED
;
;-

$GTCRF::			;
	MOV	#H$DLTH,$CRIDB+F.RSIZ ; SET RECORD SIZE TO READ HEADER
10$:				;
	GET$S	#$CRIDB		; GET NEXT RECORD
	BCS	20$		; IF C/S ERROR
	MOV	#D$LTH,F.RSIZ(R0) ; SET RECORD SIZE
	MOV	F.NRBD+2(R0),R1 ; POINT TO ADDRESS OF RECORD BUFFER
	CALL	@(SP)+		; CALL THE CALLER
	BR	10$		; GO AGAIN
20$:				;
	CMPB	#IE.EOF,F.ERR(R0) ; END-OF-FILE?
	BEQ	30$		; IF EQ YES
	ERROR$	E$R3,S$V2,#<$CRIDB+F.FNB+N.FNAM> ; UNEXPECTED I/O ERROR
30$:				;
	SEC			; SET C/BIT
	RETURN			;

	.END
