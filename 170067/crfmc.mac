	.NLIST
	.TITLE	CRFMC
	.IDENT	/01/

;
; COPYRIGHT   1974,  DIGITAL  EQUIPMENT  CORP.,  MAYNARD,   MASS.
; COPYRIGHT   1975,  DIGITAL  EQUIPMENT  CORP.,   MAYNARD,    MASS.
;
; THIS SOFTWARE IS FURNISHED TO PURCHASER UNDER A LICENSE FOR USE
; ON A SINGLE COMPUTER SYSTEM  AND  CAN BE COPIED (WITH INCLUSION
; OF DEC'S COPYRIGHT NOTICE)  ONLY FOR USE IN SUCH SYSTEM, EXCEPT
; AS MAY OTHERWISE BE PROVIDED IN WRITING BY DEC.
;
; THE INFORMATION IN  THIS DOCUMENT IS  SUBJECT TO CHANGE WITHOUT
; NOTICE AND SHOULD NOT  BE CONSTRUED AS  A COMMITMENT BY DIGITAL
; EQUIPMENT CORPORATION.
;
; DEC ASSUMES  NO  RESPONSIBILITY  FOR  THE  USE  OR  RELIABILITY
; OF ITS  SOFTWARE  ON  EQUIPMENT  WHICH IS NOT  SUPPLIED BY DEC.
;
; VERSION 00
;
; C. MONIA 18-DEC-74
;
; MODIFICATIONS:
;
;		CM002 -- 31-OCT-75
;
;			 ADD MACRO CREF
;
; CREF MACROS
;
; LOCAL MACROS
;
; GENERATE CREF FORMAT CONTROL TABLE ENTRY
;
; CRFC$	NAME,ID,MAXFMT
;
; WHERE:
;	NAME=2-WORD RADIX-50 NAME OF TASK PROVIDING
;	     CREF INPUT
;
;	ID=TASK IDENTIFICATION NUMBER CORRESPONDING TO WORD 2
;	   OF CREF INPUT FILE HEADER
;
;	MAXFMT=MAXIMUM FORMAT NUMBER (MAX. VALUE OF FORMAT
;	       BYTE IN CREF INPUT FILE DATA RECORD).
;

	.MACRO	CRFC$	NAME,ID,MAXFMT
	.PSECT	$$TDLS,D
$$$TDP=.
	.PSECT	$$FMCT,D
.=.-2
$$$TP2=.
F$MCID==.-$$$TP2
	.WORD	ID
$$$TP0=.
F$MCNM==.-$$$TP2
	.WORD	0
	.WORD	0
$$$TP1=.
.=$$$TP0
	.RAD50	/NAME/
.=$$$TP1
F$MCMF==.-$$$TP2
	.WORD	MAXFMT
F$MCTD==.-$$$TP2
	.WORD	$$$TDP
F$MCLT==.-$$$TP2
	.WORD	-1
	.PSECT
	.ENDM

;
; GENERATE ENTRY IN FORMAT TABLE DESCRIPTOR LIST
;
; CRFTD$	NAME,LABEL
;
; WHERE:
;
;	NAME=NAME OF CREF SYMBOL TABLE
;	LABEL=PAGE HEADING LABEL STRING
;

	.MACRO	CRFTD$	NAME,LABEL
	.PSECT	$$STRG,D
$$$TP0=.
	.ASCII	/NAME/
$$$TP1=.
	.PSECT	$$VSF0,D
$$$VS0=.
	.PSECT	$$VSF1,D
$$$VS1=.
	.PSECT	$$VSF2,D
$$$VS2=.
	.PSECT	$$RSF0,D
$$$RS0=.
	.PSECT	$$RSF1,D
$$$RS1=.
	.PSECT	$$TDLS
$$$TP2=.
T$DSTB==.-$$$TP2
	.BLKW	1
	.WORD	S$YLTH
	.WORD	N$DLGH
T$DNAM==.-$$$TP2
	.WORD	$$$TP1-$$$TP0
	.WORD	$$$TP0
	.PSECT	$$STRG,D
$$$TP0=.
	.ASCII	/LABEL/
$$$TP1=.
	.PSECT	$$TDLS
T$DPGH==.-$$$TP2
	.WORD	$$$TP1-$$$TP0
	.WORD	$$$TP0
T$DVS0==.-$$$TP2
	.WORD	$$$VS0
T$DVS1==.-$$$TP2
	.WORD	$$$VS1
T$DVS2==.-$$$TP2
	.WORD	$$$VS2
T$DRS0==.-$$$TP2
	.WORD	$$$RS0
T$DRS1==.-$$$TP2
	.WORD	$$$RS1
T$DLTH==.-$$$TP2
	.PSECT
	.ENDM

;
; CREATE SUBFIELD MASK TABLE ENTRY
;
; CRFSF$	TBLNAM,MASK,STRING,FLAG
;
; WHERE:
;
;	TBLNAM=NAME OF SUBFIELD MASK TABLE. MUST BE ONE
;	       OF THE FOLLOWING:
;
;		VSF0: VALUE SUBFIELD 0
;		VSF1: VALUE SUBFIELD 1
;		VSF2: VALUE SUBFIELD 2
;		RSF0: REFERENCE SUBFIELD 0
;		RSF1: REFERENCE SUBFIELD 1
;
;	MASK=ATTRIBUTES FLAG MASK WORD
;
;	STRING=ASCII STRING TO BE PRINTED ON FLAG WORD
;	       MATCH
;
;	FLAG=SUBFIELD MATCH FLAG SPECIFIED AS 'CLEAR' OR 'SET'
;	     WHERE:
;
;		SET=MATCH IF BITS CORRESPONDING TO MASK ARE SET
;		CLEAR=MATCH IF BITS CORRESPONDING TO MASK ARE CLEARED
;	DEFAULT IS 'SET'
;

	.MACRO	CRFSF$	TBLNAM,MASK,STRING,FLAG
	.PSECT	$$'TBLNAM
$$$TP2=.
S$FMSK==.-$$$TP2
	.WORD	100000!MASK
S$FFLG==.-$$$TP2
	.IF	IDN	<FLAG>,<CLEAR>
	.BYTE	0
	.IFF
	.BYTE	1
	.ENDC
	.PSECT	$$STRG,D
$$$TP0=.
	.ASCII	/STRING/
$$$TP1=.
	.PSECT	$$'TBLNAM
S$FLGH==.-$$$TP2
	.BYTE	$$$TP1-$$$TP0
S$FSTR==.-$$$TP2
	.WORD	$$$TP0
S$FLTH==.-$$$TP2
	.PSECT
	.ENDM

;
; TERMINATE BITMASK TABLES
;
; CRFND$
;

	.MACRO	CRFND$
	.PSECT	$$VSF0
	.WORD	0
	.PSECT	$$VSF1
	.WORD	0
	.PSECT	$$VSF2
	.WORD	0
	.PSECT	$$RSF0
	.WORD	0
	.PSECT	$$RSF1
	.WORD	0
	.PSECT
	.ENDM

	.LIST
