	.TITLE	ICRIO
	.IDENT	/00/

;
; COPYRIGHT   1975,  DIGITAL  EQUIPMENT  CORP.,  MAYNARD,   MASS.
;
; THIS SOFTWARE IS FURNISHED TO PURCHASER UNDER A LICENSE FOR USE
; ON A SINGLE COMPUTER SYSTEM  AND  CAN BE COPIED (WITH INCLUSION
; OF DEC'S COPYRIGHT NOTICE)  ONLY FOR USE IN SUCH SYSTEM, EXCEPT
; AS MAY OTHERWISE BE PROVIDED IN WRITING BY DEC.
;
; THE INFORMATION IN  THIS DOCUMENT IS  SUBJECT TO CHANGE WITHOUT
; NOTICE AND SHOULD NOT  BE CONSTRUED AS  A COMMITMENT BY DIGITAL
; EQUIPMENT CORPORATION.
;
; DEC ASSUMES  NO  RESPONSIBILITY  FOR  THE  USE  OR  RELIABILITY
; OF ITS  SOFTWARE  ON  EQUIPMENT  WHICH IS NOT  SUPPLIED BY DEC.
;
; VERSION 00
;
; C.MONIA 26-DEC-74
;
; INITIALIZE CREF I/O
;
; LOCAL DATA
;
; EXTENSION OF CREF INPUT FILE
;

CRFEX:	.RAD50	/CRF/

;+
; **-$ICRIO-INITIALIZE CREF I/O
;
; THIS SUBROUTINE IS CALLED TO PERFORM THE FOLLOWING
;
;	A - FILL IN THE APPROPRIATE FILENAME BLOCKS FROM
;	    THE RECEIVE BUFFER
;
;	B - INVOKE '.FIND' TO LOCATE THE CREF INPUT AND
;	    LISTING FILES
;
;	C - SETUP THE FDB FOR THE SPECIAL DEVICE WHEN
;	    CREF SPOOLING IS NECESSARY
;
; INPUTS:
;
;	$CRIDB - CREF INPUT FILE FDB
;	$CRODB - CREF OUTPUT FDB
;	$CRSDB - FDB FOR DEVICE TO RECEIVE CREF
;	         SPOOLED OUTPUT
;
;
; OUTPUTS:
;
;	FILENAME BLOCK PORTION OF ALL APPROPRIATE FDB'S ARE
;	SETUP WITH FILE I/D.
;
;	CS$POL SET IN $CRFLG IF CREF SPOOLING IS REQUIRED
;-

$ICRIO::			;
	SAVRG			; SAVE NON-VOLATILE REGISTERS
	MOV	#$RCVBF,R5	; POINT TO FILE DESCRIPTION IN RECEIVE BUFFER
	MOV	#$CRIDB,R0	; GET ADDRESS OF INPUT FDB
	CALL	SETNM		; SETUP NAMEBLOCK PORTION OF FDB
	MOV	CRFEX,N.FTYP(R1) ; FILE EXTENSION='.CRF'
	MOV	R$CRVR(R5),N.FVER(R1) ; SETUP CREF INPUT VERSION
	CALL	.ASLUN		; ASSIGN LUN TO INPUT DEVICE
	BCS	INERR		; IF C/S UNABLE TO ASSIGN INPUT LUN
	CALL	.FIND		; FIND SPECIFIED FILE
	BCS	INERR		; IF C/S FIND FAILED
	MOV	#$CRODB,R0	; GET ADDRESS OF OUTPUT FDB
	CALL	SETNM		; SETUP NAMEBLOCK
	CALL	.ASLUN		; ASSIGN OUTPUT LUN
	BCS	INERR		; IF C/S ASSIGN FAILED
	CALL	.FIND		; FIND OUTPUT FILE

	.IF	NDF,RSTS	;++RSTS V7.1
	BCS	INERR		; IF C/S FIND FAILED
	.ENDC			;++RSTS V7.1

	MOV	#$CRSDB,R0	; GET FDB FOR SPOOLING OUTPUT DEVICE
	CALL	SETNM		; SETUP NAMEBLOCK
	MOV	R$CODV(R5),N.DVNM(R1) ; SETUP TARGET DEVICE NAME
	BEQ	10$		; IF EQ NO CREF SPOOLING
	BIS	#CS$PD,$CRFLG	; SET SPOOLING FLAG FOR SPECIAL DEVICE
	MOVB	R$COUN(R5),N.UNIT(R1) ; SET UNIT NUMBER
	CALL	.ASLUN		; ASSIGN SPOOLED OUTPUT LUN
10$:				;
	RETURN			;

;
; **-SETNM-SETUP NAMEBLOCK PORTION OF FDB
;
; INPUTS:
;
;	R0=FDB ADDRESS
;	$RCVBF=CREF RECEIVE BUFFER
;
; OUTPUTS:
;
;	R0=FDB ADDRESS
;	R1= ADDRESS OF FILNAME BLOCK PORTION OF FDB
;
;	FNB SETUP WITH DEVICE,UNIT,NAME,EXTENSION
;	VERSION, AND DIRECTORY I/D FROM WORDS 0 - 12 OF
;	RECEIVE BUFFER.
;
; R3 - R5 ARE PRESERVED.
;

SETNM::				;
	MOV	R0,R1		; COPY FDB ADDRESS
	ADD	#F.FNB,R1	; POINT TO FILE NAMEBLOCK
	MOV	#$RCVBF+4,R2	; GET ADDRESS OF RECEIVE BUFFER
	MOV	(R2)+,N.FNAM(R1) ; SETUP FILANAME
	MOV	(R2)+,N.FNAM+2(R1) ; ...
	MOV	(R2)+,N.FNAM+4(R1) ; ...
	MOV	(R2)+,N.FTYP(R1) ; COPY FILE TYPE
	MOV	(R2)+,N.FVER(R1) ; COPY FILE VERSION
	MOV	(R2)+,N.DID(R1) ; SETUP DIRECTORY I/D
	MOV	(R2)+,N.DID+2(R1) ; ...
	MOV	(R2)+,N.DID+4(R1) ; ...
	MOV	(R2)+,N.DVNM(R1) ; SETUP DEVICE NAME
	MOVB	(R2),N.UNIT(R1) ; SETUP UNIT
	RETURN			;

;
; PARSE/FIND FAILURE
;

INERR:				;
	MOV	R1,R2		; COPY FNB ADDRESS
	ADD	#N.FNAM,R2	; POINT TO FILENAME
	ERROR$	E$R1,S$V2	; FATAL, NO RETURN

	.END
