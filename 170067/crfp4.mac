	.TITLE	CRFP4
	.IDENT	/00/

;
; COPYRIGHT   1974,  DIGITAL  EQUIPMENT  CORP.,  MAYNARD,   MASS.
;
; THIS SOFTWARE IS FURNISHED TO PURCHASER UNDER A LICENSE FOR USE
; ON A SINGLE COMPUTER SYSTEM  AND  CAN BE COPIED (WITH INCLUSION
; OF DEC'S COPYRIGHT NOTICE)  ONLY FOR USE IN SUCH SYSTEM, EXCEPT
; AS MAY OTHERWISE BE PROVIDED IN WRITING BY DEC.
;
; THE INFORMATION IN  THIS DOCUMENT IS  SUBJECT TO CHANGE WITHOUT
; NOTICE AND SHOULD NOT  BE CONSTRUED AS  A COMMITMENT BY DIGITAL
; EQUIPMENT CORPORATION.
;
; DEC ASSUMES  NO  RESPONSIBILITY  FOR  THE  USE  OR  RELIABILITY
; OF ITS  SOFTWARE  ON  EQUIPMENT  WHICH IS NOT  SUPPLIED BY DEC.
;
; VERSION 00
;
; C. MONIA 08-JAN-75
;
; CREF PHASE 4 - SPOOL CREF OUTPUT
;
; MACRO LIBRARY CALLS
;

	.MCALL	CLOSE$,GET$S,OFNB$R,OFNB$W,PUT$S

;+
; **-$CRFP4-SPOOL CREF OUTPUT FILE
;
; THIS SUBROUTINE IS CALLED TO SPOOL THE CREF OUTPUT FILE.
; IF LINE PRINTER SPOOLING WAS REQUESTED, THE PRINT SPOO-
; LER IS INVOKED VIA A CALL TO '.PRINT'. IF SPOO-
; LING TO A 'SPECIAL' DEVICE IS REQUIRED (IE. MT:), THE FILE
; IS COPIED AND DELETED FROM THE DISK UPON COMPLETION.
;
; INPUTS:
;
;	CREF OUTPUT FILE CREATED IN PHASE 3
;
; OUTPUTS:
;
;	IF SPECIAL DEVICE OR LP: SPOOLING IS REQUIRED, THE
;	FILE IS TRANSFERRED TO THE DESTINATION DEVICE AND
;	DELETED FROM THE DISK.
;
;-

$CRFP4::			;
	MOV	#$CRODB,R2	; POINT TO CREF OUTPUT FDB
	BIT	#<CS$POL!CS$PD>,$CRFLG ; LP: SPOOLING OR SPECIAL DEVICE?
	BEQ	50$		; IF EQ NO, EXIT NOW
	SAVRG			; SAVE THE NONVOLATILE REGISTERS
	BIT	#CS$PD,$CRFLG	; SPECIAL DEVICE SPOOLING?
	BEQ	40$		; IF EQ NO, LP: SPOOLING
	MOV	R2,R0		; SET ADDRESS OF OUTPUT FDB
	CLR	R1		; CLEAR HIGH PART OF VBN
	MOV	#1,R2		; SET LOW PART OF VBN TO BLOCK 1
	CLR	R3		; FIRST DATA BYTE
	CALL	.POINT		; POINT TO START OF FILE
	BCS	IOERR		; IF C/S I/O ERROR
	MOV	R0,R2		; RESTORE FDB ADDRESS
	OFNB$W	#$CRSDB		; OPEN OUTPUT DEVICE
	BCS	OPERR		; IF C/S OPEN FAILURE
	MOV	R0,R1		; SAVE OUTPUT FILE FDB ADDRESS
	MOVB	F.RATT(R2),F.RATT(R1) ; COPY FILE ATTRIBUTES BYTE
10$:				;
	GET$S	R2		; GET A RECORD
	BCS	20$		; IF C/S I/O ERROR OR E-O-F
	PUT$S	R1,F.NRBD+2(R2),F.NRBD(R2) ; COPY THE RECORD TO THE OUTPUT FILE
	BCC	10$		; IF C/C RECORD COPIED OK
20$:				;
	CMPB	#IE.EOF,F.ERR(R0) ; END-OF-FILE?
	BEQ	30$		; IF EQ YES
	ADD	#<F.FNB+N.FNAM>,R0 ; POINT TO FILENAME BLOCK
	MOV	R0,R2		; COPY FNB ADDRESS
	ERROR$	E$R3,S$V2	; FATAL I/O ERROR
30$:				;
	MOV	R2,R0		; COPY FDB ADDRESS OF CREF OUTPUT FILE
	CALL	.DLFNB		; DELETE FILE
	ROL	-(SP)		; SAVE C BIT
	CLOSE$	R1		; CLOSE FILE ON SPECIAL DEVICE
	ROR	(SP)+		; RETRIEVE C-BIT
	BCC	50$		; IF C/C DELETE SUCCEEDED
	ADD	#<F.FNB+N.FNAM>,R2 ; POINT TO FILENAME BLOCK
	ERROR$	E$R8,S$V2	; FAILED TO DELETE TEMP. CREF FILE
40$:				;
	MOV	R2,R0		; GET OUTPUT FILE FDB ADDRESS
	CALL	.PRINT		; SPOOL CREF OUTPUT
50$:				;
	CLOSE$	R2		; CLOSE OUTPUT FILE
	RETURN			;
 
;
; I/O ERROR - POINT FAILED
;
 
IOERR:				;
	MOV	R0,R2		; COPY FDB ADDRESS
	ADD	#<F.FNB+N.FNAM>,R2 ; POINT TO FILE NAME BLOCK
	ERROR$	E$R3,S$V2	; FATAL, NO RETURN
 
;
; OPEN FAILURE ON OUTPUT FILE
;
 
OPERR:				;
	MOV	R0,R2		; COPY FDB ADDRESS
	ADD	#<F.FNB+N.FNAM>,R2 ; OFFSET TO NAMEBLOCK
	ERROR$	E$R2,S$V2	; REPORT OPEN FAILURE

	.END
