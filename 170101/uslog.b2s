1	SUB USLOG							      &
\	ON ERROR GOTO 19000						      &
	!								      &
	!******************************************************************** &
	!								      &
	!	s u b p r o g r a m   t i t l e				      &
	!								      &
	!	  subprogram : USLOG					      &
	!	     version : V10.1					      &
	!	        edit : A					      &
	!	   edit date : 10-MAY-91				      &
	!	   author(s) : JJT,SEL					      &
	!								      &
	!******************************************************************** &
	!								      &
	!		  C O P Y R I G H T				      &
	!								      &
	!								      &
	!		      Copyright (C) 1984, 1991 by		      &
	!	Digital Equipment Corporation, Maynard, Mass.		      &
	!								      &
	!								      &
	! This software is furnished under a license and may be used and      &
	! copied  only  in accordance with the terms of such license and      &
	! with the  inclusion  of  the  above  copyright  notice.   This      &
	! software  or  any  other copies thereof may not be provided or      &
	! otherwise made available to any other person.  No title to and      &
	! ownership of the software is hereby transferred.		      &
	!								      &
	! The information in this software is subject to change  without      &
	! notice  and should not be construed as a commitment by Digital      &
	! Equipment Corporation.					      &
	!								      &
	! DIGITAL assumes no responsibility for the use  or  reliability      &
	! of its software on equipment that is not supplied by DIGITAL.	      &
	!								      &
	!******************************************************************** &
	!								      &
	!	s u b p r o g r a m   c a l l   f o r m a t		      &
	!								      &
	!	CALL USLOG						      &
	!								      &
	!	Argument	I/O	Usage				      &
	!	========	===	=====				      &
	!								      &
	!******************************************************************** &
	!								      &
	!	m o d i f i c a t i o n   h i s t o r y   l o g		      &
	!								      &
	!								      &
	! VER/ED	EDIT DATE	REASON				      &
	!								      &
	! 10.0-01	13-Jul-89	(SEL) Print to OUT.CHN% (/OUTPUT)     &
	!								      &
	!******************************************************************** &
	!								      &
	!	s u b p r o g r a m   d e s c r i p t i o n		      &
	!								      &
	!								      &
	!******************************************************************** &
	!								      &
	!	v a r i a b l e s  /  a r r a y s   u s e d		      &
	!								      &
	!	name		description				      &
	!	====		===========				      &
	!								      &
	!******************************************************************** &


700	!	i n c l u d e   r e q u i r e d   M A P s , C O M M O N s     &
	!								      &
	!								      &
	%INCLUDE "CUI:CUICMN.B2S"		!include CUI data section     &
	%INCLUDE "SYSTEM:UTYKEY.B2S"		!include CUI common data      &

750	COMMON	(DSPCMN)	INDENT.COLUMN%				      &
									      &


910	DIM	FIRQB%(30%)			!SYScall array		      &

									      &

1000	!	m a i n   s u b p r o g r a m   l o g i c		      &
	!								      &
	!								      &
									      &

3999	!	p r o c e s s    c o m m a n d				      &
									      &


5500	!SHOW LOGICAL/SYSTEM						      &
									      &
	ALL% = 0%				!DEFAULT FLAG TO NOT ALL      &
\	CALL FET$CQ BY REF (ALL.Q%)		!/ALL SPECIFIED?	      &
\	ALL% = -1% IF U.PRES%			!SET THE FLAG IF PRESENT      &
\	CALL FET$P BY REF (LOG.T%)		!GET THE LOGICAL TO LIST      &
\	IF U.PRES% THEN				!IF PRESENT		      &
		Z$ = SEG$(U.STR$,1%,U.LEN%)	!  EXTRACT IT		      &
\		Z$ = EDIT$(Z$,-2%)		!  REMOVE ALL JUNK	      &
\		Z$ = SEG$(Z$,1%,LEN(Z$)-1%)	!  STRIP OFF THE COLON	      &
		  IF SEG$(Z$,LEN(Z$),LEN(Z$)) = ":"! IF THERE IS ONE	      &
\		ALL% = 0%			!  CLEAR THE /ALL FLAG	      &
\	END IF								      &
\	FOUND% = 0%				!DEFAULT TO NOT FOUND	      &
\	INDX% = 0%				!INIT THE INDEX COUNTERS      &
\	ON ERROR GOTO 5530			!SET LOCAL ERROR TRAP	      &
\	WHILE 1% = 1%				!LOOP FOREVER		      &
\		FIRQB%(0%) = 30%		!  30 BYTES		      &
\		FIRQB%(1%) = 6%			!  SYS CALL TO FIP	      &
\		FIRQB%(2%) = 21%		!  ADD/REMOVE/LIST LOGICALS   &
\		FIRQB%(3%) = 2%			!  LIST LOGICAL CODE	      &
\		FIRQB%(4%) = 0%			!  RESERVED		      &
\		FIRQB%(5%) = INDX%		!  LOWER HALF OF INDEX	      &
\		FIRQB%(6%) = SWAP%(INDX%)	!  UPPER HALF OF INDEX	      &
\		FIRQB%(I%) = 0% FOR I% = 7% TO 30%!CLEAR OUT THE REST	      &
\		CHANGE FIRQB% TO FIRQB$		!  CHANGE ARRAY TO A STRING   &
\		CHANGE SYS(FIRQB$) TO FIRQB%	!  DO THE SYS CALL	      &
\		GOTO NEXTLOOP IF FIRQB%(27%) +	!  CHECK FOR A MOUNTED DISK   &
			SWAP%(FIRQB%(28%)) = -1%!    BY FQCLUS = -1	      &
\		M$ = ""				!  INIT THE LOGICAL NAME VAR  &
\		M$=M$+RAD$(FIRQB%(I%)+SWAP%(FIRQB%(I%+1%))) !FORM THE	      &
			FOR I% = 7% TO 11% STEP 2%	 !LOGICAL NAME	      &
\		m$=EDIT$(m$,-2%)		!  REMOVE ALL JUNK	      &
\		GOTO NEXTLOOP IF LEFT(m$,LEN(z$))<>z$ IF NOT ALL%	      &
						!  GET NEXT RECORD IF NO      &
						!  MATCH AND WE DON'T WANT ALL &
\		F$ = ""				!  INIT THE FILE NAME VAR     &
\		IF FIRQB%(23%) <> 0% THEN	!  IF THERE IS A DEVICE	      &
			F$ = F$ + CHR$(FIRQB%(23%))+CHR$(FIRQB%(24%))	      &
						!	GET DEVICE NAME	      &
\			F$ = F$ + NUM1$(FIRQB%(25%)) IF FIRQB%(26%) <> 0%     &
						!      GET UNIT # IF IT EXISTS &
\			F$ = F$ + ":"		!	ADD THE COLON	      &
\		END IF							      &
\		IF FIRQB%(5%)+SWAP%(FIRQB%(6%)) <> 0% THEN !IF A VALID PPN    &
			F$ =F$+"["+NUM1$(FIRQB%(6%))+","+NUM1$(FIRQB%(5%))+"]" &
						!	ADD THE PPN	      &
\		END IF							      &
\		PRINT #OUT.CHN%, m$;TAB(11%);"= ";F$	!  PRINT IT	      &
\		FOUND% = -1%			!  FLAG THAT WE FOUND ONE     &
\ NEXTLOOP:	INDX% = INDX% + 1%		!  BUMP THE COUNTER	      &
\	NEXT								      &

5510	ON ERROR GOTO 19000			!RESET THE ERROR TRAP	      &
\	IF FOUND% = 0% THEN			!IF NO MATCHES FOUND	      &
		IF ALL% = 0% THEN		!  IF WE WANTED A MATCH	      &
			CALL DISPLY ("?Logical name not found")		      &
\			SUBEXIT		!    PRINT ERROR & GO BACK	      &
		ELSE				!IF WE WANTED ALL & NONE FOUND &
			PRINT "%No Logicals assigned" !PRINT ERROR	      &
\			SUBEXIT		!	AND GO BACK		      &
\		END IF							      &
\	END IF								      &
\	SUBEXIT				!GO GET THE NEXT COMMAND	      &

5530	RESUME 5510				!SEE IF WE FOUND ANY	      &

									      &
									      &

19000	!	s t a n d a r d   e r r o r   h a n d l i n g		      &
	!								      &
	!								      &
	ON ERROR GO BACK			!Let the Root error	      &
						! handler handle it	      &


32767	!	e n d   o f   s u b p r o g r a m			      &
	!								      &
	!								      &
	SUBEND
