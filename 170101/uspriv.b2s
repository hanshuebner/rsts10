1	SUB USPRIV							      &
\	ON ERROR GOTO 19000						      &
	!								      &
	!******************************************************************** &
	!								      &
	!	s u b p r o g r a m   t i t l e				      &
	!								      &
	!	  subprogram : USPRIV					      &
	!	     version : V10.1					      &
	!	        edit : A					      &
	!	   edit date : 10-MAY-91				      &
	!	   author(s) : JJT					      &
	!								      &
	!******************************************************************** &
	!								      &
	!		  C O P Y R I G H T				      &
	!								      &
	!								      &
	!		      Copyright (C) 1984, 1991 by		      &
	!	Digital Equipment Corporation, Maynard, Mass.		      &
	!								      &
	!								      &
	! This software is furnished under a license and may be used and      &
	! copied  only  in accordance with the terms of such license and      &
	! with the  inclusion  of  the  above  copyright  notice.   This      &
	! software  or  any  other copies thereof may not be provided or      &
	! otherwise made available to any other person.  No title to and      &
	! ownership of the software is hereby transferred.		      &
	!								      &
	! The information in this software is subject to change  without      &
	! notice  and should not be construed as a commitment by Digital      &
	! Equipment Corporation.					      &
	!								      &
	! DIGITAL assumes no responsibility for the use  or  reliability      &
	! of its software on equipment that is not supplied by DIGITAL.	      &
	!								      &
	!******************************************************************** &
	!								      &
	!	s u b p r o g r a m   c a l l   f o r m a t		      &
	!								      &
	!	CALL USPRIV						      &
	!								      &
	!	Argument	I/O	Usage				      &
	!	========	===	=====				      &
	!								      &
	!******************************************************************** &
	!								      &
	!	m o d i f i c a t i o n   h i s t o r y   l o g		      &
	!								      &
	!								      &
	! VER/ED	EDIT DATE	REASON				      &
	! V9.7-06	15-Mar-89	(JJT) Don't allow /PRIV=NONOpriv      &
	!								      &
	!******************************************************************** &
	!								      &
	!	s u b p r o g r a m   d e s c r i p t i o n		      &
	!								      &
	!								      &
	!******************************************************************** &
	!								      &
	!	v a r i a b l e s  /  a r r a y s   u s e d		      &
	!								      &
	!	name		description				      &
	!	====		===========				      &
	!								      &
	!******************************************************************** &


700	!	i n c l u d e   r e q u i r e d   M A P s , C O M M O N s     &
	!								      &
	!								      &
	%INCLUDE "CUI:CUICMN.B2S"		!include CUI data section     &
	%INCLUDE "SYSTEM:UTYKEY.B2S"		!include CUI common data      &

750	COMMON	(DSPCMN)	INDENT.COLUMN%				      &
									      &


910	DIM	FIRQB%(30%)			!SYScall array		      &

									      &

1000	!	m a i n   s u b p r o g r a m   l o g i c		      &
	!								      &
	!								      &
									      &

3999	!	p r o c e s s    c o m m a n d				      &
									      &


4550	!SET PRIVILEGE							      &
									      &
		SETPRIV%(I%) = 0% FOR I% = 0% TO 3%!CLEAR THE SET PRIV FLAGS  &
\		CLRPRIV%(I%) = 0% FOR I% = 0% TO 3%!CLEAR THE CLEAR PRIV FLAGS &
\		ALL% = 0%			!DEFAULT TO NO "ALL" PRIV     &
\		CALL FET$A BY REF		!GET THE FIRST PRIVILEGE      &
\ PRIVLOOP:	CLR% = 0%			!DEFAULT CLEAR FLAG TO NOCLEAR &
\		Z$ = SEG$(U.STR$,1%,U.LEN%)	!EXTRACT IT		      &
\		Z$ = EDIT$(Z$,-2%)		!REMOVE ALL JUNK	      &
\		GOTO CHECKPRIV IF U.LEN% <= 6%	!GO CHECK IT IF <= 6 CHAR,    &
						!OTHERWISE IT BETTER BE NOpriv &
\		GOTO 4570			!PRINT ERROR AND EXIT	      &
		  IF SEG$(Z$,1%,2%) <> "NO"	! IF IT ISN'T NOpriv	      &
\		CLR% = -1%			!SET CLEAR FLAG		      &
\		Z$ = SEG$(Z$,3%,LEN(Z$))	!STRIP OFF "NO"		      &
\ CHECKPRIV:	GOTO 4570			!PRINT ERROR AND EXIT	      &
		  IF Z$ = "NOALL" OR Z$ = "NONONE"!IF IT'S NOALL OR NONONE    &
\		GOSUB 11000			!GO CHECK OUT THE PRIVILEGE   &
\		IF ERRSTS% THEN			!IF THERE WAS AN ERROR	      &
			GOTO 4570			!PRINT ERROR AND EXIT &
			  IF (SEG$(Z$,1%,2%) <> "NO") OR !IF IT ISN'T NOpriv  &
			    ((SEG$(Z$,1%,2%)  = "NO") AND !OR IT IS NOpriv AND &
			     (CLR% = -1%))	!	IT REALLY WAS NONOpriv &
\			CLR% = -1%		!SET CLEAR FLAG		      &
\			Z$ = SEG$(Z$,3%,LEN(Z$))!STRIP OFF "NO"		      &
\			GOTO CHECKPRIV		!GO CHECK THE STRIPPED PRIV   &
\		END IF							      &
\		IF NOT CLR% THEN		!IF NOT CLEAR (IF SET)	      &
			SETPRIV%(I%) = SETPRIV%(I%) OR	!  SET THE APPROPRIATE &
			  ((FIRQB%((2%*I%)+7%) AND 255%) OR ! BITS	      &
			   (SWAP%(FIRQB%((2%*I%)+8%) AND 255%)))	      &
				FOR I% = 0% TO 3%			      &
\			CLRPRIV%(I%) = CLRPRIV%(I%) AND	!  CLR THE APPROPRIATE &
			  NOT((FIRQB%((2%*I%)+7%) AND 255%) OR ! BITS	      &
			   (SWAP%(FIRQB%((2%*I%)+8%) AND 255%)))	      &
				FOR I% = 0% TO 3%			      &
		ELSE				!IF CLEAR		      &
			CLRPRIV%(I%) = CLRPRIV%(I%) OR	!  SET THE APPROPRIATE &
			  ((FIRQB%((2%*I%)+7%) AND 255%) OR ! BITS	      &
			   (SWAP%(FIRQB%((2%*I%)+8%) AND 255%)))	      &
				FOR I% = 0% TO 3%			      &
\			SETPRIV%(I%) = SETPRIV%(I%) AND	!  CLR THE APPROPRIATE &
			  NOT((FIRQB%((2%*I%)+7%) AND 255%) OR ! BITS	      &
			   (SWAP%(FIRQB%((2%*I%)+8%) AND 255%)))	      &
				FOR I% = 0% TO 3%			      &
\		END IF							      &
\		CALL FET$A BY REF		!GET THE NEXT PRIVILEGE	      &
\		GOTO PRIVLOOP IF U.PRES%	!GO CHECK IT OUT IF PRESENT   &
\		FIRQB%(0%) = 30%		!30 BYTES		      &
\		FIRQB%(1%) = 6%			!SYS CALL TO FIP	      &
\		FIRQB%(2%) = 28%		!SET/CLEAR/READ PRIV CODE     &
\		FIRQB%(3%) = SETPRIV%(0%) AND 255%!FIRST 8 PRIVS TO SET	      &
\		FIRQB%(4%) = SWAP%(SETPRIV%(0%)) AND 255%!SECOND 8 TO SET     &
\		FIRQB%(5%) = SETPRIV%(1%) AND 255%!THIRD 8 PRIVS TO SET	      &
\		FIRQB%(6%) = SWAP%(SETPRIV%(1%)) AND 255% !FOURTH 8 TO SET    &
\		FIRQB%(7%) = SETPRIV%(2%) AND 255%!FIFTH 8 PRIVS TO SET	      &
\		FIRQB%(8%) = SWAP%(SETPRIV%(2%)) AND 255%!SIXTH 8 TO SET      &
\		FIRQB%(9%) = SETPRIV%(3%) AND 255%!SEVENTH 8 PRIVS TO SET     &
\		FIRQB%(10%) = SWAP%(SETPRIV%(3%)) AND 255% !EIGHTH 8 TO SET   &
\		FIRQB%(I%) = 0% FOR I% = 11% TO 14%!CLR OUT SOME OF THE ARRAY &
\		FIRQB%(15%) = CLRPRIV%(0%) AND 255%!FIRST 8 PRIVS TO CLEAR    &
\		FIRQB%(16%) = SWAP%(CLRPRIV%(0%)) AND 255% !SECOND 8 TO CLEAR &
\		FIRQB%(17%) = CLRPRIV%(1%) AND 255% !THIRD 8 PRIVS TO CLEAR   &
\		FIRQB%(18%) = SWAP%(CLRPRIV%(1%)) AND 255% !FOURTH 8 TO CLEAR &
\		FIRQB%(19%) = CLRPRIV%(2%) AND 255%!FIFTH 8 PRIVS TO CLEAR    &
\		FIRQB%(20%) = SWAP%(CLRPRIV%(2%)) AND 255% !SIXTH 8 TO CLEAR  &
\		FIRQB%(21%) = CLRPRIV%(3%) AND 255% !SEVENTH 8 PRIVS TO CLEAR &
\		FIRQB%(22%) = SWAP%(CLRPRIV%(3%)) AND 255% !EIGHTH 8 TO CLEAR &
\		FIRQB%(I%) = 0% FOR I% = 23% TO 30%!CLEAR OUT THE REST	      &
\		CHANGE FIRQB% TO FIRQB$		!CHANGE THE ARRAY TO A STRING &
\		CHANGE SYS(FIRQB$) TO FIRQB%	!DO THE SYSCALL		      &
\		SUBEXIT IF ALL%			!EXIT IF "ALL" PRIV PRESENT   &
\		SETPRIV%(I%) = SETPRIV%(I%) AND	!  CLEAR THE CLEAR BITS	      &
			NOT (CLRPRIV%(I%))	!   FROM THE SET BITS	      &
				FOR I% = 0% TO 3%!   FOR EACH WORD	      &
\		FIRQB%(I%) = (FIRQB%((2%*I%)+3%) AND 255%) OR !MOVE THE BITS  &
		       (SWAP%(FIRQB%((2%*I%)+4%) AND 255%))   !FOR CONVENIENCE &
			FOR I% = 0% TO 3%				      &
\		GOTO 4560 IF ((SETPRIV%(I%) AND 2%^J%)=2%^J%) AND !WARN USER  &
			     ((FIRQB%(I%) AND 2%^J%) <> 2%^J%) !IF BIT NOT SET &
				FOR J% = 0% TO 15%	! FOR EACH BIT	      &
					FOR I% = 0% TO 3% ! FOR EACH WORD     &
\		SUBEXIT				!EVERYTHING OK, GO BACK	      &

4560		PRINT "%Not authorized to set all requested privileges"	      &
\		SUBEXIT				!PRINT WARNING AND GO BACK    &

4570		CALL DISPLY ("?Invalid privilege")			      &
\		SUBEXIT		!  PRINT ERROR AND GO BACK		      &
									      &


11000	ERRSTS% = 0%				!DEFAULT TO NO ERRORS	      &
\	IF Z$ = "ALL" THEN			!IF SET ALL PRIVILEGES	      &
		FIRQB%(I%) = 255% FOR I% = 7% TO 14% !SET ALL PRIVILEGES      &
\		ALL% = -1%			!     SET THE "ALL" FLAG      &
\		CLR% = 0%			!     CLEAR THE CLEAR FLAG    &
\		RETURN				!     GO BACK		      &
\	END IF								      &
\	IF Z$ = "NONE" THEN			!IF CLEAR ALL PRIVILEGES      &
		FIRQB%(I%) = 255% FOR I% = 7% TO 14% !SET ALL PRIVILEGES      &
\		ALL% = 0%			!     CLEAR THE ALL FLAG      &
\		CLR% = -1%			!     SET THE CLEAR FLAG      &
\		RETURN				!     GO BACK		      &
\	END IF								      &
\	ON ERROR GOTO 11010			!SET LOCAL ERROR TRAP	      &
\	CHANGE SYS(CHR$(6%)+CHR$(32%)+CHR$(1%)+STRING$(3%,0%)+Z$) TO FIRQB%   &
						!CONVERT PRIV NAME TO MASK    &
\	ON ERROR GOTO 19000			!RESET ERROR TRAP	      &
\	RETURN					!GO BACK		      &

11010	RESUME 11015				!CLEAR ERROR
11015	ERRSTS% = -1%				!SET ERROR FLAG		      &
\	ON ERROR GOTO 19000			!RESET ERROR TRAP	      &
\	RETURN					!GO BACK		      &

									      &

19000	!	s t a n d a r d   e r r o r   h a n d l i n g		      &
	!								      &
	!								      &
	ON ERROR GO BACK			!Let the Root error	      &
						! handler handle it	      &


32767	!	e n d   o f   s u b p r o g r a m			      &
	!								      &
	!								      &
	SUBEND
