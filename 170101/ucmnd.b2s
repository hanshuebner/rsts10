1	SUB UCMND							      &
\	ON ERROR GOTO 19000						      &
	!								      &
	!******************************************************************** &
	!								      &
	!	s u b p r o g r a m   t i t l e				      &
	!								      &
	!	  subprogram : UCMND					      &
	!	     version : V10.1					      &
	!	        edit : A					      &
	!	   edit date : 10-MAY-91				      &
	!	   author(s) : JJT,SEL					      &
	!								      &
	!******************************************************************** &
	!								      &
	!		  C O P Y R I G H T				      &
	!								      &
	!								      &
	!		      Copyright (C) 1984, 1991 by		      &
	!	Digital Equipment Corporation, Maynard, Mass.		      &
	!								      &
	!								      &
	! This software is furnished under a license and may be used and      &
	! copied  only  in accordance with the terms of such license and      &
	! with the  inclusion  of  the  above  copyright  notice.   This      &
	! software  or  any  other copies thereof may not be provided or      &
	! otherwise made available to any other person.  No title to and      &
	! ownership of the software is hereby transferred.		      &
	!								      &
	! The information in this software is subject to change  without      &
	! notice  and should not be construed as a commitment by Digital      &
	! Equipment Corporation.					      &
	!								      &
	! DIGITAL assumes no responsibility for the use  or  reliability      &
	! of its software on equipment that is not supplied by DIGITAL.	      &
	!								      &
	!******************************************************************** &
	!								      &
	!	s u b p r o g r a m   c a l l   f o r m a t		      &
	!								      &
	!	CALL UCMND						      &
	!								      &
	!	Argument	I/O	Usage				      &
	!	========	===	=====				      &
	!								      &
	!******************************************************************** &
	!								      &
	!	m o d i f i c a t i o n   h i s t o r y   l o g		      &
	!								      &
	!								      &
	! VER/ED	EDIT DATE	REASON				      &
	!								      &
	! V10.0-I	SEL  16-Feb-90	Add /[NO]REPLACE=[QUERY] to DEF/COMM  &
	! V10.0-L	SEL  30-MAY-90	Fix bug where /REPLACE=[QUERY] gets   &
	!				 ?Command not unique if nonexistent.  &
	!******************************************************************** &
	!								      &
	!	s u b p r o g r a m   d e s c r i p t i o n		      &
	!								      &
	!								      &
	!******************************************************************** &
	!								      &
	!	v a r i a b l e s  /  a r r a y s   u s e d		      &
	!								      &
	!	name		description				      &
	!	====		===========				      &
	!								      &
	!******************************************************************** &

									      &

700	!	i n c l u d e   r e q u i r e d   M A P s , C O M M O N s     &
	!								      &
	!								      &
	%INCLUDE "CUI:CUICMN.B2S"		!include CUI data section     &
	%INCLUDE "SYSTEM:UTYKEY.B2S"		!include CUI common data      &

750	COMMON	(DSPCMN)	INDENT.COLUMN%				      &
									      &

									      &

910	DIM	FIRQB%(30%)			!SYScall array		      &

									      &
									      &

1000	!	m a i n   s u b p r o g r a m   l o g i c		      &
	!								      &
	!								      &
									      &

3999	!	p r o c e s s    c o m m a n d				      &
									      &

5600	!CREATE/COMMAND/SYSTEM						      &
	!DELETE/COMMAND/SYSTEM						      &
									      &
	PRIV.NEED$ = "INSTAL"			!WE NEED INSTAL PRIV	      &
\	IF ASCII(SEG$(SYS(CHR$(6%)+CHR$(32%)+CHR$(1%)+ !IF WE DON'T HAVE      &
		 STRING$(3%,0%)+PRIV.NEED$),3%,3%)) THEN !THE RIGHT PRIV      &
		PRINT "?"+PRIV.NEED$+ " privilege required" ! PRINT ERROR MSG &
\		SUBEXIT				!		AND EXIT      &
\	END IF								      &
\	REPLACE%, DO.REPLACE% = 0%		!INIT FLAGS		      &

5610	SELECT	U.CMDC%				!SELECT THE COMMAND AGAIN     &
\	  CASE	DCM.K%				!CREATE/COMMAND/SYSTEM	      &
									      &
\	CALL FET$P BY REF (KEY.T%)		!GET THE COMMAND KEYWORD      &
\	CMND.POS% = U.POS%			!SAVE THE POSITION	      &
\	K$ = SEG$(U.STR$,1%,U.LEN%)		!EXTRACT IT		      &
\	CALL FET$P BY REF (COM.T%)		!GET THE FILENAME	      &
\	Z$ = SEG$(U.STR$,1%,U.LEN%)		!EXTRACT IT		      &
\	CHANGE FSS$(Z$,1%) TO FIRQB%		!DO THE FILENAME STRING SCAN  &
\	Q1% = 0%				!DEFAULT THE LINE NUMBER TO 0 &
\	CALL FET$CQ BY REF (LIN.Q%)		!/LINE_NUMBER SPECIFIED?      &
\	IF U.PRES% THEN				!IF PRESENT		      &
\		CALL FET$KA BY REF (CCL.K%)	!  =CCL SPECIFIED?	      &
\		IF U.PRES% THEN			!  IF PRESENT		      &
			Q1% = 30000%		!    SET APPROPIATE LINE NO.  &
\		END IF							      &
\		CALL FET$KA BY REF (DCL.K%)	!  =DCL SPECIFIED?	      &
\		IF U.PRES% THEN			!  IF PRESENT		      &
			Q1% = 30500%		!    SET APPROPIATE LINE NO.  &
\		END IF							      &
\		CALL FET$KA BY REF (CHN.K%)	!  =CHAIN SPECIFIED?	      &
\		IF U.PRES% THEN			!  IF PRESENT		      &
			Q1% = 31000%		!    SET APPROPIATE LINE NO.  &
\		END IF							      &
\		CALL FET$A BY REF		!  GET THE ARGUMENT	      &
\		IF U.PRES% THEN			!  IF PRESENT		      &
			Q1% = U.LOW%		!    GET THE NUMBER	      &
\		END IF							      &
\	END IF								      &
\	CALL FET$CQ BY REF (PRV.Q%)		!/[NO]PRIVILEGE SPECIFIED?    &
\	IF U.PRES% THEN				!IF PRESENT		      &
		Q1% = Q1% OR (32767%+1%) IF NOT U.NO%			      &
						!  SET THE PRIVILEGE BIT      &
\	END IF								      &
\	CALL FET$CQ BY REF (RPL.Q%)		!/[NO]REPLACE SPECIFIED?      &
\	IF U.PRES% THEN				!IF PRESENT		      &
	      IF U.NO% THEN			! IF /NOREPLACE		      &
		REPLACE% = 0% 			!    	DO NOT REPLACE	      &
\		PROMPT% = 0%			!	DO NOT PROMPT	      &
	      ELSE				! IF /REPLACE		      &
		REPLACE% = -1% 			!    	DO REPLACE	      &
\		CALL FET$KA BY REF (QER.K%) 	!   ASK FOR =QUERY	      &
\		IF U.PRES% THEN			!   IF QUERY		      &
			PROMPT% = -1%		!	PROMPT IF FOUND	      &
\		ELSE				!   IF NOQUERY		      &
		   	PROMPT% = 0% 		!    	DO NOT PROMPT	      &
\		END IF							      &
\	      END IF							      &
\	END IF								      &
\	FIRQB%(I%) = 0% FOR I% = 0% TO 4%	!START SETTING UP THE ARRAY   &
\	FIRQB%(11%), FIRQB%(12%) = 255% IF (FIRQB%(29%) AND 32%) <> 0%	      &
						!SET THE FILETYPE TO "AUTO    &
						! FILETYPE DETERMINATION" IF  &
						! ".*" WAS SPECIFIED	      &
\	FIRQB%(I%)  = 0% FOR I% = 13% TO 22%	!CLEAR OUT MORE OF THE ARRAY  &
\	FIRQB%(27%) = Q1% AND 255%		!STORE BOTTOM HALF OF LINE #  &
\	FIRQB%(28%) = SWAP%(Q1%) AND 255%	!STORE TOP HALF OF LINE #     &
									      &
\	  CASE	KCM.K%				!DELETE/COMMAND/SYSTEM	      &
									      &
\	ALL% = 0%				!DEFAULT FLAG TO NOT ALL      &
\	CALL FET$P BY REF (KEY.T%)		!GET THE COMMAND KEYWORD      &
\	CMND.POS% = U.POS%			!SAVE THE POSITION	      &
\	IF U.PRES% THEN				!IF THERE		      &
		K$ = SEG$(U.STR$,1%,U.LEN%)	!  EXTRACT IT		      &
	ELSE								      &
		CALL FET$CQ BY REF (ALL.Q%)	!  /ALL SPECIFIED?	      &
\		IF U.PRES% THEN			!  IF PRESENT		      &
			ALL% = -1%		!    SET THE FLAG	      &
\		END IF							      &
\	END IF								      &
									      &
\	END SELECT							      &
									      &
\	FOUND% = 0%				!INIT FLAG TO NONE FOUND      &
\	GOTO DELETE_ALL IF U.CMDC% = KCM.K% AND ALL% = -1% !GOTO THE DELETE   &
						!  ALL ROUTINE IF NEEDED      &
\	D% = 13%				!PREPARE TO SCAN FOR	      &
\	K$ = K$ + "-" UNLESS POS(K$,"*",1%) OR POS(K$,"-",1%) !EITHER	      &
						! UNIQUENESS POINTER	      &
\	WHILE LEN(K$) > 0%			!LOOP WHILE K$ IS NON NULL    &
\		D0% = ASCII(K$)			!  GET ASCII OF FIRST CHAR    &
\		K$ = SEG$(K$,2%,LEN(K$))	!  STRIP OFF FIRST CHAR	      &
\		IF (D0% = 42%) OR (D0% = 45%) THEN !IF A UNIQUENESS POINTER   &
			FIRQB%(4%) = D% - 13%	!    STORE # OF UNIQUE CHARS  &
		ELSE							      &
			FIRQB%(D%) = D0%	!    STORE THIS CHARACTER     &
\			D% = D% + 1%		!    INCREMENT THE POINTER    &
\		END IF							      &
\		IF D% > 22% THEN		!  IF COMMAND IS TOO LONG     &
			U.POS% = CMND.POS%	!    RESTORE THE POSITION     &
\			CALL DISPLY ("?Command too long")		      &
\			SUBEXIT			!    PRINT ERROR AND GO BACK  &
\		END IF							      &
\	NEXT					!NEXT CHARACTER		      &
\	IF D% = 13% THEN			!IF JUST THE UNIQUENESS	      &
						! POINTER WAS SPECIFIED	      &
		U.POS% = CMND.POS%		!    RESTORE THE POSITION     &
\		CALL DISPLY ("?Invalid format for Keyword")		      &
\		SUBEXIT				!  PRINT ERROR AND GO BACK    &
\	END IF								      &
\	FIRQB%(0%) = 30%			!30 BYTES		      &
\	FIRQB%(1%) = 6%				!SYS CALL TO FIP	      &
\	FIRQB%(2%) = -24%			!CREATE/DELETE SUBFUNCTION    &
\	FIRQB%(3%) = 0% IF U.CMDC% = DCM.K%	!CREATE COMMAND		      &
\	FIRQB%(3%) = 2% IF U.CMDC% = KCM.K%	!DELETE COMMAND		      &
\	CHANGE FIRQB% TO FIRQB$			!CHANGE THE ARRAY TO A STRING &
\	ON ERROR GOTO 5650			!SET LOCAL ERROR TRAP	      &
\	V$ = SYS(FIRQB$)			!DO THE SYS CALL	      &
\	IF DO.REPLACE% THEN			!DON'T EXIT IF REPLACING      &
	   U.CMDC% = DCM.K%			!CREATE COMMAND		      &
\	   DO.REPLACE% = 0%			!FLAG ONLY USED ONCE	      &
\	   GOTO 5610				!GO CREATE IT		      &
\	END IF								      &
\	SUBEXIT					!GO GET THE NEXT COMMAND      &

5650	U.POS% = CMND.POS%			!RESTORE THE POSITION	      &
\	IF ERR = 2% THEN			!IF INVALID COMMAND	      &
		CALL DISPLY ("?Invalid command")!  SAY SO		      &
\		RESUME 32767			!  AND EXIT		      &
\	END IF								      &
\	IF ERR = 3% THEN			!IF COMMAND IN USE	      &
		RESUME 5660 IF REPLACE%  	!GO REPLACE EXISITING	      &
\		CALL DISPLY ("?Command not unique") !SAY SO		      &
\		RESUME 32767			!  AND EXIT		      &
\	END IF								      &
\	IF ERR = 5% THEN			!IF COMMAND NOT FOUND	      &
		CALL DISPLY ("?Command not found") !SAY SO		      &
\		RESUME 32767			!  AND EXIT		      &
\	END IF								      &
\	GOTO 19000				!LET MAIN TRAP HANDLE REST    &

5660	ON ERROR GOTO 19000			!RESET STANDARD ERROR TRAP    &
\	GOTO 5670 IF NOT PROMPT% 		!JUST REPLACE IT	      &
\	INPUT "Command already defined - replace it <NO>"; ASK$		      &
\	SUBEXIT IF ASCII(EDIT$(ASK$,-2%)) <> 89%!EXIT IF NOT "Y"	      &

5670	U.CMDC% = KCM.K%			!DELETE COMMAND		      &
\	DO.REPLACE% = -1%			!FLAG=CREATE AFTER YOU DELETE &
\	GOTO 5610							      &

5690 DELETE_ALL:				!DELETE ALL COMMANDS ROUTINE  &
	WHILE 1% = 1%				!LOOP FOREVER		      &
\		CHANGE SYS(CHR$(6%)+CHR$(-12%)) TO FIRQB%! GET MONITOR TABLES &
						!	   PART II	      &
\		D% = FIRQB%(27%) + SWAP%(FIRQB%(28%))!GET THE BASE	      &
						!     TO THE COMMANDS	      &
\		IF D% <> 0% THEN		!IF THERE IS A BASE	      &
			V$ = SYS(CHR$(6%)+CHR$(-21%)+CHR$(0%))!REGAIN PRIVS   &
\			IF PEEK(D%) = 0% THEN	!  IF NO COMMANDS	      &
				PRINT "%No Commands installed" IF NOT FOUND%  &
						!  PRINT MESSAGE IF NONE FOUND &
\				V$ = SYS(CHR$(6%)+CHR$(-21%)+CHR$(255%))      &
						!  DROP TEMP PRIVS	      &
\				SUBEXIT	!	 AND GO BACK		      &
\			END IF						      &
\			V$ = SYS(CHR$(6%)+CHR$(-21%)+CHR$(255%))!DROP PRIVS   &
		ELSE				!NO BASE TO THE COMMANDS      &
			PRINT "%No Commands installed" IF NOT FOUND%	      &
						!  PRINT MESSAGE IF NONE FOUND &
\			SUBEXIT		!  AND GO BACK			      &
\		END IF							      &
\		GOSUB 11500			!GET THE NEXT COMMAND	      &
\		EXIT DELETE_ALL UNLESS D%	!EXIT LOOP UNLESS	      &
						! THERE IS A COMMAND	      &
\		FIRQB%(0%) = 30%		!30 BYTES		      &
\		FIRQB%(1%) = 6%			!SYS CALL TO FIP	      &
\		FIRQB%(2%) = -24%		!CREATE/DELETE SUBFUNCTION    &
\		FIRQB%(3%) = 2%			!DELETE COMMAND		      &
\		FIRQB%(I%) = 0% FOR I% = 5% TO 12%!CLEAR OUT SOME OF THE ARRAY &
\		FIRQB%(I%) = 0% FOR I% = 22% TO 30% !CLEAR OUT SOME MORE      &
\		FIRQB%(I%) = 0% IF (FIRQB%(I%) AND 255%) = 255%		      &
			FOR I% = 13% TO 21%	!GET RID OF 255'S	      &
\		CHANGE FIRQB% TO FIRQB$		!CHANGE THE ARRAY TO STRING   &
\		V$ = SYS(FIRQB$)		!DO THE SYSCALL		      &
\		PRINT "Command ";		!PRINT OUT THE COMMAND	      &
\		PRINT CHR$(FIRQB%(I%)); FOR I% = 13% TO 21% !THAT WAS	      &
\		PRINT " deleted"			    !JUST DELETED     &
\		FOUND% = -1%			!MARK THAT WE FOUND ONE	      &
\	NEXT					!GO DELETE THE NEXT ONE	      &
\	SUBEXIT								      &

									      &
									      &
									      &

11500	!								      &
	!	GET THE NEXT SYSTEM-WIDE COMMAND			      &
	!								      &
	!RETURNS:							      &
	!	D% =  0% - NO MORE COMMANDS				      &
	!	D% <> 0% - THERE IS A COMMAND				      &
	!								      &
	!	FIRQB%() - SET UP WITH THE DATA				      &
	!								      &
\	V$ = SYS(CHR$(6%)+CHR$(-21%)+CHR$(0%))	!REGAIN TEMP PRIVS	      &
\	D% = PEEK(D%)				!GET NEXT COMMAND	      &
\	V$ = SYS(CHR$(6%)+CHR$(-21%)+CHR$(255%))!DROP PRIVS		      &
\	RETURN UNLESS D%			!EXIT UNLESS THERE'S A COMMAND &
\	V$ = SYS(CHR$(6%)+CHR$(-21%)+CHR$(0%))	!REGAIN TEMP PRIVS	      &
\	FIRQB%(I%) = PEEK(D%+I%+1%) FOR I% = 1% TO 29% STEP 2%		      &
						!  SET UP THE TABLE FOR	      &
\	FIRQB%(I%) = SWAP%(FIRQB%(I%-1%)) AND 255% !BYTE REFERENCES	      &
				FOR I% = 2% TO 30% STEP 2%		      &
\	FIRQB%(I%) = FIRQB%(I%) AND 255% FOR I% = 1% TO 29% STEP 2%	      &
\	FIRQB%(4%) = PEEK(D%+28%)-D%-14%	!SET UNIQUENESS POINTER	      &
\	V$ = SYS(CHR$(6%)+CHR$(-21%)+CHR$(255%))!DROP PRIVS		      &
\	FIRQB%(I%) = FIRQB%(I%)+SWAP%(FIRQB%(I%+1%))			      &
				FOR I% = 1% TO 29% STEP 2%		      &
						!RESET WORD REFERENCES	      &
\	RETURN					!GO BACK		      &

									      &
									      &
									      &

19000	!	s t a n d a r d   e r r o r   h a n d l i n g		      &
	!								      &
	!								      &
	ON ERROR GO BACK			!Let the Root error	      &
						! handler handle it	      &

									      &

32767	!	e n d   o f   s u b p r o g r a m			      &
	!								      &
	!								      &
	SUBEND
