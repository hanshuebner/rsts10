	.TITLE	OPNFL
	.IDENT	/00/
 
;
; COPYRIGHT   1975,  DIGITAL  EQUIPMENT  CORP.,  MAYNARD,   MASS.
;
; THIS SOFTWARE IS FURNISHED TO PURCHASER UNDER A LICENSE FOR USE
; ON A SINGLE COMPUTER SYSTEM  AND  CAN BE COPIED (WITH INCLUSION
; OF DEC'S COPYRIGHT NOTICE)  ONLY FOR USE IN SUCH SYSTEM, EXCEPT
; AS MAY OTHERWISE BE PROVIDED IN WRITING BY DEC.
;
; THE INFORMATION IN  THIS DOCUMENT IS  SUBJECT TO CHANGE WITHOUT
; NOTICE AND SHOULD NOT  BE CONSTRUED AS  A COMMITMENT BY DIGITAL
; EQUIPMENT CORPORATION.
;
; DEC ASSUMES  NO  RESPONSIBILITY  FOR  THE  USE  OR  RELIABILITY
; OF ITS  SOFTWARE  ON  EQUIPMENT  WHICH IS NOT  SUPPLIED BY DEC.
;
; VERSION 00
;
; C. MONIA 01-DEC-75
;
; OPEN PATCH INPUT AND OUTPUT FILES
;
; MACRO LIBRARY CALLS
;
 
	.MCALL	OFNB$R,OFNB$W
 
;+
; **-$OPNOU-OPEN PATCH OUTPUT FILE
;
; THIS SUBROUTINE IS CALLED TO OPEN THE PATCH OUTPUT FILE
;
; INPUTS:
;
;	$OUFDB=OUTPUT FILE FDB
;
; OUTPUTS:
;
;	THE OUTPUT FILE IS OPENED BY FILENAME BLOCK FOR WRITE
;
;-
 
$OPNOU::			;
	MOV	#$OUFNB,R0	; POINT TO OUTPUT FILE NAME BLOCK
	MOV	#$OUFDB+F.FNB,R1 ; POINT TO FILE NAME BLOCK PART OF FDB
	CALL	STFNB		; SETUP FDB
	OFNB$W	#$OUFDB		; OPEN OUTPUT FILE FOR WRITE
	BCS	OPERR		; IF C/S OPEN ERROR
	RETURN			;
 
;+
; **-$OPNIN-OPEN INPUT FILE FOR READ
;
; AN INPUT FILE IS OPENED FOR READ BY FILENAME BLOCK
;
; INPUTS:
;
;	$INFDB=INPUT FILE FDB
;	R0=ADDRESS OF FILENAME BLOCK USED TO OPEN FILE
;
; OUTPUTS:
;
;	R0=ADDRESS OF INPUT FILE FDB
;	SPECIFIED FILE IS OPENED FOR READ
;
;-
 
$OPNIN::			;
	MOV	#$INFDB+F.FNB,R1 ; GET ADDRESS OF FDB NAME BLOCK
	CALL	STFNB		; SETUP FILENAME BLOCK
	OFNB$R	#$INFDB		; OPEN FILE FOR READ
	BCS	OPERR		; IF C/S OPEN ERROR
	RETURN			;
 
;
; ERROR ON OPEN
;
 
OPERR:				;
	ADD	#F.FNB+N.FNAM,R0 ; POINT TO FILENAME BLOCK
	MOV	R0,R2		; COPY ADDRESS OF FNB
	ERROR$	E$R9,S$V2	; FATAL, NO RETURN
 
;
; SETUP FILE NAME BLOCK PORTION OF FDB
;
 
STFNB:				;
	MOV	#S.FNB/2,R2	; GET SIZE OF FILE NAME BLOCK
10$:				;
	MOV	(R0)+,(R1)+	; COPY FNB
	SOB	R2,10$		; ...
	RETURN			;
 
	.END
