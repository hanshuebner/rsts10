	.TITLE	NXTFL
	.IDENT	/06.01/
;
; COPYRIGHT (c)	1983 BY DIGITAL EQUIPMENT CORPORATION.
; ALL RIGHTS RESERVED.
;
; THIS  SOFTWARE IS FURNISHED  UNDER A LICENSE AND MAY BE USED OR
; COPIED ONLY IN ACCORDANCE WITH THE TERMS OF SUCH LICENSE.
;
;
; D. N. CUTLER/C. MONIA	21-JAN-74
;
; MODIFIED BY:
;
;	C. B. PETROVIC	26-FEB-82	06.01
;		CBP039		ADD CORRECT .PSECT DEFINITIONS TO
;				ALLOW TKB TO BE LINKED AS AN I/D TASK.
;
;
;+
; **-$NXTFL-GET NEXT INPUT FILE
;
; THIS ROUTINE IS CALLED TO GET THE NEXT INPUT FILE SPECIFICATION AND
; PLACE IT IN THE OBJECT FILE RECORD BLOCK. IF $MRFLG IS NONZERO, THEN
; THE SCANNING ROUTINE IS CALLED IMMEDIATELY. ELSE THE NEXT COMMAND IN-
; PUT LINE IS READ AND THEN THE SCANNING ROUTINE IS CALLED.
;
; INPUTS:
;
;	R5=ADDRESS OF TEMP BUFFER.
;
; OUTPUTS:
;
;	C=1 IS COMMAND INPUT IS FINISHED.
;	C=0 IF NEXT INPUT FILE HAS BEEN OBTAINED.
;-
	PURE.I			; READ-ONLY I-SPACE

	.MCALL	CSI$

	CSI$			; DEFINE CSI BLOCK OFFSET DEFINITIONS

$NXTFL::TST	$MRFLG		;ANY MORE FILES?
	BEQ	10$		;IF EQ YES
5$:	CALL	$GTCML		;GET NEXT COMMAND INPUT LINE
	BCS	20$		;IF CS LOGICAL EOF
	MOV	R5,R1		;GET ADDRESS OF TEMP BUFFER
	CALL	$SYNTX$		;ANALYZE SYNTAX
	BCS	$NXTFL		;IF CS SYNTAX ERROR
	BITB	#CS.EQU,C.STAT(R1) ; EQUAL SIGN (=) SEEN?
	BEQ	7$		; IF EQ NO
	MOV	#<S$V1*400!E$R1>,R1 ; SYNTAX ERROR
	MOV	#$LNDES,R2	; GET ADDRESS OF COMMAND LINE BUFFER
	CALL	$ERMSG
	BR	$NXTFL		;
7$:
	MOV	#$SCANO,$SCANR	;SET ADDRESS OF SCANNING ROUTINE
10$:				;
	CALL	$CLRMN		; INITIALIZE LIBRARY MODULE NAME LIST
	MOV	$INIPT,R0	; GET RECORD BLOCK ADDRESS
	MOV	R5,R1		;GET ADDRESS OF TEMP BUFFER
	CALL	@$SCANR		;CALL SCANNING ROUTINE
	MOV	R0,$MRFLG	;SAVE MORE FILES FLAG
	BCS	$NXTFL		;IF CS BYPASS NULL FILE
	CALL	$BLMNT		; BUILD MODULE NAME LIST
	BCC	20$		; IF C/C OK
	MOV	#$LNDES,R2	; GET ADDRESS OF COMMAND LINE BUFFER
	MOV	#<S$V2*400!E$R78>,R1 ; GET ERROR/SEVERITY
	CALL	$ERMSG		; FATAL, NORETURN
20$:	RETURN			;

	.END
