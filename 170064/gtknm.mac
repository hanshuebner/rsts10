	IDENT	225.1,GTKNM,<GET 3-CHARACTER TASK NAME>
; 
; THIS  SOFTWARE  IS FURNISHED UNDER A LICENSE AND MAY
; BE USED OR COPIED ONLY IN ACCORDANCE WITH THE  TERMS
; OF SUCH LICENSE.
;  
; COPYRIGHT (c) 1984 BY DIGITAL EQUIPMENT CORPORATION.
; ALL RIGHTS RESERVED.
; 
;
; PREVIOUSLY MODIFIED BY
;
;	C. PUTNAM
;	J. LAWLER
;	B. SCHREIBER
;	M. HARVEY
;
; MODIFIED BY:
;
;
;	S. C. ADAMS   5-JULY-84
;		SA262 -- CHECK TO SEE IF TASK NAME IS OF FORM RTN
;
;
;
; THIS ROUTINE RETURNS THE 3-CHARACTER TASK NAME IN RADIX-50.
;

	.MCALL	GTSK$S

;+
; $GTKNM -- GET TASK NAME
;
; THIS ROUTINE RETURNS THE 3 CHARACTER TASK NAME IN RADIX-50
; IN R1.
;
; INPUTS:
;
;	R1	3 CHARACTER DEFAULT TASK NAME IN RADIX-50
;		WHICH IS USED ONLY UNDER VMS OR IF THE TASK NAME IS OF
;		THE FOLLOWING FORMS:
;
;			"TTNN"
;			"TTAN"
;			"RTNN"
;			"HTNN"
;			"VTNN"
;			"VTAN"
;
; OUTPUTS:
;
;	R1	3 CHARACTER TASK NAME IN RADIX 50.
;
; SUGGESTED CALLING SEQUENCE:
;
;	MOV	(PC)+,R1
;	.RAD50	/TSK/		;"TSK" IS DEFAULT TASK NAME
;	CALL	$GTKNM
;	MOV	#BUF,R0
;	CALL	$C5TA
;
;-

	PURE.I
$GTKNM::SUB	#32.,SP		;RESERVE SPACE ON STACK FOR DIRECTIVE
	MOV	SP,R0		;POINT FOR DIRECTIVE
	GTSK$S	R0		;GET TASK PARAMETERS
	CMP	G.TSSY(R0),#5	;IS THIS A PROCESS NAME? (VMS ONLY)
	BEQ	20$		;IF EQ YES, USE DEFAULT TASK NAME
	MOV	R1,-(SP)	;SAVE DEFAULT TASK NAME
	MOV	(R0),R1		;(TASK NAME IS FIRST TWO WORDS OF BUFFER)
	CMP	R1,#^RHT0	; Is task name of form HTNN
	BLO	10$		; If lt, no
	CMP	R1,#^RHT7	; Maybe..
	BLOS	7$		; Yes
	CMP	R1,#^RRT0	; IS TASK NAME OF FORM "RTNN?"
	BLO	12$		; IF LO, NO
	CMP	R1,#^RRT7	; IS IT REALLY?
	BLOS	7$		; IF LOS, YES
	CMP	R1,#^RTTA	; IS TASK NAME OF FORM "TTAN"?
	BLO	10$		; IF LO NO
	CMP	R1,#^RTTX	; MAYBE...
	BLOS	7$		; IF LOS YES - USE DEFAULT
	CMP	R1,#^RTT0	;IS TASK NAME OF FORM "TTNN"
	BLO	10$		;IF LO NO
	CMP	R1,#^RTT7	;MAYBE...
	BHI	10$		;IF HI NO, TRY OTHER FORMS
7$:	MOV	(SP)+,R1	;YES--GET DEFAULT TASK NAME
	BR	20$		;RETURN
10$:	CMP	R1,#^RVTA	; IS TASK NAME OF FORM "VTAN"?
	BLO	12$		; IF LO NO
	CMP 	R1,#^RVTX	; MAYBE...
	BLOS	7$		;IF LOS YES, USE DEFAULT
	CMP	R1,#^RVT0	; IS TASK NAME OF FORM "VTNN"?
	BLO	12$		; IF LO - NO
	CMP	R1,#^RVT7	; MAYBE...
	BLOS	7$		;IF LOS YES, USE DEFAULT
12$:	TST	(SP)+		; POP DEFAULT NAME OFF STACK
	CMP	(R0)+,#^R...	;IS TASK NAME "...XXX"
	BNE	20$		;IF NE NO--NAME IS CORRECT
15$:	MOV	(R0),R1		;YES--RETURN SECOND WORD OF NAME
20$:	ADD	#32.,SP		;RESTORE STACK
	RETURN

	.END
