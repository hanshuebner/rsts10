	.TITLE	MRKPG
	.IDENT	/00.02/
;
;		COPYRIGHT (c) 1983 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD
;	 MASSACHUSETTS.  ALL RIGHTS RESERVED.
;
; THIS  SOFTWARE  IS  FURNISHED  UNDER  A LICENSE AND MAY BE USED
; AND  COPIED  ONLY IN  ACCORDANCE WITH THE TERMS OF SUCH LICENSE
; AND WITH  THE INCLUSION  OF THE ABOVE  COPYRIGHT  NOTICE.  THIS
; SOFTWARE  OR ANY OTHER  COPIES  THEREOF, MAY NOT BE PROVIDED OR
; OTHERWISE MADE  AVAILABLE TO ANY OTHER PERSON.  NO TITLE TO AND
; OWNERSHIP OF THE SOFTWARE IS HEREBY TRANSFERRED.
;
; THE INFORMATION  IN THIS DOCUMENT IS SUBJECT  TO CHANGE WITHOUT
; NOTICE AND SHOULD NOT BE CONSTRUED AS A COMMITMENT  BY  DIGITAL
; EQUIPMENT CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE OR RELIABILITY OF
; ITS SOFTWARE ON EQUIPMENT THAT IS NOT SUPPLIED BY DIGITAL.
;
;
;
; C. MONIA 22-FEB-74
;
; MODIFIED BY:
;
;	C. B. PETROVIC	26-FEB-82	00.01
;		CBP039		ADD CORRECT .PSECT DEFINITIONS TO
;				ALLOW TKB TO BE LINKED AS AN I/D TASK.
;
;	C. B. PETROVIC	7-NOV-83	00.02
;		CBP114		PLACE BREAKPOINT TRAP TO CATCH SETTING
;				DYNAMIC MEMORY PAGE LOCK COUNT TO -1.
;
;
;
; ROUTINES TO SET/CLEAR PAGE STATUS BITS
;
;+
; **-$LCKPG-LOCK A PAGE IN MEMORY
; **-$UNLPG-UNLOCK A PAGE FROM MEMORY
; **-$WRMPG-MARK PAGE AS WRITTEN INTO
;
; INPUTS:
;
;	R1=VIRTUAL ADDRESS
;
; OUTPUTS:
;
;	C-CLEAR: PAGE WAS MARKED AS REQUESTED
;
;	C-SET: PAGE NOT RESIDENT
;
; ALL REGISTER CONTENTS ARE PRESERVED
;-

	PURE.I			; READ-ONLY I-SPACE

$LCKPG::			;
	SAVVR			; SAVE VOLATILE REGISTERS
	CALL	$FNDPG		; FIND SPECIFIED PAGE
	BCS	10$		; IF C/S PAGE NOT IN MEMORY
	INCB	P$GLOK(R0)	; INCREMENT LOCK COUNT
10$:				;
	RETURN			;

$UNLPG::			;
	SAVVR			; SAVE VOLATILE REGISTERS
	CALL	$FNDPG		; FIND SPECIFIED PAGE
	BCS	10$		; IF C/S PAGE NOT IN MEMORY
	DECB	P$GLOK(R0)	; DECREMENT LOCK COUNT

	.IF DF	D$EBUG!D$RDPG

	BGE	10$		; IS LOCK COUNT >= 0?
	BPT			; NO...TRAP HERE...

	.ENDC	; D$EBUG!D$RDPG
10$:				;
	RETURN			;

$WRMPG::			;
	SAVVR			;
	CALL	$FNDPG		; FIND SPECIFIED PAGE
	BCS	10$		; IF C/S NOT RESIDENT
	BISB	#PG$WRT,P$GSTS(R0) ; WRITE-MARK PAGE
10$:				;
	RETURN			;

	.END
