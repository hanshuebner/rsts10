	.TITLE	GTPPR
	.IDENT	/05.01/
;
; COPYRIGHT (c)	1983 BY DIGITAL EQUIPMENT CORPORATION.
; ALL RIGHTS RESERVED.
;
; THIS  SOFTWARE IS FURNISHED  UNDER A LICENSE AND MAY BE USED OR
; COPIED ONLY IN ACCORDANCE WITH THE TERMS OF SUCH LICENSE.
;
;
; C. MONIA 21-DEC-73
;
; MODIFIED BY:
;
;	C. B. PETROVIC	26-FEB-82	05.01
;		CBP039		ADD CORRECT .PSECT DEFINITIONS TO
;				ALLOW TKB TO BE LINKED AS AN I/D TASK.
;
;
; SUBROUTINE TO RETURN PARTITION PARAMETERS
;
;
;
; MACRO LIBRARY CALLS
;

	.MCALL	DIR$,GPRT$

;
; LOCAL DATA
;
;
; DEFAULT PARTITION NAME
;
	PURE.D			; READ-ONLY D-SPACE

DFPAR:	.RAD50	/GEN   /	;

;
; DPB-'GET PARTITION PARAMETERS' DIRECTIVE
;

PRDPB:	GPRT$	,PRBF		;

;
; BUFFER TO RECEIVE PARTITION DATA
;

	IMPURE			; READ/WRITE D-SPACE
PRBF:	.BLKW	3		;

;
;+
; **-$GTPPR - 'GET PARTITION PARAMETERS' SUBROUTINE
;
; INPUTS:
;
;	R0:	POINTS TO PARTITION NAME IN 2-WORD
;		RADIX 50 FORMAT. IF R0 IS ZERO
;		THE DEFAULT PARTITION IS RETURNED.
;
; OUTPUTS:
;
;	C-CLEAR:	SPECIFIED PARTITION FOUND
;
;		R0:	UNMODIFIED
;		R1:	BASE ADDRESS OF PARTITION
;		R2:	PARTITION SIZE IN 32W BLOCKS
;		   	(SET TO 0 FOR SYSTEM CONTROLLED PARTITION)
;
;
;	$PNAME-$PNAME+2: NAME OF DEFAULT PARTITION (IF REQUESTED)
;
;	C-SET:	REQUESTED PARTITION DOES NOT EXIST IN HOST SYSTEM.
;
;-
	PURE.I			; READ-ONLY I-SPACE

$GTPPR::			;
	TST	$RSXSY		; TEST SYSTEM RUNNING ON
	SEC			; ASSUME RSX-11D
	BPL	40$		; IF PL RSX-11D, ERROR
	SAVRG			; SAVE NON-VOLATILE REGISTERS
	MOV	R0,-(SP)	; SAVE POINTER TO NAME
	BNE	20$		; IF NE NAME SPECIFIED
	MOV	#$PNAME,R4	; GET LOCATION FOR NAME
	MOV	#DFPAR,R0	; GET ADDRESS OF DEFAULT NAME
	MOV	(R0),(R4)+	; SET DEFAULT NAME
	MOV	2(R0),(R4)+	;
20$:				;
	MOV	#PRDPB+G.PRPN,R4 ; GET ADDRESS OF NAME IN DPB
	MOV	(R0)+,(R4)+	; SETUP NAME
	MOV	(R0)+,(R4)+	;
	DIR$	#PRDPB		; ISSUE DIRECTIVE
	BCS	35$		; IF C/S NO SUCH PARTITION
	MOV	@#$DSW,R1	; GET VIRTUAL PARTITION ADDRESS
	MOV	PRBF+G.PRPS,R2	;++041 GET LENGTH/64
	BIT	#1,PRBF+G.PRFW	;++041 SYSTEM CONTROLLED PARTITION?
	BNE	35$		;++041 IF NE NO
	CLR	R2		;++041 SET LENGTH TO ZERO
35$:				;
	MOV	(SP)+,R0	; RESTORE NAME POINTER
40$:				;
	RETURN			;

	.END
