	.TITLE	P2CLS
	.IDENT	/03.05/
;
; COPYRIGHT (c)	1988 BY DIGITAL EQUIPMENT CORPORATION.
; ALL RIGHTS RESERVED.
;
; THIS  SOFTWARE IS FURNISHED  UNDER A LICENSE AND MAY BE USED OR
; COPIED ONLY IN ACCORDANCE WITH THE TERMS OF SUCH LICENSE.
;
;
;	J.A. KASSON	20-AUG-80
;
; MODIFIED FOR RSX-11M/M-PLUS V4.0/V2.0 BY:
;
;	C. B. PETROVIC
;	J. M. LAWLER
;
; MODIFIED BY:
;
;	C. B. PETROVIC	26-FEB-82	03.01
;		CBP039		ADD CORRECT .PSECT DEFINITIONS TO
;				ALLOW TKB TO BE LINKED AS AN I/D TASK.
;
;	J. M. SALMAN	07-MAY-82	03.02
;		JMS047		ALLOW THE FIRST LIBRARY IN A CLUSTER TO
;				HAVE A NON-NULL ROOT.  ALSO CHANGE CLUSTER
;				TABLE OFFSETS FROM CL$XXX TO C$LXXX.
;
;	J. M. SALMAN	14-OCT-82	03.03
;		JMS079		APPEND CLUSTER COUNT TO $CLSHD SO THAT
;				P4MAL CAN EXTEND OVERLAY DATABASE SECTIONS
;				CORRECTLY WHEN LINKING TO MORE THAN ONE
;				CLUSTER.
;
;	J. M. SALMAN	05-NOV-82	03.04
;		JMS085		PUT PROPER VALUES INTO LINK WORD AND ACCESS
;				BYTE IN CLUSTER TABLE.
;
;	L. M. PETERSON	25-JAN-88	03.05
;		LMP098		ADD SUPPORT FOR LIBRARY D-SPACE RESERVATION
;
;
; MACRO LIBRARY CALLS
;

	.MCALL	CSI$
	CSI$

;
; PARSE CLUSTER LIBRARY OPTION INPUTS
;
; LOCAL DATA
;


.PAGE

;+
; **-$CLS-PARSE CLUSTER LIBRARY KEYWORD INPUT
;
; THIS ROUTINE IS CALLED BY THE PHASE 2 OPTION PROCESSOR
; TO PARSE THE CLUSTER LIBRARY KEYWORD OPTION 'CLSTR' AND
; CREATE A CLUSTER TABLE WHICH DISCRIBES THE CLUSTER.
;
; INPUTS:
;
;	R0=ADDRESS OF CURRENT COMMAND BYTE
;	   THE FORMAT OF THE OPTION:
;		CLSTR=LIB1,LIB2,...LIB6:access[:apr]
;	   THE NUMBER OF LIBRARIES MUST BE < 7 BUT > 1.
; OUTPUTS:
;
;	R0=ADDRESS OF NEXT BYTE IN COMMAND LINE
;	R2=TERMINAL CHARACTER
;	A LINKED CLUSTER TABLE.  THE TABLE IS 17. WORDS
;	LONG WITH THE FOLLOWING FORMAT:
;		______________
;		| LINK WORD  |
;		|------------|
;		| ACC |COUNT :
;		|------------|
;		| MSK | APR  |
;		|------------|
;		| RAD50 LIB  |
;		|------------|
;		|   NAME1    |
;		|------------|
;		      .
;		      .
;		      .
;		|------------|
;		| RAD50 LIB  |
;		|------------|
;		|   NAME6    |
;		|------------|
;		| LIB1 DESCR |
;		|------------|
;		| LIB1 OFSET |
;		+------------+
;
;-
	PURE.I			; READ-ONLY I-SPACE

$CLS::				;
	SAVRG			;
	MOV	R0,-(SP)	; SAVE ADDRESS OF COMMAND LINE
	MOV	#C$LLGH,R1	; SET LENGTH OF CLUSTER TABLE
	CALL	$ALVRT		; ALLOCATE SPACE FOR CLUSTER TABLE
	MOV	R1,$CURCV	; SAVE VIRTUAL ADDRESS OF CURRENT CLUSTER
	MOV	R0,$CURCL	; SAVE ADDRESS OF CURRENT CLUSTER TABLE
	MOV	#$CLSHD,R2	; GET ADDRESS OF CLUSTER LIST HEAD
	MOV	(R2),(R0)	; LINK IN THIS CLUSTER TABLE
	MOV	R1,(R2)		;
	INC	$CLSHD+2	; INCREMENT NUMBER OF CLUSTERS
	MOV	R0,R5		; SET ADDRESS OF TABLE
	MOVB	#-1,C$LAPR(R5)	; INITIALIZE APR NUMBER
	MOV	(SP)+,R0	; RESTORE COMMAND LINE POINTER
	MOV	R5,-(SP)	; SAVE ADDRESS OF CLUSTER TABLE
	ADD	#C$LNME,R5	; POINT TO FIRST LIBRARY IN TABLE
	CLR	-(SP)		; INITIALIZE LIBRARY COUNT
20$:
	CALL	$RR		; CONVERT NAME TO RAD50
26$:
	TST	R2		; END OF STRING?
	BEQ	100$		; IF EQ YES
	CMPB	R2,#';		; COMMENT TERMINATOR?
	BEQ	100$		; IF EQ YES
	CMPB	R2,#'!		; EXCLAMATION POINT TERMINATOR?
	BEQ	100$		; IF EQ YES
	CMPB	R2,#',		; COMMA TERMINATOR?
	BEQ	30$		; IF EQ YES
	CMPB	R2,#':		; COLON TERMINATOR?
	BEQ	40$		; IF NE NO
	BR	200$		; IF NONE OF ABOVE ERROR
30$:
	INC	(SP)		; COUNT LIBRARIES
	CMPB	(SP),#6		; MORE THEN SIX IN CLUSTER?
	BGT	200$		; IF GT YES - SYNTAX ERROR
	BR	20$		; GET NEXT LIBRARY
40$:
	INC	(SP)		; COUNT LIBRARIES
	CMPB	(SP),#2		; LESS THEN 2 IN CLUSTER?
	BLT	200$		; IF LT YES - ERROR
	MOV	2(SP),R5	; GET TOP OF CLUSTER TABLE
	TST	(R5)+		; POINT TO LIBRARY COUNT AND ACCESS
	CALL	$ACC		; DETERMINE ACCESS
	MOVB	(SP),-2(R5)	; SET LIBRARY COUNT IN LOW BYTE
	INCB	1(SP)		; MARK ACCESS HAS BEEN SPECIFIED
50$:
	CMP	R2,#',		; COMMA TERMINATOR?
	BEQ	200$		; IF EQ YES - SYNTAX ERROR
	CMP	R2,#':		; COLON TERMINATOR?
	BNE	26$		; IF NE NOT APR SPEC
	CALL	$DCC		; CONVERT APR SPECIFICATION
	CMP	R2,#':		; COLON TERMINATOR?
	BNE	50$		; TEST FOR VALID TERMINATOR
	MOV	#$PARM+C.SIZE+4,R5 ; GET ADDRESS TO STORE DAPR BITMASK
	CALL	$OT		; CONVERT DAPR BITMASK
	CMP	R2,#':		; COLON TERMINATOR?
	BEQ	200$		; IF EQ YES - SYNTAX ERROR
	BR	50$		; TEST FOR VALID TERMINATOR

100$:
	CMPB	(SP),#2		; MORE THEN ONE LIBRARY IN CLUSTER?
	BLT	200$		; IF LT NO -SYNTAX ERROR
	TSTB	1(SP)		; ACCESS SPECIFIED
	BEQ	200$		; IF EQ NO
	TST	(SP)+		; CLEAN STACK
	MOV	(SP)+,R5	; GET TOP OF TABLE
	CLRB	C$LMSK(R5)	; CLEAR MASK BYTE
	RETURN
200$:
	CMP	(SP)+,(SP)+	; CLEAN STACK
	MOV	#<S$V1*400!E$R30>,R1 ; OPTION SYNTAX ERROR
	MOV	#$LNDES,R2	; POINT LINE DISCRIPTORR
	CALLR	$ERMSG		; OUTPUT ERROR



	.END
