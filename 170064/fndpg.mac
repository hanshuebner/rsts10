	.TITLE	FNDPG
	.IDENT	/02.01/
;
;		COPYRIGHT (c) 1983 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD
;	 MASSACHUSETTS.  ALL RIGHTS RESERVED.
;
; THIS  SOFTWARE  IS  FURNISHED  UNDER  A LICENSE AND MAY BE USED
; AND  COPIED  ONLY IN  ACCORDANCE WITH THE TERMS OF SUCH LICENSE
; AND WITH  THE INCLUSION  OF THE ABOVE  COPYRIGHT  NOTICE.  THIS
; SOFTWARE  OR ANY OTHER  COPIES  THEREOF, MAY NOT BE PROVIDED OR
; OTHERWISE MADE  AVAILABLE TO ANY OTHER PERSON.  NO TITLE TO AND
; OWNERSHIP OF THE SOFTWARE IS HEREBY TRANSFERRED.
;
; THE INFORMATION  IN THIS DOCUMENT IS SUBJECT  TO CHANGE WITHOUT
; NOTICE AND SHOULD NOT BE CONSTRUED AS A COMMITMENT  BY  DIGITAL
; EQUIPMENT CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE OR RELIABILITY OF
; ITS SOFTWARE ON EQUIPMENT THAT IS NOT SUPPLIED BY DIGITAL.
;
;
;
; C. MONIA 22-FEB-74
;
;
; MODIFIED BY:
;
;	C. B. PETROVIC	26-FEB-82	02.01
;		CBP039		ADD CORRECT .PSECT DEFINITIONS TO
;				ALLOW TKB TO BE LINKED AS AN I/D TASK.
;
; SCAN PAGE LIST FOR RESIDENT VIRTUAL ADDRESS
;
;+
; **-$FNDPG-SEARCH PAGE BUFFERS FOR RESIDENT ADDRESS
;
; INPUTS:
;
;	R1=VIRTUAL ADDRESS
;
; OUTPUTS:
;
;	C-CLEAR: PAGE BUFFER RESIDENT
;
;		R0=ADDRESS OF BUFFER
;
;	C-SET:   PAGE NOT RESIDENT
;
; IN EITHER CASE R1 IS LEFT UNCHANGED.
;
; IF RESIDENT, PAGE BUFFER IS TIME-STAMPED AND RETURNED
; TO CALLER
;-
	PURE.I			; READ-ONLY I-SPACE

$FNDPG::			;
	INC	$TIME		; ADVANCE TIME
	BNE	4$		; IF NE NO WRAP-AROUND
	INC	$TIME		; SET TIME TO 1
	MOV	#<$PAGHD-P$GNXT>,R0 ; GET RESIDENT PAGE LIST
2$:				;
	MOV	P$GNXT(R0),R0	; GET ADDRESS OF NEXT RESIDENT PAGE
	BEQ	4$		; IF EQ DONE
	CLR	P$GTIM(R0)	; RESET TIME
	BR	2$		; GO AGAIN
4$:				;
	MOV	R1,R2		; COPY VIRTUAL ADDRESS
	CLRB	R2		; CLEAR DISPLACEMENT IN BLOCK
	SWAB	R2		; POSITION RELATIVE BLOCK TO LOW BYTE
	MOV	$PAGLS,R0	; GET ADDRESS OF RESIDENT PAGE LIST
	BEQ	8$		; IF EQ NONE
	ASL	R2		; CONVERT BLOCK TO WORD OFFSET
	ADD	R2,R0		; COMPUTE LIST INDEX
	SEC			; ASSUME PAGE NOT RESIDENT
	MOV	(R0),R0		; GET PAGE ADDRESS
	BEQ	20$		; IF EQ PAGE NOT IN MEMORY
	CLC			; SET SUCCESS
	BR	15$		; EXIT
8$:				;
	MOV	#<$PAGHD-P$GNXT>,R0 ; GET LISTHEAD ADDRESS MINUS OFFSET
10$:				;
	SEC			; ASSUME PAGE NOT RESIDENT
	MOV	P$GNXT(R0),R0	; GET NEXT PAGE BUFFER
	BEQ	20$		; IF EQ NO MORE
	CMPB	P$GBLK(R0),R2	; CHECK RELATIVE BLOCK
	BNE	10$		; IF NE NOT REQUESTED PAGE
15$:				;
	MOV	$TIME,P$GTIM(R0) ; TIME-STAMP THIS PAGE
20$:				;
	RETURN			;

	.END
