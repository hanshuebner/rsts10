	.TITLE	P5CRF
	.IDENT	/02.02/
;
; COPYRIGHT (c)	1983,1991 BY DIGITAL EQUIPMENT CORPORATION.
; ALL RIGHTS RESERVED.
;
; THIS  SOFTWARE IS FURNISHED  UNDER A LICENSE AND MAY BE USED OR
; COPIED ONLY IN ACCORDANCE WITH THE TERMS OF SUCH LICENSE.
;
;
; C. MONIA 15-JAN-75
;
; MODIFIED BY:
;
;	C. B. PETROVIC	26-FEB-82	02.01
;		CBP039		ADD CORRECT .PSECT DEFINITIONS TO
;				ALLOW TKB TO BE LINKED AS AN I/D TASK.
;
;	C. M. KATZ	23-APR-91	02.02
;		CMK001		ADD RSTS CONDITIONAL CODE
;
;
; PHASE 5 CREF OUTPUT
;
; MACRO LIBRARY CALLS
;

	.MCALL	CLOSE$,DIR$,OFNB$W,RQST$,SDAT$

;
; EQUATED SYMBOLS
;


CRSPL=000001			; CREF SPOOLING FLAG

;
; LOCAL DATA
;
;

;
; SEND DATA BUFFER
;
	IMPURE			; READ/WRITE D-SPACE

SNDBF:	.BLKW	13.		;

;+
; **-$P5CRF-CREATE CREF OUTPUT FILE
;
; THIS SUBROUTINE IS CALLED AFTER THE MAP FILE HAS BEEN OUTPUT
; TO WRITE THE CREF OUTPUT FILE AND INVOKE THE CREF TASK. IF NO CREF WAS
; REQUESTED, THE MAP FILE IS SPOOLED(IF SPECIFIED) AND CLOSED.
;
; INPUTS:
;
;	TABLES CONSTRUCTED IN PHASE 3
;	MAP FILE FDB (FILE MUST BE OPEN)
;
; OUTPUTS:
;
;	IF A CREF FILE IS TO BE PRODUCED, THE APPROPRIATE SUBROUTINES
;	ARE CALLED TO CREATE THE FILE AND THE CREF TASK IS INVOKED.
;	ELSE, THE MAP FILE IS CLOSED AND THE SPOOLER IS REQUESTED.
;
;-
	PURE.I			; READ-ONLY I-SPACE

$P5CRF::			;
	SAVRG			; SAVE THE NON-VOLATILE REGISTERS
	MOV	#$SWTCH,R5	; GET ADDRESS OF SWITCH WORD
	BIT	#MA$PF,(R5)	; MAP FILE SPECIFIED?
	BEQ	50$		; IF EQ NO
	BIT	#CR$EF,(R5)	; CREF FILE SPECIFIED?
	BEQ	40$		; IF EQ NO
	MOV	#SNDBF,R0	; GET ADDRESS OF SEND BUFFER
	MOV	$MAPPT,R1	; GET ADDRESS OF MAP FILE FDB
	MOV	F.FNB+N.FNAM(R1),(R0)+ ; COPY FILE NAME
	MOV	F.FNB+N.FNAM+2(R1),(R0)+ ; ...
	MOV	F.FNB+N.FNAM+4(R1),(R0)+ ; ...
	MOV	F.FNB+N.FTYP(R1),(R0)+ ; COPY FILE TYPE
	MOV	F.FNB+N.FVER(R1),(R0)+ ; COPY FILE VERSION
	MOV	F.FNB+N.DID(R1),(R0)+ ; COPY DIRECTORY I/D
	MOV	F.FNB+N.DID+2(R1),(R0)+ ; ...
	MOV	F.FNB+N.DID+4(R1),(R0)+ ; ...
	MOV	F.FNB+N.DVNM(R1),(R0)+ ; COPY DEVICE NAME
	MOVB	F.FNB+N.UNIT(R1),(R0)+ ; COPY UNIT NUMBER
	CLRB	(R0)		; CLEAR FLAGS BYTE
	BIT	#SP$OL,(R5)	; SPOOL OUTPUT?
	BNE	10$		; IF NE NO
	BISB	#CRSPL,(R0)	; SET SPOOL FLAG
10$:				;
	ADD	#3,R0		; STEP TO ACTUAL OUTPUT DEV.
	MOV	$CRODV,(R0)+	; SET SPECIAL CREF OUTPUT DEVICE
	MOVB	$CROUN,(R0)+	; SET SPECIAL OUTPUT UNIT
	CLRB	(R0)+		; CLEAR FLAGS BYTE
	CLOSE$	R1		; CLOSE MAP FILE
	BCC	20$		; IF C/C CLOSED SUCCEEDED
	BIC	#<MA$PF!CR$EF>,(R5) ; CLEAR MAP, CREF SWITCHES
	BR	50$		; EXIT
20$:				;
	MOV	$CRFPT,R0	; GET ADDRESS OF CREF FDB
	MOV	R0,R4		; SAVE FDB ADDRESS
	CALL	$STRCB		; SETUP CREF RECORD BLOCK
	OFNB$W	R4		; OPEN CREF FILE FOR OUTPUT
	MOV	F.FNB+N.FVER(R4),SNDBF+<10.*2> ; SET CREF VERSION
	BCC	30$		; IF C/C OK
	BIC	#CR$EF,(R5)	; CLEAR CREF FLAG
	MOV	R$NAME(R4),R2	; GET ADDRESS OF NAMEBLOCK
	MOV	#<S$V0*400!E$R11>,R1 ; GET ERROR/SEVERITY
	CALL	$ERMSG		; OUTPUT ERROR MESSAGE
	BR	50$		;
30$:				;
	CALL	$CRFHD		; OUTPUT THE CREF HEADER RECORD
	MOV	#P5CRF,R0	; GET PHASE DEPENDANT ROUTINE ADDRESS
	CALL	$PCTRL		; CALL PHASE DEPENDANT ROUTINE
	CLOSE$	$CRFPT		; CLOSE CREF OUTPUT FILE
	BCS	50$		; IF C/S EXIT NOW
	.IF	NDF,R$RSTS
	MOV	#^RCRF,R0	; NAME OF ROUTINE TO DISPATCH
	MOV	#SNDBF,R1	; DATA BUFFER
	CALL	$DSPAT		; START UP CREF
	BR	50$		;
	.ENDC
40$:				;
	MOV	$MAPPT,R0	; GET ADDRESS OF MAP FILE FDB
	BIT	#SP$OL,(R5)	; SPOOL OUTPUT?
	BNE	45$		; IF NE NO
	CALL	.PRINT		; SUBMIT MAP FILE FOR PRINTING
45$:				;
	CLOSE$	R0		; CLOSE MAP FILE
50$:				;
	RETURN			;

;
; OUTPUT THE CREF FILE
;

P5CRF:				;
	SAVRG			; SAVE THE NON-VOLATILE REGISTERS
	MOV	#$STIMP,-(SP)	; PUSH ADDRESS OF FILE SETUP ROUTINE
10$:				;
	CALL	@(SP)+		; SETUP NEXT INPUT FILE
	BCS	20$		; IF C/S COMPLETED ALL FILES IN THIS SEGMENT
	MOV	$CRELM,R0	; GET ADDRESS OF CURRENT ELEMENT
	BIT	#SW$MA,E$LSWT(R0) ; ELEMENT IN MAP?
	BNE	10$		; IF NE NO
	CALL	$P5CEL		; GENERATE CREF OUTPUT FOR THIS FILE
	BR	10$		; GO AGAIN
20$:				;
	RETURN			;


	.END
