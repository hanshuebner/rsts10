	.TITLE	ALELD
	.IDENT	/06.01/
;
; COPYRIGHT (c)	1983 BY DIGITAL EQUIPMENT CORPORATION.
; ALL RIGHTS RESERVED.
;
; THIS  SOFTWARE IS FURNISHED  UNDER A LICENSE AND MAY BE USED OR
; COPIED ONLY IN ACCORDANCE WITH THE TERMS OF SUCH LICENSE.
;
;
; D. N. CUTLER/C. MONIA 03-FEB-74
;
; MODIFIED FOR RSX-11M/M-PLUS V4.0/V2.0 BY:
;
;	J. A. KASSON
;
; MODIFIED BY:
;
;	C. B. PETROVIC	26-FEB-82	06.01
;		CBP039		ADD CORRECT .PSECT DEFINITIONS TO
;				ALLOW TKB TO BE LINKED AS AN I/D TASK.
;
;
;+
; **-$ALELD-ALLOCATE ELEMENT DESCRIPTOR
; **-$ALELO-ALLOCATE ELEMENT DESCRIPTOR FOR OUTPUT FILE
; **-$ALEL1-ALLOCATE ELEMENT DESCRIPTOR WITH ADDITIONAL WORD
; **-$ALEL2-ALLOCATE ELEMENT DESCRIPTOR WITH TWO ADDITIONAL WORDS
;		LAST ONE FOR SUPERVISOR MODE LIBRARY'S COMPLETION ROUTINE ADDR.
;
; THIS ROUTINE IS CALLED TO ALLOCATE AND INITIALIZE AN ELEMENT DE-
; SCRIPTOR. IT IS A MONITOR DEPENDENT ROUTINE SINCE IT MUST KNOW
; THE FORMAT OF THE NAME STRING. THE DESCRIPTOR STORAGE IS ALLOCATED
; AND THE NAME STRING INFORMATION IS TAKEN FROM THE $INPPT RECORD
; BLOCK.
;
; INPUTS:
;
;	$INIPT RECORD BLOCK
;
; OUTPUTS:
;
;	AN ELEMENT DESCRIPTOR IS ALLOCATED AND INITIALIZED.
;		R0=REAL ADDRESS OF ELEMENT DESCRIPTOR
;		R1=VIRTUAL ADDRESS OF ELEMENT DESCRIPTOR
;
;	IF $ALELO IS CALLED THEN THE DIRECTORY I/D
;	IS SAVED INSTEAD OF THE FILE ID (IE. A PREVIOUS
;	CALL TO .PARSE IS ASSUMED) AND E$LNUM IS SET TO -2.
;-
	PURE.I			; READ-ONLY I-SPACE

$ALEL2::
	MOV	#E$LLGH+4,R1	; GET LENGTH OF DESCRIPTOR PLUS FOUR
	BR	ALELD1		;
$ALEL1::MOV	#E$LLGH+2,R1	;GET LENGTH OF DESCRIPTOR PLUS 2
	BR	ALELD1		;
$ALELD::MOV	#E$LLGH,R1	;GET LENGTH OF ELEMENT DESCRIPTOR
ALELD1:				;
	CALL	ALEL		; ALLOCATE DESCRIPTOR AND SETUP FILENAME
	ADD	#N.FID,R1	; POINT TO FILE I/D
	BR	ALELO1		; SETUP I/D
$ALELO::			;
	MOV	#E$LLGH,R1	; SETUP DESCRIPTOR LENGTH
	CALL	ALEL		; ALLOCATE DESCRIPTOR AND SETUP FILENAME
	DEC	E$LNUM(R0)	; SET HIGHEST SECTION NUMBER TO -2
	ADD	#N.DID,R1	; POINT TO DIRECTORY I/D
ALELO1:				;
	MOV	(R1)+,(R2)+	; STORE I/D
	MOV	(R1)+,(R2)+	;
	MOV	(R1)+,(R2)+	;
	RETURN			;

;
; SETUP ELEMENT DESCRIPTOR (LESS I/D).
; LEAVE R2 POINTING TO SPACE FOR I/D.
;

ALEL:				;
	CALL	$ALVRT		; ALLOCATE VIRTUAL MEMORY
	MOV	(SP),-(SP)	; COPY RETURN ADDRESS
	MOV	R1,2(SP)	; SAVE VIRTUAL ADDRESS
	MOV	$INIPT,R1	; GET INPUT FILE RECORD BLOCK
	DEC	E$LNUM(R0)	;SET HIGHEST SECTION NUM TO -1
	MOV	R$SWTH(R1),E$LSWT(R0);STORE SWITCH WORD
	ADD	#F.FNB,R1	;POINT TO FILE NAME BLOCK
	MOV	R0,R2		;COPY ADDR OF ELEMENT BLOCK
	ADD	#E$LMND,R2	;CALCULATE ADDR MONITOR INFO
	MOV	N.UNIT(R1),(R2)+;STORE UNIT NUMBER
	MOV	N.DVNM(R1),(R2)+;STORE DEVICE NAME
	MOV	N.FNAM(R1),(R2)+;STORE FILE NAME
	MOV	N.FNAM+2(R1),(R2)+;
	MOV	N.FNAM+4(R1),(R2)+;
	MOV	N.FTYP(R1),(R2)+;STORE FILE TYPE
	MOV	N.FVER(R1),(R2)+;STORE FILE VERSION
	CALL	@(SP)+		; CALL THE CALLER
	MOV	(SP)+,R1	; RESTORE VIRTUAL ADDRESS
	RETURN			;

	.END
