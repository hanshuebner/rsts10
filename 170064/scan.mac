	.TITLE	SCAN
	.IDENT	/03.04/
;
; COPYRIGHT (c)	1989,1991 BY DIGITAL EQUIPMENT CORPORATION.
; ALL RIGHTS RESERVED.
;
; THIS  SOFTWARE IS FURNISHED  UNDER A LICENSE AND MAY BE USED OR
; COPIED ONLY IN ACCORDANCE WITH THE TERMS OF SUCH LICENSE.
;
;
; D.N. CUTLER 16-OCT-72
;
; MODIFIED BY:
;
;	C. B. PETROVIC	26-FEB-82	03.01
;		CBP039		ADD CORRECT .PSECT DEFINITIONS TO
;				ALLOW TKB TO BE LINKED AS AN I/D TASK.
;
;	P. K. M. WEISS	23-OCT-84	03.02
;		PKW072		ADD LOGICAL NAME SUPPORT
;
;	L. M. PETERSON	1-OCT-89	03.03
;		LMP140A		DELETE OBSOLETE (VAX-11)FILESPEC TESTS 
;
;	C. M. KATZ	23-APR-91	03.04
;		CMK001		ADD RSTS CONDITIONAL CODE
; MONITOR DEPENDENT COMMAND STRING ANALYSIS ROUTINES
;
; MACRO LIBRARY CALLS
;

	.IF	NDF,R$RSTS
	.MCALL	CSI$,CSI$1,CSI$4
	.IFF
	.MCALL	CSI$,CSI$1,CSI$2                                                    
	.ENDC

;
; EQUATED SYMBOLS
;

	CSI$			;DEFINE CSI BLOCK OFFSET DEFINITIONS
T$MSIZ==C.SIZE			;SIZE OF CSI BLOCK

;+
; **-$SCANI/$SCANO-SCAN FOR INPUT/OUTPUT FILE SPECIFCICATION
;
; THIS ROUTINE IS CALLED TO SCAN A COMMAND STRING FOR THE NEXT INPUT
; OR OUTPUT FILE. $SYNTX MUST HAVE BEEN PREVIOUSLY CALLED TO SET UP
; THE COMMAND BUFFER AND TO DO THE SYNTAX ANALYSIS.
;
; INPUTS:
;
;	R0=ADDRESS OF RECORD BLOCK.
;	R1=ADDRESS OF TEMP BUFFER AREA.
;
; OUTPUTS:
;
;	C=1 IF NO FILE SPECIFIED OR ILLEGAL SWITCH.
;	C=0 IF REQUEST IS SUCESSFUL.
;	IN EITHER CASE R0 IS RETURNED AS THE MORE FILES FLAG.
;		IF R0 EQ 0, THEN MORE FILES ARE SPECIFIED.
;		IF R0 EQ 1, THEN NO MORE FILES ARE SPECIFIED.
;-
	PURE.I			; READ-ONLY I-SPACE

$SCANI::MOVB	#CS.INP,C.TYPR(R1);SET FOR INPUT FILE
	BR	SCANO		;
$SCANO::MOVB	#CS.OUT,C.TYPR(R1);SET FOR OUTPUT FILE
SCANO:	SAVRG			;SAVE NONVOLITILE REGISTERS
	MOV	R0,R5		;SAVE RECORD BLOCK ADDRESS
	.IF	NDF,R$RSTS
	CSI$4	R1,,R$SWBK(R5)	;GET FILE SPECIFICATION
	.IFF
	CSI$2	R1,,R$SWBK(R5)	;GET FILE SPECIFICATION
	.ENDC
	MOV	R1,R2		;COPY CSI BLOCK ADDRESS
	MOVB	C.STAT(R2),R4	;GET STATUS BYTE
	BCS	10$		;IF CS ILLEGAL SWITCH
	BITB	#CS.NMF!CS.DVF,R4;FILE OR DEVICE SPECIFIED?
	BEQ	60$		;IF EQ NO
	BITB	#CS.WLD,R4	;WILD CARD SPECIFIED?
	BNE	20$		;IF NE YES
	MOV	F.DSPT(R5),R5	;GET ADDRESS OF DATA SET DESCRIPTOR
	ADD	#C.DSDS,R2	;POINT TO CSI DATA SET DESCRIPTOR
	MOV	#3,-(SP)	;SET LOOP COUNT
3$:	MOV	(R2)+,R3	;GET LENGTH OF LINE SEGMENT
	MOV	R3,(R5)+	;STORE IN DESCRIPTOR
	MOV	(R2)+,R1	;GET ADDRESS OF LINE SEGMENT
	MOV	(R5)+,R0	;GET ADDRESS TO STORE LINE SEGMENT
5$:	DEC	R3		;DECREMENT BYTE COUNT
	BLT	7$		;IF LT DONE
	MOVB	(R1)+,(R0)+	;
	BR	5$		;
7$:	DEC	(SP)		;DECREMENT LOOP COUNT
	BGT	3$		;IF GT MORE TO GO
	TST	(SP)+		;CLEAN STACK
	BR	70$		;

;
; ILLEGAL SWITCH SPECIFICATION
;

10$:	MOV	(PC)+,R1	;GET ERROR,SEVERITY
	.BYTE	E$R3,S$V2	;FATAL-NO RETURN
	BR	30$		;

;
; ILLEGAL FILE SPECIFICATION
;

20$:	MOV	(PC)+,R1	;GET ERROR,SEVERITY
	.BYTE	E$R55,S$V2	;FATAL-NO RETURN
30$:	ADD	#C.FILD,R2	;POINT TO FILENAME DESCRIPTOR
	CALL	$ERMSG		;OUTPUT ERROR MESSAGE
60$:	SEC			;SET CARRY
70$:	ROR	-(SP)		;SAVE CARRY BIT
	CLR	R0		;ASSUME MORE FILES ARE SPECIFIED
	BITB	#CS.MOR,R4	;MORE FILES?
	BNE	80$		;IF NE YES
	INC	R0		;SET NO FILES
80$:	ROL	(SP)+		;RESTORE CARRY
	RETURN			;

;+
; **-$SYNTX-COMMAND STRING SYNTAX ANALYSIS
;
; THIS ROUTINE IS CALLED TO ANALYZE A COMMAND STRING FOR CORRECT SYN-
; TAX AND TO SET UP THE COMMAND BUFFER FOR THE ABOVE SCAN ROUTINES.
;
; INPUTS:
;
;	R0=ADDRESS OF BYTE COUNT,REC ADDR DOUBLEWORD.
;	R1=ADDRESS OF TEMP BUFFER AREA
;
; OUTPUTS:
;
;	C=1 IF SYNTAX ERROR.
;	C=0 IF NO SYNTAX ERROR.
;-

$SYNTX::MOV	(R0)+,C.CMLD(R1);SET LENGTH OF LINE
	MOV	(R0),C.CMLD+2(R1);SET ADDRESS OF LINE
	CSI$1	R1		;ANALYZE SYNTAX
	BCC	10$		;IF CS RETURN
	MOV	R1,R2		;COPY ADDRESS OF CSI BLOCK
	ADD	#C.CMLD,R2	;POINT TO LINE SEGMENT DESCRIPTOR
	MOV	(PC)+,R1	;SYNTAX ERROR
	.BYTE	E$R1,S$V1	;DIAGNOSTIC
	CALL	$ERMSG		;OUTPUT ERROR MESSAGE
	SEC			;SET CARRY
10$:	RETURN			;

	.END
