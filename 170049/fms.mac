.INCLUDE /CMN:COMMON/							;006
.INCLUDE /CMN:KERNEL/							;006
.INCLUDE /CMN:KBDEF/							;006
TITLE	FMS,<FMS FIELD PRIMITIVES>,0A,10-MAY-91,ARF/KPH

;
;		COPYRIGHT (c) 1974, 1991 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

.SBTTL	EDIT HISTORY FOR FMS
;+
;
;  000	ARF  06-MAR-81	CREATION - COPIED FROM THIN AIR
;  001  ARF  14-APR-81	First pass at fms primitives
;  002  ARF  18-JUN-81	Added level three queue echoing
;  003  ARF  02-SEP-81	Moved a BEQ from FMS$KB to 10$
;  004  ARF  30-OCT-81	Added extra check for field defined in FMS
;
;			[RSTS/E V9.3]
;  005  KPH  01-Dec-86	Remove patch space
;
;			[RSTS/E V9.6]
;  006  KPH  13-Oct-87	Add .INCLUDEs
;			Use direct L3Q dispatching
;			Modify for DDB extension by INIT
;  007	DRP  08-Mar-88	Modify L3Q Processor to use DDLINK
;-

.SBTTL	FMS PHASE HEADER

MERGE	FMS

MERGE	L3QTBL,L3QSIZ							;006
MERGE	L3QPAR,L3QSIZ							;006

MERGE	END

	DEFORG	FMSPAT

	DEFORG	FMS

	INCLUDE	<QUESUB,FMSTIO>

.SBTTL	FMS$KB - FMS LEVEL THREE QUEUE PROCCESSING

	L3QENT	QKBFMS,,FMSAP5						;006

FMS$KB:	MOV	#FMSHED,R0	;GET THE QUEUE HEADER
	SPLC	5		;;PR 5
	CALLX	POPQUE		;;GET A DDB
	ROL	R1		;;SAVE CARRY
	SPLC	3		;;BACK TO THREE FOR A WHILE
	ROR	R1		;RESTORE CARRY
	BCS	30$		;NO MORE DDBS
	MOV	R5,R1		;GET DDB POINTER INTO R1
	SUB	#DDLINK,R1	;Point to DDB start itself		;007
	MOVB	DDUNT(R1),R0	;GET UNIT NUMBER
	ASL	R0		;MAKE IT TIMES TWO
10$:	CALLX	FREBUF,R5,BFQ.KB ;CHECK FOR SMALL BUFFER SPACE
	BCS	FMS$KB		;NONE, NEXT DDB
	TST	DDBUFC+BC(R1)	;SOME, OVER QUOTA ?
	BLE	FMS$KB		;TOO FAR OVER QUOTA
	BITB	#FMSFLD,DDFLG2(R1) ;ENSURE FIELD DEFINED (AVOID RACE CONDITION)
	BEQ	FMS$KB		;NO FIELD, GO AWAY
	SPLC	5		;;LOCK OUT TERMINAL INTERRUPTS
	CALLX	FETCH,R5,TTINEC+EP ;;GET A CHARACTER FROM THE TYPE AHEAD CHAIN
	BIC	#^C<377>,R2	;;CLEAR HIGH BYTE
	BCS	FMS$KB		;;NO TYPE AHEAD REMAINS
	SPLC	3		;;BACK TO THREE (NOTE SAVED CARRY)
	CALLX	$FMSIN		;NOW 'FORCE' THE CHARACTER TO FMS
	BITB	#FMSRDY,DDFLG2(R1) ;IS THE FIELD COMPLETE ?
	BEQ	10$		;NO, BACK FOR MORE
20$:	CALLX	IOFINI,R5,JS.KB	;WAKE UP THIS GUY
	CLR	TIM.KB(R0)	;CLEAR CLOCK SINCE DELIMITER FOUND
	BR	FMS$KB		;DONE WITH THIS ONE, NEXT DDB
30$:	JMPX	RTI3		;RETURN FROM WHENCE WE CAME

GLOBAL	<TIM.KB,FMSHED,BFQ.KB,TTINEC,FMSAP5>				;007

	.END
