;
; COPYRIGHT 1975, DIGITAL EQUIPMENT CORP. MAYNARD, MASS 01754
;
; THIS SOFTWARE IS FURNISHED TO PURCHASER UNDER A LICENSE FOR USE
; ON A SINGLE COMPUTER SYSTEM AND CAN BE COPIED (WITH INCLUSION
; OF DEC'S COPYRIGHT NOTICE) ONLY FOR USE IN SUCH SYSTEM, EXCEPT
; AS MAY OTHERWISE BE PROVIDED IN WRITING BY DEC.
;
; THE INFORMATION IN THIS DOCUMENT IS SUBJECT TO CHANGE WITHOUT
; NOTICE AND SHOULD NOT BE CONSTRUED AS A COMMITMENT BY DIGITAL
; EQUIPMENT CORPORATION.
;
; DEC ASSUMES NO RESPONSIBILITY FOR THE USE OR RELIABILITY
; OF ITS SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DEC.
;
; VERSION 01
;
; MACROS FOR THE SNAPSHOT DUMP DEBUGGING AID
;
;
; WRITTEN BY:
;	H. LEV, C. D'ELIA   FEBRUARY 1975
;
;
;
; MODIFIED BY:
;
;	J. E. PROVINO  1-FEB-78
;		JP044 -- MOVE REQUESTING TASK'S DSW TO THE SNAP BLOCK
;


;
; *-* SNPDF$ -*- GENERATE SNAP OFFSET AND BIT DEFINITIONS
;

	.MACRO	SNPDF$	GBL
	.IF IDN	<GBL>,<DEF$G>
	.GLOBL	SB.CTL,SB.DEV,SB.UNT,SB.EFN,SB.ID,SB.LM1,SB.PMD
	.GLOBL	SC.HDR,SC.LUN,SC.OVL,SC.STK,SC.WRD,SC.BYT
	.ENDC

SB.CTL = 0			;CONTROL WORD
SB.DEV = 2			;DEVICE NAME
SB.UNT = 4			;DEVICE UNIT NUMBER
SB.DSW = 5			;DIRECTIVE STATUS WORD				; JP044
SB.EFN = 6			;EVENT FLAG NUMBER
SB.ID  = ^O<10>			;SNAPSHOT IDENTIFICATION WORD
SB.LM1 = ^O<12>			;FIRST WORD OF ADDRESS LIMITS
SB.PMD = ^O<32>			;.RAD50 /PMD.../

SC.HDR = 1			;DUMP TASK HEADER (INCL REGISTERS)
SC.LUN = 2			;DUMP ASSIGNED LUN INFORMATION
SC.OVL = 4			;DUMP OVERLAY STRUCTURE INFORMATION
SC.STK = ^O<10>			;DUMP TASK STACK
SC.WRD = ^O<20>			;OUTPUT IN WORD/RAD50 FORMAT
SC.BYT = ^O<40>			;OUTPUT IN BYTE/ASCII FORMAT

; MACRO REDEFINITION TO RELEASE SPACE

	.MACRO	SNPDF$	X
	.ENDM

	.ENDM	SNPDF$


;
; *-* SNPBK$ -*- GENERATE A SNAP CONTROL BLOCK
;

	.MACRO	SNPBK$	DEV,UNIT,CTL,EFN,ID,L1,H1,L2,H2,L3,H3,L4,H4

; NEVER GENERATE MORE THAN ONE BLOCK

	.IF DF	..SPBK
	.IIF NE	<..SPBK-.>,	.MEXIT
	.ENDC

	.MCALL	SNPDF$
	SNPDF$

; BUILD THE CONTROL BLOCK

..SPBK::.WORD	CTL		;CONTROL WORD
	.ASCII	/DEV/		;OUTPUT DEVICE NAME
	.IF NE	.-<..SPBK+SB.DEV+2>
	.ERROR		;INVALID DEVICE NAME ''DEV''  ;
	.MEXIT
	.ENDC
	.BYTE	UNIT,0		;DEVICE UNIT NUMBER & DIRECTIVE STATUS WORD	; JP044
	.WORD	EFN		;EVENT FLAG NUMBER				;**-1
	.WORD	ID		;SNAPSHOT IDENTIFICATION WORD
	.WORD	L1,H1		;DUMP ADDRESS LIMITS
	.WORD	L2,H2
	.WORD	L3,H3
	.WORD	L4,H4
	.RAD50	/PMD.../	;SNAPSHOT TASK NAME

	.ENDM	SNPBK$


;
; *-* SNAP$ -*- REQUEST A SNAPSHOT DUMP
;

	.MACRO	SNAP$	CTL,EFN,ID,L1,H1,L2,H2,L3,H3,L4,H4,?LBL1,?LBL2
	.MCALL	SNPDF$,WTSE$S,SDAT$S,RQST$S,CLEF$S
	SNPDF$
	.IIF NB	<CTL>,	MOV	CTL,..SPBK+SB.CTL
	.IIF NB	<EFN>,	MOV	EFN,..SPBK+SB.EFN
	.IIF NB	<ID>,	MOV	ID,..SPBK+SB.ID
	MOVB	@#$DSW,..SPBK+SB.DSW	;MOVE REQUESTING TASK'S DSW TO SNAP BLOC; JP044
...SNP = SB.LM1
	.IRP	X,<L1,H1,L2,H2,L3,H3,L4,H4>
	.IF NB	<X>
	MOV	X,..SPBK+...SNP
	.ENDC
...SNP = ...SNP+2
	.ENDM
	CLEF$S	..SPBK+SB.EFN	;CLEAR EVENT FLAG
	BCS	LBL2
	SDAT$S	#..SPBK+SB.PMD,#..SPBK  ;SEND SNAP INFORMATION
	BCS	LBL2
	RQST$S	#..SPBK+SB.PMD	;REQUEST TASK 'PMD...'
	BCC	LBL1
	CMP	#IE.ACT,@#$DSW	;O.K. IF TASK ALREADY ACTIVE
	BEQ	LBL1
	SEC
	BR	LBL2
LBL1:	WTSE$S	..SPBK+SB.EFN	;WAIT FOR SNAPSHOT TO FINISH
LBL2:
	.ENDM	SNAP$
