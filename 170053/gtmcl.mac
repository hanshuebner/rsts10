	.TITLE	.GTMCL
	.IDENT	/05/

;
; COPYRIGHT   1974,  DIGITAL  EQUIPMENT  CORP.,  MAYNARD,   MASS.
;
; THIS SOFTWARE IS FURNISHED TO PURCHASER UNDER A LICENSE FOR USE
; ON A  SINGLE COMPUTER SYSTEM AND CAN BE  COPIED (WITH INCLUSION
; OF DEC'S COPYRIGHT  NOTICE) ONLY FOR USE IN SUCH SYSTEM, EXCEPT
; AS MAY OTHERWISE BE PROVIDED IN WRITING BY DEC.
;
; THE  INFORMATION  IN THIS DOCUMENT IS SUBJECT TO CHANGE WITHOUT
; NOTICE  AND  SHOULD NOT BE CONSTRUED AS A COMMITMENT BY DIGITAL
; EQUIPMENT CORPORATION.
;
; DEC  ASSUMES NO  RESPONSIBILITY  FOR  THE  USE  OR  RELIABILITY
; OF  ITS  SOFTWARE ON  EQUIPMENT WHICH  IS  NOT SUPPLIED BY DEC.
;
; VERSION 05
;
; C.MONIA 24-JUN-74
;
; MODIFIED BY:
;
;	T. M. MARTIN	9-NOV-81
;			PLACE CODE IN PIC I-SPACE PSECT
;
; FORTRAN CALLABLE ROUTINE TO ISSUE THE "GET MCR COMMAND LINE" DIRECTIVE
;
; MACRO LIBRARY CALLS
;
 
	.MCALL	CALL,DIR$,RETURN,.PICOD
 
;
; LOCAL MACROS
;
; SOB	REG,TAG
;

	.MACRO	SOB	REG,TAG
	DEC	REG
	BNE	TAG
	.ENDM
.PAGE
.SBTTL	"GET MCR COMMAND LINE" DIRECTIVE
;+
; GETMCR -- SUBROUTINE TO ISSUE A "GET MCR COMMAND LINE" DIRECTIVE
;
; CALLING SEQUENCE:
;
;	CALL GETMCR (IBUF,[ISW])
;
; ARGUMENT DESCRIPTIONS:
;
;	'IBUF' IS AN 80 BYTE ARRAY TO RECEIVE THE COMMAND LINE.
;	'ISW' IS AN INTEGER TO RECEIVE THE DIRECTIVE STATUS WORD.
;
;-
;
	.PICOD			;PLACE CODE IN PIC I-SPACE PSECT

GETMCR::
	MOV	#2,R2		; SPECIFY MAX ARG CT
	MOV	R2,R1		; SPECIFY REQ'D ARG BITMASK
	CALL	.X.PA1		; PUSH ARGUMENT ADDRESSES ONTO STACK,
	MOV	(SP)+,R1	; GET ARRAY ADDRESS IN R1
	MOV	(SP)+,R2	; ADDRESS OF DIRECTIVE STATUS RETURN
	SUB	#80.,SP		; USE STACK AS LINE BUFFER.
	MOV	(PC)+,-(SP)	; PUSH DIRECTIVE ONTO BUFFER
	.BYTE	127.,41.	; RSX 11-D DIRECTIVE
	MOV	SP,R0		; GET DPB ADDRESS
	MOV	R0,-(SP)	; PUSH DBP ADDRESS
;
	DIR$			; ISSUE DIRECTIVE
;
	TST	R2		; DSW SPECIFIED?
	BEQ	5$		; IF EQ NO
	MOV	@#$DSW,(R2)	; ELSE RETURN REQUESTED STATUS
5$:				;
	MOV	#40.,R0		; SET BYTE-PAIR COUNT
	TST	(SP)+		; POP FIRST WORD OF DPB
10$:
	MOVB	(SP),(R1)+	; MOVE LOW BYTE.
	SWAB	(SP)		; SET IN POSISITION TO..
	MOVB	(SP)+,(R1)+	; MOVE HIGH BYTE
	SOB	R0,10$		; MOVE ALL BYTE PAIRS.
;
	RETURN			; EXIT
;
	.END
;
;

