	.TITLE	RLSCT
	.IDENT	/02/
 
;
; COPYRIGHT (C) 1976
; DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE FOR USE ONLY ON A
; SINGLE COMPUTER SYSTEM AND MAY  BE  COPIED   ONLY  WITH  THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE,  OR
; ANY OTHER COPIES THEREOF, MAY NOT BE PROVIDED  OR  OTHERWISE
; MADE AVAILABLE TO ANY OTHER PERSON   EXCEPT FOR  USE ON SUCH
; SYSTEM AND TO ONE WHO AGREES TO THESE LICENSE  TERMS.  TITLE
; TO AND OWNERSHIP OF THE SOFTWARE SHALL AT ALL  TIMES  REMAIN
; IN DEC.
;
; THE INFORMATION IN THIS DOCUMENT IS SUBJECT TO CHANGE WITHOUT
; NOTICE AND SHOULD NOT BE CONSTRUED AS A COMMITMENT BY DIGITAL
; EQUIPMENT CORPORATION.
;
; DEC ASSUMES NO RESPONSIBILITY FOR THE USE OR RELIABILITY OF
; ITS SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DEC.
;         
; VERSION 02
;
; C. MONIA 10-JUL-76
;
; MODIFIED BY:
;
;	T. M. MARTIN	5-APR-81
;			ADD SUPPORT FOR USER I&D SPACE SUPPORT
;
;	T. M. MARTIN	9-NOV-81
;			PLACE CODE IN PIC I-SPACE PSECT
;
; FORTRAN CALLABLE ROUTINE TO RELEASE PHYSICAL MEMORY OWNED BY A WINDOW
; DESCRIPTOR
;
; MACRO LIBRARY CALLS
;
 
	.MCALL	RDBDF$,WDBDF$,.PICOD
	RDBDF$
	WDBDF$
 
;
; EQUATED SYMBOLS
;
; OFFSET TO REGION DESCRIPTOR THREAD WORD
;
;
 
	.ASECT
 
.=R.GPRO+2
RGNXT:	.BLKW	1		;
 
;
; ADDITIONAL WINDOW DESCRIPTOR OFFSETS
;
 
.=W.NSRB+2
WNBAS:	.BLKW	1		; OFFSET TO BLOCK OWNED BY WINDOW
WNSIZ:	.BLKW	1		; NEXT ADDRESS ABOVE WINDOW ALLOCATION
WNXT:	.BLKW	1		; THREAD WORD
 
	.PICOD			; PLACE CODE IN PIC I-SPACE PSECT
 
;+
;
; **-RLSCT-FORTRAN CALLABLE ROUTINE TO RELEASE PHYSICAL MEMORY
;	   OWNED BY A WINDOW DESCRIPTOR
;
; THIS SUBROUTINE IS CALLED BY A FORTRAN TASK TO RELEASE A BLOCK
; OF PHYSICAL MEMORY PREVIOUSLY ALLOCATED TO A WINDOW DESCRIPTOR.
;
; CALLING SEQUENCE:
;
;	CALL RLSCT (IREG,IWND)
;
; ARGUMENT DESCRIPTIONS:
;
;	IREG AND IWND ARE INTEGER ARRAYS SUPPLIED AS ARGUMENTS IN A
;	PREVIOUS CALL TO ALSCT. SPECIFIC ARRAY CONTENTS ARE AS DESCRI-
;	BED BELOW:
;
;	IREG  IS A ONE-DIMENSIONAL INTEGER ARRAY, NINE WORDS IN LENGTH.
;	      WORDS 1 THROUGH 8 CONTAIN A REGION DESCRIPTOR IN THE FORMAT
;	      REQUIRED BY THE SYSTEM MAPPING DIRECTIVES. THESE ELEMENTS
;	      ARE NOT REFERENCED BY RLSCT. WORD 9 CONTAINS THE WINDOW
;	      DESCRIPTOR THREAD WORD AS SETUP BY A PREVIOUS CALL TO
;	      ALSCT.
;
;	IWND  IS A ONE-DIMENSIONAL INTEGER ARRAY 11 WORDS IN LENGTH.
;	      WORDS 1 THROUGH 8 CONTAIN A WINDOW DESCRIPTOR IN THE FOR-
;	      MAT REQUIRED BY THE SYSTEM MAPPING DIRECTIVES. THESE ELEMENTS
;	      ARE NOT REFERENCED BY RLSCT. WORDS 9 THROUGH 11 CONTAIN
;	      THE FOLLOWING INFORMATION SETUP BY A PREVIOUS CALL TO ALSCT:
;
;			IWND(9) - BASE OFFSET OF ALLOCATION
;			IWND(10) - NEXT OFFSET ABOVE ALLOCATION
;			IWND(11) - WINDOW DESCRIPTOR BLOCK THREAD WORD
;
; UPON RETURN TO THE CALLER IWND(10) CONTAINS ALLOCATION SIZE IN UNITS OF
; 64-BYTE BLOCKS. THE DESCRIPTOR IS UNLINKED FROM THE REGION'S WINDOW LIST
; THUS DEALLOCATING THE PHYSICAL MEMORY PREVIOUSLY OWNED BY THE WINDOW.
;
;-
 
RLSCT::				;
	MOV	#2,R2		; SPECIFY MAX ARG CT
	MOV	#3,R1		; SPECIFY REQ'D ARG BITMASK
	CALL	.X.PA1		; PUSH ARGUMENT ADDRESSES
	MOV	(SP)+,R1	; GET ADDRESS OF REGION DESCRIPTOR
	MOV	(SP)+,R2	; GET WINDOW DESCRIPTOR ADDRESS
	ADD	#RGNXT-WNXT,R1	; ADJUST THREAD WORD BIAS
10$:				;
	MOV	R1,R0		; SET ADDRESS OF PREVIOUS
	MOV	WNXT(R1),R1	; GET ADDRESS OF NEXT
	BEQ	20$		; IF EQ DONE
	CMP	R1,R2		; FOUND WINDOW BLOCK?
	BNE	10$		; IF NE NO
	MOV	WNXT(R2),WNXT(R0) ; RELINK WINDOW DESCRIPTORS
	SUB	WNBAS(R2),WNSIZ(R2) ; COMPUTE WINDOW SIZE
20$:				;
	RETURN			;
 
	.END
 
