.IIF	NDF	L$$IST,	.NLIST
.IIF	NDF	L$$IST,	.DSABL	CRF
TITLE	RSXPRE,<RSX EMULATION PREFIX DEFINITION>,05,29-AUG-85,SSS/MJG

;
;		COPYRIGHT (c) 1974, 1985 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

.EQUATE	IOSTS,FIRQB

.SBTTL	FIP MONITOR CALL MACROS

.MACRO	DOFIP	OPCODE
	CALLX	..DOFP,R5
	  .WORD	OPCODE'FQ
.ENDM	DOFIP

.MACRO	UUO	OPCODE
	 MOVB	#UU.'OPCODE,FIRQB+FQFUN
	 .UUO
.ENDM	UUO

.SBTTL	PLAS MONITOR CALL MACRO

.MACRO	PLAS	OPCODE,ARG
	 MOV	#FIRQB+FQERNO,R4
	 MOV	#OPCODE'FQ,(R4)+
.IF	NB	<ARG>
	 CALL	MOVDPB
.ENDC
	 .PLAS
.ENDM	PLAS


.SBTTL	MESSAGE TEXT SETUP MACRO

.MACRO	MSGTXT	TEXT,TTYMOD,?TXT.B,?TXT.E
	  .WORD	TXT.B,TXT.E-TXT.B
	.IF	NB	<TTYMOD>
	  .WORD	TTYMOD
	.ENDC
M.TEXT	=.
TXT.B:	  .ASCII TEXT
TXT.E:	  .EVEN
.ENDM	MSGTXT

.SBTTL	KEYBOARD MONITOR KEYWORD MACRO

.MACRO	KBMCMD	ADDR,CMD
	  .WORD	ADDR
KB.CMD=.
	  .ASCIZ\CMD\
	  .EVEN
.ENDM	KBMCMD

.SBTTL	GENERALLY USEFUL MACROS

.MACRO	PUSH	LIST
.IF B	<LIST>
	 CLR	-(SP)
.IFF
.IRP	LOC,<LIST>
	 MOV	LOC,-(SP)
.ENDM	;.IRP
.ENDC	;.IF B <LIST>
.ENDM	PUSH

.MACRO	POP	LIST
.IF B	<LIST>
	 TST	(SP)+
.IFF
.IRP	LOC,<LIST>
	 MOV	 (SP)+,LOC
.ENDM	;.IRP
.ENDC	;.IF B <LIST>
.ENDM	POP

.MACRO	MOVPIC	LBL,LOC
.NTYPE	$$$$$1,LBL
.NTYPE	$$$$$2,LOC
.IF NE	$$$$$1-27
	.ERROR	; LBL IS NOT A LITERAL REFERENCE
.IFF
.IF EQ	$$$$$2-46
	 MOV	PC,LOC
	 ADD	LBL-.,(SP)
.IFF
.IIF NE	$$$$$2&70,	.ERROR	; LOC IS NOT A REGISTER OR -(SP) REFERENCE
	 MOV	PC,LOC
	 ADD	LBL-.,LOC
.ENDC	;$$$$$2-46
.ENDC	;$$$$$1-27
.ENDM	MOVPIC

.MACRO	MOVCLR	SOURCE,DESTINATION,SIZE
	CALLX	..MCLR,R5
	  .WORD	SOURCE
	  .WORD	DESTINATION
	  .WORD	SIZE
.ENDM	MOVCLR

.MACRO	MOVE	SOURCE,DESTINATION,SIZE
	CALLX	..MOVE,R5
	  .WORD	SOURCE
	  .WORD	DESTINATION
	  .WORD	SIZE
.ENDM	MOVE

.MACRO	CLEAR	SOURCE,SIZE
	CALLX	..CLR,R5
	  .WORD	SOURCE
	  .WORD	SIZE
.ENDM	CLEAR

.SBTTL	REGISTER SAVE AREA ON STACK FOR DIRECTIVE EMULATION

.DSECT

	.BLKW	6			;R0 THRU R5
USERSP:	.BLKW				;SP
USERPC:	.BLKW				;PC
USERPS:	.BLKW				;PS
USERRG:					;NO. OF BYTES SAVED ON STACK


.ENABL	GBL,AMA

.IIF	NDF	L$$IST,	.ENABL	CRF
.IIF	NDF	L$$IST,	.LIST
