;+
;	Title:	 BOM
;	Version: 01.00
;
;		Copyright (c) 1984 , 1985 BY
;	Digital Equipment Corporation, Maynard
;	 Massachusetts.  All rights reserved.
;
; This  software  is  furnished  under  a license and may be used
; and  copied  only in  accordance with the terms of such license
; and with  the inclusion  of the above  copyright  notice.  This
; software  or any other  copies  thereof, may not be provided or
; otherwise made  available to any other person.  No title to and
; ownership of the software is hereby transfered.
;
; The information  in this document is subject  to change without
; notice and should not be construed as a commitment  by  Digital
; Equipment Corporation.
;
; Digital assumes no responsibility for the use or reliability of
; its software on equipment that is not supplied by Digital.
;
;	Date:	21-November-1984
; 	Author:	B. S. McCarthy
;
;	Contents: Breakpoint or message (BOM$) Macro definitions
;-

;+
;			'BOM$C'
;
; THE BOM$C MACRO GENERATES A DPB FOR THE "BREAKPOINT OR MESSAGE"
; DIRECTIVE IN A SEPARATE PSECT AND AN EXECUTIVE TRAP IN 
; THE USER SPECIFIED PROGRAM PSECT.
;
; MACRO CALL:		BOM$C	STS,FLG,PSCT,ERR
;
; DESCRIPTION:  THIS MACRO GENERATES A DPB FOR THE
; "BREAKPOINT OR MESSAGE" DIRECTIVE IN THE PROGRAM SECTION
; NAMED '$DPB$.'.  THE DPB PARAMETERS ARE DESCRIBED
; IN THE BOM$ MACRO.  THIS MACRO THEN GENERATES
; AN EXECUTIVE TRAP IN THE ORIGINAL PROGRAM CONTROL
; SECTION NAMED IN 'PSCT'.  THE ERROR SERVICE ADDRESS,
; 'ERR', IS DESCRIBED IN THE DIR$ MACRO.  THE GENERATION
; OF SYMBOLIC OFFSETS IS CONTROLLED BY THE BOM$
; MACRO.
;
;-
	.MACRO	BOM$C	STS,FLG,CS,ERR
	.MCALL	BOM$,DIR$
	.IF NDF	$$$GLB
	.PSECT	$DPB$.,D
$$$=.
	.IFTF
	BOM$	STS,FLG
	.IFT
	.PSECT	CS
	DIR$	#$$$,ERR
	.ENDC
	.ENDM	BOM$C
;+
;			'BOM$S'
;
; THE BOM$S MACRO GENERATES THE CODE TO PUSH A DPB FOR THE
; "BREAKPOINT OR MESSAGE" DIRECTIVE ON THE STACK AND AN EXECUTIVE TRAP.
;
; MACRO CALL:		BOM$S	STS,FLG,ERR
;
; DESCRIPTION:  THIS MACRO GENERATES THE CODE TO PUSH A DPB 
; FOR THE "BREAKPOINT OR MESSAGE" DIRECTIVE ON THE STACK.  THE DPB
; ARGUMENTS MUST BE VALID ASSEMBLER SOURCE OPERANDS,
; AND THEY MUST SPECIFY THE INFORMATION DESCRIBED IN
; THE BOM$ MACRO.  THIS MACRO ALSO GENERATES AN EXECUTIVE
; TRAP, USING THE ERROR SERVICE ADDRESS, 'ERR', AS DESCRIBED
; IN THE DIR$ MACRO.
;
;-
	.MACRO	BOM$S	STS,FLG,ERR
	.MCALL	MOV$,DIR$,OFF$
	MOV$	FLG
	MOV$	STS
	MOV	(PC)+,-(SP)
	.BYTE	211.,3
	DIR$	,ERR
	.ENDM	BOM$S
;+
;			'BOM$'
;
; THE BOM$ MACRO GENERATES A DPB FOR THE "BREAKPOINT OR MESSAGE"
; DIRECTIVE.
;
; MACRO CALL:		BOM$	STS,FLG
;
; DESCRIPTION:  THIS MACRO GENERATES A DPB FOR THE
; "BREAKPOINT OR MESSAGE" DIRECTIVE.  THE ARGUMENTS ARE ASSUMED
; TO HAVE THE FOLLOWING MEANINGS:
;
;	STS=EXIT STATUS.
;	FLG=FLAGS WORD
;
; THE FOLLOWING SYMBOLS ARE LOCALLY DEFINED WITH THEIR
; ASSIGNED VALUES EQUAL TO THE BYTE OFFSET FROM THE
; START OF THE DPB TO THE RESPECTIVE DPB ELEMENTS:
;
;	B.OMCD-(LENGTH 2) ABORT CODE
;	B.OMFL-(LENGTH 2) FLAGS WORD
;
; IF THE MACRO IS INVOKED WITH THE SYMBOL '$$$GLB'
; DEFINED, THE DPB IS NOT GENERATED, AND THE
; SYMBOLIC OFFSETS ARE DEFINED GLOBALLY.
;
;-
	.MACRO	BOM$	STS,FLG
	.MCALL	OFF$
	.IF NDF	$$$GLB
	.BYTE	211.,3
	.WORD	STS
	.IF NB FLG
	.WORD	FLG
	.IFF
	.WORD	0
	.ENDC
	.ENDC
	.IF NDF	B.OMCD
	.NLIST
$$$OST=0
	OFF$
	OFF$	B.OMCD,2
	OFF$	B.OMFL,2
	.LIST
	.ENDC
	.ENDM	BOM$

;+
;			'BOMDF$'
;
; THE BOMDF$ MACRO DEFINES THE BOM ABORT CODES AND FLAGS BITS.
;
;	* * *   W A R N I N G   * * *
;
; THE CODES ARE CURRENTLY ALSO DEFINED IN ABODF$ FOR TKTN. THIS
; SHOULD BE CLEANED UP SOME DAY.
;
; MACRO CALL:		BOMDF$
;
;-
	.MACRO	BOMDF$
	.NLIST
;
; TERMINATION CODES FOR BOM$
;
; NOTE:
;
; THE NORMAL TKTN ERROR CODES SPAN -4 THROUGH 42. THE BOM CODES,
; ALTHOUGH DEFINED FOR THE TASK, ETC, AS 0 THROUGH N, ARE PASSED TO
; TKTN AS -127 THROUGH -127+N. AN UNRECOGNIZED CODE IS PASSED AS -128,
; UNKNOWN ERROR.
;

S.BUNK=-128.			;UNKNOWN BOM$ ERROR
S.BFEI=0.			;ERROR IN HIGH LEVEL LANGUAGE INTERFACE
S.BOVL=1.			;LOAD OVERLAY FAILURE
;
; BIT DEFINITIONS FOR BOM$ FLAGS WORD
;
S.BBIF=1.			;ENABLE CONDITIONAL BREAKPOINT
	.LIST
	.ENDM	BOMDF$


