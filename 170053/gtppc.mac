	.TITLE	.GTPAR
	.IDENT	/05/
;
;		COPYRIGHT (c) 1981 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD
;	 MASSACHUSETTS.  ALL RIGHTS RESERVED.
;
; COPYRIGHT   1974,  DIGITAL  EQUIPMENT  CORP.,  MAYNARD,   MASS.
;
; THIS  SOFTWARE  IS  FURNISHED  UNDER  A LICENSE AND MAY BE USED
; AND  COPIED  ONLY IN  ACCORDANCE WITH THE TERMS OF SUCH LICENSE
; AND WITH  THE INCLUSION  OF THE ABOVE  COPYRIGHT  NOTICE.  THIS
; SOFTWARE  OR ANY OTHER  COPIES  THEREOF, MAY NOT BE PROVIDED OR
; OTHERWISE MADE  AVAILABLE TO ANY OTHER PERSON.  NO TITLE TO AND
; OWNERSHIP OF THE SOFTWARE IS HEREBY TRANSFERED.
;
; THE INFORMATION  IN THIS DOCUMENT IS SUBJECT  TO CHANGE WITHOUT
; NOTICE AND SHOULD NOT BE CONSTRUED AS A COMMITMENT  BY  DIGITAL
; EQUIPMENT CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE OR RELIABILITY OF
; ITS SOFTWARE ON EQUIPMENT THAT IS NOT SUPPLIED BY DIGITAL.
;
;
; VERSION 05
;
; C.MONIA 24-JUN-74
;
; MODIFIED BY:
;
;	T. M. MARTIN	5-APR-81
;			ADD SUPPORT FOR USER I&D SPACE
;
;	T. M. MARTIN	9-NOV-81
;			PLACE CODE IN PIC I-SPACE PSECT
;
; FORTRAN CALLABLE SUBROUTINES TO ISSUE THE FOLLOWING DIRECTIVES:
;
;	"GET PARTITION PARAMETERS"
;	"GET COMMON BLOCK PARAMETERS"
;
; MACRO LIBRARY CALLS
;
 
	.MCALL	CALL,RETURN,.PICOD
 
;
; LOCAL MACROS
;
; SOB	REG,TAG
;

	.MACRO	SOB	REG,TAG
	DEC	REG
	BNE	TAG
	.ENDM

.PAGE
.SBTTL	"GET PARTITION PARAMETERS" DIRECTIVE.
;
;+
; GETPAR -- SUBROUTINE TO ISSUE A "GET PARTITION PARAMETERS" DIRECTIVE.
;
; CALLING SEQUENCE:
;
;	CALL GETPAR ([PARNAM],IBUF,[IDS])
;
; ARGUMENT DESCRIPTIONS:
;
;	'PARNAM' IS A TWO-WORD 1-6 CHAR PARTITION NAME IN RADIX-50 FORM
;	'IBUF' IS A THREE WORD INTEGER ARRAY TO RECEIVE PARTITION
;		PARAMETERS.
;	'IDS' IS AN INTEGER  TO RECEIVE THE DIRECTIVE STATUS WORD
;
;-
;
	.PICOD			; PLACE CODE IN PIC I-SPACE PSECT
GETPAR::
	MOV	(PC)+,R0	; GET FIRST WORD OF DIRECTIVE IN R0
	.BYTE	65.,4.
	MOV	#3,R2		; SPECIFY MAX ARG CT
	MOV	#002,R1		; SPECIFY REQ'D ARG BITMASK
	CALL	.X.PA1		; PUSH ARGUMENT ADDRESSESS,
;
	BR	PARCMN		; TO COMMON CODE
;


	.IF	NDF	R$$11M

.SBTTL	"GET COMMON BLOCK PARAMETERS" DIRECTIVE.
;
;+
; GETCMN -- SUBROUTINE TO ISSUE A "GET COMMON BLOCK PARAMETERS"
;	    DIRECTIVE.
;
; CALLING SEQUENCE:
;
;	CALL GETCMN (CMNNAM,IBUF,[ISW])
;
; ARGUMENT DESCRIPTIONS:
;
;	CMNNAM IS A 1-6 CHARACTER COMMON BLOCK NAME IN RADIX-50 FORM
;	IBUF IS AN 8 WORD INTEGER ARRAY TO RECEIVE COMMON BLOCK
;	     PARAMTERS
;	ISW IS AN INTEGER TO RECEIVE THE DIRECTIVE STATUS WORD.
;
;-
;
GETCMN::
	MOV	(PC)+,R0	; GET FIRST WORD OF DIRECTIVE IN R0
	.BYTE	67.,4.
;
	MOV	#3,R2		; SPECIFY MAX ARG CT
	MOV	#006,R1		; SPECIFY REQ'D ARG BITMASK
	CALL	.X.PA1		; PUSH ARGUMENT ADDRESSES, 

	.ENDC

PARCMN:				; COMMON CODE FOR 'GET PARAMETERS'
				; DIRECTIVES
;
;
	MOV	(SP)+,R4	; SET R4 TO ADDRESS OF  NAME
	BNE	10$		; WAS NAME SPECIFIED?
	CLR	-(SP)		; NO -- PUSH TWO ZEROS
	CLR	-(SP)		;
	BR	20$		;
;
10$:
	MOV	2(R4),-(SP)	; PUSH SECOND HALF OF NAME
	MOV	(R4),-(SP)	; FIRST HALF.
20$:
	MOV	R0,-(SP)	; PUSH FIRST WORD OF DIRECTIVE.
	JMP	.X.EX1		; ISSUE DIRECTIVE, EXIT
;
	.END
;

