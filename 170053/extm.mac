;+
;		'EXTM$C'
;
; THE EXTM$C MACRO GENERATES A DPB FOR THE "EXTEND TASK WITH MASK"
; DIRECTIVE IN A SEPARATE PSECT AND A MONITOR TRAP IN THE USER
; SPECIFIED PSECT.
;
; MACRO CALL:	EXTM$C	INC,MASK,PSCT,ERR
;
; DESCRIPTION: THIS MACRO GENERATES A DPB FOR THE "EXTEND TASK WITH
; MASK" DIRECTIVE IN THE CONTROL SECTION NAMED '$DPB$.'.  THE DPB
; PARAMETERS ARE DESCRIBED IN THE EXTM$ MACRO.  THIS MACRO
; THEN GENERATES A MONITOR TRAP IN THE ORIGINAL PROGRAM CONTROL
; SECTION NAMED IN 'PSCT'.  THE ERROR SERVICE ADDRESS, 'ERR',
; IS DESCRIBED IN THE DIR$ MACRO.  THE GENERATION OF SYMBOLIC
; OFFSETS IS CONTROLLED BY THE EXTM$ MACRO.
;
;-
	.MACRO	EXTM$C	INC,MASK,CS,ERR
	.MCALL	EXTM$,DIR$
	.IF NDF	$$$GLB
	.PSECT	$DPB$.,D
$$$=.
	.IFTF
	EXTM$	INC,MASK
	.IFT
	.PSECT	CS
	DIR$	#$$$,ERR
	.ENDC
	.ENDM	EXTM$C
;+
;		'EXTM$S'
;
; THE EXTM$S MACRO GENERATES THE CODE TO PUSH A DPB FOR THE "EXTEND
; TASK WITH MASK" DIRECTIVE ON THE STACK AND A MONITOR TRAP.
;
; MACRO CALL:	EXTM$S	INC,MASK,ERR
;
; DESCRIPTION: THIS MACRO GENERATES THE CODE TO PUSH A DPB FOR
; THE "EXTEND TASK" DIRECTIVE ON THE STACK.  THE DPB ARGUMENTS
; MUST BE VALID ASSEMBLER SOURCE OPERANDS, AND THEY MUST SPECIFY
; THE INFORMATION DESCRIBED IN THE EXTM$ MACRO.  THIS MACRO ALSO
; GENERATES A MONITOR TRAP, USING THE ERROR SERVICE ADDRESS,
; 'ERR', AS DESCRIBED IN THE DIR$ MACRO.
;
;-
	.MACRO	EXTM$S	INC,MASK,ERR
	.MCALL	MOV$,DIR$
	MOV$	MASK
	MOV$	INC
	MOV	(PC)+,-(SP)
	.BYTE	89.,3
	DIR$	,ERR
	.ENDM	EXTM$S
;+
;		'EXTM$'
;
; THE EXTM$ MACRO GENERATES A DPB FOR THE "EXTEND TASK WITH MASK"
; DIRECTIVE.
;
; MACRO CALL:	EXTM$	INC,MASK
;
; DESCRIPTION: THIS MACRO GENERATES A DPB FOR THE "EXTEND TASK WITH MASK"
; DIRECTIVE.  THE ARGUMENT IS ASSUMED TO HAVE THE FOLLOWING
; MEANING:
;
;	INC=TASK SIZE INCREMENT IN 32 WORD BLOCKS.
;
; THE FOLLOWING SYMBOL IS LOCALLY DEFINED WITH ITS ASSIGNED
; VALUE EQUAL TO THE BYTE OFFSET FROM THE START OF THE DPB TO
; THE RESPECTIVE DPB ELEMENT:
;
;	E.XTIN-(LENGTH 2 BYTES) TASK SIZE INCREMENT.
;
; IF THE MACRO IS INVOKED WITH THE SYMBOL '$$$GLB' DEFINED, THE
; DPB IS NOT GENERATED, AND THE SYMBOLIC OFFSETS ARE DEFINED
; GLOBALLY.
;
;-
	.MACRO	EXTM$	INC,MASK
	.MCALL	OFF$
	.IF NDF	$$$GLB
	.BYTE	89.,3
	.WORD	INC
	.IF NB MASK
	.WORD	MASK
	.IFF
	.WORD	0
	.ENDC
	.ENDC
	.IF NDF	E.XTIN
	.NLIST
	OFF$
	OFF$	E.XTIN,2
	.LIST
	.ENDC
	.ENDM	EXTM$
