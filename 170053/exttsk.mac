	.TITLE	EXTTSK
	.IDENT	/02/
;
;		COPYRIGHT (c) 1981 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD
;	 MASSACHUSETTS.  ALL RIGHTS RESERVED.
;
; COPYRIGHT (C) 1976
;
; THIS  SOFTWARE  IS  FURNISHED  UNDER  A LICENSE AND MAY BE USED
; AND  COPIED  ONLY IN  ACCORDANCE WITH THE TERMS OF SUCH LICENSE
; AND WITH  THE INCLUSION  OF THE ABOVE  COPYRIGHT  NOTICE.  THIS
; SOFTWARE  OR ANY OTHER  COPIES  THEREOF, MAY NOT BE PROVIDED OR
; OTHERWISE MADE  AVAILABLE TO ANY OTHER PERSON.  NO TITLE TO AND
; OWNERSHIP OF THE SOFTWARE IS HEREBY TRANSFERED.
;
; THE INFORMATION  IN THIS DOCUMENT IS SUBJECT  TO CHANGE WITHOUT
; NOTICE AND SHOULD NOT BE CONSTRUED AS A COMMITMENT  BY  DIGITAL
; EQUIPMENT CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE OR RELIABILITY OF
; ITS SOFTWARE ON EQUIPMENT THAT IS NOT SUPPLIED BY DIGITAL.
;
;
;         
; VERSION 02
;
; C. MONIA 12-JUL-76
;
; FORTRAN CALLABLE SUBROUTINE TO EXTEND THE TASK REGION
;
;
; MODIFIED BY:
;
;	T. M. MARTIN	5-APR-81
;			ADD SUPPORT FOR USER I&D SPACE
;
;	T. M. MARTIN	9-NOV-81
;			PLACE CODE IN PIC I-SPACE PSECT
;
;+
; **-EXTTSK-EXTEND THE TASK REGION
;
; CALLING SEQUENCE:
;
;	CALL EXTTSK ([INC][,IDSW])
;
; ARGUMENT DESCRIPTIONS:
;
;	INC  IS AN INTEGER CONTAINING THE EXTEND INCREMENT IN UNITS OF
;	     64-BYTE BLOCKS. IF UNSPECIFIED A VALUE OF ZERO IS ASSUMED.
;
;	IDSW  IS AN INTEGER TO RECEIVE THE CONTENTS OF THE DIRECTIVE
;	      STATUS WORD.
;
;
;-
	.MCALL	.PICOD
	.PICOD			; PLACE CODE IN PIC I-SPACE PSECT 
EXTTSK::			;
	MOV	#2,R2		; SPECIFY MAX ARG CT
	CLR	R1		; SPECIFY REQ'D ARG BITMASK
	CALL	.X.PA1		; PUSH ARGUMENT ADDRESSES, SET R1=R2=SP
	MOV	(R1)+,-(SP)	; COPY ADDRESS OF EXTEND INCREMENT
	BEQ	10$		; IF EQ NONE
	MOV	@(SP)+,-(SP)	; REPLACE ADDRESS WITH INCREMENT
	CLR	(R2)		; CLEAR SECOND WORD OF DPB
10$:				;
	MOV	(PC)+,-(SP)	; PUSH DIC AND DPB LENGTH
	.BYTE	89.,3.		; DIC, DPB SIZE
	JMP	.X.EX1		; ISSUE DIRECTIVE, EXIT
 
	.END
