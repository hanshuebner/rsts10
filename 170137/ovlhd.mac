	.TITLE OHANDL.006 OVERLAY HANDLER
	.IDENT	/V01.00 /

; RT-11 OVERLAY HANDLER
;
; COPYRIGHT (C) 1979
; DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS. 01754
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE FOR USE ONLY ON A
; SINGLE COMPUTER SYSTEM AND MAY BE COPIED ONLY WITH THE INCLUSION
; OF THE ABOVE COPYRIGHT NOTICE. THIS SOFTWARE, OR ANY OTHER
; COPIES THEREOF, MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE
; TO ANY OTHER PERSON EXCEPT FOR USE ON SUCH SYSTEM AND TO ONE WHO
; AGREES TO THESE LICENSE TERMS. TITLE TO AND OWNERSHIP OF THE 
; SOFTWARE SHALL AT ALL TIMES REMAIN IN DEC.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE WITHOUT
; NOTICE AND SHOULD NOT BE CONSTRUED AS A COMMITMENT BY DIGITAL
; EQUIPMENT CORPORATION.
;
; DEC ASSUMES NO RESPONSIBILITY FOR THE USE OR RELIABILITY OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DEC.
;
; MAS


; EDIT LOG SINCE V01.00
; ADD NEW GLOBAL NAMES TO ALLOW RELOCATION OF HANDLER CODE		;MAS01

.SBTTL	THE RUN-TIME OVERLAY HANDLER

;+
; THE FOLLOWING CODE IS INCLUDED IN THE USER'S PROGRAM BY THE
; LINKER WHENEVER OVERLAYS ARE REQUESTED BY THE USER.
; THE RUN-TIME OVERLAY HANDLER IS CALLED BY A DUMMY
; SUBROUTINE OF THE FOLLOWING FORM:
;
;	JSR	R5,$OVRH	;CALL TO COMMON CODE
;	.WORD	<OVERLAY #*6>	;# OF DESIRED SEGMENT
;	.WORD	<ENTRY ADDR>	;ACTUAL CORE ADDR (VIRTUAL ADDR)
;
; ONE DUMMY ROUTINE OF THE ABOVE FORM IS STORED IN THE RESIDENT PORTION
; OF THE USER'S PROGRAM FOR EACH ENTRY POINT TO AN OVERLAY SEGMENT.
; ALL REFERENCES TO THE ENTRY POINT ARE MODIFIED BY THE LINKER TO INSTEAD
; BE REFERENCES TO THE APPROPRIATE DUMMY ROUTINE.  EACH OVERLAY SEGMENT
; IS CALLED INTO CORE AS A UNIT AND MUST BE CONTIGUOUS IN CORE.  AN
; OVERLAY SEGMENT MAY HAVE ANY NUMBER OF ENTRY POINTS, TO THE LIMITS
; OF CORE MEMORY.  ONLY ONE SEGMENT AT A TIME MAY OCCUPY AN OVERLAY REGION.
;-



.SBTTL	DEFINITIONS, AND MISC.

;+
; UNDEFINED GLOBALS IN THE OVERLAY HANDLER MUST BE NAMED "$OVDF1" TO
; "$OVDFn" SUCH THAT A RANGE CHECK MAY BE DONE BY LINK TO DETERMINE IF 
; THE UNDEFINED GLOBAL NAME IS FROM THE OVERLAY HANDLER. A CHECK IS 
; DONE ON THE .RAD50 CHARACTERS "$OV", AND THEN A RANGE CHECK IS DONE ON
; THE .RAD50 CHARATERS "DF1" TO "DFn". THESE GLOBAL SYMBOLS DO NOT APPEAR
; ON LINK MAPS, SINCE THEIR VALUE IS NOT KNOWN UNTILL AFTER THE MAP HAS BEEN
; PRINTED. CURRENTLY $OVDF1 TO $OVDF5 ARE IN USE.
;
;
; GLOBAL SYMBOLS O$READ, AND O$DONE ARE USEFULL WHEN DEBUGGING
; OVERLAID PROGRAMS.
;
; O$READ:: WILL APPEAR IN THE LINK MAP, AND LOCATES THE .READ STATEMENT
; IN THE OVERLAY HANDLER.
; 
; O$DONE:: WILL APPEAR IN THE LINK MAP, AND LOCATES THE FIRST INSTRUCTION 
; AFTER A .READ IS COMPLEATED IN THE OVERLAY HANDLER.
;-


.MCALL	.READW,..V1..
	..V1..

.SBTTL	OVERLAY HANDLER CODE

.PSECT	$OHAND,GBL

.ENABL	GBL
.ENABL	LSB

; $OVRH IS THE ENTRY POINT TO THE OVERLAY HANDLER

$OVRH::	MOV	R0,-(SP)	;MUST SAVE SINCE READ ETC USE IT
	MOV	R1,-(SP)	;/O OVERLAY ENTRY POINT
	MOV	R2,-(SP)

2$:
;	MOV	@R5,R1		;PICK UP OVERLAY NUMBER			
	BR	7$		;FIRST CALL ONLY * * *
	ADD	#$OVTAB-6,R1	;CALC TABLE ADDR			
	MOV	(R1)+,R2	;GET FIRST ARG. OF OVERLAY SEG. ENTRY

3$:	CMP	(R5)+,@R2	;IS OVERLAY ALREADY RESIDENT?
	BEQ	4$		;YES, BRANCH TO IT			

;+
; THE .READ USES INFORMATION AS FOLLOWS:
; CHANNEL NUMBER, CORE ADDRESS, LENGTH TO READ, RELATIVE BLOCK ON DISK.
; THESE ARE PICKED UP IN REVERSE ORDER OF THAT SPECIFIED IN THE CALL.
;-

O$READ::.READW	17,R2,@R1,(R1)+ ;READ FROM OVERLAY FILE		
O$DONE::BCS	5$
4$:	MOV	(SP)+,R2	;RESTORE USERS REGS
	MOV	(SP)+,R1
	MOV	(SP)+,R0
	MOV	@R5,R5		;GET ENTRY ADDRESS
	RTS	R5		;ENTER OVERLAY ROUTINE AND RESTORE USER'S R5

5$:	EMT	376		;SYSTEM ERROR 10 (OVERLAY I/O)
	.BYTE	0,373

7$:	MOV	#11501,2$	;RESTORE SWITCH INSTR (MOV @R5,R1)	
	MOV	(PC)+,R1	;START ADDR FOR CLEAR OPERATION
$ODF1::	 .WORD	$OVDF1		;HIGH ADDR OF ROOT SEGMENT		;MAS01
8$:	CLR	(R1)+		;CLEAR ALL OVERLAY REGIONS
	CMP	R1,$ODF2	;DONE?						
	BLO	8$		;LO -> NO, REPEAT
	BR	2$		;AND RETURN TO CALL IN PROGRESS

$ODF2::	.WORD 	$OVDF2		;HIGH ADDRESS OF /O OVERLAYS		;MAS01

.DSABL	LSB

.SBTTL	$OVTAB	OVERLAY TABLE

;+
; OVERLAY SEGMENT TABLE FOLLOWS:
; $OVTAB:	.WORD	<CORE ADDR>,<RELATIVE BLK>,<WORD COUNT>   /O OVERLAYS
;
; THREE WORDS PER ENTRY, ONE ENTRY PER OVERLAY SEGMENT.
;
; ALSO, THERE IS ONE WORD PREFIXED TO EACH OVERLAY REGION
; THAT IDENTIFIES THE SEGMENT CURRENTLY RESIDENT IN THAT REGION.
; THIS WORD IS AN INDEX INTO THE TABLE.
;-

.PSECT	$OTABL,D,GBL,OVR

$OVTAB::

.END
