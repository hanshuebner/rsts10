.IIF	NDF	PFXLST,	.NLIST
.IIF	NDF	PFXLST,	.DSABL	CRF
TITLE	SGNCOM,<SYSGEN COMMON DEFINITIONS>,0A,10-MAY-91,JDM/GW/ABC/MJG/PRL

;
;		COPYRIGHT (c) 1974, 1991 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

.SBTTL	SGNCOM EDIT HISTORY
;+
; EDIT	BY	  DATE			REASON
;
;  01	JJT	09-Jul-85		Make PARAM macro create global symbols
;  02	JJT	09-Jul-85		Create PARAML macro for local symbols
;-


	.ENABL	LC

	.EQUATE	IOSTS,FIRQB,NOCREF ;LOCATION OF ERROR

; FREQUENTLY USED CHARACTERS

	.EQUATE	HT,	11,NOCREF
	.EQUATE	LF,	12,NOCREF
	.EQUATE	FF,	14,NOCREF
	.EQUATE	CR,	15,NOCREF
	.EQUATE	ESC,	33,NOCREF
	.EQUATE	CRLF,	5015,NOCREF
	.EQUATE	TERM,	200,NOCREF

; THE FOLLOWING SYMBOL VALUE(S) ARE REPLACED IN THE SUBR "WRITE"
; (IN SGNRT.MAC) BY ASCII CHARS DEFINED IN THE TABLE STARTING
; AT SPLCHR: (IN SYSGEN.MAC). THE LOW-ORDER 7 BIT VALUE OF EACH
; SYMBOL SERVES AS THE INDEX INTO THE SPLCHR TABLE.

	.EQUATE	PPN,	201,NOCREF

.SBTTL	DEFINE MESSAGE MACROS

.MACRO	.MESS			;BEGIN MESSAGE
.DSABL	CRF
.NLIST
.IRP	N,<\MESNUM>
	.WORD	MSS'N
.IRP	MSG,<\MSGSEG>
.ENABL	CRF
	TMPORG	Q'MSG'MESS
.DSABL	CRF
.ENDM
	.EVEN
	.WORD	MAS'N		;POINTS TO ABBREVIATION, IF ANY
.LIST
.LIST

MSS'N:
.NLIST
.NLIST
	.WORD	MSE'N-MSS'N-2	;BYTE COUNT
.ENDM
.NLIST	BEX
.LIST
.ENABL	CRF
.ENDM	.MESS

.MACRO	ME.SS			;END MESSAGE - BEGIN ABBREVIATION
.DSABL	CRF
.NLIST
.IRP	N,<\MESNUM>
	.BYTE	TERM		;MESSAGE TERMINATOR
MSE'N:				;END OF MESSAGE
	.EVEN
MAS'N:
	.WORD	MAE'N-MAS'N-2
.ENDM
.LIST
.ENABL	CRF
.ENDM	ME.SS

.MACRO	MESS.			;END ABBREVIATION AND ENTIRE MESSAGE
.DSABL	CRF
.NLIST
.IRP	N,<\MESNUM>
	.BYTE	TERM		;MESSAGE TERMINATOR
MAE'N:				;END OF ABBREVIATION
.IF	NDF	MAS'N		;IF NO ABBREVIATION,
MAS'N	=	0		;	INDICATE IT AND
MSE'N:				;	MESSAGE ENDS HERE
.ENDC
.ENDM
MESNUM	=	MESNUM+1	;INCREMENT MESSAGE NUMBER
.LIST
.LIST

.NLIST
.ENABL	CRF
	UNORG			;BACK TO CODE AREA
.ENDM	MESS.

.SBTTL	DEFINE MISCELLANEOUS USEFUL MACROS

.MACRO	.TAG			;DEFINE NEXT SEQUENTIAL TAG
.DSABL	CRF
TAGNUM	=	TAGNUM+1
.IRP	N,<\TAGNUM>
.ENABL	CRF
.LIST
TAG'N:
.NLIST
.ENDM
.ENDM	.TAG

.MACRO	LOOP	C,FUDGE		;BRANCH ON CONDITION 'C' TO CURRENT "TAG"
.DSABL	CRF
.IRP	N,<\<FUDGE+TAGNUM>>
.ENABL	CRF
.IF	NB	<C>
	 B'C	TAG'N
.IFF
	 BR	TAG'N
.ENDC
.ENDM
.ENDM	LOOP

.MACRO	SKIP	C,FUDGE=1	;SKIP TO NEXT "TAG" ON CONDITION 'C'
	LOOP	<C>,+'FUDGE
.ENDM	SKIP

.MACRO	BACK	C,FUDGE=1	;BACK TO PREVIOUS "TAG" ON CONDITION "C"
	LOOP	<C>,-'FUDGE
.ENDM	BACK

.MACRO	ASK			;ASK A QUESTION
	CALLX	QUERY,R5
.ENDM	ASK

.MACRO	TELL			;TELL SOMETHING - WARNING, ERROR, ETC.
	CALLX	PRINT,R5
.ENDM	TELL

.MACRO	TELLR0			;TELL SOMETHING - WARNING, ERROR, ETC
	CALLX	PRINT0,R5
.ENDM	TELLR0

.MACRO	BATCH			;WRITE TEXT TO BATCH FILE
	CALLX	WRITE,R5,LNKBAT
GLOBAL	<LNKBAT>
.ENDM	BATCH

.MACRO	CONFIG			;WRITE TEXT TO CONFIGURATION FILE
	CALLX	WRITE,R5,LNKCFG
GLOBAL	<LNKCFG>
.ENDM	CONFIG

.MACRO	PARAM	NAME,VALUE,COMMENT
	.ASCII	%'NAME'%<HT>%==%					;01
	.BYTE	40,40
	.=.-2
	.ODD
	.BYTE	HT
NAME':	.ASCII	<40><40><40><40><40><40><40>
.	=	NAME
	.ASCII	%'VALUE'%
.	=	NAME'+7
	.ASCII	<HT>%;'COMMENT'%<CR><LF>
.ENDM	PARAM

.MACRO	PARAML	NAME,VALUE,COMMENT					;02
	.ASCII	%'NAME'%<HT>%=%						;02
	.BYTE	40,40							;02
	.=.-2								;02
	.ODD								;02
	.BYTE	HT							;02
NAME':	.ASCII	<40><40><40><40><40><40><40>				;02
.	=	NAME							;02
	.ASCII	%'VALUE'%						;02
.	=	NAME'+7							;02
	.ASCII	<HT>%;'COMMENT'%<CR><LF>				;02
.ENDM	PARAML								;02

.IIF	NDF	PFXLST,	.ENABL	CRF
.IIF	NDF	PFXLST,	.LIST

