	.INCLUDE /CMN:COMMON/
	.INCLUDE /SGN:SGNCOM/
TITLE	SGNOPT,<SYSGEN USER OPTIONS>,0A,10-MAY-91,JDM/GW/ABC/MJG/AWL/SJM/AWL/PRL/KCG/KPH/JJT

;
;		COPYRIGHT (c) 1974, 1991 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

.SBTTL	SGNOPT EDIT HISTORY
;+
;EDIT	BY		DATE		REASON
; 09	AWL  		03-AUG-81	DSK phase is required
;                                       Removed DPHASE
;                                       Remove question for large file processor
;
;                       06-OCT-81       Add SMLBUF = XXX. to CONFIG.MAC
; 10	SJM		21-OCT-81	Conditionally add back Small FIP
;
; 11    AWL             29-OCT-81       Add resident overlays to CONFIG.MAC
; 12    AWL             06-NOV-81       Add <FF> after copyright
;
; 13	PRL		26-JAN-82	Add APT = X to CONFIG.MAC
; 14	PRL		23-Mar-82	Remove "small link" code
; 
; --	KCG		14-Sep-82	Creation of 8.0 from 7.2
; 15	KCG		14-Sep-82	Add EMTLOG question after STATS.
; 16    KCG		05-Oct-82	Increase System Wide Logical Max
;					to 100.
; 17	KCG		05-Oct-82	Make Resident Send/Rec mandatory if
;					EMT logging wanted.
; 18 	KCG		30-Nov-82	Make default number of System Wide
;					Logicals to 25.
; 19	KCG		09-Jan-83	Change FIP Buffer to Directory Caching
;					       Extended Data Buffering to 
;								Data Caching
;					Resident Code text revision.
; 20	KPH		20-Jan-83	Make resident libraries and RSX
;					emulation mandatory.
;
; --    JJT		26-Apr-84	Creation of 9.0 from 8.0
; 21	KPH		25-Sep-84	Remove resident overlay stuff
; 22	JJT		09-Jul-85	Make some config.mac symbols local
; 23	JJT		07-Jan-86	V9.3 changes
;
;			[RSTS V9.6]
;  024  KPH  		06-Nov-87	Remove APT stuff
;  025	JJT		03-Mar-88	Remove Statistic question
;  026	JJT		03-Jun-88	Increase default for small buffers
;-

	.PAGE
.SBTTL	USER SELECTABLE OPTIONS

	ORG	Q6CODE

.ENABL	LC

MESNUM	=	0
MSGSEG	=	6
TAGNUM	=	0
SMLFIP	=	0		; IF <>0 THEN ASK WHICH FIP TO USE



SGNOPT::	NOP		;REPLACE WITH 000003 TO ENABLE ODT
	CLR	CHGSEC		;INIT THE CHANGE SECTION QUESTION FLAG
	TST	CHGCFG		;ARE WE CHANGING THE CONFIGURATION?
	BEQ	FSTQST		;NO, GO AUTO-ANSWER THE FIRST QUESTION
.TAG
	TELL
	.MESS
	.ASCII	<CR><LF><LF>
	.ASCII	%The following questions  deal  with  the%<CR><LF>
	.ASCII	%capacity  and  features  of  this RSTS/E%<CR><LF>
	.ASCII	%system  provided at the system manager's%<CR><LF>
	.ASCII	%option.%<CR><LF>
	MESS.

.TAG
	MOV	#-1,CHGSEC	;DEFAULT TO CHANGES TO THIS SECTION
	MOV	#ANSNO,R1	;DEFAULT ANSWER IS NO

	ASK
	.MESS
	.ASCII	<CR><LF>
	.ASCII	%The SYSGEN process can use  the  default%<CR><LF>
	.ASCII	%configuration information as the default%<CR><LF>
	.ASCII	%to these questions. If there will not be%<CR><LF>
	.ASCII	%any changes made in this section, answer%<CR><LF>
	.ASCII	%YES to this question.  Otherwise, answer%<CR><LF>
	.ASCII	%NO.%<CR><LF>
	ME.SS
	.ASCII	<CR><LF>
	.ASCII	%Accept Software defaults ?%
	MESS.

	BACK				;USER TYPED ESCAPE
	CALLX	TESTNY,R5		;CHECK THE RESPONSE
	LOOP				;BAD
	SKIP				;NO
	CLR	CHGSEC			;YES

GLOBAL	<CHGCFG,ANSNO,CHGSEC>


.TAG
FSTQST:	MOV	#1,LRGFIL	;FORCE LARGE FIP

GLOBAL	<LRGFIL>

	MOV	#AJBMAX,R1	;GET THE ANSWER FROM THE TEMPLATE MONITOR
	TST	PRVMON		;ARE WE USING A TEMPLATE MONITOR
	BNE	15$		;YES, GO ASK THE QUESTION
	MOV	#ANS25,R1	;SET AUTO ANSWER TO 25
	CMP	AQBU,#'Y	;Q-BUS
	BNE	10$		; NO, SO LEAVE AT 25.
	MOV	#ANS10,R1	;Q-BUS QUESS IS 10 JOBS
10$:	INC	DEFLAG		;A GUESS

15$:	ASK
	.MESS
	.ASCII	<CR><LF>
	.ASCII	%With sufficient hardware RSTS/E can han-%<CR><LF>
	.ASCII	%dle up to 63 simultaneous jobs.  What is%<CR><LF>
	.ASCII	%the maximum number  of  jobs (JOBMAX) to%<CR><LF>
	.ASCII	%be used at this installation (1 to 63)?%<CR><LF>
	ME.SS
	.ASCII	<CR><LF>
	.ASCII	%Maximum jobs ? %
	MESS.

	BACK			;ESCAPE TYPED
	CALLX	NUMBER,R5	;GET VALUE
	.WORD	JOBMAX		;OF JOBMAX
	.WORD	1.		;FROM 1
	.WORD	63.		;TO 63 VALID
	LOOP			;INVALID NUMERIC RESPONSE
	MOV	R1,NUJOBS	;SAVE JOBMAX

	TMPORG	Q6DATA
NUJOBS:	.WORD	0		;JOBMAX
	.EVEN
	UNORG

GLOBAL	<DEFLAG,AQBU,PRVMON,AJBMAX>

.TAG
	MOV	#ASBUF+1,R1	;GET THE ANSWER FROM THE TEMPLATE MONITOR
	TST	PRVMON		;ARE WE USING A TEMPLATE MONITOR
	BNE	ASKBUF		;YES, GO ASK THE QUESTION
QBUF:	MOV	NUJOBS,R5	;GET THE NUMBER OF JOBS
	MUL	#13.,R5		;TIMES 13				;026
	ADD	#80.,R5		;ADD A BASE OF 80
	ADD	#50.,R5		;ADD A LITTLE MORE FOR DECNET		;023
5$:	CMP	R5,#500.	;LIMIT OF
	BLOS	10$		; 500. FOR
	MOV	#500.,R5	;  SANITY...
10$:	CLR	R4		;SET UP FOR A DIVIDE
	DIV	#100.,R4	;R4=100'S DIGIT;R5=10 AND UNIT DIGITS
	MOV	R4,R2		;SET UP FOR CHGDEC
	CALLX	CHGDEC		;CONVERT IT TO ASCII
	MOV	R2,ASBUF	;STORE THE ASCII IN THE ANSWER
	MOV	R5,R2		;NOW WE DO THE REST
	CALLX	CHGDEC		;CONVERTING IT
	MOV	R2,ASBUF+2	;AND STORING IT
	MOV	#ASBUF+1,R1	;NOW MAKE IT THE AUTO ANSWER
	INC	DEFLAG		;NOT SURE
ASKBUF:	ASK
	.MESS
	.ASCII	<CR><LF>
	.ASCII	%Small buffers are 16 word blocks in mon-%<CR><LF>
	.ASCII	%itor  storage that are dynamically allo-%<CR><LF>
	.ASCII	%cated as input and output buffers,  file%<CR><LF>
	.ASCII	%parameter  blocks,  etc.  Storage may be%<CR><LF>
	.ASCII	%allocated for  30 to 999 small  buffers.%<CR><LF>
	.ASCII	%The recommended  minimum  is at least 10%<CR><LF>
	.ASCII	%for each  possible  job.  How many small%<CR><LF>
	.ASCII	%buffers would  this installation like to%<CR><LF>
	.ASCII	%have (30 to 999)?%<CR><LF>
	ME.SS
	.ASCII	<CR><LF>
	.ASCII	%Small buffers ? %
	MESS.
	BACK			;USER TYPED ESCAPE
	CALLX	NUMBER,R5	;GET NUMBER
	.WORD	SMLBUF		;OF SMALL BUFFERS
	.WORD	30.		;FROM 30
	.WORD	999.		;TO 999 VALID
	LOOP			;INVALID

GLOBAL	<DEFLAG,ASBUF,PRVMON>						;023

	TMPORG	Q6DATA
ANS10:	.ASCII	%10%<CR>
ANS25:	.ASCII	%25%<CR>
	.EVEN
	UNORG

.TAG
	CLR	EML		; ASSUME NO EMT LOGGING
	MOV	#AEMTLG,R1	;GET THE ANSWER FROM THE TEMPLATE MONITOR
	TST	PRVMON		;ARE WE USING A TEMPLATE MONITOR
	BNE	15$		;YES, GO ASK THE QUESTION
	MOV	#ANSNO,R1	; GUESS NO
	INC	DEFLAG		; ITS A GUESS

15$:	ASK
	.MESS
	.ASCII	<CR><LF>
	.ASCII	%The RSTS/E Monitor can be configured  to%<CR><LF>
	.ASCII	%pass  information on selected directives%<CR><LF>
	.ASCII	%to an installation-written  program  via%<CR><LF>
	.ASCII	%SEND-RECEIVE, which can then make use of%<CR><LF>
	.ASCII	%such  data  to  log  selected  kinds  of%<CR><LF>
	.ASCII	%system  activity.    If  you  wish  your%<CR><LF>
	.ASCII	%Monitor to be capable of supplying  such%<CR><LF>
	.ASCII	%data, type YES otherwise type NO.%<CR><LF>  
	ME.SS
	.ASCII	<CR><LF>
	.ASCII	%EMT Logging ?%
	MESS.
	BACK			;USER TYPED ESCAPE
	CALLX	TESTNY,R5	;CHECK ANSWER
	LOOP			;BAD
	SKIP			;NO
	INC	EML		;YES

GLOBAL	<EML,ANSNO,DEFLAG,PRVMON,AEMTLG>

.TAG
	CLR	FIPBUF		;YES, CLEAR THE NORMAL BUFFERING FLAG
	INC	FIPBEX		; AND SET FOR EXTENDED BUFFERING

GLOBAL	<FIPBEX,FIPBUF>

	.ENABL	LSB
	CONFIG			;WRITE CONFIGURATION FILE
	.MESS
.NLIST	MEB
	.ASCII	%; SYSTEM PARAMETERS%<CR><LF><CR><LF>
	PARAML	JOBMAX,32.,^%MAXIMUM # OF JOBS%				;022
	.ASCII	<CR><LF>
	PARAML	SMLBUF,999.,^%# OF SMALL BUFFERS%			;022
.LIST	MEB
	MESS.

	MOVB	JOBMAX,AJBMAX		;STORE THE FIRST CHAR OF JOB MAX
	MOVB	JOBMAX+1,AJBMAX+1	;STORE THE SECOND CHAR
	TST	EML		;EMT LOGGING?				;025
	BEQ	20$		;NO
	CONFIG
	.MESS
.NLIST	MEB
	.ASCII	<CR><LF>
	PARAML	EMTLOG,1,^%1 FOR EMT LOGGING%				;022
.LIST	MEB
	MESS.

20$:	CONFIG			;WRITE END ON CONFIGURATION FILE	;024
	.MESS
.NLIST	MEB
	.ASCII	<FF>
.LIST	MEB
	MESS.
	.DSABL	LSB

GLOBAL	<EML,AJBMAX>							;025

	RETURN			;RETURN TO ROOT SEGMENT

	.END
