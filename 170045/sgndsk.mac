	.INCLUDE /CMN:COMMON/
	.INCLUDE /SGN:SGNCOM/
TITLE	SGNDSK,<SYSGEN DISKS>,0A,10-MAY-91,JDM/GW/ABC/MJG/AWL/PRL/KCG/JJT

;
;		COPYRIGHT (c) 1974, 1991 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

.SBTTL	SGNDSK EDIT HISTORY
;+
; EDIT	BY		DATE		REASON
;   04	AWL		12-Aug-81	Remove question about RF11
;                       22-Aug-81       Add question about RF11
;   05  AWL             06-Nov-81       Add <FF> after copyright
;   06  PRL		01-Feb-82	Add RA80/UDA50 dialog
;   07  PRL		16-Feb-82	Add restriction note to UDA dialog
;   08  PRL		28-Apr-82	Change max. UDA50's from 4 to 2
;   09	PRL		07-May-82	Remove restriction note to UDA dialog
;
;   --  KCG		09-Jul-82	Creation of 8.0 from 7.2 
;   10  KCG		09-Jul-82	Insert RM80 text into dialouge
;   11  KCG		11-Aug-82	Remove RC11 code from program
;   12  KCG		24-Aug-82	Change all dialouge questions dealing 
;					with disk capacity to describe mega 
;					bytes instead of million words.
;   13  KCG		04-Oct-82	Replace UDA50 question with one that
;					more accurately describes the MSCP.
;   14  KCG		28-Oct-82	Restore CNT.DC to Config file.
;   15  KCG		30-Nov-82	Add text for RC25 MSCP controller.
;   16  KCG		30-Nov-82	Remove RF11 from system disk list.
;
;   --  JJT		26-Apr-84	Creation of 9.0 from 8.0
;   --	JJT		03-Jun-85	Creation of 9.1 from 9.0
;   17	JJT		03-Jun-85	Change MSCP controllers to units
;   18  JJT		01-Jul-85	Accept INIT's value for # of RK05's
;   19	JJT		17-Jul-85	Allow up to 16 MSCP disk units
;   20	JJT		11-Mar-86	Change MSCP question to yes or no
;   21	JJT		11-Mar-86	Accept the number of RP02/3's as is
;
;   22	JJT		05-Jan-89	Remove unsupported hardware questions
;-

.SBTTL	QUESTIONS CONCERNING DISKS

	DEFORG	Q4CODE

.ENABL	LC

MESNUM	=	0
MSGSEG	=	4
TAGNUM	=	0


;   COUNTERS FOR THE DISKS. NON SYSTEM DISKS COME FIRST

	TMPORG	Q4DATA
NURK:	.BYTE	0							;018
NUSYSD:									;018
NURB:	.BYTE	0
NURR:	.BYTE	0
NURM:	.BYTE	0
NURL:	.BYTE	0
NURA:	.BYTE	0
	.BYTE	-1
	.EVEN
	UNORG


SGNDSK::
	CLR	CHGSEC		;INIT THE CHANGE SECTION QUESTION FLAG
	TST	CHGCFG		;ARE WE CHANGING THE CONFIGURATION?
	BEQ	FSTQST		;NO, GO AUTO-ANSWER THE FIRST QUESTION

.TAG
	TELL
	.MESS
	.ASCII	<CR><LF><LF>
	.ASCII	%The following questions deal  with  the%<CR><LF>
	.ASCII	%numbers and kinds of disk units on this%<CR><LF>
	.ASCII	%system.%<CR><LF>
	MESS.

.TAG
	MOV	#-1,CHGSEC	;DEFAULT TO CHANGES TO THIS SECTION
	MOV	#ANSNO,R1	;DEFAULT ANSWER IS NO

	ASK
	.MESS
	.ASCII	<CR><LF>
	.ASCII	%The SYSGEN process can use  the  default%<CR><LF>
	.ASCII	%configuration information as the default%<CR><LF>
	.ASCII	%to these questions. If there will not be%<CR><LF>
	.ASCII	%any changes made in this section, answer%<CR><LF>
	.ASCII	%YES to this question.  Otherwise, answer%<CR><LF>
	.ASCII	%NO.%<CR><LF>
	ME.SS
	.ASCII	<CR><LF>
	.ASCII	%Accept Disk defaults ?%
	MESS.

	BACK				;USER TYPED ESCAPE
	CALLX	TESTNY,R5		;CHECK THE RESPONSE
	LOOP				;BAD
	SKIP				;NO
	CLR	CHGSEC			;YES

GLOBAL	<CHGCFG,ANSNO,CHGSEC>

.TAG
FSTQST:	MOVB	#'1,CNT.DV	;THE DV DISK WILL ALWAYS BE INCLUDED

	MOV	#ARK11,R1	;GET AUTO ANSWER
	TST	PRVMON		;ARE WE USING A TEMPLATE MONITOR?
	BNE	10$		;YES, GO ASK THE QUESTION

10$:	ASK
	.MESS
	.ASCII	<CR><LF>
	.ASCII	%The RK11 controller is used  to  control%<CR><LF>
	.ASCII	%up to  8  of  the RK05  2.4  mega  bytes%<CR><LF>
	.ASCII	%moving-head cartridge disk  drives. Note%<CR><LF>
	.ASCII	%that an RK05F is equivalent to 2 RK05's.%<CR><LF>
	.ASCII	%How many RK05 drives are there (0 to 8)?%<CR><LF>
	ME.SS
	.ASCII	<CR><LF>
	.ASCII	%RK05's ? %
	MESS.

	BACK			;USER TYPED ESCAPE
	CALLX	NUMBER,R5	;GET NUMBER
	.WORD	CNT.DK		;OF RK03/RK05 DRIVES
	.WORD	0.		;FROM 0
	.WORD	8.		;TO   8 VALID
	LOOP			;ILLEGAL NUMBER
	MOVB	R1,NURK		;SAVE # OF RK03/RK05'S
	CALL	LAP,R5		;CHECK FOR OVERLAPPED SEEKING
	.WORD	ARKSEK		;DEFAULT ANSWER
	.WORD	RKSEEK		;STORE THE ANSWER HERE
	LOOP			;ESCAPE TYPED

GLOBAL	<ARK11,RKSEEK,ARKSEK,PRVMON>

.TAG
	MOV	#ARL11,R1	;GET AUTO ANSWER

	ASK
	.MESS
	.ASCII	<CR><LF>
	.ASCII	%The RL11 controller is used  to  control%<CR><LF>
	.ASCII	%any  combination of up to four RL01 (5.2%<CR><LF>
	.ASCII	%mega bytes) or  RL02  (10.4  mega bytes)%<CR><LF>
	.ASCII	%top loading cartridge disk drives. Enter%<CR><LF>
	.ASCII	%the total number of RL01 and RL02 drives%<CR><LF>
	.ASCII	%on this system (0 to 4).%<CR><LF>
	ME.SS
	.ASCII	<CR><LF>
	.ASCII	%RL01/RL02's ? %
	MESS.

	BACK			;USER TYPED ESCAPE
	CALLX	NUMBER,R5	;GET NUMBER
	.WORD	CNT.DL		;OF RL01/RL02 DRIVES
	.WORD	0.		;FROM 0
	.WORD	4.		;TO   4 VALID
	LOOP			;ILLEGAL NUMBER
	MOVB	R1,NURL		;SAVE # OF RL01/RL02'S
	CALL	LAP,R5		;CHECK FOR OVERLAPPED SEEKING
	.WORD	ARLSEK		;DEFAULT ANSWER
	.WORD	RLSEEK		;STORE THE ANSWER HERE
	LOOP			;ESCAPE TYPED

GLOBAL	<ARL11,RLSEEK,ARLSEK>

.TAG
	MOV	#ARK06,R1	;GET AUTO ANSWER

	ASK
	.MESS
	.ASCII	<CR><LF>

	.ASCII	%The RK611 controller is  used to control%<CR><LF>
	.ASCII	%any  combination of up to eight RK06 (14%<CR><LF>
	.ASCII	%mega bytes) or RK07 (28 mega bytes)  top%<CR><LF>
	.ASCII	%loading  cartridge  disk  drives.  Enter%<CR><LF>
	.ASCII	%the total number of RK06 and RK07 drives%<CR><LF>
	.ASCII	%on this system (0 to 8).%<CR><LF>
	ME.SS
	.ASCII	<CR><LF>
	.ASCII	%RK06/RK07's ? %
	MESS.

	BACK			;USER TYPED ESCAPE
	CALLX	NUMBER,R5	;GET NUMBER
	.WORD	CNT.DM		;OF RK06/RK07 DRIVES
	.WORD	0.		;FROM 0
	.WORD	8.		;TO   8 VALID
	LOOP			;ILLEGAL NUMBER
	MOVB	R1,NURM		;SAVE # OF RK06/RK07'S
	CALL	LAP,R5		;CHECK FOR OVERLAPPED SEEKING
	.WORD	ARMSEK		;DEFAULT ANSWER
	.WORD	RMSEEK		;STORE THE ANSWER HERE
	LOOP			;USER TYPED ESCAPE

GLOBAL	<ARK06,RMSEEK,ARMSEK>

.TAG
	MOV	#ARM03,R1	;GET AUTO ANSWER

	ASK
	.MESS
	.ASCII	<CR><LF>
	.ASCII	%The  RH11  controller  can  support  any%<CR><LF>
	.ASCII	%combination of up to eight  RM02,  RM03,%<CR><LF>
	.ASCII	%RM05, RM80, RP04, RP05,  and  RP06  disk%<CR><LF>
	.ASCII	%drives.   Type   the   total  number  of%<CR><LF>
	.ASCII	%RM02/03/05/80 and RP04/05/06  drives  on%<CR><LF>
	.ASCII	%this DR controller (0 to 8).%<CR><LF>
	ME.SS
	.ASCII	<CR><LF>
	.ASCII	%Disks on DR controller ? %
	MESS.

	BACK			;USER TYPED ESCAPE
	CALLX	NUMBER,R5	;GET NUMBER
	.WORD	CNT.DR		;OF RM02/RM03/RM05/RM80 DRIVES
	.WORD	0.		;FROM 0
	.WORD	8.		;TO   8 MAX
	LOOP			;ILLEGAL NUMBER
	MOVB	R1,NURR		;SAVE # OF RM02/RM03/RM05/RM80'S
	CALL	LAP,R5		;CHECK FOR OVERLAPPED SEEKING
	.WORD	ARRSEK		;DEFAULT ANSWER
	.WORD	RRSEEK		;STORE THE ANSWER HERE
	LOOP			;ESCAPE TYPED

GLOBAL	<ARM03,RRSEEK,ARRSEK>

.TAG
	MOV	#ARP04,R1	;GET AUTO ANSWER

	ASK
	.MESS
	.ASCII	<CR><LF>
	.ASCII	%The  RH11  controller  can  support  any%<CR><LF>
	.ASCII	%combination of up to eight  RP04,  RP05,%<CR><LF>
	.ASCII	%RP06, RM02, RM03, RM05,  and  RM80  disk%<CR><LF>
	.ASCII	%drives.   Type   the   total  number  of%<CR><LF>
	.ASCII	%RP04/05/06 and RM02/03/05/80  drives  on%<CR><LF>
	.ASCII	%this DB controller (0 to 8).%<CR><LF>
	ME.SS
	.ASCII	<CR><LF>
	.ASCII	%Disks on DB controller ? %
	MESS.

	BACK			;USER TYPED ESCAPE
	CALLX	NUMBER,R5	;GET NUMBER
	.WORD	CNT.DB		;OF RP04/RP05/RP06 DRIVES
	.WORD	0.		;FROM 0
	.WORD	8.		;TO   8 MAX
	LOOP			;ILLEGAL NUMBER
	MOVB	R1,NURB		;SAVE # OF RP04/RP05/RP06'S
	CALL	LAP,R5		;CHECK FOR OVERLAPPED SEEKING
	.WORD	ARBSEK		;DEFAULT ANSWER
	.WORD	RBSEEK		;STORE THE ANSWER HERE
	LOOP			;ESCAPE TYPED

GLOBAL	<ARP04,RBSEEK,ARBSEK>

.TAG
	MOV	#AMSCP,R1	;GET AUTO ANSWER
	CLR	CNT.DU		;ASSUME NO				;020

	ASK
	.MESS
	.ASCII	<CR><LF>
	.ASCII	%RSTS/E can support  up  to sixteen MSCP-%<CR><LF>
	.ASCII	%class disks on four controllers.The MSCP%<CR><LF>
	.ASCII	%controllers include the UDA50, RC25, and%<CR><LF>
	.ASCII	%the RQDX1.%<CR><LF>
	.ASCII	<CR><LF>
	.ASCII	%The UDA50 can control up to four of  the%<CR><LF>
	.ASCII	%following   drives:    RA60   (205  mega%<CR><LF>
	.ASCII	%bytes),  RA80  (120  mega  bytes   fixed%<CR><LF>
	.ASCII	%media),  and  RA81 (456 mega bytes fixed%<CR><LF>
	.ASCII	%media).%<CR><LF>
	.ASCII	<CR><LF>
	.ASCII	%The RC25 can  control  up  to  two  RC25%<CR><LF>
	.ASCII	%drives   (25   mega   bytes   fixed  and%<CR><LF>
	.ASCII	%removable media).%<CR><LF>
	.ASCII	<CR><LF>
	.ASCII	%Do you want MSCP support for this system%<CR><LF>
	.ASCII	%(YES or NO)?%<CR><LF>
	ME.SS
	.ASCII	<CR><LF>
	.ASCII	%MSCP support ?%					;020
	MESS.

	BACK			;USER TYPED ESCAPE
	CALLX	TESTNY,R5	;GET AND STORE THE ANSWER		;020+
	LOOP			;BAD ANSWER
	BR	10$		;NO
	MOV	#16.,CNT.DU	;YES, STORE THE MAX
10$:	MOV	CNT.DU,R1	;TEMPORARILY STORE THE COUNT
	MOV	R1,E.MCP	;SAVE # UNITS IN MCP FLAG
	MOV	R1,E.MSCP	;SAVE # UNITS IN MSCP FLAG
	MOVB	R1,NURA		;SAVE # OF UNITS IN SYS DISK TABLE
	CALLX	STRDEC,R5	;CONVERT VALUE TO ASCII
	.WORD	CNT.DU		;STORE THE ASCII OF THE ANSWER		;020-

GLOBAL	<AMSCP,E.MCP,E.MSCP>

.TAG
	MOV	#NUSYSD,R0	;GET START OF LIST OF NUMBER OF DRIVES
10$:	TSTB	(R0)+		;IS THERE A SYSTEM DISK
	SKIP	GT		;YES
	BEQ	10$		;GO BACK FOR THE NEXT ONE

	TELL			;NONE - TELL HIM IT'S NOT LEGAL
	.MESS
	.ASCII	<CR><LF>
	.ASCII	%You have indicated that there are no DL,%<CR><LF>	;018
	.ASCII	%DM,  DR,  DB,  or   DU   disks.   RSTS/E%<CR><LF>	;022
	.ASCII	%requires at least one  of these for  its%<CR><LF>
	.ASCII	%system  disk.    Please   answer   those%<CR><LF>
	.ASCII	%questions  again.%<CR><LF>
	MESS.
	JMP	SGNDSK		;START DISK QUESTIONS AGAIN

.TAG
	CONFIG			;WRITE OUT THE CONFIGURATION FILE
	.MESS
	.NLIST	MEB
	.ASCII	%; DISKS%<CR><LF><CR><LF>
	.ASCII	%CNT.DC	=	0.     	;1. IF RC11; ELSE 0%<CR><LF>
	.ASCII	%CNT.DF =	0.      ;1. IF RF11/RS11; ELSE 0%<CR><LF>
	.ASCII	%CNT.DS =	0.      ;# OF RS03/RS04'S; ELSE 0%<CR><LF>
	.ASCII	%CNT.DP ==	0.      ;# OF RP02/03'S; ELSE 0%<CR><LF>
	PARAM	CNT.DV,0.,^%1. IF MEMORY DISK; ELSE 0%
	PARAM	CNT.DK,0.,^%# OF RK05'S; ELSE 0%
	PARAM	CNT.DL,0.,^%# OF RL01/2'S; ELSE 0%
	PARAM	CNT.DM,0.,^%# OF RK06/7'S; ELSE 0%
	PARAM	CNT.DR,0.,^%# OF RM02/03/05/80'S; ELSE 0%
	PARAM	CNT.DB,0.,^%# OF RP04/5/6'S; ELSE 0%
	PARAM	CNT.DU,0.,^%# OF RA80'S; ELSE 0%
	.ASCII	<CR><LF>
	MESS.
	.LIST	MEB

	MOVB	CNT.DK,ARK11+1
	MOVB	CNT.DL,ARL11+1
	MOVB	CNT.DM,ARK06+1
	MOVB	CNT.DR,ARM03+1
	MOVB	CNT.DB,ARP04+1

	MOVB	#'Y,ARKSEK	;DEFAULT TO "Y "
	MOVB	#' ,ARKSEK+1	;SECOND CHAR
	TST	RKSEEK		;OVERLAPPED SEEK?
	BNE	10$		;YES, CONTINUE
	MOVB	#'N,ARKSEK	;FIRST CHAR OF "N "
	MOVB	#' ,ARKSEK+1	;SECOND CHAR
10$:	MOVB	#'Y,ARLSEK	;DEFAULT TO "Y "
	MOVB	#' ,ARLSEK+1	;SECOND CHAR
	TST	RLSEEK		;OVERLAPPED SEEK?
	BNE	20$		;YES, CONTINUE
	MOVB	#'N,ARLSEK	;FIRST CHAR OF "N "
	MOVB	#' ,ARLSEK+1	;SECOND CHAR
20$:	MOVB	#'Y,ARMSEK	;DEFAULT TO "Y "
	MOVB	#' ,ARMSEK+1	;SECOND CHAR
	TST	RMSEEK		;OVERLAPPED SEEK?
	BNE	30$		;YES, CONTINUE
	MOVB	#'N,ARMSEK	;FIRST CHAR OF "N "
	MOVB	#' ,ARMSEK+1	;SECOND CHAR
30$:	MOVB	#'Y,ARRSEK	;DEFAULT TO "Y "			;022
	MOVB	#' ,ARRSEK+1	;SECOND CHAR
	TST	RRSEEK		;OVERLAPPED SEEK?
	BNE	50$		;YES, CONTINUE
	MOVB	#'N,ARRSEK	;FIRST CHAR OF "N "
	MOVB	#' ,ARRSEK+1	;SECOND CHAR
50$:	MOVB	#'Y,ARBSEK	;DEFAULT TO "Y "
	MOVB	#' ,ARBSEK+1	;SECOND CHAR
	TST	RBSEEK		;OVERLAPPED SEEK?
	BNE	60$		;YES, CONTINUE
	MOVB	#'N,ARBSEK	;FIRST CHAR OF "N "
	MOVB	#' ,ARBSEK+1	;SECOND CHAR
60$:

	RETURN			;BACK TO SYSGEN ROOT SEGMENT

;   OVERLAPPED SEEKS QUESTIONING
;
;	   CALL	LAP
;	+	.WORD OF ADDRESS OF DEFAULT ANSWER
;	+	WORD
;	+	RETURN FOR ESCAPES
;	+	NORMAL RETURN

;	R1=NUMBER OF DRIVES
;
;	IF THE NUMBER OF DRIVES IS LESS THAN 2 JUST RETURN. OTHERWISE
;	ENTER THE USER DIALOG TO DETERMINE IF WE SHOULD USE OVERLAPPED
;	SEEKS. WORD IS SET TO 0 FOR NO AND -1 FOR YES. R1 IS UNDEFINED.


	.ENABL	LSB
LAP:	MOV	QUERSW,QUERS2	;SAVE THE FORM FLAG
	DEC	R1		;1 OR LESS DRIVES?
	BLE	20$		;YES
	TST	ONCE		;HAVE WE GIVEN THE LONG FORM BEFORE
	BEQ	10$		;NO
	MOV	#1,QUERSW	;YES. DON'T DO IT AGAIN UNLESS WE HAVE TO
10$:	MOV	R5,SAVER5	;SAVE R5
	MOV	#ANSYES,R1	;SET UP AN AUTO ANSWER OF YES
	TST	PRVMON		;ARE WE USING A TEMPLATE MONITOR?
	BEQ	15$		;NO, GO USE AUTO ANSWER OF YES
	MOV	(R5),R1		;SET UP THE AUTO ANSWER

15$:	TST	(R5)+		;JUMP OVER THE AUTO ANSWER
	ASK
	.MESS
	.ASCII	<CR><LF>
	.ASCII	%By using the overlapped seek driver, you%<CR><LF>
	.ASCII	%can increase the  efficiency of disk I/O%<CR><LF>
	.ASCII	%at  the  expense  of  some memory.  Type%<CR><LF>
	.ASCII	%YES to  use the  overlapped seek driver.%<CR><LF>
	.ASCII	%Type NO to  use  the non-overlapped seek%<CR><LF>
	.ASCII	%driver.%<CR><LF>
	ME.SS
	.ASCII	<CR><LF>
	.ASCII	%Overlapped seek ?%
	MESS.

	BR	30$		;USER TYPED ESCAPE
	INC	ONCE		;GIVE LONG FORM ONCE ONLY
	CALLX	TESTNY,R5	;CHECK THE ANSWER
	BR	35$		;ILLEGAL
	BR	25$		;NO
	MOV	#-1,@(R5)+	;YES. SET WORD NON ZERO
	BR	30$		;DONE

20$:	TST	(R5)+		;SKIP OVER THE AUTO ANSWER
25$:	CLR	@(R5)+		;SET WORD TO ZERO
30$:	TST	(R5)+		;SKIP OVER ESCAPE RETURN
	MOV	QUERS2,QUERSW	;RESTORE FORM FLAG
	RETURN	R5		;RETURN

35$:	MOV	SAVER5,R5	;RESTORE R5
	BR	10$		;ASK AGAIN
	.DSABL	LSB

	TMPORG	Q4DATA
ONCE:	.WORD	0		;PRINT LONG FORM FIRST TIME ONLY FLAG
QUERS2:	.WORD	0		;SAVED FORM FLAG
SAVER5:	.WORD	0		;PLACE TO SAVE R5
	UNORG

GLOBAL	<ANSYES,QUERSW,PRVMON>

	.END

