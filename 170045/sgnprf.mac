	.INCLUDE /CMN:COMMON/
	.INCLUDE /SGN:SGNCOM/
TITLE	SGNPRF,<SYSGEN PERIPHERALS>,0A,10-MAY-91,JDM/GW/ABC/MJG/AWL/PRL/KCG/FEK/JJT/KPH

;
;		COPYRIGHT (c) 1974, 1991 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

.SBTTL	SGNPRF EDIT HISTORY
;+
; EDIT	BY	  DATE			     REASON
;
;  01 RVR/DRP	15-Dec-80	Add DMV11/DMP11 support to SGNPRF.
;  02   DRP	04-FEB-81	Added DECnet/E V2.0 support.
;  03   DRP	21-APR-81	Modify the DECnet changes.
;  04   AWL     10-JUL-81       Replace LOOP with BACK after RX11 question
;                               Autosize AUTO ANSWER for DECNET question
;  05   AWL     04-NOV-81       Clearify KMC11 & 2780 dialogue
;
;  06   AWL     06-NOV-81       ADD <FF> after copyright
;
;  07   PRL	28-JAN-82	No DECNET auto answer unless same system
;  08   PRL	11-FEB-82	Clarify some long-form prompts
;  09	PRL	16-FEB-82	Add restriction note to DECNET & RJ2780 dialog
;  10	PRL	23-Mar-82	Remove "small link" code
;  11	PRL	07-May-82	Remove restriction notes from DECNET/RJ2780
;  12	PRL	07-May-82	No RJ2780 prompt if any UDA's selected
;
;  --	KCG	20-Jul-82	Creation of 8.0 from 7.2 
;  13	KCG	20-Jul-82	Removal of LS11 and LV11 from long text
;  14	KCG	20-Jul-82	Combination of two paper tape machine questions
;				into one question.  ( Dropping PR11 in future )
;  15	KCG	20-Jul-82	Corrected comments for config file for PR11.
;  16	KCG	09-Jan-83	Add text for TU80 and TSV05 to TS11 question.
;  17   FEK	25-Aug-83	Add DHV/DHU remove 2741 change DZV/DZ/DMV/DMP
;
;  --   JJT	26-Apr-84	Creation of 9.0 from 8.0
; 018	KPH	26-Sep-84	Add magtape phase stuff
;  --	JJT	22-May-85	Creation of 9.1 from 9.0
; 019   JJT     22-May-85	Add TMSCP
; 020	JJT	03-Jul-85	Always prompt for TMSCP if its new
; 021	JJT	07-Jan-86	V9.3 changes
; 022   KPH     11-Mar-86	Disallow zero II units
; 023   JJT	16-Feb-87	Accept INIT's value for TU16's as is
; 024	JJT	26-Feb-88	Remove KMC11 and IBM II questions
; 025	JJT	05-Jan-89	Remove unsupp hardware questions
;-


.SBTTL	QUESTIONS CONCERNING PERIPHERALS

	ORG	Q5CODE

.ENABL	LC

MESNUM	=	0
MSGSEG	=	5
TAGNUM	=	0


SGNPRF::	NOP		;NOP INSTRUCTION IS HERE FOR DEBUGING
				;PURPOSES. USE ONLPAT TO CHANGE TO A
				;BPT INSTRUCTION FOR DEBUGGING THIS MODULE.

	CLR	CHGSEC		;INIT THE CHANGE SECTION QUESTION FLAG
	TST	CHGCFG		;ARE WE CHANGING THE CONFIGURATION?
	BEQ	FSTQST		;NO, GO AUTO-ANSWER THE FIRST QUESTION

.TAG
	TELL			;TELL HIM WHAT'S NEXT
	.MESS
	.ASCII	<CR><LF><LF>
	.ASCII	%The following questions  deal  with  the%<CR><LF>
	.ASCII	%peripheral   devices  attached  to  this%<CR><LF>
	.ASCII	%RSTS/E system.%<CR><LF>
	MESS.
.TAG
	MOV	#-1,CHGSEC	;DEFAULT TO CHANGES TO THIS SECTION
	MOV	#ANSNO,R1	;DEFAULT ANSWER IS NO

	ASK
	.MESS
	.ASCII	<CR><LF>
	.ASCII	%The SYSGEN process can use  the  default%<CR><LF>
	.ASCII	%configuration information as the default%<CR><LF>
	.ASCII	%to these questions. If there will not be%<CR><LF>
	.ASCII	%any changes made in this section, answer%<CR><LF>
	.ASCII	%YES to this question.  Otherwise, answer%<CR><LF>
	.ASCII	%NO.%<CR><LF>
	ME.SS
	.ASCII	<CR><LF>
	.ASCII	%Accept Peripheral defaults ?%
	MESS.

	BACK				;USER TYPED ESCAPE
	CALLX	TESTNY,R5		;CHECK THE RESPONSE
	LOOP				;BAD
	SKIP				;NO
	CLR	CHGSEC			;YES

GLOBAL	<CHGCFG,ANSNO,CHGSEC>


.TAG
FSTQST:	MOV	#ATU16,R1	;SET THE AUTO ANSWER
	TST	PRVMON		;ARE WE USING A TEMPLATE MONITOR?
	BNE	10$		;YES, GO ASK THE QUESTION

10$:	ASK
	.MESS
	.ASCII	<CR><LF>
	.ASCII	%The TU16/TE16 magtape system consists of%<CR><LF>
	.ASCII	%an  RH11  massbus  interface,  TM02/TM03%<CR><LF>
	.ASCII	%controller  and from one to  eight TU16,%<CR><LF>
	.ASCII	%TE16, or TU45 drives, or up to four TU77%<CR><LF>
	.ASCII	%drives. Please enter the number of TU16,%<CR><LF>
	.ASCII	%TE16,  TU45,  and  TU77  drives  on this%<CR><LF>
	.ASCII	%system (0 to 8)?%<CR><LF>
	ME.SS
	.ASCII	<CR><LF>
	.ASCII	%TU16/TE16/TU45/TU77's ? %
	MESS.
	BACK			;USER TYPED ESCAPE
	CALLX	NUMBER,R5	;GET NUMBER
	.WORD	TM02		;OF TU16'S
	.WORD	0.		;FROM 0
	.WORD	8.		;TO 8 VALID
	LOOP			;INVALID
	MOV	R1,NUMM		;SAVE # OF TU16'S
	MOV	R1,MMDVRM	;And indicate we need MM driver		;018

	TMPORG	Q5DATA
NUMM:	.WORD	0		;NUMBER OF TU16'S
	UNORG

GLOBAL	<ATU16,DEFLAG,PRVMON,MMDVRM>					;018

.TAG
	MOV	#ATU10,R1	;GET THE AUTO ANSWER
	TST	PRVMON		;ARE WE USING A TEMPLATE MONITOR?
	BNE	10$		;YES, GO ASK THE QUESTION
	CALLX	CHKDEF		;ARE YOU SURE

10$:	ASK
	.MESS
	.ASCII	<CR><LF>
	.ASCII	%The  TM11/TMA11/TMB11 magtape controller%<CR><LF>
	.ASCII	%can support up to eight  TU10,  TE10, or%<CR><LF>
	.ASCII	%TS03 magtape drives.  Please  enter  the%<CR><LF>
	.ASCII	%number of TU10, TE10, and TS03 drives on%<CR><LF>
	.ASCII	%this system (0 to 8).%<CR><LF>
	ME.SS
	.ASCII	<CR><LF>
	.ASCII	%TU10/TE10/TS03's ? %
	MESS.
	BACK			;USER TYPED ESCAPE
	CALLX	NUMBER,R5	;GET NUMBER
	.WORD	TM11		;OF TU10'S
	.WORD	0.		;FROM 0
	.WORD	8.		;TO 8 VALID
	LOOP			;INVALID
	MOV	R1,NUMT		;SAVE # OF TU10'S
	MOV	R1,MTDVRM	;And indicate we need MT driver		;018

	TMPORG	Q5DATA
NUMT:	.WORD	0		;NUMBER OF TU 10'S
	UNORG

GLOBAL	<ATU10,PRVMON,MTDVRM>						;018

.TAG
	MOV	#ATS11,R1	;SET THE AUTO ANSWER
	ASK
	.MESS
	.ASCII	<CR><LF>
	.ASCII	%RSTS/E  supports any combination  of  up%<CR><LF>
	.ASCII	%to  four  TS11,  TK25,  TSV05,  or  TU80%<CR><LF>
	.ASCII	%magtape drives.  Please enter  the total%<CR><LF>
	.ASCII	%number  of   TS11/TK25/TSV05/TU80's  for%<CR><LF>
	.ASCII	%this system (0 to 4).%<CR><LF>
	ME.SS
	.ASCII	<CR><LF>
	.ASCII	%TS11/TK25/TSV05/TU80's ? %
	MESS.
	BACK			;USER TYPED ESCAPE

	CALLX	NUMBER,R5	;GET NUMBER
	.WORD	TS11		;OF TS11'S
	.WORD	0.		;FROM 0
	.WORD	4.		;TO 4 VALID
	LOOP			;INVALID
	MOV	R1,NUMMS	;SAVE # OF TS11'S
	MOV	R1,MSDVRM	;And indicate we need MS driver		;018

	TMPORG	Q5DATA
NUMMS:	.WORD	0		;NUMBER OF TS11'S
	UNORG

GLOBAL	<ATS11,MSDVRM>							;018

.TAG
	TST	ASKTMS		;DO WE WANT TO PROMPT UNCONDITIONALLY	;020
	BEQ	10$		;NO, CONTINUE				;020
	INC	ASKIT		;YES, SET THE FLAG			;020
10$:	MOV	#ATMSCP,R1	;GET AUTO ANSWER

	ASK
	.MESS
	.ASCII	<CR><LF>
	.ASCII	%RSTS/E can support up to two TMSCP-class%<CR><LF>
	.ASCII	%tape drives.  Each TMSCP controller  can%<CR><LF>
	.ASCII	%support  one  of  the   following   tape%<CR><LF>
	.ASCII	%drives:  TK50 or TU81%<CR><LF>
	.ASCII	<CR><LF>
	.ASCII	%Please enter the total  number  of TMSCP%<CR><LF>
	.ASCII	%tape drives for this system (0 to 2).%<CR><LF>
	ME.SS
	.ASCII	<CR><LF>
	.ASCII	%TMSCP tape drives ?%
	MESS.

	BACK			;USER TYPED ESCAPE
	CALLX	NUMBER,R5	;GET NUMBER
	.WORD	TMSCP		;OF TMSCP UNITS
	.WORD	0.		;FROM 0
	.WORD	2.		;TO   2 MAX
	LOOP			;ILLEGAL NUMBER
	MOV	R1,MUDVRM	;SAVE # OF TMSCP UNITS
	CALLX	STRDEC,R5	;CONVERT THE NUMBER TO ASCII		;020
	.WORD	TMSCP							;020

; Now set up E.MVR to include if we've asked for any tape drivers

	MOV	MTDVRM,E.MVR	;Make E.MVR non-zero if MT support	;018
	BIS	MMDVRM,E.MVR	;Now make E.MVR non-zero if MM support	;018
	BIS	MSDVRM,E.MVR	;Now make E.MVR non-zero if MS support	;018
	BIS	MUDVRM,E.MVR	;Now make E.MVR non-zero if MU support	;020
	BIS	MUDVRM,E.MCP	;Now make E.MCP non-zero if MU support	;020

GLOBAL	<ATMSCP,MUDVRM,E.MVR,E.MCP,ASKIT,ASKTMS>			;020

.TAG
	MOV	#ALP,R1		;GET THE AUTO ANSWER

	ASK
	.MESS
	.ASCII	<CR><LF>
	.ASCII	%The RSTS/E system can have  up to  eight%<CR><LF>
	.ASCII	%line  printers of  the LP11 type.  These%<CR><LF>
	.ASCII	%printers are referred to  by the  device%<CR><LF>
	.ASCII	%names LP0:  through LP7:.  Please  enter%<CR><LF>
	.ASCII	%the  total number  of line  printers for%<CR><LF>
	.ASCII	%this system (0 to 8).%<CR><LF>
	ME.SS
	.ASCII	<CR><LF>
	.ASCII	%Printers ? %
	MESS.
	BACK			;USER TYPED ESCAPE
	CALLX	NUMBER,R5	;GET NUMBER
	.WORD	LP11		;OF LINE PRINTERS
	.WORD	0.		;FROM 0
	.WORD	8.		;TO 8 VALID
	LOOP			;INVALID

GLOBAL	<ALP>

.TAG
	MOV	#ARX01,R1	;GET AUTO ANSWER
	TST	PRVMON		;ARE WE USING A TEMPLATE MONITOR?
	BNE	10$		;YES, GO ASK THE QUESTION
	CALLX	CHKDEF		;CHECK THE AUTO ANSWER

10$:	ASK
	.MESS
	.ASCII	<CR><LF>
	.ASCII	%The  RX11  or  RX211  floppy disk system%<CR><LF>
	.ASCII	%consists of  a UNIBUS interface  and  an%<CR><LF>
	.ASCII	%RX01  (single density)  or  RX02 (double%<CR><LF>
	.ASCII	%density) floppy disk subsystem. The RX01%<CR><LF>
	.ASCII	%and RX02 each include  a disk controller%<CR><LF>
	.ASCII	%and  two  floppy  disk  drives.   RSTS/E%<CR><LF>
	.ASCII	%supports  up  to   four  RX11  or  RX211%<CR><LF>
	.ASCII	%systems for a maximum of  eight  drives.%<CR><LF>
	.ASCII	%Please  enter  the number of floppy disk%<CR><LF>
	.ASCII	%drives attached to this system (0 to 8).%<CR><LF>
	ME.SS
	.ASCII	<CR><LF>
	.ASCII	%RX01/RX02's ? %
	MESS.

	BACK			;USER TYPED ESCAPE
	CALLX	NUMBER,R5	;GET NUMBER
	.WORD	RX11		;SET THIS CONFIG PARAMETER
	.WORD	0.		;NONE IS OKAY
	.WORD	8.		;BUT 8 IS MAX
	LOOP			;ILLEGAL NUMBER

GLOBAL	<ARX01,PRVMON>

CARDS:
.TAG
	CLR	CR26		;CLEAR ALL CARD CODE FLAGS. 026
	CLR	CR29		;029
	CLR	CR1401		;1401
	MOV	#ACR,R1		;SET AUTO ANSWER
	MOVB	#'0,CR11	;ASSUME ANSWER WILL BE NO
	ASK
	.MESS
	.ASCII	<CR><LF>
	.ASCII	%Does  this  system  have a CR11  punched%<CR><LF>
	.ASCII	%card reader or a CM11 marked card reader%<CR><LF>
	.ASCII	%(YES or NO)?%<CR><LF>
	ME.SS
	.ASCII	<CR><LF>
	.ASCII	%CR11/CM11 card reader ?%
	MESS.
	BACK			;USER TYPED ESCAPE
	CALLX	NUMBER,R5	;CHECK THE ANSWER
	.WORD	CR11
	.WORD	0
	.WORD	1.
	LOOP			;BAD

GLOBAL	<ACR,CR26,CR29,CR1401>

.TAG
	MOV	#ACD,R1		;SET AUTO ANSWER
	MOVB	#'0,CD11	;ASSUME ANSWER WILL BE ZERO
	ASK
	.MESS
	.ASCII	<CR><LF>
	.ASCII	%Does this system have a CD11  high-speed%<CR><LF>
	.ASCII	%punched card reader (YES or NO)?%<CR><LF>
	ME.SS
	.ASCII	<CR><LF>
	.ASCII	%CD11 card reader ?%
	MESS.
	BACK			;USER TYPED ESCAPE
	CALLX	NUMBER,R5	;CHECK THE ANSWER
	.WORD	CD11
	.WORD	0
	.WORD	1.
	LOOP			;BAD

GLOBAL	<ACD>

.TAG
	CMPB	CR11,#'0	;IS THERE A CR11
	BNE	10$		;YES
	CMPB	CD11,#'0	;IS THERE A CD11
	SKIP	EQ		;NO
10$:	MOV	#ACDCOD,R1	;ASSUME AUTO ANSWER
	INC	DEFLAG		;NOT SURE
	ASK
	.MESS
	.ASCII	<CR><LF>
	.ASCII	%Cards read by RSTS/E are normally inter-%<CR><LF>
	.ASCII	%preted with an ANSI standard card decode%<CR><LF>
	.ASCII	%table.  If this  installation uses cards%<CR><LF>
	.ASCII	%punched in DEC 029, DEC 026, or IBM 1401%<CR><LF>
	.ASCII	%card code, you may pick  the appropriate%<CR><LF>
	.ASCII	%decoding table.  Answer "029", "026", or%<CR><LF>
	.ASCII	%"1401"  if you want  one of the  special%<CR><LF>
	.ASCII	%tables; otherwise, answer "ANSI".%<CR><LF>
	ME.SS
	.ASCII	<CR><LF>
	.ASCII	%Card decode ? %
	MESS.

	BACK			;USER TYPED ESCAPE
	CMP	(R0),#"AN	;DID HE ANSWER ANSI
	BNE	20$		;NOPE
	CMP	2(R0),#"SI	;SECOND HALF
	SKIP	EQ		;OK
20$:	CALLX	NUMBER,R5	;GET NUMERIC VALUE
	.WORD	PARAM		;OF CARD DECODE TABLE
	.WORD	026.		;EITHER 026 (OR 029)
	.WORD	1401.		;OR 1401
	LOOP			;INVALID
	CMP	R1,#029.	;WAS IT 029
	BNE	30$		;NO
	INC	CR29		;FLAG IT
	SKIP			;DONE
30$:	CMP	R1,#026.	;026 ?
	BNE	40$		;NO
	INC	CR26		;FLAG IT
	SKIP			;DONE
40$:	CMP	R1,#1401.	;1401 ?
	LOOP	NE		;NONE OF THE ABOVE
	INC	CR1401		;FLAG IT

GLOBAL	<DEFLAG,CR26,CR29,CR1401,PARAM,ACDCOD>

.TAG
ASKDMC:	CLR	OVRIDE		;CLEAR THE OVERRIDE FLAG
	CLR	DSDMC		;ASSUME NO DMC11/DMR11'S
	MOV	#ADMC11,R1	;SET THE AUTO ANSWER
	ASK
	.MESS
	.ASCII	<CR><LF>
	.ASCII	%The  DMC11 and DMR11  network  links are%<CR><LF>
	.ASCII	%synchronous  communication  line  inter-%<CR><LF>
	.ASCII	%faces which implements  the  DDCMP  line%<CR><LF>
	.ASCII	%protocol  in  hardware.  RSTS/E supports%<CR><LF>
	.ASCII	%up to a maximum of 16 DMC11's or DMR11's%<CR><LF>
	.ASCII	%per system.  How many DMC11's or DMR11's%<CR><LF>
	.ASCII	%are attached to this system (0 to 16)?%<CR><LF>
	ME.SS
	.ASCII	<CR><LF>
	.ASCII	%DMC11's/DMR11's ?%
	MESS.
	BR	CARDS		;USER TYPED ESCAPE			;025
	CALLX	NUMBER,R5	;CHECK AND STORE THE NUMBER
	.WORD	DMC11
	.WORD	0.
	.WORD	16.
	LOOP			;BAD ANSWER
	SKIP			;PROCEED TO NEXT QUESTION

GLOBAL	<ADMC11,DSDMC>

.TAG
ASKDMP:
	CLR	DSDMP		;ASSUME NO DMP11'S OR DMV11'S
	MOV	#ADMP11,R1	;SET THE AUTO ANSWER			;021
	CLR	OVRIDE		;CLEAR THE OVERRIDE FLAG
	CMPB	#'?,(R1)	;IS THE ANSWER "??"
	BNE	11$		;NO, GO ASK THE QUESTION
	INC	OVRIDE		;FLAG IT
	
11$:	ASK
	.MESS
	.ASCII	<CR><LF>
	.ASCII	%The DMV11 and DMP11  network  links  are%<CR><LF>
	.ASCII	%synchronous  communication  line  inter-%<CR><LF>
	.ASCII	%faces which implements  the  DDCMP  line%<CR><LF>
	.ASCII	%protocol  in hardware.  In the multidrop%<CR><LF>
	.ASCII	%mode, the DMV11 can  support  up  to  12%<CR><LF>
	.ASCII	%tributaries,  and  the DMP11 can support%<CR><LF>
	.ASCII	%up to  32  tributaries. RSTS/E  supports%<CR><LF>
	.ASCII	%up to a maximum of 16 DMV11's or DMP11's%<CR><LF>
	.ASCII	%per system.  How many DMV11's or DMP11's%<CR><LF>
	.ASCII	%are  attached to this system (0 to 16)?%<CR><LF>
	ME.SS
	.ASCII	<CR><LF>
	.ASCII	%DMV11's/DMP11's ?%
	MESS.
	BACK			;USER TYPED ESCAPE
	CALLX	NUMBER,R5	;CHECK AND STORE THE NUMBER
	.WORD	DMP11
	.WORD	0.
	.WORD	16.
	LOOP			;BAD ANSWER
	MOV	R1,DMP.11	;SAVE NUMBER OF DMV11'S/DMP11'S
	BEQ	CHKLNK		;SKIP IF NO CONTROLLERS

	TMPORG	Q5DATA
DMP.11:	.WORD	0		;NUMBER OF DMV11/DMP11 CONTROLLERS
OVRIDE:	.WORD	0		;OVERRIDE ACCEPT DEFAULTS FLAG
	UNORG

GLOBAL	<ADMP11,DSDMP>

.TAG

	CLR	DSDMP		;AGAIN, ASSUME NO DMP11'S OR DMV11'S
	MOVB	#'V,XDUCHR	;ASSUME DMV11
	MOVB	#'1,XDUNUM	;SET FIRST DIGIT OF TRIB. MAX. TO "1"
	CMPB	AQBU,#'Y	;Q-BUS?
	BEQ	20$		; YES, SO LEAVE ALONE
	MOVB	#'P,XDUCHR	;SET DEVICE TYPE TO "P"
	MOVB	#'3,XDUNUM	;SET FIRST DIGIT OF TRIB. MAX. TO "3"
20$:

	TMPORG	Q5DATA
XDUCHR:	.BYTE	0		;DEVICE TYPE TO BE USED IN PROMPT
XDUNUM:	.BYTE	0		;FIRST DIGIT OF TRIB. MAX. FOR PROMPT
	UNORG

GLOBAL	<DSDMP,DEFLAG,AQBU>

.TAG
	CLR	TOTDMP		;INITIALIZE NUMBER OF DMP/DMV LINES
	CLR	XDUNIT		;START WITH UNIT 1
XDLOOP:	MOV	XDUNIT,R2	;GET CURRENT DMP UNIT NUMBER
	CMP	R2,DMP.11	;ARE WE DONE
	BEQ	CHKLNK		;YES
	CALLX	CHGDEC		;CHANGE NUMBER TO ASCII
	MOV	R2,XDUNT1	;SET IT IN THE TEXT

	TMPORG	Q5DATA
.NLIST	BEX
XDLIST:	.BYTE	0,0,0,0,0,0,0,0	;SIXTEEN POSSIBLE DMP11'S/DMV11'S
	.BYTE	0,0,0,0,0,0,0,0	;
XDUNIT:	.WORD	0		;DMP11 LOOP VARIABLE
TOTDMP:	.WORD	0		;TOTAL NUMBER OF DMV/DMP LINES ENABLED
.LIST	BEX
	UNORG


.TAG
.ENABL	LSB

	MOV	#XDNTBL,R1	;POINT TO TABLE OF NAMES TO CHANGE
5$:	MOV	(R1)+,R0	;GET A POINTER
	BEQ	10$		;ON TO NEXT ITEM IF NO MORE
	MOVB	XDUCHR,(R0)	;SET CONTROLLER NAME
	BR	5$
10$:	MOV	#XDMTBL,R1	;POINT TO TABLE OF MAX UNIT #'S TO CHANGE
15$:	MOV	(R1)+,R0	;GET A POINTER
	BEQ	20$		;ON TO NEXT ITEM IF NO MORE
	MOVB	XDUNUM,(R0)	;SET MAX UNIT #
	BR	15$

20$:	TST	OVRIDE		;ARE WE OVERRIDING ACCEPT DEFAULTS
	BNE	23$		;YES, GO USE THE AUTO ANSWER
	TST	PRVMON		;ARE WE USING A TEMPLATE MONITOR
	BEQ	23$		;NO, GO USE THE AUTO ANSWER
	CMPB	#'0,ADMP11	;YES, WERE THERE ANY DMP11'S
	BNE	22$		;YES, GO GET THE # OF TRIBUTARIES
	CMPB	#'0,ADMP11+1	;MAYBE NOT
	BEQ	23$		;NO, GO USE THE AUTO ANSWER
22$:	MOV	XDUNIT,R1	;GET THE CURRENT UNIT
	MUL	#3,R1		;MULTIPLY BY 3 TO GET THE OFFSET
	ADD	#XD0UN,R1	;ADD THE BEGINNING OF THE XD ANSWER TABLE
	BR	25$		;GO ASK THE QUESTION

23$:	INC	DEFLAG		;NOT SURE OF DEFAULT
	MOV	#ANS12,R1	;SET UP AUTO ANSWER FOR DMV11'S
	CMPB	#'P,XDUCHR	;IS THE CONTROLLER A DMP11 ?
	BNE	25$		;NO
	MOV	#ANS32,R1	;SET UP AUTO ANSWER FOR DMP11'S

	TMPORG	Q5DATA

XDNTBL:	.WORD	30$		;POINTERS TO CONTROLLER NAME ("V")
	.WORD	50$
	.WORD	60$
	.WORD	0		;END THE TABLE

XDMTBL:	.WORD	35$		;POINTERS TO ASCII MAX UNIT #'S ("1")
	.WORD	0		;END THE TABLE

	UNORG

25$:	ASK
	.MESS
	.ASCII	<CR><LF>
	.ASCII	%If this DM%
30$:	.ASCII	%V11 will be  operated in "mul-%<CR><LF>
	.ASCII	%tipoint  control"   mode,   specify  the%<CR><LF>
	.ASCII	%maximum  number of tributaries that will%<CR><LF>
	.ASCII	%be "on" at  any  one time, now or in the%<CR><LF>
	.ASCII	%foreseeable future. The  maximum  number%<CR><LF>
	.ASCII	%of simultaneously active  tributaries is%<CR><LF>
35$:	.ASCII	%12. %
	.ASCII	%More tributaries than  specified may%<CR><LF>
	.ASCII	%be  attached  to the  line but  only the%<CR><LF>
	.ASCII	%number  specified may be  simultaneously%<CR><LF>
	.ASCII	%active.  If the DM%
50$:	.ASCII	%V11 will  be  operated%<CR><LF>
	.ASCII	%only in  "multipoint slave" or in "point%<CR><LF>
	.ASCII	%to point" mode, specify 1.%<CR><LF>
	ME.SS
	.ASCII	<CR><LF><CR>
	.ASCII	%DM%
60$:	.ASCII	%V11 unit %
XDUNT1:	.ASCII	%00 tributaries ? %
	MESS.

	BR	ASKDMP		;USER TYPED ESCAPE
	CALLX	NUMBER,R5	;GET NUMBER
	.WORD	PARAM		;SHOVE IT HERE
	.WORD	0		;ANYTHING FROM 0
	.WORD	32.		;TO FORCED MAX
	LOOP			;BAD NUMBER
	MOV	XDUNIT,R0	;GET DMP/DMV UNIT INDEX
	MOVB	R1,XDLIST(R0)	;SAVE NUMBER OF LINES ENABLED
	ADD	R1,TOTDMP	;ADD LINES ENABLED TO TOTAL DMP/DMV COUNT
	INC	XDUNIT		;PREPARE FOR THE NEXT UNIT
	CMP	TOTDMP,#128.	;DID WE EXCEED THE MAXIMUM OF 128 TRIBS.
	BGT	70$		;YES. TELL THE USER
	BR	XDLOOP		;GO BACK FOR THE NEXT DMP/DMV

70$:	TELL
	.MESS
	.ASCII	<CR><LF>
	.ASCII	%You have specified a total of more than%<CR><LF>
	.ASCII	%128 tributaries.RSTS/E will not support%<CR><LF>
	.ASCII	%more than 128 tributaries.%<CR><LF>
	MESS.

	JMP	ASKDMP

.DSABL	LSB
GLOBAL	<ANS12,ANS32,DEFLAG,PARAM,XD0UN,PRVMON>

.TAG
CHKLNK:
10$:	CLR	OVRIDE		;CLEAR THE OVERRIDE FLAG
	CMPB	DMC11,#'0	;ANY DMC'S OR DMR'S ?
	BEQ	20$		;NO, CONTINUE ON
	INC	DSDMC		;WE HAVE AT LEAST ONE DMC11/DMR11
20$:	CMPB	DMP11,#'0	;ANY DMP'S OR DMV'S?
	SKIP	EQ		;NO, LEAVE FLAG ALONE
	INC	DSDMP		;SAY AT LEAST ONE DMP/DMV

GLOBAL	<DSDMC,DSDMP>

.TAG
KMCSUP:	MOVB	AKMC11,II11	;Assume we have as many IIs as KMCs	;024
	MOVB	AKMC11+1,II11+1	;  ...					;024
	TST	PRVMON		;ARE WE USING A TEMPLATE MONITOR?	;024
	BEQ	10$		;NO, GO USE THE AUTO ANSWER
	MOV	#ASILNK,R1	;GET THE ANSWER FROM THE TEMPLATE MONITOR
	BR	15$		;And use that				;022

10$:	MOV	#AKMC11,R1	;Use the number of KMC11s as the default;024

15$:	ASK
	.MESS
	.ASCII	<CR><LF>
	.ASCII	%Each KMC11 can control  one  DUP11  syn-%<CR><LF>
	.ASCII	%chronous  line  interface  to  the  3271%<CR><LF>
	.ASCII	%or 2780/3780 HPE host.  How many  simul-%<CR><LF>
	.ASCII	%taneous  KMC/3271  or  KMC/2780/3780 HPE%<CR><LF>
	.ASCII	%links do you want (0 to 16)?%<CR><LF>			;024
	ME.SS
	.ASCII	<CR><LF>
	.ASCII	%IBM 3271 or 2780/3780 simultaneous links ?%		;024
	MESS.
	BR	20$		;USER TYPED ESCAPE			;024
	CALLX	NUMBER,R5	;CHECK AND STORE THE NUMBER
	.WORD	II11
	.WORD	0.							;022
	.WORD	16.
	LOOP			;BAD ANSWER
	MOVB	II11,KMC11	;MOVE ANSWER INTO NUMBER OF KMC11s	;024
	MOVB	II11+1,KMC11+1	;DITTO					;024
	CLR	DSKMC		;Assume no KMCs at all			;024
	CMPB	KMC11,#'0	;ANY KMC'S?				;024
	BEQ	17$		;NO, don't flag that we have one	;024
	INC	DSKMC		;Yes, so indicate we have at least one	;024
17$:	BR	LPCONT		;CONTINUE				;024

20$:	JMP	ASKDMP		;USER TYPED ESCAPE			;024

GLOBAL	<AKMC11,ANSNO,DEFLAG,ASILNK,AIBMI,DSKMC>			;022

LPCONT:	CLR	LPBEX		;ASSUME NO LINE PRINTER XBUF		;024+
	CMPB	LP11,#'0	;ANY LINE PRINTERS?
	SKIP	EQ		;NO, THEN GO ONTO NEXT QUESTION
	INC	LPBEX		;SET FLAG

GLOBAL	<LPBEX>								;024-

.SBTTL	2780 SUPPORT
.ENABL	LSB

.TAG
ST2780:	CLR	DS2780		;ASSUME NO 2780
	CALLX	ZERO,R5,RJ2780 	;ASSUME NO 2780
	CLR	PATRJE		;ASSUME NO PATCHING
	CLRB	RJQFLG		;CLEAR THE Q-BUS FLAG
	CLRB	RJDPFG		;CLEAR THE DP11 INTERFACE FLAG
	MOV	#ARJ278,R1	;GET THE ANSWER FROM THE TEMPLATE MONITOR
	TST	PRVMON		;ARE WE USING A TEMPLATE MONITOR?
	BNE	5$		;YES, GO ASK THE QUESTION
	MOV	#ANSNO,R1	;SET AUTO ANSWER
	INC	DEFLAG		;NOT SURE
	
5$:	ASK
	.MESS
	.ASCII	<CR><LF>
	.ASCII	%The RSTS/E RJ2780 package  emulates  the%<CR><LF>
	.ASCII	%operation of the IBM RJ2780 Model 1 data%<CR><LF>
	.ASCII	%transmission terminal.  The package per-%<CR><LF>
	.ASCII	%mits  the RSTS/E  system  to communicate%<CR><LF>
	.ASCII	%with any IBM system  which supports  the%<CR><LF>
	.ASCII	%device, or another RSTS/E RJ2780 system.%<CR><LF>
	.ASCII	%The 2780 package  is  available  through%<CR><LF>	;024
	.ASCII	%the DECUS Library. It  is  not  included%<CR><LF>	;024
	.ASCII	%in the standard RSTS/E kit.  Do you have%<CR><LF>
	.ASCII	%the RJ2780 package and wish  to  include%<CR><LF>
	.ASCII	%it in this system (YES or NO)?%<CR><LF>
	ME.SS
	.ASCII	<CR><LF>
	.ASCII	%RJ2780 support ? %
	MESS.

	BR	KMCSUP		;USER TYPED ESCAPE			;024
	CALLX	TESTNY,R5	;CHECK ANSWER
	LOOP			;BAD
	BR	30$		;NO
	CALLX	SWSCAN,R5	;CHECK FOR SWITCHES
	.BYTE	'Q		;LOOK FOR THE /Q QBUS SWITCH
RJQFLG:	.BYTE	0		;
	.BYTE	'P		;/P - DP11 IS THE INTERFACE
RJDPFG:	.BYTE	0		;
	.WORD	0
	LOOP			;ILLEGAL SWITCH
	TSTB	RJDPFG		;WAS /P SPECIFIED?
	BEQ	7$		;NO, JUST CONTINUE
	TSTB	RJQFLG		;YES, WAS /Q SPECIFIED?
	LOOP	NE		;YES, THAT'S BAD, GO ASK AGAIN

7$:	INC	DS2780		;SET FLAG
	TST	PRVMON		;ARE WE USING A TEMPLATE MONITOR?
	BEQ	100$		;NO, GO ON TO THE NEXT QUESTION
	TSTB	RJDPFG		;WAS /P SPECIFIED?
	BNE	100$		;YES, OVERRIDE TEMPLATE MONITOR
	CMPB	#'Y,AQRJ27	;YES, DID THE MONITOR HAVE QBUS RJ SUPPORT?
	BNE	100$		;NO, GO ON TO THE NEXT QUESTION
	MOVB	#377,RJQFLG	;YES, SET THE FLAG
	BR	100$

30$:	JMP	CFGQPU		;NO, GO ON

.DSABL	LSB
GLOBAL	<DS2780,PATRJE,ANSNO,DEFLAG,PRVMON,ARJ278,AQRJ27>

100$:
	TSTB	RJQFLG		;DID THEY SAY RJ-QBUS VERSION
	BEQ	103$		;NO, SO GO ASK
	MOV	ANSDU,R0	; YES, THEN IT MUST BE DU
	BR	DU2780

103$:	TSTB	RJDPFG		;DID THEY SAY DP11 INTERFACE
	BEQ	104$		;NO, CONTINUE
	MOV	ANSDP,R0	; YES, SET IT AS THE INTERFACE
	BR	DP2780

104$:	MOV	ARJDEV,R0	;GET THE ANSWER OUT OF THE TEMPLATE MONITOR
	TST	PRVMON		;ARE WE USING A TEMPLATE MONITOR
	BEQ	105$		;NO, SET UP THE DEFAULT BASED ON THE HARDWARE
	CMPB	#'?,R0		;IS THE DEFAULT UNKNOWN
	BEQ	105$		;YES, GO FIGURE IT OUT BASED ON THE HARDWARE
	BR	130$		;NO, GO USE IT

105$:	MOV	ANSDU,R0	;SET AUTO ANSWER TO DU11
	CMPB	ADUP,#'Y	;IS THERE A DUP11
	BEQ	130$		;YES, GO USE DU11
	CMPB	ADU,#'Y		;IS THERE ONLY A DU?
	BEQ	130$		;YES, GO USE DU11
	CMPB	ADP,#'Y		;IS THERE ONLY A DP11?
	BNE	130$		;NO, GO USE DU11
	MOV	ANSDP,R0	;PUT IN DP11

130$:	CMP	ANSDP,R0	;IS IT DP11
	BEQ	DP2780		;YES, GO PUT IT IN

DU2780:	INC	DS2780		;SAY DU11 2780
DP2780:	MOVB	#'",RJ2780	;SET CONFIG PARAMETER
	MOVB	R0,RJ2780+1	;SET CONFIG PARAMETER
	SWAB	R0		;GET THE HIGH BYTE
	MOVB	R0,RJ2780+2	;TO DP OR DU


	TMPORG	Q5DATA
ANSDU:	.ASCII	%DU%
ANSDP:	.ASCII	%DP%
	.EVEN
	UNORG

GLOBAL	<ADUP,ADU,ADP,DEFLAG,ANSDEF,DS2780,PRVMON,ARJDEV>

RPTASK:	CLR	PATRJE		;ASSUME NO PATCHING
	DEC	PATRJE		;ALWAYS PATCH RJ2780

GLOBAL	<PATRJE>

	MOV	#"SY,RPTDEV		;SYSTEM DISK IS PATCH FILE MEDIUM

	TMPORG	Q5DATA
ANPTDV:	.ASCII	%SY%<CR>
	.EVEN
	UNORG

GLOBAL	<RPTDEV>

CFGQPU:
.TAG
	CONFIG			;WRITE THE CONFIG FILE
	.MESS
.NLIST	MEB
	.ASCII	%; OTHER DEVICES%<CR><LF><CR><LF>
	.ASCII	%TC11	==	0.     	;# OF DECTAPE DRIVES; ELSE 0%<CR><LF>
	.ASCII	%PR11	==	0.     	;1 IF PAPER TAPE READER/PUNCH; ELSE 0%<CR><LF>
	.ASCII	%PP11	== 	0.     	;SAME AS PR11%<CR><LF>
	PARAM	TM02,0.,^%# OF TU16/TE16/TU45/TU77'S; ELSE 0%
	.ASCII	<CR><LF>
	PARAM	TM11,0.,^%# OF TU10/TE10/TS03'S; ELSE 0%
	.ASCII	<CR><LF>
	PARAM	TS11,0.,^%# OF TS11'S; ELSE 0%
	.ASCII	<CR><LF>
	PARAM	TMSCP,0.,^%# OF TMSCP UNIT'S; ELSE 0%
	.ASCII	<CR><LF>
	PARAM	TU58,0.,^%# OF DECTAPE II DRIVES; ELSE 0%
	.ASCII	<CR><LF>
	PARAM	LP11,0.,^%# OF LINE PRINTERS; ELSE 0%
	.ASCII	<CR><LF>
	PARAM	RX11,0.,^%# OF FLOPPY DRIVES; ELSE 0%
	.ASCII	<CR><LF>
	PARAM	DMC11,0.,^%# OF DMC11'S/DMR11'S; ELSE 0%
	.ASCII	<CR><LF>
.LIST	MEB
	MESS.


; WRITE OUT DMV11/DMP11 PARAMETERS

.TAG
	CONFIG
	.MESS
.NLIST	MEB
	PARAM	DMP11,0.,^%# OF DMP11'S/DMV11'S; ELSE 0%
.LIST	MEB
	MESS.

	CLR	XDCNT		;CLEAR UNIT COUNTER
XDCFGL:	MOV	XDCNT,R2	;GET THE UNIT NUMBER
	CMP	R2,DMP.11	;DONE ?
	SKIP	EQ		;IF EQUAL YES
	CALLX	CHGDEC		;MAKE THE NUMBER ASCII
	CMPB	R2,#'0		;IS THE HIGH DIGIT A 0
	BNE	10$		;NO
	CLRB	R2		;YES. CLEAR IT
10$:	MOV	R2,XDCFG1	;STORE THE TEXT
	MOV	XDCNT,R0	;GET UNIT # AGAIN
	MOVB	XDLIST(R0),R1	;GET NUMBER OF LINES
	CALLX	STRDEC,R5,XDCFGN ;SET IT
	CONFIG
	.MESS
	.ASCII	%XD%
XDCFG1:	.BYTE	0,0
	.ASCII	%UN%<HT>%==%<HT>
XDCFGN:	.ASCII	<40><40><40><40><40><40><40><HT>
	.ASCII	%;NUMBER OF DROPS ENABLED%<CR><LF>
	MESS.

	INC	XDCNT
	BR	XDCFGL

	TMPORG	Q5DATA
XDCNT:	.WORD	0		;UNIT COUNTER FOR CONFIG OUTPUT
	UNORG

.TAG
	CONFIG
	.MESS
.NLIST	MEB
	.ASCII	<CR><LF>
	PARAM	KMC11,0.,^%# OF KMC11'S; ELSE 0%
	.ASCII	<CR><LF>
	PARAM	II11,0.,^%# OF KMC/3271 LINKS; ELSE 0%
	.ASCII	<CR><LF>
	PARAM	CR11,0.,^%1 IF CR11/CM11; ELSE 0%
	.ASCII	<CR><LF>
	PARAM	CD11,0.,^%1 IF CD11; ELSE 0%
	.ASCII	<CR><LF>
	PARAML	RJ2780,0,^%"DP OR "DU FOR 2780; ELSE 0%			;020
	.ASCII	<CR><LF>
	.LIST	MEB
	MESS.

.ENABL	LSB

	TSTB	RJQFLG		;IS THIS A Q-BUS RJ??
	BEQ	10$		;NO
	CONFIG
	.MESS
	PARAML	QBUSRJ,1.,^%1 IF NO KG11; 0 IF KG11 AVAILABLE%		;020
	.ASCII	<CR><LF>
	MESS.

10$:	RETURN			;RETURN TO ROOT SEGMENT

.DSABL	LSB

	.END
