	.INCLUDE /CMN:COMMON/
	.INCLUDE /SGN:SGNCOM/
TITLE	SGNDIR,<SYSGEN SIL DIRECTORY PRINTER>,0A,10-MAY-91,JJT

;
;		COPYRIGHT (c) 1974, 1991 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

	.SBTTL	SGNDIR EDIT HISTORY
;+
; EDIT	 BY	  DATE			REASON
;
;  00	JJT	23-Dec-87	Subprogram creation
;  01	JJT	21-Feb-90	Find the previously installed .SIL
;
;-

.EQUATE	.PRIV	EMT!377
.EQUATE	.DATTIM	EMT!361

	DEFORG	Q12COD

MESNUM	=	0
MSGSEG	=	12
TAGNUM	=	0

	.SBTTL	SYSGEN SIL DIRECTORY PRINTER

SGNDIR::NOP
	CALL	REGSAV,R5		;SAVE EVERYBODY
	TST	PRTDIR			;Should we display anything	;002
	BEQ	CONT			;No, so continue		;002
	TELL				;PRINT A HEADER
	.MESS
	.ASCII	<CR><LF>
	.ASCII	%The following is a list of the .SILs in [0,1]:%<CR><LF>
	.ASCII	<CR><LF>
	.ASCII	<HT>
	.ASCII	% File .Typ%<HT>%Creation%<CR><LF>
	MESS.


CONT:	MOV	#SYSLOG,R0		;GET THE ADDRESS OF THE LOGICAL
	EMT	365			;FSS IT TO GET THE DEVICE AND PPN
	TST	XRB+10			;WAS THE LOGICAL DEFINED?
	BPL	10$			;YES, GO USE IT
	MOV	#1,FIRQB+FQPPN		;NO, GO LOOK IN [0,1]
	MOV	#"SY,FIRQB+FQDEV	; ON SY
	MOV	#255.*256.,FIRQB+FQDEVN	;      0:

10$:	CLR	FIRQB+FQFIL		;INDEX = 0
LOOP:	MOVB	#UU.LOK,FIRQB+FQFUN	;UU.LOK SUBFUNCTION
	MOV	#134745,FIRQB+FQNAM1	;WILDCARD FILENAME (1ST PART)
	MOV	#134745,FIRQB+FQNAM1+2	;WILDCARD FILENAME (2ND PART)
	MOV	#^RSIL,FIRQB+FQEXT	;FILE TYPE OF .SIL
	CLRB	FIRQB+FQMODE		;CLEAR MARKED-FOR-DELETE FLAG
	.PRIV,	.UUO			;DO THE LOOKUP
	TSTB	FIRQB			;ANY ERRORS?
	BNE	EXIT			;YES, EXIT
	TST	PRTDIR			;Are we display anything	;002
	BEQ	FNDSIL			;No, so continue		;002
	MOV	#FILNAM,R4		;WHERE TO STORE 1ST THREE CHARS
	MOV	FIRQB+FQNAM1,R0		;RAD-50 OF 1ST THREE CHARS
	CALL	RADCNV			;CONVERT IT
	MOV	#FILNAM+3,R4		;WHERE TO STORE 2ND THREE CHARS
	MOV	FIRQB+FQNAM1+2,R0	;RAD-50 OF 2ND THREE CHARS
	CALL	RADCNV			;CONVERT IT
	MOV	FIRQB+FQFLAG,XRB	;MOVE THE CREATION DATE
	CLR	XRB+2			;NO TIME WANTED
	MOV	#DATE,R0		;GET THE ADDRESS OF THE DATE STRING
	.DATTIM				;CONVERT THE DATE TO A STRING
	
	TELL
	.MESS
	.ASCII	<HT>
FILNAM:	.ASCII	%XXXXXX.SIL%<HT>
DATE:	.ASCII	%XX-XXX-XX%<CR><LF>
	MESS.
	BR	NXTFIL			;Go find the next .SIL		;002+

FNDSIL:	CMP	#^RSYS,FIRQB+FQNAM1	;Is this SIL SYSGEN?
	BNE	20$			;No, the first 3 chars weren't "SYS"
	CMP	#^RGEN,FIRQB+FQNAM1+2	;Check last 3 characters
	BNE	20$			;No, it isn't SYSGEN
	TST	STORED			;Yes, was there a previous .SIL
	BNE	EXIT			;Yes, we are all set, so exit
	INC	SGNFND			;No, mark that we found SYSGEN.SIL
	BR	NXTFIL			;and get the next file

20$:	MOV	FIRQB+FQNAM1,SILNM1	;RAD-50 of 1st three chars
	MOV	FIRQB+FQNAM1+2,SILNM2	;RAD-50 of 2nd three chars
	TST	SGNFND			;Have we found SYSGEN.SIL yet
	BNE	EXIT			;Yes, we are all set, so exit
	INC	STORED			;Mark that we stored a .SIL
	.BR	NXTFIL			;and get the next file
	
NXTFIL:	INC	FIRQB+FQFIL		;INC THE INDEX			;002-
	BR	LOOP			;GO GET THE NEXT FILE


EXIT:	CALL	REGRES,R5		;RESTORE EVERYBODY
	RETURN

	TMPORG	Q12DAT
SYSLOG:	.ASCIZ	/SYSTEM$$:/
	.EVEN
SGNFND:	.WORD	0			;SYSGEN.SIL FOUND FLAG		;002
STORED:	.WORD	0			;STORED AT LEAST ONE .SIL FLAG	;002
	UNORG

GLOBAL	<RADCNV,PRTDIR,SILNM1,SILNM2>


GLOBAL	<REGSAV,REGRES>

	.END
