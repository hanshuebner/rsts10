.INCLUDE	%CMN:COMMON%
TITLE	CHKPRV,<CHECK PRIVILEGE>,0A,10-MAY-91,PRL

;
;		COPYRIGHT (c) 1974, 1991 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

.SBTTL	Edit history for CHKPRV

;+
;Edit   By   Date	Reason
;
; 000	PRL  14-May-84	Creation for Version 9.0
; 001	PRL  27-May-84	Bug fixes
;
;-

.SBTTL	CALL IN NEEDED MACROS

	.MCALL	PUSH,POP

.SBTTL	CHKPRV	- CHECK IF PRIVILEGE ENABLED

;+
;
; CHKPRV:
;
;	CHKPRV checks if the specified privilege is
;	currently enabled or disabled.
;
; INPUTS:
;
;		R0 -> 6-char priv name to check,
;		      padded with nulls.  Name
;		      must begin on word boundary.
;
;		R1 =  3rd party PPN to install
;		      privileges.  0 means use
;		      current privileges.
;
;		R2 -> 3rd party privilege mask.
;		      Ignored if R1 is 0.
;
; CALL:
;
;	JSR	PC,CHKPRV
;
; BACK:
;
;	C=0 if priv enabled
;	C=1 if priv disabled or invalid priv name
;	Current privileges always restored
;	All regs preserved
;
;-

	.PSECT	PBSLIB,RO,CON

CHKPRV::PUSH	<R0,R1,R3>		;save some work regs
	TST	R1			;any PPN specified?
	BEQ	5$			;no, so skip 3rd party privs
	CALLX	SETPRV			;set 3rd party privs
5$:	CALLX	CLRFQB			;clear the FIRQB
	MOVB	#UU.CHK,@#FIRQB+FQFUN	;load UU.CHK function
	MOVB	#1,@#FIRQB+FQFIL	;load subfun 1
	MOV	#FIRQB+FQNAM1,R3	;R3 -> FIRQB+FQNAM1
	MOV	(R0)+,(R3)+		;Copy
	MOV	(R0)+,(R3)+		; priv name
	MOV	(R0),(R3)		;  into FQNAM1
	.UUO				;test the priv name
	TSTB	@#FIRQB			;any errors?
	BNE	20$			;yes, so exit with carry set
	TSTB	@#FIRQB+FQERNO		;do we have the priv?
	BNE	20$			;no, so exit with carry set
	TST	(PC)+			;clear carry for priv
20$:	SEC				;set carry for no priv or error
	ROL	-(SP)			;save carry on the stack
	TST	R1			;did we set 3rd party privs?
	BEQ	30$			;no, so skip restoring our privs
	CLR	R1			;yes, clear PPN reg
	CALLX	SETPRV			;and restore our privs
30$:	ROR	(SP)+			;restore carry
	POP	<R3,R1,R0>		;restore work regs
	RETURN				;and exit

	.END
