.INCLUDE	%CMN:COMMON%
TITLE	BLDPPN,<BUILD PPN STRING>,0A,10-MAY-91,PRL

;
;		COPYRIGHT (c) 1974, 1991 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

.SBTTL	EDIT HISTORY FOR BLDPPN
;+
;
;  001  PRL  09-Jan-83	CREATION - And it was a good thing
;
;-

.SBTTL	DECLARE PSECT FOR BLDPPN

	.PSECT	PBSLIB,RO,CON

.SBTTL	BLDPPN	- BUILD PPN STRING [PROJ,PROG]

;+
;
; BLDPPN:
;
;
; INPUTS:
;
;		R1 =  PPN (PROJ IN HI BYTE, PROG IN LO BYTE)
;		R2 -> BUFFER TO OUTPUT PPN STRING
;		      (MINIMUM BUFFER LENGTH = 9)
;
; CALL:
;	JSR	PC,BLDPPN
;
;
; BACK:
;
;		PPN STRING IN BUFFER
;		R1 = LENGTH OF PPN STRING
;		ALL OTHER REGISTERS PRESERVED
;
;-

BLDPPN::PUSH	<R0,R2,R3,R4,R5>	;SAVE REGS
	MOV	R2,R5			;R5 -> BUFFER START
	MOVB	#'[,(R5)+		;LOAD START OF PPN
	MOV	R1,R3			;R3 = PPN WORD
	BIC	#377,R3			;MASK OFF PROG NO.
	SWAB	R3			;AND MOVE PROJ NO. INTO LO BYTE
	CALL	BIN2D			;CALL BINARY-TO-DECIMAL CONVERTER
	MOVB	#',,(R5)+		;LOAD COMMA
	MOV	R1,R3			;R3 = PPN WORD
	BIC	#^C<377>,R3		;MASK OFF PROJ NO.
	CALL	BIN2D			;CALL BINARY-TO-DECIMAL CONVERTER
	MOVB	#'],(R5)+		;LOAD END OF PPN STRING
	MOV	R5,R1			;SAVE END ADDR OF STRING
	SUB	R2,R1			;RETURN LEN OF PPN STRING IN R1
	POP	<R5,R4,R3,R2,R0>	;RESTORE REGS
	RETURN

	GLOBAL	<BIN2D>

	.END
