.INCLUDE	%CMN:COMMON%
TITLE	SETPRV,<SET THIRD PARTY PRIVILEGE>,0A,10-MAY-91,KCG

;
;		COPYRIGHT (c) 1974, 1991 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

.SBTTL	Edit history for SETPRV

;+
;Edit	By   Date	Reason
;
; 000	KCG  26-Mar-84	Creation for Version 9.0
; 001	PRL  26-Apr-84	Fix PPN in FQFUN bug
; 002	PRL  22-May-84	Use PBSCON macro instead of SPLCMD
;			Add .INCLUDE's and .LIBRARY's
;
;-

.SBTTL	CALL IN NEEDED MACROS

.LIBRARY	%SYSTEM:PBSMAC%

	.MCALL	PUSH,POP,MOVSTR,PBSCON					;002

	PBSCON				;define package constants	;002

.SBTTL	SETPRV	- SET THIRD PARTY PRIVILEGES

;+
;
; SETPRV:
;
;	SETS PRIV MASK POINTED TO BY R2 AS CURRENT PRIV MASK.
;	A ZERO FOR PPN SPECIFIES A RESET.
;
; INPUTS:
;
;		R1 = PPN
;		R2 -> PRIV MASK
;
; CALL:
;
;	JSR	PC,SETPRV
;
; BACK:
;
;	C=0 ON SUCCESS, C=1 FAILURE
;
;	ALL REGS PRESERVED
;
;-

	.PSECT	PBSLIB,RO,CON

SETPRV::NOP
	PUSH	<R0,R1,R2>
	CALLX	CLRFQB			;SET KNOWN FIRQB STATE
	MOVB	#UU.3PP,@#FIRQB+FQFUN	;DO THIRD PARTY PRIV SET
	MOV	R1,@#FIRQB+FQPPN	;SET PPN FOR 3PP
	BEQ	10$			;IF NO PPN WAS SET
	MOV	R2,R0			;SET UP FOR PRIV MASK COPY
	MOV	#FIRQB+FQNAM1,R1	; TO FIRQB
	MOVSTR	R0,R1,#<PRVSIZ*2.>	;PERFORM THE COPY
10$:	.UUO				;SET THE PRIVS
	TST	@#FIRQB			;ERRORS?
	BNE	20$			;YES, TELL SOMEBODY WHO CARES
	TST	(PC)+			;SUCCESS
20$:	SEC				;FAILURE
	POP	<R2,R1,R0>
	RETURN

	.END
