.INCLUDE	%CMN:COMMON%
TITLE	PROMPT,<PRINT PROMPT AND READ LINE>,0A,10-MAY-91,RTW/SJM

;
;		COPYRIGHT (c) 1974, 1991 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

.SBTTL	SET UP PSECT

	.PSECT	PBSLIB,RO,CON


.SBTTL	PROMPT	- PRINT PROMPT AND READ FROM KB:
;+
;PROMPT	- PRINT PROMPT AND READ FROM KB:
;
;CALL:	R0 = 0 FOR ECHO <>0 FOR NOECHO
;	R3 ->  PROMPT STRING ASCIZ
;	R4 =   BUFFER LENGTH
;	R5 ->  BUFFER
;
;BACK:	C=0 DATA IN BUFFER CVT$$ED AND POS(KB) = 0
;	C=1 KB DETACHED
;-

PROMPT::
	.TTRST			;ARE WE ATTECHED?
	TSTB	@#FIRQB		;YES IF 0
	BNE	30$		;NOPE ALL DONE THEN
	PUSH	<R4,R5>		;SAVE ACROSS PRINT
	MOV	R3,R4		;COPY POINTER
	MOV	R3,R5		;AGAIN
10$:	TSTB	(R4)+		;AT END?
	BNE	10$		;NOPE KEEP LOOKING
	DEC	R4		;BACK OFF NULL
	SUB	R3,R4		;GET LENGTH OF PROMPT
	CALL	40$		;PRINT <CR><LF> IF NEEDED
	CLR	R3		;NO RECORD MODIFYER
	CALLX	PRINT		;PRINT PROMPT
	POP	<R5,R4>		;RESTORE
	TST	R0		;ECHO Y/N?
	BEQ	20$		;YES
	.TTNCH			;TURN OFF ECHO
20$:	CALLX	GETLIN		;READ THE LINE
	CALL	40$		;DO CRLF IF NEEDED
	MOV	#1775,R0	;SET CVT$$ FLAG
	CALLX	CVT$$		;CLEAN STRING AND RETURN
	.TTECH			;TURN ON ECHO IF OFF AND SEE IF DETACHED
	TST	(PC)+		;SET C=0 AND SKIP SEC THEN RETURN
30$:	SEC			;FLAG THE ERROR
	RETURN			;AND BACK

40$:	PUSH	<R4,R5>		;SAVE ACROSS PRINT
	CALLX	CLRXRB		;ZERO FOR POSTN
	.POSTN			;FIND OUT IF <CR><LF> NEEDED
	TSTB	@#XRB+2		;AT LEFT MARGIN?
	BEQ	50$		;YES ALL DONE
	MOVPIC	#CRLF,R5	;POINT TO BUFFER
	MOV	#2,R4		;AND SET LENGTH
	CLR	R3		;NO RECORD MODIFYER
	CALLX	PRINT		;DO PRINT
50$:	POP	<R5,R4>		;PUT WORLD BACK
	RETURN			;AND BACK

CRLF:	.BYTE	15,12		;A BUFFER WITH A <CR> AND A <LF>
	.END
