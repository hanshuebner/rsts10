.INCLUDE	%CMN:COMMON%
TITLE	R502A,<RAD50 TO ASCII ROUTINES>,0A,10-MAY-91,RTW

;
;		COPYRIGHT (c) 1974, 1991 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

.SBTTL	MACRO CALLS

.MCALL	PUSH,POP

;+
; SET UP PSECT
;-
	.PSECT	PBSLIB,RO,CON

.SBTTL	RAD50 TO ASCII CONVERSION ROUTINE.
;+
; R502A - RAD 50 TO ASCII CONVERSION ROUTINE.
;
; CALL:	R4 =  AMOUNT OF SPACE LEFT IN BUFFER
;	R5 -> OUTPUT BUFFER
;	R0 =  THREE CHARACTERS OF RAD50.
;
; BACK:	R5 -> OUTPUT BUFFER + 3
;	R4 =  R4-3
;-
.ENABL	LSB

R502A::PUSH	<R0,R1>		;SAVE SOME WORKING REGS
	CALL	10$		;GENERATE AN ASCII CHARACTER IN R1.
	PUSH	R1		;SAVE IT
	CALL	10$		;GENERATE AN ASCII CHARACTER IN R1.
	PUSH	R1		;SAVE IT
	CALL	10$		;GENERATE AN ASCII CHARACTER IN R1.
	MOVB	R1,(R5)+	;OUTPUT FIRST CHAR
	MOVB	(SP)+,(R5)+	;OUTPUT SECOND CAHR
	MOVB	(SP)+,(R5)+	;STRING IS NOW IN OUTPUT BUFFER.
	SUB	#3,R4		;UPDATE SPACE LEFT IN BUFFER
	POP	<R1,R0>		;PUT THE WORLD BACK
	RETURN			;RETURN TO CALLER.

10$:	MOV	R0,R1		;SET UP FOR DIVIDES.
	CLR	R0		;ZERO THE HIGH BITS
	DIV	#50,R0		;EXTRACT ONE RAD 50 CHARACTER.
	TST	R1		;BLANK?
	BNE	20$		;NO.
	MOV	#' ,R1		;RETURN BLANK IN R1.
	RETURN

20$:	SUB	#^R  Z,R1	;ALPHA?
	BGT	30$		;NO.
	ADD	#'@+^R  Z,R1	;YES, RETURN ALPHA CHARACTER.
	RETURN

30$:	SUB	#^R  0-^R  Z,R1	;SPECIAL CHARACTERS?
	BLT	40$		;YES.
	ADD	#'0,R1		;NO, RETURN NUMERIC CHARACTER.
	RETURN

40$:	MOVB	50$(R1),R1	;LOAD R1 WITH SPECIAL CHARACTERS.
	RETURN

	.BYTE	'$		;NEGATIVE INDEXING USED IN THIS TABLE.
	.BYTE	'.	
	.BYTE	'?		;UNUSED RAD50 VALUE, RETURN AS ?.
50$:
	.EVEN
	.DSABL	LSB

	.END
