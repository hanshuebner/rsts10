TITLE	GETUNQ,<SCAN UNIQUE STRING>,0A,10-MAY-91,ABC

;
;		COPYRIGHT (c) 1974, 1991 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

;+
; GETUNQ - FIND UNIQUE STRING FROM TABLE OF UNIQUE/CORRECT TEXT
;
; CALL:	R5 -> STRING TO BE MATCHED (NON-PARITY ASCII)
;	CALL	GETUNQ,R0,<PTR TO UNQTXT TO MATCH>
;
; BACK:	IF MATCH FOUND, R5 -> FIRST UNMATCHED CHARACTER
;		R1 = INDEX OF STRING MATCHED
;		C=1
;	IF NO MATCH, C=0 AND R5 UNCHANGED
;
; NOTE:	FORMAT OF UNQTXT TABLE IS:
;	FOR EACH TEXT STRING, REQUIRED PART IS NON-PARITY ASCII
;		OPTIONAL PART FOLLOWS IN PARITY ASCII
;		EACH ENTRY TERMINATES WITH A 377 BYTE
;	ENTIRE TABLE ENDS WITH 0 BYTE
;-
.CSECT

GETUNQ::MOV	R5,-(SP)	;SAVE R5 TEXT POINTER
	MOV	(R0)+,R1	;TEXT TO CHECK
	CLR	-(SP)		;INDEX IS 0
10$:	MOV	2(SP),R5	;RESET R5
20$:	MOVB	(R1)+,-(SP)	;GET A BYTE OF TEXT
	BIC	#^C<177>,(SP)	;TRIM PARITY
	CMPB	(SP)+,(R5)+	;A MATCH?
	BEQ	20$		;YES, LOOP
	TSTB	-(R1)		;NO, ARE WE VALID?
	BMI	40$		;YES, EXIT WITH SUCCESS
	INC	(SP)		;NO, INCREMENT THE INDEX
30$:	CMPB	#377,(R1)+	;SCAN TO END OF THIS COMMAND
	BNE	30$
	TSTB	(R1)		;ANY MORE?
	BNE	10$		;YES, TRY AGAIN
	MOV	(SP)+,R5	;DUMP THE INDEX
	MOV	(SP)+,R5	;RESTORE R5
	RETURN	R0		;RETURN WITH C=0

40$:	MOV	(SP)+,R1	;GET THE INDEX
	COM	(SP)+		;DUMP OLD R5, SET CARRY
	DEC	R5		;BACK OFF TO UNSCANNED CHARACTER
	RETURN	R0

.END

