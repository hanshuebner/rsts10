TITLE	VAL,<CONVERT STRING TO INTEGER>,0A,10-MAY-91,ABC

;
;		COPYRIGHT (c) 1974, 1991 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

;+
; VAL - CONVERT STRING TO INTEGER
;
; CALL:	R5 -> STRING TO CONVERT
;	CALL	VAL
;
; BACK:	(SP) = VALUE OBTAINED
;	R5 -> FIRST UNCONVERTED CHARACTER
;	LEADING, EMBEDDED, AND TRAILING BLANKS ARE IGNORED
;	C=1 MEANS OVERFLOW DETECTED OR NO NUMBER PRESENT
;		AND ILLEGAL NUMBER CODE SET IN IOSTS
;-
.CSECT

VAL::	CMPB	(R5)+,#40	;PRESKIP BLANKS
	BEQ	VAL
	MOV	(SP),-(SP)	;SAVE RETURN ADDRESS
	CMPB	-(R5),#'0	;MAKE SURE A NUMBER IS THERE
	BLO	40$		;NO, RETURN ERROR
	CMPB	#'9,(R5)	;MUST BE 0-9
	BLO	40$		;NO NUMBER FOUND
	MOV	R0,-(SP)	;GET WORKING REGS
	MOV	R1,-(SP)
	CLR	R1		;ACCUMULATOR = 0
20$:	CMPB	(R5)+,#40	;SKIP BLANKS
	BEQ	20$
	MOVB	-(R5),R0	;NEXT BYTE
	SUB	#'9+1,R0	;CHECK FOR NUMERIC
	ADD	#'9+1-'0,R0
	BCC	30$		;NOT, SO EXIT
	INC	R5		;BUMP OVER THE NUMERIC
	CMP	#6553.,R1	;ARE WE GOING TO OVERFLOW?
	BLO	30$		;YES
	MUL	#10.,R1		;NO, SHIFT
	ADD	R0,R1		;ADD IN THIS DIGIT
	BCC	20$		;LOOP IF NO OVERFLOW
30$:	MOV	R1,6(SP)	;SET RETURN VALUE
	MOV	(SP)+,R1	;RESTORE REGS
	MOV	(SP)+,R0
40$:	BCC	50$		;NO ERROR
	MOVB	#BDNERR,@#402	;SAY 'ILLEGAL NUMBER'
50$:	RETURN			;RETURN (C=1 IF ERROR)

GLOBAL	<BDNERR>

.END

