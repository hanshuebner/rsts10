TITLE	PKGLOC,<RETURN PACKAGE LOCATION>,0A,10-MAY-91,SJK/JTC

;
;		COPYRIGHT (c) 1974, 1991 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

;+
; PKGLOC - RETURN THE PACKAGE LOCATION OF EXECUTING PROGRAM
;
;	STANDARD R5 CALLING CONVENTION
;	PARAMETER PASSED IS A 13 LOGICAL*1 ARRAY
;
;	CALL	PKGLOC(ARRAY)
;
;	ARRAY CONTAINS A DEVICE AND PPN FROM WHERE THE CURRENTLY
;	EXECUTING PROGRAM IS STORED. THIS MUST BE DONE BEFORE ANY
;	CALLS WHICH MODIFY THE FIRQB ARE PERFORMED.
;-

.MCALL	.PRINT,.EXIT

.CSECT

PKGLOC::MOVB	(R5)+,R0	; GET NUMBER OF PARAMETERS
	DEC	R0		; MAKE SURE IT IS EQUAL TO 1
	BNE	30$		;  TELL THE USER HE IS DUMB
	TSTB	(R5)+		; BUMP THE R5 POINTER TO THE ARRAY
	MOV	(R5),R3		; GET THE POINTER HERE
	MOV	R3,R5		;   AND HERE (R3 USED TO SAVE POINTER)
	MOV	#FIRQB+FQDEV,R1	; GET A POINTER TO THE DEVICE NAME
	MOVB	(R1)+,(R5)+	; GET THE FIRST CHARACTER OF THE DEVNAME
	MOVB	(R1)+,(R5)+	; GET THE 2ND CHARACTER
	MOVB	(R1)+,R0	; GET THE UNIT NUMBER
	ADD	#'0,R0		; ADD IN THE ASCII BASE
	MOVB	R0,(R5)+	;  OUTPUT THE ASCII DEVICE UNIT NUMBER
	MOVB	@#FIRQB+FQPPN+1,R0 ; GET THE PROJECT NUMBER
	BIC	#^C377,R0	; MAKE SURE IT IS A LEGAL PROJ NO.
	MOVB	#'[,(R5)+	; SET A PPN OPENER
	CALLX	NUM$		; CONVERT IT
	MOVB	#',,(R5)+	; COMMA
	MOVB	@#FIRQB+FQPPN,R0; GET THE PROGRAMMER NUMBER
	BIC	#^C377,R0	; MAKE IT A LEGAL PROG NUMBER
	CALLX	NUM$		; CONVERT IT
	MOVB	#'],(R5)+	; CLOSE THE PPN
	SUB	R5,R3		; GET THE NUMBER OF BYTES OUTPUT
	ADD	#13.,R3		; FIND NUMBER OF BYTES TO SPACE PAD
	BEQ	20$		;  NO PADDING NECESSARY
10$:	MOVB	#SPACE,(R5)+	; PAD A BYTE
	SOB	R3,10$		; LOOP UNTIL DONE
20$:	RETURN			; RETURN

30$:	.PRINT	#40$		; TELL THE USER THAT HE IS REALLY STUPID
	.EXIT

	.NLIST	BEX		; DON'T EXPAND THE NEXT TEXT
40$:	.ASCIZ	/?PKGLOC called with wrong number of arguments/
	.EVEN	;THIS WON'T HELP
.END
