TITLE	DECNUM,<DECIMAL NUMBER TO ASCII CONVERTER>,0A,10-MAY-91,ABC

;
;		COPYRIGHT (c) 1974, 1991 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

.SBTTL	Edit History
;+
; EDIT	BY	DATE		REASON
;
; 001	JJT	06-May-86	Fix 32-bit algorithm
;-


;+
; DECNUM - EMIT SINGLE PRECISION FIXED FIELD DECIMAL NUMBER
; DECDBL - EMIT DOUBLE PRECISION FIXED FIELD DECIMAL NUMBER
; DECDBP - EMIT DOUBLE PRESICION FIXED FIELD DECIMAL NUMBER FROM MEMORY
;
;	R0 =  NUMBER TO PRINT LSB (DECNUM AND DECDBL)
;	   -> NUMBER MSB/NUMBER LSB (DECDBP ONLY)
;	R1 =  NUMBER TO PRINT MSB (DECDBL ONLY)
;	R5 -> OUTPUT STRING
;
;	CALL	DEC???,R4,<FIELD WIDTH>
;
;	R5 -> NEXT BYTE
;
; NOTE:	NUMBER WILL BE RIGHT JUSTIFIED IN FIELD. USE SIZE 0 FOR NO FILL
;-

.CSECT

.ENABL	LSB

DECDBP::MOV	(R4)+,30$	; SAVE FIELD WIDTH
	CALLX	REGSAV,R5	; SAVE REGISTERS
	MOV	(R0)+,R1	; GET MSB
	MOV	(R0),R0		;  AND LSB
	BR	10$		; MERGE

DECDBL::TST	(PC)+		; C=0 FOR DOUBLE PRECISION
DECNUM::SEC			; C=1 FOR SINGLE PRECISION
	MOV	(R4)+,30$	; SAVE FIELD WIDTH
	CALLX	REGSAV,R5	; SAVE REGISTERS
	BCC	10$		; NUMBER MSB IS SET IF DOUBLE PRECISION
	CLR	R1		; MSB IS 0 FOR SINGLE PRECISION
10$:	MOV	R0,R3		; COPY LSB TO AN ODD REGISTER
	CLR	R4		; NO DIGITS SAVED YET
20$:	CLR	R0		; CLEAR FOR DIVIDE
	DIV	#10.,R0		; DIVIDE MSB BY 10
	MOV	R1,R2		; PROPAGATE REMAINDER TO MSB OF LOW ORDER
	MOV	R0,R1		; COPY THE QUOTIENT AS NEW MSB
	DIV	#20.,R2		; DIVIDE LOW ORDER QUOTIENT BY 20	;001
	ASL	R2		; TO OBTAIN 15 BIT RESULT		;001
	CMP	#10.,R3		; AND THEN CORRECT			;001
	BGT	25$		; TO OBTAIN DIVISION BY 10.		;001
	SUB	#10.,R3		; GET REMAINDER LESS THAN 10.		;001
	INC	R2		; ADD CARRY TO QUOTIENT			;001
25$:	BIS	#'0,R3		; MAKE REMAINDER ASCII
	MOV	R3,-(SP)	; SAVE A BYTE
	INC	R4		; ONE MORE BYTE SAVED
	MOV	R2,R3		; COPY QUOTIENT AS NEW LSB
	BIS	R1,R2		; ENTIRE NUMBER NOW 0?
	BNE	20$		; NO, MORE TO DO
	MOV	(PC)+,R0	; GET FIELD WIDTH
30$:	 .WORD	0		; SAVE AREA
	SUB	R4,R0		; COMPUTE NUMBER OF BLANKS TO FILL
	BLE	50$		; IF NONE, DON'T FILL
40$:	MOVB	#40,(R5)+	; BLANK
	SOB	R0,40$		;  FILL
50$:	MOVB	(SP)+,(R5)+	; MOVE
	SOB	R4,50$		;  NUMBER
	MOV	R5,5*2(SP)	; SAVE NEW TEXT POINTER
	CALLX	REGRES,R5	; RESTORE REGISTERS
	RETURN	R4

.DSABL	LSB

.END

