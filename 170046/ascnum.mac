TITLE	ASCNUM,<DECIMAL TO ASCII CONVERSION>,0A,10-MAY-91,SJK

;
;		COPYRIGHT (c) 1974, 1991 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

;+
; ASCNUM - EMIT SINGLE PRECISION FIXED FIELD DECIMAL NUMBER
; ASCDEC - EMIT SINGLE PRECISION FIXED FIELD DECIMAL NUMBER
; ASCDBL - EMIT DOUBLE PRECISION FIXED FIELD DECIMAL NUMBER
; ASCDBP - EMIT DOUBLE PRESICION FIXED FIELD DECIMAL NUMBER FROM MEMORY
;
;	R0 =  NUMBER TO PRINT MSB (ASCDBL ONLY)
;	   =  NUMBER TO PRINT LSB (ASCDEC ONLY)
;	   -> NUMBER MSB/NUMBER LSB (ASCDBP ONLY)
;	R1 =  NUMBER TO PRINT LSB (ASCNUM AND ASCDBL ONLY)
;	R5 -> OUTPUT STRING
;
;	CALL	ASC???,R4,<FIELD WIDTH>
;
;	R5 -> NEXT BYTE
;
; NOTE:	NUMBER WILL BE RIGHT JUSTIFIED IN FIELD. USE SIZE 0 FOR NO FILL
;-

.CSECT

.ENABL	LSB

ASCDEC::MOV	(R4)+,40$	; SAVE FIELD WIDTH
	CALLX	REGSAV,R5	; SAVE THE REGISTERS
	MOV	R0,R1		; SET THE NUMBER
	BR	10$		;  AND MERGE

ASCDBP::MOV	(R4)+,40$	; SAVE FIELD WIDTH
	CALLX	REGSAV,R5	; SAVE REGISTERS
	MOV	2(R0),R1	; GET LSB
	MOV	(R0),R0		;  AND MSB
	BR	20$		; MERGE

ASCDBL::TST	(PC)+		; C=0 FOR DOUBLE PRECISION
ASCNUM::SEC			; C=1 FOR SINGLE PRECISION
	MOV	(R4)+,40$	; SAVE FIELD WIDTH
	CALLX	REGSAV,R5	; SAVE REGISTERS
	BCC	20$		; NUMBER MSB IS SET IF DOUBLE PRECISION
10$:	CLR	R0		; MSB IS 0 FOR SINGLE PRECISION
20$:	CLR	R4		; SAY NO NUMBER OUTPUT YET
	MOV	#10.,R3		; WE ARE DIVIDING BY TEN
30$:	CALLX	DIVDSP		; DIVIDE DOUBLE BY SINGLE PRECISION
	BIS	#'0,R2		; MAKE THE REMAINDER INTO AN ASCII NUMBER
	MOV	R2,-(SP)	;  AND SAVE IT ON THE STACK
	INC	R4		; COUNT ONE MORE CHARACTER
	TST	R0		; ANYTHING LEFT IN HIGH BYTE?
	BNE	30$		;  YES, KEEP ON DIVIDING
	TST	R1		; ANYTHING LEFT IN LOW BYTE?
	BNE	30$		;  YES, KEEP ON DIVIDING
	MOV	(PC)+,R0	; GET FIELD WIDTH
40$:	 .WORD	0		; SAVE AREA
	SUB	R4,R0		; COMPUTE NUMBER OF BLANKS TO FILL
	BLE	60$		; IF NONE, DON'T FILL
50$:	MOVB	#40,(R5)+	; BLANK
	SOB	R0,50$		;  FILL
60$:	MOVB	(SP)+,(R5)+	; MOVE
	SOB	R4,60$		;  NUMBER
	MOV	R5,5*2(SP)	; SAVE NEW TEXT POINTER
	CALLX	REGRES,R5	; RESTORE REGISTERS
	RETURN	R4

.DSABL	LSB

.END

