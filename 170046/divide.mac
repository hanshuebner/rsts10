TITLE	DIVIDE,<UNSIGNED DIVISION ROUTINES>,0A,10-MAY-91,SJK

;
;		COPYRIGHT (c) 1974, 1991 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

;+
; DIVDSP - UNSIGNED DOUBLE BY SINGLE PRECISION DIVIDE
; DIVSSP - UNSIGNED SINGLE BY SINGLE PRECISION DIVIDE
;
; 	R0  = MSB OF DIVIDEND
; 	R1  = LSB OF DIVIDEND
;	R3  = DIVISOR (UNSIGNED)
;
;	CALL	DIV?SP
;
;	R0  = MSB QUOTIENT
;	R1  = LSB QUOTIENT
;	R2  = REMAINDER (UNSIGNED)
;	R3  = DIVISOR
;-

.CSECT

.ENABL	LSB

DIVSSP::CLR	R0		;MAKE SURE R0 IS CLEAR
10$:	DIV	R3,R0		;DO A HARDWARE DIVIDE - ITS MUCH FASTER
	MOV	R1,R2		;SAVE THE REMAINDER
	MOV	R0,R1		;SAVE THE QUOTIENT
	CLR	R0		;MAKE SURE THE MSB OF QUOTIENT IS ZERO
	RETURN			;  AND RETURN

DIVDSP::TST	R0		;IS DIVIDEND SINGLE PRECISION?
	BNE	15$		; YES, SO DO THE HARDWARE DIVIDE
	TST	R1		;IS THE DIVIDEND 16 BITWISE NEGATIVE?
	BPL	10$		; NOPE, SO USE THE EIS DIV FUNCTION
15$:	PUSH	R5		;SAVE THE REMAINDER
	CLR	R2		;CLEAR THE REMAINDER
	MOV	#32.,R5		;NUMBER OF BITS IN A DOUBLE WORD
20$:	ASHC	#1,R0		;SHIFT THE DIVIDEND
	ROL	R2		; INTO THE REMAINDER
	CMP	R3,R2		;CAN WE SUBTRACT?
	BHI	30$		;NOPE
	SUB	R3,R2		;YEP
	INC	R1		; AND COUNT IN ANSWER
30$:	SOB	R5,20$		;LOOP IF MORE
	POP	R5		;THAT IS ALL
	RETURN			;NO, EXIT

.DSABL	LSB

.END
