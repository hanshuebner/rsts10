TITLE	ASCR50,<CONVERT ASCII TO RAD50>,0A,10-MAY-91,ABC

;
;		COPYRIGHT (c) 1974, 1991 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

;+
; ASCR50 - CONVERT ASCII STRING TO RADIX 50
;
; CALL:	R5 -> STRING TO CONVERT
;	CALL	ASCR50
;
; BACK:	RAD50 VALUE IN R0
;	R5 UPDATED
;	SCAN STOPS AT FIRST NON-RAD50 CHARACTER, OR AFTER 3
;		CHARACTERS HAVE BEEN SCANNED
;-
.CSECT

ASCR50::CALLX	SAVREG,R5	;SAVE REGISTERS
	CLR	R3		;CLEAR WORKING REGISTER
	MOV	#10$,R1		;A HANDY POINTER
	CALL	(R1)		;SCAN ONE
	CALL	(R1)		;     TWO
	CALL	(R1)		;     THREE
	MOV	R5,5*2+2(SP)	;RETURN UPDATED R5
	MOV	R3,0*2+2(SP)	;RETURN VALUE IN R0
	RETURN

10$:	MOVB	(R5),R0		;GET A BYTE
	SUB	#40,R0		;IS IT A SPACE?
	BEQ	20$		;YES, ACCEPT AS 0
	SUB	#'9+1-40,R0	;CHECK FOR NUMBER
	ADD	#'9+1-'0,R0
	BCC	40$		;NO, GO TRY FOR A LETTER
	ADD	#^R  0,R0	;YES, MAKE IT A RADIX 50 DIGIT
20$:	INC	R5		;BUMP PAST ACCEPTED CHARACTER
30$:	MUL	#50,R3		;SHIFT OLD VALUE
	ADD	R0,R3		;ADD IN NEW CHARACTER
	RETURN

40$:	SUB	#'Z+1-'0,R0	;CHECK FOR LETTER (AND REDUCE BY 'A)
	ADD	#'Z+1-'A,R0
	BCC	50$		;INVALID, USE BLANK FILL
	INC	R0		;CONVERT TO RAD50 LETTER
	BR	20$		;AND ACCEPT IT

50$:	CLR	R0		;BLANK
	BR	30$		;BUT DON'T SKIP IT

.END

