TITLE	$TTYIN,<INPUT ONE CHARACTER FROM CONSOLE>,0A,10-MAY-91,SJK

;
;		COPYRIGHT (c) 1974, 1991 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

;+
; $TTYIN - EMULATE THE RT11 .TTYIN CALL
;
;	CALLX	$TTYIN
;
;	R0 =  CHARACTER
;	XRB   IS DESTROYED
;
;	C=1 AND R0=0  IF ERROR ON READ (I.E. CONTROL Z)
;
;-

.CSECT

$TTYIN::CLR	-(SP)		; MAKE ROOM FOR THE CHARACTER ON THE STACK
	MOV	#XRB,R0		; GET A POINTER TO THE XRB
	MOV	#1,(R0)+	; SAY WE WANT  TO READ 1 CHARACTER
	CLR	(R0)+		; CLEARED ALWAYS FOR READ
	MOV	SP,(R0)+	; SET THE ADDRESS OF THE BUFFER
	CLR	(R0)+		; READ FROM CHANNEL ZERO
	CLR	(R0)+		; BLOCK 0 SINCE TTY IS SEQUENTIAL
	CLR	(R0)+		; NO WAIT TIME
	CLR	(R0)+		; NO MODIFIERS
	.PRIV,	.READ		; READ FROM THE TERMINAL, 1 BYTE
	TSTB	FIRQB		; WAS THERE AN ERROR ON THE READ?
	BEQ	10$		; NO, CONTINUE WITH NORMAL PROCESSING
	CLR	(SP)		; CLEAR ANY GARBAGE THAT MAY HAVE COME IN
	SEC			; SET THE CARRY BIT TO FLAG THE ERROR
10$:	MOV	(SP)+,R0	; PLACE THE CHARACTER INTO R0
	RETURN			;  AND RETURN TO THE CALLER

.END

