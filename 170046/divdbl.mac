TITLE	DIVDBL,<UNSIGNED LONGWORD DIVIDE>,0A,10-MAY-91,SJK

;
;		COPYRIGHT (c) 1974, 1991 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

;+
; DIVDBL - UNSIGNED LONG WORD DIVIDE
;
; 	R0  = MSB OF DIVIDEND
; 	R1  = LSB OF DIVIDEND
;	R2  = MSB OF DIVISOR
;	R3  = LSB OF DIVISOR
;
;	CALL	DIVDBL
;
;	R0  = MSB QUOTIENT
;	R1  = LSB QUOTIENT
;	R2  = MSB OF DIVISOR
;	R3  = LSB OF DIVISOR
;	R4  = MSB OF REMAINDER
;	R5  = LSB OF REMAINDER
;-

.CSECT

.ENABL	LSB

DIVDBL::CLR	R4		;CLEAR THE REMAINDER
	CLR	R5		;  AND THE LSB ALSO
	MOV	#32.,-(SP)	;NUMBER OF BITS IN A LONG WORD
10$:	ASHC	#1,R0		;SHIFT THE DIVIDEND
	ROL	R5		; INTO THE REMAINDER
	ROL	R4		;  BOTH HALVES
	CMP	R2,R4		;CAN WE SUBTRACT?
	BHI	30$		; NOPE
	BLO	20$		;  YES, AND NO NEED TO CHECK THE LSB
	CMP	R3,R5		;  DOES THE LSB AGREE?
	BHI	30$		;   NOPE
20$:	SUB	R3,R5		;YEP
	SBC	R4		; PROPAGATE ANY BORROW
	SUB	R2,R4		;  AND SUBTRACT MSB
	INC	R1		; AND COUNT IN ANSWER
30$:	DEC	(SP)		;NEED TO DO MORE?
	BGT	10$		; YEP, LOOP
	TST	(SP)+		;NOPE, POP THE STACK
	RETURN			; AND EXIT

.DSABL	LSB

.END
