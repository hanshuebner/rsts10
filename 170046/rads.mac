TITLE	RAD$,<CONVERT INTEGER TO RAD50>,0A,10-MAY-91,ABC/SJK

;
;		COPYRIGHT (c) 1974, 1991 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

;+
; RAD$   - CONVERT INTEGER IN R0 TO RAD50 STRING
; RAD$R0 - CONVERT INTEGER (R0)  TO RAD50 STRING
; RAD$W2 - CONVERT 2 INTEGERS (R0) TO RAD50 STRING
; RAD$FN - CONVERT 3-WORD FILE NAME (R0) TO RAD50 STRING
;
; CALL:	R0 = INTEGER TO CONVERT (FOR RAD$)
;	R0 -> INTEGER(S) TO CONVERT FOR RAD$R0, RAD$FN, AND RAD$W2
;	R5 -> OUTPUT STRING
;	CALL	RAD$XX
;
; BACK:	R0 UPDATED IF RAD$R0, RAD$FN, OR RAD$W2
;	R5 UPDATED TO POINT PAST NEW STRING
;-
.CSECT

RAD$FN::CALL	RAD$W2		;CONVERT FILENAME
	MOVB	#'.,(R5)+	;SET IN THE DOT
	BR	RAD$R0		; AND DO THE EXTENSION
	
RAD$W2::MOV	PC,-(SP)	;EXECUTE RAD$R0 TWICE
RAD$R0::TST	(PC)+		;CLEAR CARRY
RAD$::	SEC			;SET CARRY
	CALLX	SAVREG,R5	;SAVE ALL REGS
	MOV	#40$,R4		;DIVISORS
	MOV	R0,R3		;COPY FOR DIVIDE
	BCS	10$		;R0 HAS VALUE
	MOV	(R0)+,R3	;R0 -> VALUE
	MOV	R0,2(SP)	;UPDATE R0
10$:	CLR	R2		;CLEAR FOR DIVIDE
	DIV	(R4)+,R2	;DIVIDE
	TST	R2		;IS IT BLANK?
	BEQ	20$		;YES
	SUB	#26.,R2		;REDUCE 27.-39. TO 1.-13.
	BGT	20$		;GO IF NOT 1.-26. (ALPHA)
	ADD	#26.+'A-1,R2	;YES, CONVERT ALPHABETIC
	BR	30$		;AND SEND IT OUT

20$:	MOVB	50$(R2),R2	;GET THE CONVERTED BYTE
30$:	MOVB	R2,(R5)+	;PUT INTO LINE
	TST	(R4)		;MORE TO DO?
	BNE	10$		;NO
	MOV	R5,5*2+2(SP)	;RETURN UPDATED R5
	RETURN

40$:	.WORD	50*50,50,1
	.WORD	0
50$:	.ASCII	" $.?0123456789" ;TRANSLATION FOR SPACE, 27.-39.
	.EVEN

.END

