TITLE	NUM$,<CONVERT INTEGER TO STRING>,0A,10-MAY-91,ABC

;
;		COPYRIGHT (c) 1974, 1991 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

;+
; NUM$ - CONVERT NUMBER TO STRING
;
; CALL:	R0 = INTEGER TO CONVERT
;	R5 -> OUTPUT AREA FOR STRING
;	CALL	NUM$
;
; BACK:	NUMBER CONVERTED (UNSIGNED) WITH NO BLANK FILL
;	R5 -> PAST END OF STRING
;-
.CSECT

NUM$::	CALLX	SAVREG,R5	;SAVE THE WORLD
	MOV	#60$,R4		;POINT TO POWERS OF 10
10$:	TST	(R4)		;END OF TABLE (IT WAS 65535.)?
	BEQ	20$		;YES
	CMP	R0,(R4)+	;START YET?
	BHIS	10$		;NO
	TST	-(R4)		;BACK UP TABLE
20$:	TST	-(R4)
	BNE	30$		;NON-0 NUMBER TO PRINT
	MOVB	#'0,(R5)+	;PRINT A ZERO
	BR	50$

30$:	MOV	R0,R3		;GET FOR DIVIDE
40$:	CLR	R2		;CLEAR FOR DIVIDE
	DIV	(R4),R2		;DIVIDE BY POWER OF 10
	ADD	#'0,R2		;MAKE QUOTIENT A CHARACTER
	MOVB	R2,(R5)+	;INSERT IN LINE
	TST	-(R4)		;GET ANOTHER POWER
	BNE	40$		;AND LOOP
50$:	MOV	R5,14(SP)	;UPDATE HIS R5
	RETURN

	.WORD	0		;STOPPER FOR SCAN
60$:	.WORD	1,10.,100.,1000.,10000.,0

.END

