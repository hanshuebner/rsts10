TITLE	SYMMAC,<Local Symbol Macros>,0A,10-MAY-91,CEK

;
;		COPYRIGHT (c) 1974, 1991 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

.SBTTL	EDIT HISTORY FOR SYMMAC
;+
;
;  001	CEK  16-APR-82	Added edit history
;
;-

.MACRO	SYMMAC

; NXT.SYM is the number of the local symbol that will be allocated
; by the next call to GET.SYM.

NXT.SYM	=	^D40000

; Clobber ourselves.

.MACRO	SYMMAC
NXT.SYM	=	^D40000		;But remember to reinitialize on pass 2.
	.ENDM
;+
; GET.SYM symnum
;	symnum is a MACRO symbol.  
;
;	Sets symnum to an almost unique number, which can be concatenated to 
;	$ to create a local symbol.  The number is in the range 40000 (decimal)
;	to 65534 (decimal).  When all of the numbers in that range have been 
;	used, then GET.SYM starts over again.  
;-

.MACRO	GET.SYM	SYMNUM
	.DSABL	CRF
SYMNUM	=	NXT.SYM
	.IF	EQ <NXT.SYM - ^D65534>;If we're about to overflow then
	NXT.SYM = ^D40000	;  Wrap around.
	.IFF
	NXT.SYM = NXT.SYM + 1
	.ENDC
	.ENABL	CRF
	.ENDM
;+
; BR.SYM instr,symnum
;	"symnum" is a MACRO symbol, or a decimal number.
;	"instr" is a branch instruction mnemonic, such as BR, BGE, etc.  JMP
;	can also be used.
;
;       Makes a branch instruction to local symbol n$, where n is the value
;	of symnum.
;-

.MACRO	BR.SYM	INSTR,SYMNUM
	.RADIX	10.
	.IRP	S,<\SYMNUM>
	 INSTR	S'$
	.ENDR
	.RADIX	8.
	.ENDM
;+
; PUT.SYM symnum
;	"symnum" is a decimal number.
;	"listoption" may be either LIST or NLIST, and may be omitted.
;	If it is LIST, the label appears in the listing.  NLIST is the
;	default.
;
;	Makes local symbol n$, where n is the value of symnum.
;-

.MACRO	PUT.SYM SYMNUM,LISTOPTION=NLIST
	.RADIX	10.
	.IF	IDN	LISTOPTION,LIST
	.LIST
	 SYMNUM'$:
	.NLIST
	.IFF	;IDN	LISTOPTION,LIST
	.IIF	DIF	LISTOPTION,NLIST	.ERROR	;LISTOPTION:  invalid.
	 SYMNUM'$:
	.ENDC	;IDN	LISTOPTION,LIST
	.ENDM	PUT.SYM
	.NLIST
	.RADIX	8.
	.ENDM	SYMMAC
