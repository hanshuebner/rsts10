.INCLUDE	/CMN:COMMON.MAC/
.LIBRARY	/CUI:CUIMAC.MLB/
.INCLUDE	/CUI:CUIPFX.MAC/
TITLE	CUIFSS,<CUI Filename String Scan>,0A,10-MAY-91,CEK

;
;		COPYRIGHT (c) 1974, 1991 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

.SBTTL	EDIT HISTORY FOR CUIFSS
;+
;
;  001	CEK  30-May-82	Creation
;
;-

;+
; CUIFSS - CUI Filename String Scan
;
; INPUTS:
;	len	=	length of filespec string
;	addr	->	filespec string
;	User logical table at standard address.  (Do NOT use this routine 
;			under RT11 emulation.)
;
; USAGE:
;	CALBX	U$FSS <len,addr>
;
; OUTPUTS:
; C=0 if success.
;	R0	=	0
;	FIRQB and XRB describe the filespec just scanned.
; C=1 if failure.
;	R0	=	RSTS/E error code.
;	FIRQB and XRB random.
;
; DESCRIPTION:
;	This routine scans a RSTS/E filespec string.
;
;	Unlike the .FSS directive, the FSS routine (a) ignores previous
;	contents of FIRQB, (b) returns "?Not a valid device"
;	when appropriate, and (c) does not allow the scan to terminate
;	before the end of the string.
;
;	You should get the error code out of R0, NOT out of the FIRQB.
;-

	.PSECT	CUIFSS,RO,REL,LCL,CON
	DEFORG	CUIFSS
	.ENABL	AMA
	.ENABL	LSB
	.MCALL	SUBR,.ASSERT

.ENABL	LSB

U$FSS::	SUBR	<LEN,ADDR>
	CALLX	CLRFQX		;Zero the FIRQB and XRB.
	MOV	#XRB+XRLEN,R1	;Point at XRLEN field.
	TST	(R5)+		;Point at LEN argument.
	MOV	(R5),(R1)+	;Put length into XRLEN field.
	.ASSUME	XRBC EQ XRLEN+2	;XRBC field is next.
	MOV	(R5)+,(R1)+	;Put length into XRBC field.
	.ASSUME	ADDR EQ LEN+2	;ADDR is next argument.
	.ASSUME	XRLOC EQ XRBC+2	;XRLOC field is next in XRB.
	MOV	(R5),(R1)	;Set up XRLOC field.
	SYS	.FSS		;Scan the filespec.
	.ASSERT	R0 EQ #0	;CALB pre-clears R0.
	BISB	FIRQB,R0	;Get error code.
	BNE	20$		;Error. Quit.
	TST	-(R1)		;Unscanned characters?  (Look at XRBC field.)
	BEQ	10$		;No.
	MOV	#BADNAM,R0	;Yes. ?Illegal file name.
	BR	20$		;Quit.

10$:	TST	XRB+10		;Untranslated device name?
	BGE	20$		;No. Return.
	MOV	#NODEVC,R0	;Yes. ?Not a valid device.
20$:	RETURN

.DSABL	LSB
GLOBAL	<NODEVC,BADNAM>
.END
