.INCLUDE	/CMN:COMMON.MAC/
.LIBRARY	/CUI:CUIMAC.MLB/
TITLE	SAVREG,<REGISTER SAVING COROUTINE>,0A,10-MAY-91,<RTW,CEK>

;
;		COPYRIGHT (c) 1974, 1991 BY
;	DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
;
; THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND  COPIED
; ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH  LICENSE AND WITH THE
; INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR  ANY  OTHER
; COPIES  THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
; OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF  THE  SOFTWARE  IS  HEREBY
; TRANSFERRED.
;
; THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE  WITHOUT  NOTICE
; AND  SHOULD  NOT  BE  CONSTRUED  AS  A COMMITMENT BY DIGITAL EQUIPMENT
; CORPORATION.
;
; DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE  OR  RELIABILITY  OF ITS
; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.
;

.SBTTL	EDIT HISTORY FOR SAVREG
;+
;
;  001	CEK  16-APR-82	Added edit history
;  002	CEK  16-APR-82	Removed @# notation
;
;-

	.ENABL	LC
;+
; SAVREG - Save registers.  Restore them when caller returns.
;
; CALL:	The routine using SAVREG must have been entered via JSR PC,x.
;
;	SAVREG
;
; BACK:	All registers are saved on the stack.  SAVREG calls caller as 
;	coroutine.
;	When caller RETURNs, SAVREG restores registers and 
;	returns to caller's caller.
;	Caller can reference the register value that is saved on the stack 
;	as TOS.Rn(SP) (where Rn is the register).
;	A caller can reference its own return address as TOS.RA(SP).  
;-

.MACRO	SAVREG
	 JSR	R5,SAVREG
	.GLOBL	SAVREG
	.DSABL	CRF
TOS.R0	=	2.
TOS.R1	=	4.
TOS.R2	=	6.
TOS.R3	=	8.
TOS.R4	=	10.
TOS.R5	=	12.
TOS.RA	=	14.
	.ENABL	CRF
	.ENDM	SAVREG

	.PSECT	RSTSLB,RO,CON
	.LIST	MEB

SAVREG::PUSH	<R4,R3,R2,R1,R0>
	PUSH	R5		;Our return address.
	MOV	6*2(SP),R5	;Fix R5.
	CALL	@(SP)+		;Call caller back.
	POP	<R0,R1,R2,R3,R4,R5>
	RETURN
.END
